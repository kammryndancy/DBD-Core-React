0010 ! ZZGQIM.AP4  File Maintenance Program for AP4
0020 ! "6.0 - 11/29/05 - 10:47 PM - jir
0030 ! "Copyright 2004 Computer Software Inc.; Norcross, Georgia
0035 ! "5.4 - 11/09/05 - 15.152777 - jir - SSP# 189260
0060 ! 
0070 ! PROCESS "UTGAP4","../UTG/UT.EN"
0080 ! 
0090 ! EXIT 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="UTGAP4"
0135 CALL "$CTL-10;PB_CREATE",0,0,17,1.4,20,1,0
0140 X0=-1; X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
0160 ! 
0170 ! Dim Variables
0180 DIM Z[NUM(X3$(60,3))],AP4[2],AP5[6]
0190 ! 
0195 DIM VEND_CAT$[100,2],BK_CODE$[100,2],T_CODE$[100,2],GL_CODE$[100,2]
0200 ! open files
0210 Z$="01O AP4...  02O AP2...  03O AP3...  04O GL1...  05O ZYB...  06O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0300 ! iolist
0310 IOLIST AP4$,AP4{ALL}
0320 IOLIST AP2$,AP2{ALL}
0330 IOLIST AP3$,AP3{ALL}
0340 IOLIST GL1$
0350 IOLIST ZYB$
0400 ! 
0450 PROS_FLG=0
0485 REFRESH_FLG=1
0490 ! RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 ! 
0530 GOSUB PROCESS_FILE
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 PROCESS "APGUT5","../APG/AP.EN","YES"
0970 EXIT 
1000 ! Get the vendors info
1010 PROCESS_FILE:
1020 KEY_1$=""; COUNT=0
1030 READ (Z[1],KEY=KEY_1$,DOM=*NEXT)
1040 KEY_1$=KEY(Z[1],END=END_ROUTINE); READ (Z[1])IOL=0310
1045 VENDOR$=KEY_1$; VEND_NAME$=AP4$(11,35)
1050 FIND (Z[3],KEY=AP4$(247,9),DOM=CAT_ERR)
1060 FIND (Z[5],KEY=AP4$(283,3),DOM=BANK_ERR)
1070 FIND (Z[2],KEY=AP4$(286,2),DOM=TERM_ERR)
1080 IF NOT(NUL(AP4$(271,12))) THEN FIND (Z[4],KEY=AP4$(271,12),DOM=GL_ERR)
1090 ++COUNT; CALL "$CTL-10;PB_VALUE",COUNT
1100 IF POS(AP4$(246,1)="AI")=0 THEN AP4$(246,1)="A"
1105 IF AP4$(290,1)=" " THEN AP4$(290,1)="G"
1108 IF ASC(AP4$(290,1))<65 OR ASC(AP4$(290,1))>90 THEN AP4$(290,1)="G"
1110 IF AP4$(288,2)="  " THEN AP4$(288,2)="00"
1112 IF AP4$(291,1)=" " THEN AP4$(291,1)="N"
1115 IF AP4$(231,1)=" " THEN AP4$(231,1)="N"
1117 IF POS(AP4$(231,1)="YN")=0 THEN AP4$(231,1)="N"
1120 IF AP4$(293,1)=" " THEN AP4$(293,1)="N"
1122 IF POS(AP4$(293,1)="YN")=0 THEN AP4$(293,1)="N"
1490 WRITE (Z(1),KEY=KEY_1$)IOL=0310
1499 GOTO 1040
1500 ! Process Vendor Category
1510 CAT_ERR:
1520 DIM BB$(39); BB$(1,9)=AP4$(247,9),BB$(10,30)="** NOF **"
1530 WRITE (Z(3),KEY=BB$(1,9))BB$
1595 GOTO 1060
1600 ! Process Bank Code 
1605 BANK_ERR:
1610 DIM BB$(150); BB$(1,3)=AP4$(283,3),BB$(4,30)="** NOF **"
1620 WRITE (Z(5),KEY=BB$(1,3))BB$
1655 GOTO 1070
1700 ! Get the Vendor Information
1705 TERM_ERR:
1710 DIM BB$(100); BB$(1,2)=AP4$(286,2),BB$(3,30)="** NOF **"
1720 WRITE (Z[2],KEY=BB$(1,2))BB$
1755 GOTO 1080
1800 ! Get the Vendor Information
1805 GL_ERR:
1810 DIM BB$(60); BB$(1,12)=AP4$(271,12),BB$(13,35)="** NOF **"
1820 WRITE (Z(4),KEY=BB$(1,12))BB$
1855 GOTO 1090
2000 ! Find The changes requested
2010 FIND_CHANGE:
2015 ARRAY_LINE=0
2020 SWITCH ERR_FLG
2030 CASE 1
2040 FOR XX=1 TO 100
2050 IF NUL(VEND_CAT$[XX,1]) THEN NOT_FOUND=1; ARRAY_LINE=XX; XX=100; GOTO 2090
2060 MATCH$=VEND_CAT$[XX,1]
2070 IF MATCH$(1,9)<>AP4$(247,9) THEN GOTO 2090
2080 AP4$(247,9)=VEND_CAT$[XX,2]
2085 GOSUB WRITE_REC
2087 XX=100
2090 NEXT XX
2095 BREAK
2100 CASE 2
2110 FOR XX=1 TO 100
2120 IF NUL(BK_CODE$[XX,1]) THEN NOT_FOUND=1; ARRAY_LINE=XX; XX=100; GOTO 2170
2130 MATCH$=BK_CODE$[XX,1]
2140 IF MATCH$(1,3)<>AP4$(283,3) THEN GOTO 2170
2150 AP4$(283,3)=BK_CODE$[XX,2]
2160 GOSUB WRITE_REC
2165 XX=100
2170 NEXT XX
2180 BREAK
2200 CASE 3
2210 FOR XX=1 TO 100
2220 IF NUL(T_CODE$[XX,1]) THEN NOT_FOUND=1; ARRAY_LINE=XX; XX=100; GOTO 2270
2230 MATCH$=T_CODE$[XX,1]
2240 IF MATCH$(1,2)<>AP4$(286,2) THEN GOTO 2270
2250 AP4$(286,2)=T_CODE$[XX,2]
2260 GOSUB WRITE_REC
2265 XX=100
2270 NEXT XX
2280 BREAK
2300 CASE 4
2310 FOR XX=1 TO 100
2320 IF NUL(GL_CODE$[XX,1]) THEN NOT_FOUND=1; ARRAY_LINE=XX; XX=100; GOTO 2370
2330 MATCH$=GL_CODE$[XX,1]
2340 IF MATCH$(1,12)<>AP4$(271,12) THEN GOTO 2370
2350 AP4$(271,12)=GL_CODE$[XX,2]
2360 GOSUB WRITE_REC
2365 XX=100
2370 NEXT XX
2380 BREAK
2400 END SWITCH 
2900 RETURN 
3000 ! Enter the new Vendor Category
3010 VEND_CAT_ENTRY:
3020 AP4$(247,9)=NEW_VEND_CAT$
3030 VEND_CAT$[ARRAY_LINE,2]=AP4$(247,9); VEND_CAT$[ARRAY_LINE,1]=OLD_VEND_CAT$
3040 GOSUB WRITE_REC
3060 CALL "*wingrp;Hide",CAT_1.GRP$; CALL "*wingrp;Hide",HEAD_1.GRP$
3070 PROS_FLG=1; CALL "*wingrp;show",PROC_1.GRP$
3080 REFRESH_FLG=1
3090 RETURN 
3100 ! Enter the new Bank Code
3110 BANK_CODE_ENTRY:
3120 AP4$(283,3)=NEW_BANK_CODE$
3130 BK_CODE$[ARRAY_LINE,2]=AP4$(283,3); BK_CODE$[ARRAY_LINE,1]=OLD_BANK_CODE$
3140 GOSUB WRITE_REC
3160 CALL "*wingrp;Hide",BANK_1.GRP$; CALL "*wingrp;Hide",HEAD_1.GRP$
3170 PROS_FLG=1; CALL "*wingrp;show",PROC_1.GRP$
3180 REFRESH_FLG=1
3190 RETURN 
3200 ! Enter the new Terms Code
3210 TERM_CODE_ENTRY:
3220 AP4$(286,2)=NEW_TERMS_CODE$
3230 T_CODE$[ARRAY_LINE,2]=AP4$(286,2); T_CODE$[ARRAY_LINE,1]=OLD_TERMS_CODE$
3240 GOSUB WRITE_REC
3260 CALL "*wingrp;Hide",TERM_1.GRP$; CALL "*wingrp;Hide",HEAD_1.GRP$
3270 PROS_FLG=1; CALL "*wingrp;show",PROC_1.GRP$
3280 REFRESH_FLG=1
3290 RETURN 
3300 ! Enter the new GL Code
3310 GL_CODE_ENTRY:
3320 AP4$(271,12)=NEW_GL_ACCT$
3330 GL_CODE$[ARRAY_LINE,2]=AP4$(271,12); GL_CODE$[ARRAY_LINE,1]=OLD_GL_ACCT$
3340 GOSUB WRITE_REC
3360 CALL "*wingrp;Hide",GL_1.GRP$; CALL "*wingrp;Hide",HEAD_1.GRP$
3370 PROS_FLG=1; CALL "*wingrp;show",PROC_1.GRP$
3380 REFRESH_FLG=1
3390 RETURN 
4000 ! Write record out....
4010 WRITE_REC:
4015 ! 
4020 WRITE (Z[1],KEY=KEY_1$)IOL=0310
4030 ! MSGBOX "The information has been updated...","FYI"
4080 B_AP5$=AP5$
4090 RETURN 
9000 ! End Routine
9010 END_ROUTINE:
9020 ! 
9030 CALL "$CTL-10;PB_DEL"
9050 CMD_STR$="END"
9090 RETURN 
