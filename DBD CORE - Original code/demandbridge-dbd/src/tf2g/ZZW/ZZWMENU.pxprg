0010 REM "System Menu Selection Program <ZZWMENU>
0035 REM "5.7 - 09/05/19 - 9.836111 - jvv - SSP# 307208
0037 REM "307208-GUI Menu cannot be maximized.                               
0040 REM "Copyright 2019 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 ! ^100 - Process Main Menu Panel
0105 ERROR_HANDLER "ERROR"
0106 PRINT 'SHOW'(-1),'CURSOR'(1),
0108 IF %RIGHT THEN PROCESS "RIGHT_MENU","../ZZW/ZZ.EN",ERR=*NEXT; GOTO 0120
0110 PROCESS "MENU",".."+DLM+"ZZW"+DLM+"ZZ.EN"
0120 RELEASE 
0130 ! 
0200 ! ^100
0210 INIT:
0211 MESSAGE_LIB %FLMAINT_MSG$ ! 194087
0215 SETESC OFF 
0216 OFF$="D",%_MESSAGE=0
0220 IOLIST MENU_FULLNAME$,MPATH$,APP$,_OBJ_SEL$,MNU_OPT$
0230 IOLIST X3$,M1$,X4$,D0$,X$,A9$
0231 IOLIST X3$,MMM1$,XX4$,DD0$,XX$,AA9$
0235 ADDR "ZZGCLE"
0250 _TREE=GFN
0260 OPEN (_TREE,IOL=0220)"*MEMORY*"
0265 GOSUB CLEAN_UP
0270 GOSUB FILES
0275 IF MID(_OPERATOR$,64,1)="1" THEN SETESC ON ; ON$="D",OFF$=""
0280 GOSUB FUNCTIONS
0290 GOSUB INIT_MENU
0300 GOSUB MSGLIB
0310 GOSUB CHECK_APP
0320 GOSUB RESIZE_PANEL ! SSP 229216
0340 IF MOD(NUM(DTE(0:"%D")),2)=0 THEN _SOUND_NAME$="newstart.wav"; PERFORM "$CTL-10;Sound_play" ELSE _SOUND_NAME$="topform.wav"; PERFORM "$CTL-10;Sound_play"
0350 ! _SOUND_NAME$="topform.wav"; PERFORM "$CTL-10;Sound_play"
0355 ! CALL "$CTL-10;CHECK_BMP_FILE" ! 238083
0356 PERFORM "$CTL-10;CHECK_VISUAL_EFFECT" ! 238455
0370 EXIT 
0400 ! ^100
0410 FILES:
0420 _ZX4=GFN,_FILE_NAME$="ZX4" ! ssp 214331
0430 OPEN (_ZX4,ERR=OPEN_ERR)_FILE_NAME$
0440 _ZX0=GFN,_FILE_NAME$="ZX0" ! ssp 214331
0450 OPEN (_ZX0,ERR=OPEN_ERR)_FILE_NAME$
0455 _ZZPARM=FFN("ZZPARM"); IF _ZZPARM<=0 THEN _ZZPARM=GFN,_FILENAME$="ZZPARM"; OPEN (_ZZPARM,ERR=OPEN_ERR)"ZZPARM" ! ssp 214331
0460 ! _ZZPARM=GFN,_FILE_NAME$="ZZPARM" ! ssp 214331
0470 ! OPEN (_ZZPARM,ERR=OPEN_ERR)_FILE_NAME$
0475 _SEC=GFN,_FILE_NAME$="ZZWSEC"; OPEN (_SEC,ERR=DEFINE_SEC)_FILE_NAME$ ! ssp 214331
0477 _FILENAME$="ZZN"+%C$,_ZZN=GFN; OPEN (_ZZN,IOL=*,ERR=OPEN_ERR,REC=ZZN$)_FILENAME$ ! ssp 214331
0478 READ (_ZZPARM,KEY="o"+%X3_OP_ID$,ERR=*NEXT)_OPERATOR$
0480 IF %_LOG_HISTORY=0 THEN GOTO *RETURN
0482 _LOG=GFN; OPEN (_LOG,ERR=CREATE_LOG_HISTORY)"LOG"+%C$ ! ssp 214331
0485 RETURN 
0490 ! 
0500 ! ^100
0510 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
0520 CMD_STR$="END"
0530 RETURN 
0540 ! 
0600 ! ^100
0610 MSGLIB:
0620 SWITCH %X3_LANG_CODE$
0630 CASE " "
0640 MSGLIB$="*tfmsglib.en"
0650 BREAK
0660 DEFAULT 
0670 MSGLIB$="*tfmsglib.en"
0680 END SWITCH 
0685 IF NOT(NUL(%FLMAINT_MSG$)) THEN MSGLIB$=%FLMAINT_MSG$ ! 194087
0690 RETURN 
0700 ! 
0800 ! ^100
0810 INIT_MENU:
0811 ! GOTO *RETURN
0820 CALL "ZZCOMP",ERR=0885,X0$,X1$,X2$,X3$,X4$,X5$,-1,0,0
0910 READ (_ZZPARM,KEY=X3$(1,8))IOL=0230
0915 X3$(185,1)="M"
0920 DIM D0$(28); D0$(1,6)=X3$(1,2)+"DATE"
0930 READ (_ZZPARM,KEY=D0$(1,6),DOM=*NEXT)D0$(1)
0940 GOSUB 4200; %X3_MACH_DATE$=D0$(7,6),X3$(21,6)=D0$(7,6)
0950 ! IF POS(" "<>X3$(15,6))>0 THEN IF POS(" "<>D0$(23,6))>0 THEN GOTO 0965
0960 %X3_TODAY$=D0$(7,6),X3$(15,6)=D0$(7,6),D0$(20,9)="SYS"+D0$(7,6); WRITE (_ZZPARM,KEY=D0$(1,6))D0$
0965 WRITE (_ZZPARM,KEY=X3$(1,8))IOL=0230
0970 RETURN 
0980 ! 
1000 ! ^1000
1010 MNU_SELECTION:
1011 WAIT 0
1030 LBNUM=LIST_BOX_1.CTL
1040 LIST_BOX READ LBNUM,M1$,MODE$,ERR=1050
1050 IF STP(M1$,2)="" THEN LBNUM=0; GOSUB SELECT_CTRLS; LBNUM=0; GOTO MNU_SELECTION_END
1070 ! Get Module Codes
1080 READ (_TREE,KEY="",ERR=*NEXT)
1085 _OBJ_SEL$=""
1090 READ (_TREE,KEY=M1$,ERR=MNU_SELECTION_END)IOL=0220
1095 IF MID(_OBJ_SEL$,1,1)="G" THEN LBNUM=0; GOTO MNU_SELECTION_END
1098 GOSUB CHECK_SECURITY; IF YES=0 THEN _SOUND_NAME$="no06.wav"; PERFORM "$CTL-10;Sound_Play"; CALL "$CTL-10;AGENT_ANIM","Getattention"; MSGBOX MSG("ACCESS_DEN"),MSG("WARNING"),"!,TIM=3"; EXIT ; REM RETURN ! SSP247971 jdf
1100 ! Display Menu
1110 GOSUB 1500
1120 _APP=UNT,_FND=0
1125 IF STP(_OBJ_SEL$)<>"" THEN FROM_TREE=1; GOSUB 12000; FROM_TREE=0; GOTO MNU_SELECTION_END
1130 OPEN (_APP,ERR=*NEXT)MENU_FULLNAME$; CLOSE (_APP); _FND=1
1140 ! 
1150 IF _FND THEN {
1160 LBNUM'ENABLED=0,REFRESH_FLG=1
1170 ! PRINT 'SHOW'(-1)
1180 GOSUB 12000 ! PROCESS "MENU",MPATH$; RETURN
1190 ! PRINT 'SHOW'(3)
1200  }
1210 MNU_SELECTION_END:
1220 ! 
1225 IF LIST_BOX_1.CTL=0 THEN GOTO 1240
1230 LIST_BOX_1.CTL'ENABLED=1,MODE$=""
1240 MPATH$="",M1$="",APP$="",OBJ_SEL$=""
1250 REFRESH_FLG=1,CHANGE_FLG=0
1255 IF LBNUM=CTL THEN GOTO 1010
1260 EXIT 
1400 ! ^100 Check Application
1410 CHECK_APP:
1420 CALL "TF"+%LIC_COMP$+";OSCAR",APPS_ALLOWED$
1425 %HAS_IMAGE_LIB=0; IF FN%HAS_MODULE("IL") THEN FIND (_ZZPARM,KEY=%C$+"I/L",DOM=*NEXT); %HAS_IMAGE_LIB=1
1430 RETURN 
1500 ! ^100 Set date & time
1510 SET_DATE_TIME:
1520 IF MID(X3$,77,1)="U" THEN Q1$=DAY; GOTO 1535
1530 IF MID(X3$,1,2)<>"T0" OR MID(SCRN_PRT$,2,3)<>"   " THEN GOTO 1560 ELSE Q1$=DAY
1540 Q1$=DTE(JUL(0,0,0):"YYYYMMDD"),Q1$=CHR(NUM(Q1$(1,3))-125)+Q1$(4)
1550 IF Q1$=MID(D0$,7,6) THEN GOTO 1560
1560 RETURN 
1570 ! 
1600 ! ^ 100 LOAD TREE
1610 LOAD_TREE:
1615 GOSUB LOAD_FAV; IF %CB_FAV THEN GOTO 1995 ! SSP 221970
1620 IF _ZX0=0 OR _ZX4=0 THEN GOSUB CLOSE_FILES; GOSUB FILES
1630 READ (_ZX4,KEY="",ERR=*NEXT)
1635 ! 
1640 K$=KEY(_ZX4,END=1990); DIM MENU$(200); READ (_ZX4,KEY=K$)MENU$
1650 CUR_MNU$=MENU$(76,2)
1660 MENU_NAME$=MENU$(3,73),P=POS("|"=MENU_NAME$); IF P<=0 THEN GOTO 1640
1670 CUR_DESC$=STP(MENU_NAME$(P+1),2)
1680 MENU_KEY$=CUR_DESC$
1690 FULLPATH$=STP(MENU_NAME$(1,P-1),2)+SEP+MENU_KEY$
1695 ! 
1700 DIM ZX0$(80); READ (_ZX0,KEY=CUR_MNU$,DOM=1640)ZX0$
1705 ! 
1710 IF FNAPP_INSTALLED(CUR_MNU$) THEN {
1720 APP$=ZX0$(41,2); IF APP$="FS" THEN APP$="OP" ELSE IF APP$="BS" THEN APP$="BR"
1730 MENU_FULLNAME$=".."+DLM+APP$+"G"+DLM+APP$+"G"+"MEN"
1740 MPATH$=HWD+DLM+"tf2g"+DLM+APP$+"G"+DLM+APP$+".EN"
1750 _APP=UNT,_FND=0
1760 OPEN (_APP,ERR=*NEXT)MENU_FULLNAME$; CLOSE (_APP); _FND=1
1770 IF ERR<>12 THEN _FND=1
1780 IF _FND THEN M_ICON$="{!DONE}" ELSE M_ICON$="{!FILE}"
1790 LIST_BOX LOAD LIST_BOX_1.CTL,0,M_ICON$+FULLPATH$
1800 WRITE (_TREE,KEY=CUR_DESC$)IOL=0220; WRITE (_TREE,KEY=FULLPATH$)IOL=0220
1801 ! IF APP$<>"GL" THEN GOTO 1810
1805 CALL "TREE_LOAD;LD_TEMPFL",_TREE,MPATH$,LIST_BOX_1.CTL,FULLPATH$,APP$,MENU_FULLNAME$,TMP_FL1,LIB_FN; CLOSE (TMP_FL1,ERR=*NEXT)
1806 CLOSE (LIB_FN,ERR=*NEXT)
1810  } ELSE {
1820 LIST_BOX LOAD LIST_BOX_1.CTL,0,"{!KEY}"+FULLPATH$
1830  }
1840 GOTO 1640
1850 ! 
1990 PERFORM "ZZWMENU;CUSTOM_FLDR" ! SSP 221970
1995 RETURN ! SSP 221970
2000 ! 
2025 ! PRINT 'SHOW'(-1)
4200 REM "Set date & time
4210 Q1$=DAY
4220 Q1$=DTE(JUL(0,0,0):"YYYYMMDD"),Q1$=CHR(NUM(Q1$(1,3))-125)+Q1$(4)
4230 IF Q1$=MID(D0$,7,6) THEN GOTO *RETURN
4235 Q$=FN%NTD$(JUL(0,0,0),"YYYYMMDD"),Q$=CHR(NUM(Q$(1,3))-125)+Q$(4),D0$(7,6)=Q$,DATE$=Q$
4236 Q$=STR(INT(TIM):"00")+STR(FPT(TIM)*100:"00"),D0$(13,4)=Q$; CALL "ZZDISP","TX",D0$(13,4),"",X3$,D9$,"",0,0,X4$; TIME$=D9$(1,2)+D9$(4,2)
4240 GOSUB GET_DAY_OF_WEEK
4250 PROCESS "ZMENU.D","../ZZW/ZZ.EN",DATE$,TIME$,DAY_OF_WEEK$ ! SSP213459
4255 D0$(7,6)=DATE$ ! Need logic to fix time if changed?
4260 WRITE (_ZZPARM,KEY=D0$(1,6))D0$; Q1$=D0$(7,6); GOSUB LAST_ACCESS
4270 RETURN 
4280 ! 
4300 LAST_ACCESS:
4310 DIM O0$(120); EXTRACT (_ZZPARM,KEY="o"+X3$(40,3))O0$(1); O0$(76,6)=D0$(7,6),O0$(82,4)=STR(TIM*100:"0000"),O0$(86,8)=X3$(1,8); WRITE (_ZZPARM)O0$; G9=0
4340 LAST_ACCESS_END:RETURN 
4345 ! 
5000 GET_DAY_OF_WEEK:! From ZZSETD
5020 CALL "ZZDAYT",DATE$,Q0; F9=GFN; OPEN (F9,ERR=OPEN_ERR)"ZY2"; FIND (F9,KEY=%X3_LANG_CODE$+STR(Q0:"00"),DOM=5025)W0$; GOTO 5030 ! ssp 214331
5025 DIM W0$(100); Q0$=""; IF %X3_LANG_CODE$<>" " THEN READ (F9,KEY=" "+STR(Q0:"00"),DOM=5030)W0$
5030 DAY_OF_WEEK$=STP(W0$(80,20),1)
5045 GET_DAY_OF_WEEK_END:RETURN 
5049 ! 
5100 SAVE_PANEL_SETTINGS:! save panel settings to ZZPARM key="@"+operator + tcp address
5105 X$=""; CALL "[wdx]*windx.utl;get_addr",X$,ERR=SAVE_SETTINGS_END; IF POS("rs232"=X$)>0 THEN X$=MID(X$,POS(" "=X$,-1)+1)
5110 ADDR$=MID(X$,1,POS(";"=X$)-1),OPERATOR$=MID(X$,POS(";"=X$,1,2)+1)
5120 SP$=PAD("@"+OPERATOR$+ADDR$,16)
5130 X$=OBJ(0),M$=MSE
5132 COL=INT((DEC(X$(21,2))+1)/DEC($00$+M$(10,1)))
5134 LIN=INT((DEC(X$(23,2))+1)/DEC($00$+M$(11,1)))
5135 IF LIN<0 OR COL<0 THEN GOTO SAVE_SETTINGS_END ! Don't save if suspicious looking values SSP 229216
5150 WRITE (_ZZPARM,KEY=SP$)SP$,COL,LIN,_SV_PNL_W,_SV_PNL_H
5190 SAVE_SETTINGS_END:
5195 RETURN 
5199 ! ******************************************************
5200 GET_PANEL_SETTINGS:! Get the panel settings, if found and resize the panel
5205 X$=""; CALL "[wdx]*windx.utl;get_addr",X$,ERR=GET_SETTINGS_END; IF POS("rs232"=X$)>0 THEN X$=MID(X$,POS(" "=X$,-1)+1)
5210 ADDR$=MID(X$,1,POS(";"=X$)-1),OPERATOR$=MID(X$,POS(";"=X$,1,2)+1)
5220 SP$=PAD("@"+OPERATOR$+ADDR$,16)
5225 FIND_SETTINGS:READ (_ZZPARM,KEY=SP$,ERR=*NEXT)KY$,COL,LIN,WID,HHT; GOTO GOT_SETTINGS
5229 ! If not exact match then is next key a match? then use it , else is prev key a match? then use it, else give up
5230 NEXT$=KEY(_ZZPARM,END=*PROCEED); PREV$=KEP(_ZZPARM,END=*PROCEED)
5235 IF MID(NEXT$,1,LEN(OPERATOR$)+1)=MID(SP$,1,LEN(OPERATOR$)+1) THEN SP$=NEXT$; GOTO FIND_SETTINGS
5240 IF MID(PREV$,1,LEN(OPERATOR$)+1)=MID(SP$,1,LEN(OPERATOR$)+1) THEN SP$=PREV$; GOTO FIND_SETTINGS
5245 GOTO GET_SETTINGS_END
5270 GOT_SETTINGS:
5271 IF COL<0 OR LIN<0 THEN GOTO GET_SETTINGS_END ! If wacky values then skip
5272 IF COL>150 THEN COL=5,LIN=5 ! SSP 307208
5274 WID=MIN(_PNL_MAX_W,WID),HHT=MIN(HHT,_PNL_MAX_H)
5275 _ORIG_OBJ_C=COL,_ORIG_OBJ_L=LIN,_ORIG_OBJ_W=WID,_ORIG_OBJ_H=HHT; PRINT (0,ERR=*NEXT)'SIZE'(_ORIG_OBJ_W,_ORIG_OBJ_H),'MOVE'(_ORIG_OBJ_C,_OBJ_L), ! SSP 229216
5290 GET_SETTINGS_END:
5295 RETURN 
5299 ! ****************************************************
6000 ! ^6000 Show Window
6010 SHOW_MENU:
6011 PRINT 'CURSOR'(0),
6022 GOSUB RESIZE_PANEL
6025 MENU_BAR -1000,"-[Help]"
6028 GOSUB CHECK_ZZN
6030 RETURN 
8000 ! ^ 8000,10 Resize for server only at this time
8010 RESIZE_PANEL:
8030 ! PRINT 'MOVE'(0,0)
8040 ! PRINT 'SIZE'(132,35)
8050 ! PRINT 'SHOW'(2)
8052 GOSUB GET_PANEL_SETTINGS ! SSP 229216
8053 IF _ORIG_OBJ_W=0 THEN GOTO 8070
8055 ! NEW_W=_PNL_MAX_W,NEW_H=MIN(_PNL_MAX_H,INT(_ORIG_OBJ_H*NEW_W/_ORIG_OBJ_W)) ! SSP 229216
8060 ! PRINT 'SIZE'(NEW_W,NEW_H) ! SSP 229216
8065 ! GOSUB GET_PANEL_SETTINGS ! 229216
8070 ! _X$=OBJ(0),_X1=DEC($00$+_X$(33,2)),_X2=DEC($00$+_X$(35,2)); PERFORM "*winproc.rsz;Resize" ! SSP 229216
8090 RETURN 
8100 ! 
8200 ! ^8200,10
8210 INIT_LISTBOX:
8220 GOSUB LOAD_TREE
8230 GOSUB LOAD_PICTURE
8240 GOSUB RESIZE_LISTBOX
8250 REFRESH_FLG=1
8260 RETURN 
8270 ! 
8400 ! ^ 8400,10
8410 LOAD_PICTURE:
8420 IF DEC(MID(MSE,22,1))>0 THEN %WDX$="[WDX]" ELSE %WDX$=""
8430 IF %WDX$<>"" THEN {
8432 CALL %WDX$+"*windx.utl;get_windx",LOCDIR$ ! SSP 307208
8434 XX=POS("\"=LOCDIR$,-1); IF XX<>0 THEN LOCDIR$=LOCDIR$(1,XX-1) ! First time, one level SSP 307208
8436 XX=POS("\"=LOCDIR$,-1); IF XX<>0 THEN LOCDIR$=LOCDIR$(1,XX-1) ! Second time, two levels up SSP 307208
8438 %WINDX_DIR$=LOCDIR$+"\windx" ! SSP 307208
8440 ! CALL %WDX$+"*WINDX.UTL;GET_LWD",%WINDX_DIR$
8450 IF %WINDX_DIR$(LEN(%WINDX_DIR$),1)<>DLM THEN %WINDX_DIR$+=DLM
8460  }
8470 IF NOT(LEN(%WDX$)) THEN {
8480 PICTURE$=".\db_logo.bmp" ! SSP212146
8490  } ELSE {
8500 PICTURE$=%WINDX$+%WINDX_DIR$+"topform\db_logo.bmp" ! SSP212146
8505 IF POS("WindX V5.14 Plugin"=%WINDX_DIR$)<>0 THEN PICTURE$=%WINDX$+%WINDX_DIR$+"WindX\topform\db_logo.bmp" ! SSP212146
8510  }
8515 IF %PICTURE$<>"" THEN PICTURE1$=PICTURE$,PICTURE$=%PICTURE$
8520 RETURN 
8530 ! 
8600 ! ^8600,10
8610 RESIZE_LISTBOX:
8620 LBNUM=LIST_BOX_1.CTL
8630 LBNUM'VISIBLE=1
8640 RETURN 
8650 ! 
8800 ! ^8800
8810 FUNCTIONS:
8820 DEF FNAPP_INSTALLED(LOCAL APP_NAME$)
8830 IF POS(APP_NAME$=APPS_ALLOWED$)=0 THEN RESULT=0 ELSE RESULT=1
8840 RETURN RESULT
8850 END DEF
8860 RETURN 
9000 ! ^9000,10
9010 WRAP_UP:
9011 WAIT 0
9012 IF %NO_ASK=0 THEN MSGBOX MSG("RELEASE"),MSG("CONFIRMING"),"?,YESNO",OPT$; IF OPT$="NO" THEN IGNORE_EXIT$="Y"; RETURN 
9015 REMOVE (_ZZPARM,KEY=%X3_TERM_ID$+DIM(6),DOM=*NEXT)
9018 GOSUB SAVE_PANEL_SETTINGS
9020 GOSUB CLOSE_FILES
9025 _SOUND_NAME$="logoff.mp3"; PERFORM "$CTL-10;Sound_play"
9026 WAIT 4.5
9027 PERFORM "$CTL-10;DEL_AGENT",ERR=*NEXT
9034 RUN "ZZEND"
9035 EXIT 
9040 ! 
9100 ! ^9100,10
9110 CLOSE_FILES:
9120 CLOSE (_ZX0,ERR=*NEXT)
9125 REMOVE (_ZZPARM,KEY="RUN"+FID(0),ERR=*NEXT)
9130 CLOSE (_ZZPARM,ERR=*NEXT)
9140 CLOSE (_TREE,ERR=*NEXT)
9145 CLOSE (_ZX4,ERR=*NEXT)
9146 IF F9 THEN CLOSE (F9)
9147 CLOSE (MENU,ERR=*NEXT)
9150 RETURN 
9999 END 
10000 ! ^10000
10010 CLEAN_UP:! Clean up anything when coming from a program
10015 PRINT (0,ERR=*NEXT)'DROP'(200), ! Drop prompt/message window if needed
10095 RETURN 
10099 ! 
12000 ! 12000,20
12020 IF _OBJ_SEL$<>"" THEN BACK$=_OBJ_SEL$
12040 APPP$=APP$ ! IF APPP$="OP" THEN APP$="FS" ELSE IF APPP$="BR" THEN APP$="BS"
12060 FIND (_ZX0,KEY=APPP$,DOM=*NEXT)DESC$; DESC$=STP(DESC$(3,35))
12080 MM1$=M1$ ! PRINT 'DIALOGUE'(0,MXL(0)-6,MXC(0)-6,6,"",OPT="h"),'SB','BLUE','FONT'("Arial",2,"BIUR"),'TEXT'(@X(40),@Y(MXL(0)-4),@X(MXC(0)),@Y(MXL(0)-2),M1$),
12100 UUU$=MM1$(POS(SEP=MM1$)+1),DDD=POS(SEP=UUU$); IF DDD<>0 THEN DESC$=UUU$(1,DDD-1) ELSE DESC$=UUU$
12120 UUU$=UUU$(POS(SEP=UUU$)+1)
12140 DDD=POS(SEP=UUU$); IF DDD<>0 THEN UUU$(DDD,1)=">"; GOTO *SAME
12160 IF STP(DESC$)=STP(UUU$) THEN UUU$=""
12180 PRINT 'CAPTION'(MSG("TF")+$AE$+" "+MSG("SW_VERSION")+" - "+DESC$+">"+UUU$),; M1.CTL'VALUE$=STP(DESC$) ! SSP213071
12190 IF POS("Purchase Orders"=UUU$)<>0 THEN APP$="PO"
12200 IF APPP$="~~" THEN GOTO 12230 ELSE %MODULE$=APP$(1,1)+"/"+APP$(2),CHANGE_DATE=0; PERFORM "$CTL-10;SELECT_ACT_DATE" ! SSP 221970
12220 WRITE (_ZZPARM,KEY="RUN"+FID(0))FID(0),%X3_OP_ID$,%X3_COMP$,APP$,"",DTE(0:"%Dl, %Ml %D, %Yl - %Hz:%mz %P")
12230 CLOSE (MENU,ERR=*NEXT)
12240 MNU$="",MENU=GFN; OPEN LOAD (MENU,ERR=END_MENU)MPATH$ ! ssp 214331
12260 LIST_BOX ENABLE LBNUM
12280 IOLIST _OBJ_NME$,_OBJ_C,_OBJ_L,_OBJ_W,_OBJ_H,_OBJ_TYPE$,INIT_TEXT$,INIT_VAL$,_OBJ_TAB,_OBJ_DEF$,_OBJ_DSP$,_OBJ_FCS$,_OBJ_SEL$,_OBJ_MSG$,_OBJ_HLP$,_OBJ_ATTR$,_OBJ_IDX$,_OBJ_HOTKEY$,_OBJ_QRY$,_OBJ_SEC$,_OBJ_STS$,_OBJ_GRP$,_OBJ_NULL$,_OBJ_TAG$,_OBJ_TBL$,_OBJ_INP$,_OBJ_OUT$,_OBJ_VALID$,_OBJ_CLASS$,_OBJ_TIP$,_OBJ_ORIG$,_OBJ_FONT$,_OBJ_COLOR$,_OBJ_LISTBOX_TYPE$,_OBJ_SEP$,_OBJ_SCRATCH$,_OBJ_POPUP$,_OBJ_SIZING$,_OBJ_LOGIC1$,_OBJ_LOGIC2$,_OBJ_POPUP_LOGIC$
12290 IF TCB(29)>=7100000 THEN _P=MENU,_MENU_PNL$="MENU        "; PERFORM "WINPROC_MNU;BUILD_MENU"; MNU$=_MENU_DEF$; GOTO END_MENU ! ssp 219339
12300 READ (MENU,KEY="MENU",DOM=*NEXT)
12320 K$=KEY(MENU,END=END_MENU); READ (MENU)IOL=12280
12340 IF K$(1,4)<>"MENU" THEN GOTO END_MENU
12360 IF K$(13,2)<>"M " THEN GOTO 12320
12380 MNU$+=INIT_TEXT$
12400 GOTO 12320
12420 END_MENU: _OBJ_SEL$=""
12440 IF MNU$="" THEN PROCESS "MENU",MPATH$; RETURN 
12450 ! XX=POS("]"=MNU$); IF XX<>0 THEN MNU$=MNU$(1,XX-1)+",&_Help=20000"+MNU$(XX)
12455 XX=POS("]"=MNU$); IF XX<>0 THEN GOSUB CHECK_HELP; MNU$=MNU$(1,XX-1)+HELP$+MNU$(XX)
12457 IF %CB_FAV THEN GOTO 12470
12460 MENU_BAR -1000,MNU$
12480 IF CTL<0 AND CTL>-1000 THEN CALL "*control" ! SSP265388, restore from before SSP263500 change
12500 IF BACK$<>"" THEN _OBJ_SEL$=BACK$,BACK$=""; GOTO 12900
12520 CONTROLS: %GRID=0
12525 IF FROMERR=1 THEN FROMERR=0; GOTO 12540
12527 START_TIME=TMR(0)
12530 %NOCONTROL=1; GOSUB CHECK_REMINDER; %NOCONTROL=0 ! GOSUB CHECK_ZZN
12535 ! CALL "$CTL-10;AGENT_SHOW"
12540 SET_FOCUS LIST_BOX_1.CTL; OBTAIN (0,SIZ=1,TIM=120,ERR=CHECK_REMINDER_FROMERR)'ME',*,'MN'
12560 WAIT 0
12570 _SOUND_NAME$="start.wav"; PERFORM "$CTL-10;Sound_play"
12580 SELECT_CTRLS: ANT=CTL
12585 IF CTL=20001 THEN LIST_BOX_1.CTL'EXPANDED=0 ! ssp 213509
12590 IF CTL=1 OR CTL=20000 THEN GOSUB HELP_FILE; SET_FOCUS LIST_BOX_1.CTL; GOTO CONTROLS
12595 IF CTL=12 THEN CALL "$CTL-23;PRT_SCR",ERR=12540 ELSE IF CTL=-23 THEN CALL "$CTL-23;WORD_SCR",ERR=12540; GOTO 12540
12600 IF CTL=18001 OR CTL=19001 THEN GOSUB DROP_ON; GOTO CONTROLS
12610 ! CLOSE (%SCR_LIB,ERR=*NEXT) ! SSP 225634 230219
12612 ! %SCR_LIB=GFN; OPEN (%SCR_LIB)"../ZZW/ZZ.EN" ! SSP 225634 230219
12620 IF CTL=-1105 THEN GOSUB WINPROC_RESIZE; GOTO CONTROLS ! SSP 229216
12630 ! %SAVE_SCR_LIB=%SCR_LIB,%SAVE_SCR_LIB$=PTH(%SAVE_SCR_LIB),%SCR_LIB=0; CLOSE (%SAVE_SCR_LIB,ERR=*NEXT) ! SSP 225634
12640 IF CTL=-1999 THEN MSGBOX MSG("RELEASE"),MSG("CONFIRMING"),"?,YESNO",OPT$; IF OPT$="YES" THEN GOSUB SAVE_PANEL_SETTINGS; CMD_STR$="END",%NO_ASK=1; RETURN ELSE GOTO CONTROLS ! SSP 229216
12660 IF LIST_BOX_1.CTL=CTL THEN GOTO END_OPTION
12670 IF CTL=10002 THEN _SOUND_NAME$="mail01.wav"; PERFORM "$CTL-10;Sound_Play"; PROCESS "GGOPTION.3","../ZZG/ZZ.EN"; GOTO CONTROLS
12680 IF CTL=-1402 THEN PERFORM "$CTL-10;TODAY_DATE" ! PRINT 'MESSAGE'(DEF 0),; PROCESS "ZZGDAY","../ZZG/ZZ.EN"; PRINT 'MESSAGE'('_BLUE'+'WHITE'+"Today's Date: "+DTE(0:"%Dl, %Ml %D, %Yl ")+" - User:"+%X3_OP_ID$,2),; GOTO 13140
12700 IF CTL=-1400 THEN PERFORM "$CTL-10;start_process"; GOTO 13140
12720 IF CTL=-1401 THEN PERFORM "$CTL-10;Company"; GOTO 13140
12740 IF CTL=-1083 OR CTL=-1411 THEN PERFORM "$CTL-10;OPTIONS"; IF %ESC$<>"" THEN WAIT 0; EXECUTE %ESC$; WAIT 0; %ESC$="" END_IF ; GOTO 13140
12760 IF CTL=-1411 THEN PROCESS "ZZGOPT","../ZZG/ZZ.EN"; GOTO 13140
12780 IF CTL<0 AND CTL>-1000 THEN CALL "*control"; PRINT 'MESSAGE'(DEF -65,-35),; GOTO CONTROLS ! SSP265388, restore from before SSP263500 change
12800 XX=CTL; IF CTL<=16999 OR CTL>=18000 THEN GOTO CONTROLS
12820 READ (MENU,KEY="MENU        M"+STR(XX-17000:"000"),DOM=12840)IOL=12280
12830 GOSUB CHECK_SECURITY_MENU_BAR; IF YES=0 THEN _SOUND_NAME$="no06.wav"; PERFORM "$CTL-10;Sound_Play"; CALL "$CTL-10;AGENT_ANIM","GetAttention"; MSGBOX MSG("ACCESS_DEN"),MSG("WARNING"),"!,TIM=3"; GOTO 13240
12840 PRINT 'CAPTION'(MSG("TF")+$AE$+" "+MSG("SW_VERSION")+" - "+DESC$+">"+STP(INIT_TEXT$,3,"&")),; M1.CTL'VALUE$=STP(DESC$) ! SSP213071
12860 ! MENU_BAR OFF WHERE$,ERR=*NEXT
12880 ! MENU_BAR READ WHERE$
12900 IF _OBJ_SEL$(1,1)="X" AND POS("_SCRN"=UCS(_OBJ_SEL$))<>0 AND POS("_LIB"=UCS(_OBJ_SEL$))<>0 THEN GOTO NEW_MENU
12920 IF MID(_OBJ_SEL$,1,2)="XP" THEN GOTO 12980
12940 PASS$="",XX=POS(","=_OBJ_SEL$); IF XX THEN PASS$=_OBJ_SEL$(XX+1),_OBJ_SEL$=_OBJ_SEL$(1,XX-1),PASS$=STP(PASS$,3,QUO)
12960 IF _OBJ_SEL$(1,1)="E" THEN GOTO END_OPTION
12980 ! PRINT 'MESSAGE'(DEF 0),
12990 CALL "$CTL-10;AGENT_HIDE"
13000 NAME$=_OBJ_SEL$(2),NAME$=STP(NAME$,3,QUO),SUBNAME$=NAME$,XX=POS("/"=SUBNAME$,-1); IF XX<>0 THEN SUBNAME$=SUBNAME$(XX+1)
13020 WRITE (_ZZPARM,KEY="RUN"+FID(0))FID(0),%X3_OP_ID$,%X3_COMP$,APP$,NAME$,DTE(0:"%Dl, %Ml %D, %Yl - %Hz:%mz %P"); B_DATE$=BIN(NUM(DTE(0:"%Yl%Mz%Dz")),4),B_TIME$=BIN(NUM(DTE(0:"%Hz%mz")),2); IF %_LOG_HISTORY THEN WRITE (_LOG,KEY="x"+%X3_OP_ID$+B_DATE$+B_TIME$+SUBNAME$,ERR=*NEXT)%X3_OP_ID$,B_DATE$,B_TIME$,SUBNAME$,APP$,%X3_COMP$,"Run"
13030 READ (_ZZPARM,KEY=FID(0)+"      ")IOL=0231; X3$(146,8)=SUBNAME$; WRITE (_ZZPARM,KEY=FID(0)+"      ")IOL=0231
13060 IF _OBJ_SEL$(1,1)="L" THEN PROCESS NAME$,MPATH$,ERR=*NEXT
13080 IF POS(_OBJ_SEL$(1,1)="PC") AND PASS$<>"" THEN CALL NAME$,ERR=*NEXT,PASS$; GOTO 13130
13100 IF POS(_OBJ_SEL$(1,1)="PC") THEN CALL NAME$,ERR=*NEXT
13120 IF MID(_OBJ_SEL$,1,2)="XP" THEN EXECUTE _OBJ_SEL$(2)
13130 GOSUB CLOSE_ALL
13140 ! PRINT 'MESSAGE'(DEF -65,-35),
13150 _ZZPARM=FFN("ZZPARM"); IF _ZZPARM<=0 THEN _ZZPARM=GFN,_FILENAME$="ZZPARM"; OPEN (_ZZPARM,ERR=OPEN_ERR)"ZZPARM" ! ssp 214331
13160 WRITE (_ZZPARM,KEY="RUN"+FID(0))FID(0),%X3_OP_ID$,%X3_COMP$,APP$,"",DTE(0:"%Dl, %Ml %D, %Yl - %Hz:%mz %P")
13180 M1.CTL'VALUE$=DESC$
13200 MENU_BAR ON WHERE$,ERR=*NEXT
13220 ! MENU_BAR GOTO ! SSP213667, VISTA operating system crashes 
13240 PRINT 'CAPTION'(MSG("TF")+$AE$+" "+MSG("SW_VERSION")+" - "+DESC$), ! SSP213071
13250 IF %_LOG_HISTORY THEN WRITE (_LOG,KEY="x"+%X3_OP_ID$+B_DATE$+B_TIME$+SUBNAME$,ERR=*NEXT)%X3_OP_ID$,B_DATE$,B_TIME$,SUBNAME$,APP$,%X3_COMP$,"Finish",DTE(0:"%Hz%mz")
13260 IF FROM_TREE THEN FROM_TREE=0
13265 ! Make sure Audit flag is cleared
13266 ZZPARM_IOL:IOLIST X3$,_ZZPARM_1$,X4$,_ZZPARM_2$,_ZZPARM_3$,ZZ_PARM_4$
13267 READ (_ZZPARM,KEY=FID(0)+DIM(6))IOL=ZZPARM_IOL
13270 %ZZCOMP_SKIP=0,%X3$(174,4)="",X3$(185,1)="",X3$(146,6)="",X3$(174,4)="",X3$(178,6)="",%NO_NEW_BATCH=0; WRITE (_ZZPARM,KEY=FID(0)+DIM(6))IOL=ZZPARM_IOL
13275 IF %B_WINDATE_FMT$<>"" THEN %WINDATE_FMT$=%B_WINDATE_FMT$
13278 %GRID=0; IF APP$<>"ZZ" THEN CALL "ZZGCLE",ERR=*NEXT,APP$
13280 CALL "ZZ2CLR" ! Clear GL Variable
13285 %CUSTOMER$="",%ITEM_CODE$=""
13290 GOTO CONTROLS
13300 END_OPTION:
13320 ! MENU_BAR RESET
13340 PRINT 'MESSAGE'("",0),
13360 CLOSE (MENU,ERR=*NEXT)
13380 MENU_BAR -1000,"-[Help]"
13400 PRINT 'CAPTION'(MSG("TF")+$AE$+" "+MSG("SW_VERSION")), ! SSP213071
13420 GOTO MNU_SELECTION ! SSP 222546
13440 NEW_MENU:
13460 XD=POS(","=_OBJ_SEL$); IF XD<>0 THEN ONE$=_OBJ_SEL$(1,XD-1),TWO$=_OBJ_SEL$(XD+1) ELSE ONE$=_OBJ_SEL$
13480 XD=POS("_LIB"=UCS(ONE$)); IF XD<>0 THEN XD=POS("="=ONE$); IF XD<>0 THEN MPATH$=QUO+"../"+ONE$(XD+2)
13500 XD=POS("_LIB"=UCS(TWO$)); IF XD<>0 THEN XD=POS("="=TWO$); IF XD<>0 THEN MPATH$=QUO+"../"+TWO$(XD+2)
13520 CLOSE (MENU)
13540 MPATH$=STP(MPATH$,3,QUO)
13560 GOTO 12240
13580 DROP_ON:
13600 LIST_BOX READ LBNUM,M1$,MODE$,ERR=1050
13610 GOSUB CHECK_SECURITY; IF YES=0 THEN _SOUND_NAME$="no06.wav"; PERFORM "$CTL-10;Sound_Play"; CALL "$CTL-10;AGENT_ANIM","GetAttention"; MSGBOX MSG("ACCESS_DEN"),MSG("WARNING"),"!,TIM=3"; RETURN 
13620 IF POS(MSG("FAVORITES")=M1$)<>0 THEN ADD_OP=0 ELSE ADD_OP=1
13640 READ (_TREE,KEY=M1$,ERR=*RETURN)FAV_NAME$,FAV_PATH$,FAV_APP$,FAV_OBJ$,FAV_MNU_OPT$; FAV_PASS$=PASS$,FAV_LVL$=LVL$
13660 XXX=POS(SEP=M1$); IF XXX=0 THEN RETURN 
13680 M1$=M1$(XXX+1),APLI$=M1$ ! ,XXX=POS(SEP=M1$); IF XXX=0 THEN RETURN 
13685 GOTO 13740
13700 APLI$=M1$(1,XXX-1),M1$=M1$(XXX+1); IF ADD_OP THEN XXX=POS(SEP=M1$,-1); IF XXX=0 THEN RETURN END_IF ; ELSE XXX=0
13720 OPT$=M1$(XXX+1),COUNT=0
13740 ! IF ADD_OP THEN MSG$=MSG("FAV_ADD") ELSE MSG$=MSG("FAV_DEL") END_IF ; MSGBOX MSG$+SEP+SEP+"From :"+APLI$+" "+OPT$,MSG("CONFIRMING"),"?,YESNO",OPT1$
13745 IF POS("FAVORITES"=UCS(MM1$(1,11)))>0 THEN IF POS(CHR(138)=MM1$,1,2)>0 THEN OPT=11; GOTO 13800 ELSE GOTO 13760 ! SSP249556 jdf
13750 IF NOT(NUL(_OBJ_SEL$)) AND POS("ZZWMENU"=UCS(_OBJ_SEL$))=0 THEN GOTO *NEXT ELSE OPT=11; GOTO 13800 ! SSP249556 jdf
13760 IF ADD_OP THEN OPT_MNU$="[From: "+APLI$+" "+OPT$+"=D,,"+"&"+MSG("FAV_ADD")+"],D:[&Yes=10,&No=11]" ELSE OPT_MNU$="[From: "+APLI$+" "+OPT$+"=D,,"+"&"+MSG("FAV_DEL")+"],D:[&Yes=10,&No=11]"
13780 POPUP_MENU OPT_MNU$,OPT
13800 IF OPT=10 THEN OPT1$="YES" ELSE OPT1$="NO"; RETURN 
13820 IF ADD_OP=0 THEN GOTO DEL_OP
13840 VAR$=MSG("FAVORITES")+SEP+APLI$+SEP+OPT$
13860 READ (_ZZPARM,KEY="FAV"+%X3_OP_ID$,DOM=*NEXT)
13880 FAV_KEY$=KEY(_ZZPARM,END=END_ZZP); READ (_ZZPARM)
13900 IF MID(FAV_KEY$,1,3+LEN(%X3_OP_ID$))<>"FAV"+%X3_OP_ID$ THEN GOTO END_ZZP
13920 COUNT=NUM(FAV_KEY$(7))
13940 GOTO 13880
13960 END_ZZP:
13966 VAR1$="Favorites"+SEP+M1$(POS(SEP=M1$,-1)+1),VAR$=VAR1$,VAR1$=""
13980 COUNT+=1
14000 LIST_BOX LOAD LIST_BOX_1.CTL,0,VAR$
14020 WRITE (_TREE,KEY=VAR$,ERR=*RETURN)FAV_NAME$,FAV_PATH$,FAV_APP$,FAV_OBJ$,FAV_MNU_OPT$,FAV_PASS$,FAV_LVL$
14030 REM WRITE (_SEC,KEY=VAR$,ERR=*RETURN)FAV_NAME$,FAV_PATH$,FAV_APP$,FAV_OBJ$,FAV_MNU_OPT$,FAV_PASS$,FAV_LVL$  ! SSP247971 jdf
14040 XXX=POS(SEP=VAR$); IF XXX THEN VAR$(XXX,1)="|"; GOTO *SAME
14060 WRITE (_ZZPARM,KEY="FAV"+%X3_OP_ID$+STR(COUNT:"000"))VAR$,FAV_NAME$,FAV_PATH$,FAV_APP$,FAV_OBJ$,FAV_MNU_OPT$,FAV_PASS$,FAV_LVL$
14080 REFRESH_FLG=1
14100 RETURN 
14120 LOAD_FAV:
14140 READ (_ZZPARM,KEY="FAV"+%X3_OP_ID$,DOM=*NEXT)
14160 FAV_KEY$=KEY(_ZZPARM,END=LOAD_FAV_END)
14180 READ (_ZZPARM,KEY=FAV_KEY$)VAR$,FAV_NAME$,FAV_PATH$,FAV_APP$,FAV_OBJ$,FAV_MNU_OPT$,FAV_PASS$,FAV_LVL$
14200 IF MID(FAV_KEY$,1,3+LEN(%X3_OP_ID$))<>"FAV"+%X3_OP_ID$ THEN GOTO LOAD_FAV_END
14220 XXX=POS("|"=VAR$); IF XXX THEN VAR$(XXX,1)=SEP; GOTO 14220
14240 LIST_BOX LOAD LIST_BOX_1.CTL,0,VAR$
14260 WRITE (_TREE,KEY=VAR$,ERR=*RETURN)FAV_NAME$,FAV_PATH$,FAV_APP$,FAV_OBJ$,FAV_MNU_OPT$,FAV_PASS$,FAV_LVL$
14280 GOTO 14160
14300 LOAD_FAV_END:RETURN 
14320 DEL_OP:
14340 IDX=LIST_BOX_1.CTL'CURRENTITEM
14360 LIST_BOX FIND LIST_BOX_1.CTL,IDX,VV$
14380 LIST_BOX LOAD LIST_BOX_1.CTL,IDX,*
14400 REMOVE (_TREE,KEY=VV$,DOM=*NEXT)
14410 REMOVE (_SEC,KEY=VV$,DOM=*NEXT)
14420 XXX=POS(SEP=VV$); IF XXX THEN VV$(XXX,1)="|"; GOTO *SAME
14440 READ (_ZZPARM,KEY="FAV"+%X3_OP_ID$,DOM=*NEXT)
14460 FAV_KEY$=KEY(_ZZPARM,END=DEL_OP_END); READ (_ZZPARM)VAR$
14480 IF MID(FAV_KEY$,1,3+LEN(%X3_OP_ID$))<>"FAV"+%X3_OP_ID$ THEN GOTO DEL_OP_END
14500 IF VAR$<>VV$ THEN GOTO 14460
14520 REMOVE (_ZZPARM,KEY=FAV_KEY$)
14540 DEL_OP_END:RETURN 
14560 IDX+=1
14580 IF IDX=1 THEN GOTO DEL_OP_END
14600 LIST_BOX FIND LIST_BOX_1.CTL,IDX,VVV$
14620 XXX=POS(SEP=VVV$); IF XXX THEN VVV$(XXX,1)="|"; GOTO *SAME
14640 IF VV$(1,POS("|"=VV$)-1)<>VVV$(1,POS("|"=VVV$)) THEN IDX-=2; GOTO 14360
14680 CHECK_SECURITY: PASS=0,LVL$="",PASS$=""
14700 BK_OBJ_SEL$=_OBJ_SEL$; READ (_SEC,KEY=M1$,DOM=END_SECUTIRY)*,*,*,*,*,PASSWORD$,LVL$,ROLE$
14705 IF _OBJ_SEL$<>BK_OBJ_SEL$ THEN _OBJ_SEL$=BK_OBJ_SEL$
14710 IF ROLE$<>"" AND POS(%ROLE$=ROLE$)=0 THEN YES=0; RETURN 
14720 IF POS(%ROLE$=ROLE$)<>0 THEN YES=1; GOTO 14760
14740 IF %X3_OP_PRTY$<LVL$ THEN YES=0; RETURN ELSE YES=1
14760 IF PASSWORD$="" THEN YES=1; RETURN 
14770 IF PASSW$=PASSWORD$ THEN YES=1; RETURN ELSE PASSW$=""
14775 ! CALL "$CTL-10;AGENT_MSG",%USER_NAME$+", Please Enter Password",3,15,15; CALL "$CTL-10;AGENT_HIDE",ERR=*NEXT,1
14780 PROCESS "PASSWORD","../ZZG/ZZ.EN",PASS$
14800 IF PASS$<>PASSWORD$ THEN PASS+=1; IF PASS<2 THEN _SOUND_NAME$="error.wav"; PERFORM "$CTL-10;Sound_Play"; GOTO 14780 ELSE YES=0; RETURN ""
14820 YES=1,PASSW$=PASS$
14840 RETURN 
14860 END_SECUTIRY: YES=1; RETURN 
14870 CHECK_SECURITY_MENU_BAR:
14875 MENU_BAR OFF WHERE$,ERR=*NEXT
14880 MENU_BAR READ WHERE$
14890 BK_OBJ_SEL$=_OBJ_SEL$; READ (_SEC,KEY=APP$+WHERE$,DOM=END_SECUTIRY)IOL=0220,PASSWORD$,LVL$,ROLE$
14895 IF _OBJ_SEL$<>BK_OBJ_SEL$ THEN _OBJ_SEL$=BK_OBJ_SEL$
14900 GOSUB 14710
14910 IF YES=0 THEN MENU_BAR DISABLE WHERE$ ELSE MENU_BAR ON WHERE$
14920 RETURN 
14930 DEFINE_SEC:
14940 KEYED %DATAPATH$+"/ZZWSEC",127,,1024,ERR=*NEXT
14950 RETRY 
15000 CHECK_ZZN:! 15000,10
15005 CALL "*WINGRP;HIDE",MAIL.GRP$
15010 _ZZN=FFN("ZZN"+%C$); IF _ZZN<=0 THEN _ZZN=GFN; OPEN (_ZZN,IOL=*,ERR=*RETURN,REC=ZZN$)"ZZN"+%C$ ! ssp 214331
15030 READ (_ZZN,KEY=%X3_OP_ID$+"I",DOM=*NEXT)
15040 K_ZZN$=KEY(_ZZN,END=END_CHECK_ZZN); READ (_ZZN,REC=ZZN$)
15050 IF MID(K_ZZN$,1,4)<>%X3_OP_ID$+"I" THEN MESSAGE=0; GOTO END_CHECK_ZZN
15060 IF STP(ZZN.VIEWED_DATE$)<>"" THEN GOTO 15040
15062 IF %_MESSAGE=0 THEN _SOUND_NAME$="mail01.wav"; PERFORM "$CTL-10;Sound_Play"; %_MESSAGE=1; WAIT 1
15065 CALL "*WINGRP;SHOW",MAIL.GRP$; GOTO END_CHECK_ZZN
15070 PROCESS "GGOPTION.3","../ZZG/ZZ.EN"
15080 END_CHECK_ZZN:SETERR 0000; RETURN 
15900 CHECK_REMINDER_FROMERR: FROMERR=1,ACTUAL_TIME=TMR(1); IF MID(_OPERATOR$,62,2)<>"UL" AND NUM(MID(_OPERATOR$,62,2),ERR=*NEXT)*60<ACTUAL_TIME THEN GOSUB ASK_TO_EXIT
16000 CHECK_REMINDER:
16005 GOSUB CHECK_ZZN
16008 _ZZPARM=FFN("ZZPARM"); IF _ZZPARM<=0 THEN _ZZPARM=GFN,_FILENAME$="ZZPARM"; OPEN (_ZZPARM,ERR=OPEN_ERR)"ZZPARM" ! ssp 214331
16010 K_REM$="r"+%X3_OP_ID$+BIN(NUM(DTE(0:"%Yl%Mz%Dz")),4)
16020 READ (_ZZPARM,KEY=K_REM$,DOM=*NEXT)
16030 K_ZZP_REM$=KEY(_ZZPARM,END=NO_REMINDER); READ (_ZZPARM)*,_REMDATE$,_DBTIME$,*,ML_DEL
16040 IF MID(K_ZZP_REM$,1,LEN(K_REM$))<>K_REM$ THEN GOTO NO_REMINDER
16042 IF _REMDATE$>DTE(0:"%Yl%Mz%Dz") THEN GOTO NO_REMINDER
16045 GOSUB CHECK_DB_TIME; IF D_T$>C_T$ THEN GOTO 16030
16050 PROCESS "GGOPTION.6","../ZZG/ZZ.EN"
16060 NO_REMINDER:
16065 IF %NOCONTROL THEN RETURN 
16068 ! IF NOT(NUL(%PLAY_SHOW$)) THEN PERFORM "$CTL-10;display_SHOW"
16070 GOTO CONTROLS
16100 CHECK_DB_TIME:
16110 C_T$=DTE(0:"%Hz%mz"),D_T$=_DBTIME$(1,2)+_DBTIME$(4,2); IF _DBTIME$(7,1)="P" AND _DBTIME$(1,2)<>"12" THEN D_T$(1,2)=STR(NUM(D_T$(1,2))+12)
16140 RETURN 
17000 ASK_TO_EXIT:
17010 MSGBOX "Do you wish to continue","Software Logout","?,TIM=10",OPT$
17020 IF OPT$="OK" THEN START_TIME=TMR(0); RETURN 
17030 %NO_ASK=1,OPT$=""
17040 GOTO WRAP_UP
17100 CREATE_LOG_HISTORY:
17110 KEYED %DATAPATH$+DLM+"LOG"+%C$,20,0,128,ERR=NO_HISTORY
17115 _LOG=GFN; OPEN (_LOG)"LOG"+%C$ ! ssp 214331
17120 RETURN 
17130 NO_HISTORY:
17140 %_LOG_HISTORY=0
17150 RETURN 
18000 HELP_FILE:
18020 FIND (_ZZPARM,KEY="HLP"+%MODULE$,DOM=NO_HELP)*,PTH$
18025 IF POS(QUO=PTH$)=0 THEN PTH$=QUO+PTH$+QUO
18030 PTH$=EVS(PTH$)
18040 SYSTEM_HELP PTH$
18050 RETURN 
18060 NO_HELP:
18070 MSGBOX MSG("NOHELP")+SEP+SEP+"Module: "+%MODULE$+" - "+DESC$,MSG("Sorry"),"INFO,TIM=4"
18080 RETURN 
18100 CHECK_HELP: HELP$="Help",TXT$=MNU$(1,XX)
18110 FOR AA=1 TO LEN(HELP$)
18120 XXX=POS("&"+HELP$(AA,1)=TXT$); IF XXX=0 THEN EXITTO UNDER
18130 NEXT AA
18140 IF TCB(29)>=7100000 THEN HELP$=",&_Help=20000,&*Collapse Menu=20001" ELSE HELP$=",&_Help=20000" ! ssp 213509
18150 RETURN 
18160 UNDER:
18170 IF TCB(29)>=7100000 THEN HELP$=","+HELP$(1,AA-1)+"&"+HELP$(AA)+"=20000,&*Collapse Menu=20001" ELSE HELP$=","+HELP$(1,AA-1)+"&"+HELP$(AA)+"=20000" ! SSP 213509
18180 RETURN 
19000 ! Close the files that is not needed
19010 CLOSE_ALL:
19015 IF NOT(NUL(%REAL_FLMAINT_MSG$)) THEN FLMAINT_MSG$=%REAL_FLMAINT_MSG$ ! 194087
19020 FILE_CHN$=CHN
19030 IF STP(FILE_CHN$)="" THEN RETURN 
19040 FILE_N=DEC($00$+FILE_CHN$(1,2)),FILE_CHN$=FILE_CHN$(3)
19050 ! FILE_FIB$=FIB(FILE_N,ERR=*RETURN)
19060 ! FILE_CHAN=DEC(FILE_FIB$(11,1))
19070 IF FILE_N>0 AND FILE_N<32768 THEN CLOSE (FILE_N) ! ssp 214331
19080 GOTO 19030
19090 RETURN 
20000 CUSTOM_FLDR:! SSP 221970 - INCL 20000-20999. This routine performed from ZZWSAA and ZZGFAV as well...
20020 ! IF POS(%C$="500")=0 THEN RETURN 
20030 MPATH$="CUS"+%C$+".EN"
20040 CFLD=UNT; OPEN (CFLD,ERR=20800)MPATH$
20060 FULLPATH$="Custom ("+STP(%X3_COMP_NAME$,2)+")"
20170 APP$="~~",MENU_FULLNAME$=""
20190 CALL "TREE_LOAD;LD_TEMPFL",_TREE,MPATH$,LIST_BOX_1.CTL,FULLPATH$,APP$,MENU_FULLNAME$,TMP_FL1,LIB_FN; CLOSE (TMP_FL1,ERR=*NEXT)
20200 CLOSE (LIB_FN,ERR=*NEXT)
20800 CLOSE (CFLD,ERR=*NEXT)
20999 EXIT 
21000 WINPROC_RESIZE:! SSP 229216
21010 _X$=OBJ(0),_X1=DEC($00$+_X$(33,2)),_X2=DEC($00$+_X$(35,2)); PERFORM "*winproc.rsz;Resize" ! SSP 229216
21020 RETURN ! SSP 229216
56000 ! "189263-Program to copy role from one user to another               
56001 ! "195863-Wants his company code to appear in the bottom of the Order 
56002 ! 213509-Feature to collapse all folders in GUI"
56004 ! 213667-VISTA crashes on line 13220
56006 ! 212146-Change name from TopForm to DB Distributor
56008 ! 213071-Change release from 6.0 to 7.0 by using msg SW_VERSION
56010 ! 214331-Files are being left open, adjust the menu to close open files
56011 ! "      (Except for those required by menu)
56012 ! "219339-Make GUI menu display correctly for libraries where the MENU
56013 ! "       panel is edited and saved in ver 7.1"
56015 ! "222546-Issue with ZZWMENU, in infinite loop waiting for input      
56018 ! "225634-PE - Winproc Error                                          
56019 ! "229216-Issue with screen sizing on oversized monitor- opens
56020 ! "       DB in the full screen"
56021 REM "238083-Ability to update Windx files from server in CHUI
56022 REM "238455-Visual affects no longer save when you log out and back in  
56024 REM "249556-When adding a favorite menu option, this is not saving.     
56026 REM "265388-263500 closed; the fix broke GUI feature, see 265205
56027 REM "306779-The GUI windows for Janet (JMB) are opening up in the middle
56028 REM "307208-GUI Menu cannot be maximized.                               
