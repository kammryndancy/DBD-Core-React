0010 ! FMGSHA - File Maintenance Program for FMP1_CustDef (FMP1)
0020 ! Generated by ProvideX® NOMADS II File Maintenance on Jun. 28, 2002 13:29
0030 ! ************************************************************************
0031 ! 
0035 REM "5.5 - 05/01/07 - 10.323611 - jvv - SSP# 201896
0037 REM "201896-Would like to know if they can make an OP parameter about   
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 _CUSTOMER$=%CUSTOMER$
0060 PROCESS "FMGSHA","../ARG/AR.EN"
0070 %CUSTOMER$=_CUSTOMER$
0080 ! IF TCB(13)=1 THEN RUN "ZMENU"
0090 EXIT 
0100 ! 
0110 ! 100 - Initialization
0120 INIT:
0130 X0=-1; X2=0; X0$="FMGSHA"
0135 IF %NEW_CUST THEN %NEW_CUST=0
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2
0150 ! 
0155 DIM Z[NUM(X3$(60,3))]
0159 ! [202271]-Modified line 160 to open XPD and XPZ
0160 Z$="01O FMP...  02O AR1...  03O AP4...  04O AR3...  05O FM1...  06O ZZPARM  07O FM0...  08O XPD...  09O XPZ...   45O OR1...   " ! [203891]    
0170 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0172 _OR1_CHN=Z[45]
0180 ! 
0190 ! dim Variables
0200 DIM FMP[6],AR1[15],AP4[2],B_FMP[6],FMP9[3],B_FMP9[3]
0220 DIM FMP$(300),FMP1$(200),FMP2$(200),FMP3$(200),AP4$(200)
0225 DIM FMP4$(200),FMP5$(200),FMP6$(200),FMP9$(200)
0230 ! IO List
0240 IOLIST FMP$(1),FMP{ALL}
0250 IOLIST FMP1$
0260 IOLIST FMP2$
0270 IOLIST FMP3$
0280 IOLIST AR1$,AR1{ALL}
0290 IOLIST AP4$,AP4{ALL}
0300 IOLIST FMP4$
0310 IOLIST FMP5$
0320 IOLIST FMP6$
0330 IOLIST FMP9$,FMP9{ALL}
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF %CUSTOMER$<>"" THEN {
0530 CUSTOMER$=%CUSTOMER$
0540 DISABLE CONTROL CUSTOMER.CTL
0550 GOSUB GET_CUSTOMER
0560 GOTO GET_INFO }
0570 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
0580 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 ! 
0930 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! Get Customer Information
1010 GET_CUSTOMER:
1020 CALL "ZZWLKU;Parse_cust",CUSTOMER$,B.CUST_DIV$,B.CUST_CODE$
1030 CUSTOMER$=B.CUST_DIV$+B.CUST_CODE$
1040 READ (Z[2],KEY=CUSTOMER$,DOM=*RETURN)IOL=0280
1050 CUST_NAME$=AR1$(11,35)
1055 JOBCARD$=AR1$(456,15); REM SSP193284
1058 FOUR51_ENABLE$=AR1$(474,1); REM SSP193728
1060 WHSE_BAS_PO$=AR1$(475,4); WHSE_STOCK_PO$=AR1$(479,4); REM SSP 198994
1065 RETAIN_SELL$=AR1$(483,1); REM SSP201896
1070 B_AR1$=AR1$
1090 RETURN 
2000 ! get information from FMP
2010 GET_INFO:
2020 GOSUB GET_PARM
2030 READ (Z[1],KEY="D"+CUSTOMER$,DOM=*RETURN)IOL=0240
2035 READ (Z[1],KEY="A"+CUSTOMER$,DOM=*NEXT)IOL=0330
2040 FIND (Z[3],KEY=FMP$(13,10),DOM=*NEXT)IOL=0290
2050 FIND (Z[1],KEY="F"+FMP$(28,1),DOM=*NEXT)IOL=0250
2060 FIND (Z[1],KEY="H"+FMP$(29,1),DOM=*NEXT)IOL=0260
2070 FIND (Z[1],KEY="S"+FMP$(45,1),DOM=*NEXT)IOL=0270
2080 ! DIM D1$; FIND (Z[1],KEY="a"+FMP$(2,10)+FMP$(109,3),DOM=*NEXT)D1$
2090 FIND (Z[1],KEY="M"+FMP$(93,2),DOM=*NEXT)IOL=0300
2100 FIND (Z[1],KEY="M"+FMP$(95,2),DOM=*NEXT)IOL=0310
2110 FIND (Z[1],KEY="M"+FMP$(97,2),DOM=*NEXT)IOL=0320
2280 B_FMP$=FMP$; B_FMP{ALL}=FMP{ALL}
2290 GOSUB LOAD_PANEL
2300 RETURN 
3000 ! get info from ZZPARM
3010 GET_PARM:
3020 READ (Z[6],KEY=X3$(9,3)+"A/R",DOM=WRAPUP)P0$
3030 READ (Z[6],KEY=X3$(9,3)+"AR1",DOM=WRAPUP)P2$
3040 READ (Z[6],KEY=X3$(9,3)+"F/M",DOM=WRAPUP)P1$
3050 READ (Z[6],KEY=X3$(9,3)+"I/C",DOM=WRAPUP)P3$
3060 FIND (Z[6],KEY=X3$(9,3)+"F/M",DOM=*NEXT)P0$
3070 FIND (Z[6],KEY=X3$(9,3)+"G/B",DOM=*NEXT)GB$; GB$="Y"
3080 DIM OP_SEC$(255); FIND (Z[6],KEY="osec"+X3$(40,3),DOM=*NEXT)OP_SEC$
3090 FIND (Z[6],KEY=X3$(9,3)+"A/R",DOM=*NEXT)AR_PARMS$
3100 FIND (Z[6],KEY=X3$(9,3)+"AR1",DOM=*NEXT)ARS_PARMS$
3290 RETURN 
4000 ! load information on panel
4010 LOAD_PANEL:
4015 IF NUL(FMP$) THEN RETURN 
4020 CUST_PO_REQ$=FMP$(25,1); UNIQUE_PO_REQ$=FMP$(23,1)
4025 WMS_PICK_PRIO$=AR1$(471,2) ! [203891]
4026 HLD_WMS_PICK_PRIO$=WMS_PICK_PRIO$ ! [203891] save the value
4030 FOB_CODE$=FMP$(28,1); %FOB_DESC$=FMP1$(3,15)
4031 REQUIRE_RC_NUM$=FMP$(62,1)
4032 REQUIRE_ORD_BY$=FMP$(63,1)
4033 CCPLUS_TERMS_CD$=FMP$(64,2) ! SPP 189822
4040 THANK_YOU_TYPE$=FMP$(26,1); HOW_TO_SHIP_CODE$=FMP$(29,1); %HOW_TO_SHIP_DESC$=FMP2$(3,15)
4045 WHICH_ORDER$=FMP$(105,1)
4050 PRINT_ACKNOWLEDGE$=FMP$(27,1); INSIDE_DELIVERY$=FMP$(30,15)
4060 FRT_MARKUP_TABLE$=FMP$(50,2); SHIP_VIA_CODE$=FMP$(45,1); %SHIP_VIA_DESC$=FMP3$(3,15)
4070 FIND_FEE_TYPE$=FMP$(12,1); FIND_FEE_RATE=FMP[0]
4080 VENDOR$=FMP$(13,10); %VEND_NAME$=AP4$(11,35)
4085 IF NOT(NUL(FMP$(133,4))) THEN CALL "*WINGRP;DISABLE",SHIP_F.GRP$ ELSE CALL "*WINGRP;ENABLE",SHIP_F.GRP$
4090 SUMMARY_BILL_FOR_IRW$=FMP$(113,4); DETAIL_SHPMNT_REPORT$=FMP$(49,1)
4100 SUMMARY_BILL_PRINT$=FMP$(117,4)
4110 FMS_1$=FMP$(24,1); TMP_FMS$=FMS_1$; ALWAYS_LOWEST_QBG$=FMP$(107,1) ! SSP 191739
4120 FMZ_FILE$=FMP$(103,1); RON_FORMAT$=FMP$(108,1),FOREIGN_CURR_CODE$=FMP$(90,3) ! SSP 191739
4130 REDUCE_ON_HAND_BY_COMM$=FMP$(133,1)
4140 CON_PICK_LIST$=FMP$(66,1)
4200 IF FMS_1$="Y" THEN GOSUB HIDE_SHOW_BUTTONS ELSE GOSUB SHOW_HIDE_BUTTONS
4250 IF POS(P1$(160,1)="AS")>0 THEN {
4260 CHECK_BOX ENABLE CON_PICK_LIST.CTL }
4270 NO_PRINT_FLG$=FMP$(102,1); IF FMP$(102,1)="Y" THEN ENABLE CONTROL FMZ_FILE.CTL ELSE DISABLE CONTROL FMZ_FILE.CTL
4280 FMX_FILE$=FMP$(103,1)
4300 ! Freight Parameter
4310 FRT_MARKUP_I_TYPE$=FMP$(93,2); FRT_T_DESC$=FMP4$(4,30)
4320 FRT_MARKUP_R_TYPE$=FMP$(95,2); FRT_T_DESC$=FMP5$(4,30)
4330 FRT_MARKUP_W_TYPE$=FMP$(97,2); FRT_T_DESC$=FMP6$(4,30)
4340 ! IF GB$="Y" THEN BUTTON ENABLE OVERRUN.CTL; BUTTON ENABLE SAMP_REB.CTL
4350 OVERRUN$=FMP$(99,1); SAMP_REB=FMP[2]; TAX_ITEMS_WHEN$=MID(FMP$,244,1)
4360 PRODUCT_CODE$=FMP$(227,3); SALESPERSON$=FMP$(230,4)
4370 ! IF POS(X3$(9,2)="500118",3)>0 THEN BUTTON ENABLE PRODUCT_CODE.CTL; BUTTON ENABLE SALESPERSON.CTL
4380 IMMEDIATE_PRINT$=FMP$(234,1); SUB_WH=FMP[3]
4390 UPDATE_CC$=FMP$(100,1); ELIM_POSTING$=FMP$(101,1); PRINT_LOT_VALUE$=FMP$(104,1)
4400 ! 
4410 CURR_FY$=FMP$(52,4); CURR_ACCTPD$=FMP$(56,2)
4420 NUM_PER_FMZ_RETAIN$=FMP$(58,2); NUM_YR_STAT_RETAIN$=FMP$(60,1)
4430 DFLT_CNT_CYCLE$=FMP$(46,2); STOCK_ITEMS_FMS$=FMP$(112,1)
4440 MULTIPLE_SHIP$=FMP$(124,1); DFLT_SHIP_CODE$=FMP$(125,4)
4450 STD_WHSE$=FMP$(238,4); COST_CODE_FORMAT$=FMP$(243,1)
4460 COST_CODE_FMT$=FMP$(235,1); STORE_COST_CODE$=FMP$(236,1)
4470 ! 
4480 CUST_GROUP_CODE$=FMP$(130,3); INCLUDE_DIST_INV$=FMP$(134,1)
4490 FMS_PRICING$=FMP$(195,1); FMS_PRCNT_SELL_PRICE=FMP[1]
4500 STD_INV_VALUE_METH$=FMP$(237,1); REDUCE_ONHAND_BY_COMM$=FMP$(133,1)
4510 PRIMARY_CUST_CONT$=FMP$(109,1); RON_FORMAT$=FMP$(108,1)
4520 SUBTRACT_COMMITTED$=FMP$(226,1); RON_GRAPH_TYPE$=FMP$(61,1)
4530 RON_CUST_NOTE$=FMP$(135,60)
4535 REFRESH_FLG=1
4540 ! 
4545 IF NUL(FMP9$) THEN RETURN 
4550 DFLT_FMS_FLAG$=FMP9$(12,1); DFLT_ROP_DAYS$=FMP9$(13,3)
4560 DFLT_RESTOCK_STAT$=FMP9$(16,1); DFLT_STND_WHSE$=FMP9$(17,4)
4570 DFLT_RESUPP_DAYS$=FMP9$(21,3); DFLT_RESTOCK_DAYS$=FMP9$(24,3)
4580 DFLT_RESUPP_QTY=FMP9[0]; DFLT_RESTOCK_QTY=FMP9[1]
4585 REC_REQURE$=FMP$(62,1); MIN_PERC_MARKUP=FMP[4]
4980 REFRESH_FLG=1
4990 RETURN 
5500 ! Setup Screen 5
5510 SCREEN_5_SETUP:
5520 IF GB$="Y" THEN CALL "*wingrp;Enable",OVER.GRP$
5530 IF POS(X3$(9,3)="500118",3)>0 THEN CALL "*wingrp;Enable",REB.GRP$
5540 IF MID(AR_PARMS$,176,1)="Y" THEN CALL "*wingrp;enable",TAX.GRP$
5580 REFRESH_FLG=1
5590 RETURN 
5600 ! Setup Screen6
5610 SCREEN_6_SETUP:
5620 IF GB$="Y" THEN CALL "*wingrp;eNABLE",OVER.GRP$
5630 IF POS(X3$(9,3)="500118",3)>0 THEN CALL "*wingrp;Enable",REB.GRP$
5640 IF MID(AR_PARMS$,176,1)="Y" THEN CALL "*wingrp;enable",TAX.GRP$
5680 REFRESH_FLG=1
5690 RETURN 
6000 ! Check if any changes have been made
6010 CHECK_CHANGES:
6030 IF B_FMP$=FMP$ AND B_FMP[0]=FMP[0] AND B_FMP[1]=FMP[1] AND B_FMP[2]=FMP[2] AND B_FMP[3]=FMP[3] AND HLD_WMS_PICK_PRIO$=WMS_PICK_PRIO$ AND B_AR1$=AR1$ THEN RETURN 
6040 MSG_1$="Changes have been made."+SEP+"Would you like to save your changes?"
6050 MSGBOX MSG_1$,"Update Record","YesNo",ANS$
6060 IF ANS$="NO" THEN RETURN 
6070 GOSUB WRITE_REC
6090 RETURN 
7000 ! Folder Operations
7010 FOLDER_OPS:
7020 ! IF MID(OP_SEC$,45,1)="Y" THEN CALL "*WINGRP;DISABLE",SEC_1.GRP$
7080 REFRESH_FLG=1
7090 RETURN 
8000 ! show and hid buttons section.
8010 ! show and hide buttons for FMS Customers.
8020 HIDE_SHOW_BUTTONS:
8030 BUTTON HIDE NEXT_1.CTL
8040 BUTTON SHOW NEXT_2.CTL
8050 RETURN 
8060 SHOW_HIDE_BUTTONS:
8070 BUTTON HIDE NEXT_2.CTL
8080 BUTTON SHOW NEXT_1.CTL
8090 RETURN 
8100 ! Show and hide buttons for Frieght screen
8110 FR_HIDE_SHOW_BUTTONS:
8120 ! BUTTON HIDE EXIT_1.CTL
8130 ! BUTTON SHOW NEXT_3.CTL
8140 BUTTON HIDE WRITE_1.CTL
8150 REFRESH_FLG=1
8160 RETURN 
9999 END 
10000 ! load all the variables
10010 LOAD_VARIABLES:
10015 FMP$(1,1)="D"; FMP$(2,10)=CUSTOMER$
10020 FMP$(25,1)=CUST_PO_REQ$; FMP$(23,1)=UNIQUE_PO_REQ$
10030 FMP$(28,1)=FOB_CODE$; FMP$(26,1)=THANK_YOU_TYPE$
10040 FMP$(29,1)=HOW_TO_SHIP_CODE$; FMP$(27,1)=PRINT_ACKNOWLEDGE$
10041 FMP$(62,1)=REQUIRE_RC_NUM$; FMP[4]=MIN_PERC_MARKUP
10042 FMP$(63,1)=REQUIRE_ORD_BY$
10043 FMP$(64,2)=CCPLUS_TERMS_CD$ ! SSP 189822
10050 FMP$(30,15)=INSIDE_DELIVERY$; FMP$(50,2)=FRT_MARKUP_TABLE$
10060 FMP$(45,1)=SHIP_VIA_CODE$
10070 FMP$(12,1)=FIND_FEE_TYPE$; FMP[0]=FIND_FEE_RATE
10080 FMP$(13,10)=VENDOR$
10090 FMP$(113,4)=SUMMARY_BILL_FOR_IRW$; FMP$(49,1)=DETAIL_SHPMNT_REPORT$
10100 FMP$(117,4)=SUMMARY_BILL_PRINT$; FMP$(24,1)=FMS_1$
10110 FMP$(107,1)=ALWAYS_LOWEST_QBG$; FMP$(103,1)=FMZ_FILE$ ! SSP 191739
10115 FMP$(105,1)=WHICH_ORDER$
10120 FMP$(108,1)=RON_FORMAT$; FMP$(133,1)=REDUCE_ON_HAND_BY_COMM$ ! SSP 191739
10125 FMP$(90,3)=FOREIGN_CURR_CODE$
10130 FMP$(66,1)=CON_PICK_LIST$
10140 FMP$(102,1)=NO_PRINT_FLAG$
10200 ! 
10210 FMP$(93,2)=FRT_MARKUP_I_TYPE$; FMP$(95,2)=FRT_MARKUP_R_TYPE$
10220 FMP$(97,2)=FRT_MARKUP_W_TYPE$; FMP$(99,1)=OVERRUN$; FMP$(244,1)=TAX_ITEMS_WHEN$
10230 FMP[2]=SAMP_REB; FMP[3]=SUB_WH
10240 FMP$(227,3)=PRODUCT_CODE$; FMP$(230,4)=SALESPERSON$
10250 FMP$(234,1)=IMMEDIATE_PRINT$
10260 FMP$(100,1)=UPDATE_CC$; FMP$(101,1)=ELIM_POSTING$
10270 FMP$(104,1)=PRINT_LOT_VALUE$
10300 ! 
10310 FMP$(52,4)=CURR_FY$; FMP$(56,2)=CURR_ACCTPD$
10320 FMP$(58,2)=NUM_PER_FMZ_RETAIN$; FMP$(60,1)=NUM_YR_STAT_RETAIN$
10330 FMP$(46,2)=DFLT_CNT_CYCLE$; FMP$(112,1)=STOCK_ITEMS_FMS$
10340 FMP$(124,1)=MULTIPLE_SHIP$; FMP$(125,4)=DFLT_SHIP_CODE$
10350 FMP$(238,4)=STD_WHSE$; FMP$(243,1)=COST_CODE_FORMAT$
10360 FMP$(235,1)=COST_CODE_FMT$; FMP$(236,1)=STORE_COST_CODE$
10400 ! 
10410 FMP$(130,3)=CUST_GROUP_CODE$; FMP$(134,1)=INCLUDE_DIST_INV$
10420 FMP$(195,1)=FMS_PRICING$
10430 FMP[1]=FMS_PRCNT_SELL_PRICE
10440 FMP$(237,1)=STD_INV_VALUE_METH$; FMP$(133,1)=REDUCE_ONHAND_BY_COMM$
10450 FMP$(109,1)=PRIMARY_CUST_CONT$; FMP$(108,1)=RON_FORMAT$
10460 FMP$(226,1)=SUBTRACT_COMMITTED$; FMP$(61,1)=RON_GRAPH_TYPES$
10470 FMP$(135,60)=RON_CUST_NOTE$
10600 ! 
10605 IF FMS_1$<>"Y" THEN RETURN 
10607 FMP9$(1,1)="A"; FMP9$(2,10)=CUSTOMER$; REM " DDF
10610 FMP9$(12,1)=DFLT_FMS_FLAG$; FMP9$(13,3)=DFLT_ROP_DAYS$
10620 FMP9$(16,1)=DFLT_RESTOCK_STAT$; FMP9$(17,4)=DFLT_STND_WHSE$
10630 FMP9$(21,3)=DFLT_RESUPP_DAYS$; FMP9$(24,3)=DFLT_RESTOCK_DAYS$
10640 FMP9[0]=DFLT_RESUPP_QTY; FMP9[1]=DFLT_RESTOCK_QTY
10890 RETURN 
12000 ! Write record
12005 WRITE_REC:
12010 KEY_1$="D"+CUSTOMER$
12020 KEY_2$="A"+CUSTOMER$
12022 AR1$(471,2)=WMS_PICK_PRIO$
12023 AR1$(474,1)=FOUR51_ENABLE$; REM SSP193728
12024 AR1$(475,4)=WHSE_BAS_PO$; AR1$(479,4)=WHSE_STOCK_PO$; REM SSP198994
12025 AR1$(456,15)=JOBCARD$; REM SSP 193284
12026 AR1$(483,1)=RETAIN_SELL$; REM SSP 201896
12028 WRITE (Z[2],KEY=CUSTOMER$)IOL=0280; REM SSP 193284
12030 WRITE (Z[1],KEY=KEY_1$)IOL=0240
12034 ! [202271]-Added line 12035 to update XPD
12035 CALL "UPDFMP;DUPLICATE_XPD",Z[8],FMP$,FMP{ALL},"U"
12040 IF FMS_1$<>"Y" THEN GOSUB UPDATE_MSG; RETURN 
12050 WRITE (Z[1],KEY=KEY_2$)IOL=0330
12054 ! [202271]-Added line 12055 to update XPZ
12055 CALL "UPDFMP;DUPLICATE_XPZ",Z[9],FMP9$,FMP9{ALL},"U"
12060 GOSUB UPDATE_MSG
12070 B_FMP$=FMP$; B_FMP{ALL}=FMP{ALL}
12090 RETURN 
13000 ! Delete Record
13010 DELETE_REC:
13020 MSGBOX "Are you sure you want to delete this information?","Delete","YesNo",ANS$
13030 IF ANS$="NO" THEN RETURN 
13040 KEY_1$="D"+CUSTOMER$
13050 KEY_2$="A"+CUSTOMER$
13060 REMOVE (Z[1],KEY=KEY_1$)
13064 ! [202271]-Added line 13065 to update XPD
13065 CALL "UPDFMP;DUPLICATE_XPD",Z[8],FMP$,FMP{ALL},"D"
13070 IF FMS_1$<>"Y" THEN GOSUB UPDATE_MSG; RETURN 
13080 REMOVE (Z[1],KEY=KEY_2$)
13084 ! [202271]-Added line 13085 to update XPZ
13085 CALL "UPDFMP;DUPLICATE_XPZ",Z[9],FMP9$,FMP9{ALL},"D"
13090 GOSUB UPDATE_MSG
13100 REFRESH_FLG=1
13110 RETURN 
15000 ! Update Message
15010 UPDATE_MSG:
15020 MSGBOX "The Information has been Updated.","FYI"
15030 RETURN 
17000 ! Print Management options
17020 PRINT_MANAGEMENT_OPTS:
17030 IF FMS_1$="Y" THEN GOSUB HIDE_SHOW_BUTTONS ELSE GOSUB SHOW_HIDE_BUTTONS
17040 IF FMS_1$="Y" AND DETAIL_SHPMNT_REPORT$<>"Y" THEN DETAIL_SHPMNT_REPORT$="Y" ! MSGBOX "Monthy Detail Shipment Reporting is set to 'N'!","Warning"
17050 IF FMS_1$<>TMP_FMS$ THEN {
17070 IF FMS_1$="N" THEN {
17080 MSGBOX "All existing forms for this customer will now have their FMS status changed to 'N'","FYI","!,Cancel",ANS$
17085 IF ANS$="CANCEL" THEN FMS_1$="Y"; REFRESH_FLG=1; RETURN 
17090 GOSUB CHANGE_FMS_FLG
17100  } ELSE {
17110 MSGBOX "Set all of this customer's items to Print Management?","FMS Flag","YesNo",ANS$
17120 IF ANS$="YES" THEN GOSUB CHANGE_FMS_FLG ELSE GOSUB SET_LOC_FLG
17130  } }
17150 RETURN 
17500 ! Change Forms Management Flag
17510 CHANGE_FMS_FLG:
17520 READ (Z[5],KEY=CUSTOMER$,DOM=*NEXT)
17530 EXTRACT RECORD (Z[5],END=17600)XX$
17540 IF XX$(1,10)<>CUSTOMER$ THEN GOTO 17600
17550 XX$(108,1)=FMS_1$
17560 WRITE RECORD (Z[5])XX$
17570 GOTO 17530
17600 XX$=""; IF FMS_1$="Y" THEN GOSUB SET_LOC_FLG ELSE FMS_LOC$=" "; GOTO 17840
17610 RETURN 
17800 ! Set Location Flags
17810 SET_LOC_FLG:
17820 FMS_LOC$="I"
17830 CALL "*wingrp;show",FMS1.GRP$
17840 READ (Z[7],KEY="C"+CUSTOMER$,DOM=*NEXT)
17850 KEY_7$=KEY(Z[7],END=*RETURN)
17860 READ RECORD (Z[7])G$
17870 IF G$(1,11)<>"C"+CUSTOMER$ THEN RETURN 
17880 G$(223,1)=FMS_LOC$
17890 WRITE RECORD (Z[7],KEY=KEY_7$)G$ ! SSP 189431
17900 GOTO 17850
17990 RETURN 
20000 LOAD_FISCAL_YEAR:
20010 ZZPARM=HFN; OPEN (ZZPARM)"ZZPARM"
20030 READ (ZZPARM,KEY=%C$+"G/L",DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(34,4),CR_PD$=GL_REC$(38,2)
20035 IF NOT(NUL(CURR_FY$)) THEN CR_YEAR$=CURR_FY$
20040 KE$=%C$+"G/LYE",CURR_FY$=""
20050 READ (ZZPARM,KEY=KE$,DOM=*NEXT)
20060 K$=KEY(ZZPARM,END=20110); READ (ZZPARM)Z9$
20070 IF LEN(K$)<8 OR KE$<>K$(1,8) THEN GOTO END_LOAD_FISCAL_YEAR
20080 CURR_FY$=CURR_FY$+K$(9)+"/"
20090 IF K$(9,4)=CR_YEAR$ THEN CC$=Z9$(21)
20100 GOTO 20060
20110 END_LOAD_FISCAL_YEAR:
20120 CC$=CC$(NUM(CR_PD$)*6-5,6),END_DATE$=CC$,REFRESH_FLG=1
20130 DROP_BOX LOAD CURR_FY.CTL,CURR_FY$
20140 DROP_BOX WRITE CURR_FY.CTL,CR_YEAR$
20165 CURR_FY$=CR_YEAR$
20167 IF NOT(NUL(CURR_ACCTPD$)) THEN CR_PD$=CURR_ACCTPD$
20170 CURR_ACCTPD$=""
20180 FOR ACCT=1 TO 13; CURR_ACCTPD$=CURR_ACCTPD$+STR(ACCT:"00")+"/"; NEXT ACCT
20190 DROP_BOX LOAD CURR_ACCTPD.CTL,CURR_ACCTPD$
20200 DROP_BOX WRITE CURR_ACCTPD.CTL,CR_PD$
20205 CURR_ACCTPD$=CR_PD$
20210 REFRESH_FLG=1
20230 CLOSE (ZZPARM)
20240 RETURN 
20400 VALIDATE_PICK_PRIORITY:! [203891]-added routine
20401 REM "This routine validates the entered pick priority
20410 WMS_PICK_PRIO$=PAD(WMS_PICK_PRIO$,2)
20420 WMS_PICK_PRIO$=STR(NUM(WMS_PICK_PRIO$,ERR=*NEXT):"00")
20430 IF LEN(STP(WMS_PICK_PRIO$))>0 THEN {
20440 FIND (_OR1_CHN,KEY=WMS_PICK_PRIO$,DOM=INVALID_PICK_PRIORITY)OR1_REC$
20450  }
20460 WMS_PICK_PRIO.CTL'VALUE$=WMS_PICK_PRIO$
20470 AR1$(471,2)=WMS_PICK_PRIO$
20495 RETURN 
20500 INVALID_PICK_PRIORITY:! [230891]-added routine
20501 REM "This routine displays an invalid pick priority message.
20520 MSGBOX WMS_PICK_PRIO$+" is an invalid Picking Priority.","Invalid Entry","i,OK"
20560 NEXT_ID=WMS_PICK_PRIO.CTL
20570 WMS_PICK_PRIO$=""
20595 RETURN 
56000 REM + Modification History
56001 ! "191739-Issues w/ Foreign Exchange Rate in GUI
56002 ! "189431-if a customer was once setup as FM, and you are setting it
56003 ! "
56004 ! "189822-Credit Card Processing for webec back orders filled
56005 REM "193728-Four51 Webservices; ShipNoticeRequest,
56006 ! 
56007 REM "5.5 - 12/11/06 - 11.264722 - mhe - SSP# 202271
56008 REM "202271-Modify FMGSHA to also call UPDFMP to update XPD after       
56010 REM "5.5 - 01/29/07 - 15.766944 - mhe - SSP# 203891
56012 REM "203891-Need to validate Picking Priority code in Ship Via          
