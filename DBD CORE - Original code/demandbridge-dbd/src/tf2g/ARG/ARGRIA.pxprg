0010 REM "Commission Payable <AR2RIA>
0035 REM "5.5 - 09/10/07 - 15.895833 - jdf - SSP# 211380
0037 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2RIA",X1$="Commissions Payable"
0120 DIM A$(52),A[9],B$(244),C$(397),C[15],D$(139),D[13],E$(124),E[6],P2$(50),P[30],F$(67),F[2],G[1],Z8$(100); REM " SSP# 151967
0125 F0$="*"
0140 Z0$="#####,###.00-",Z1$="####,###.00-",Z2$="###,###.00-",Z3$="###.00-",Z4$="-########.00"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0210 T=1,T0=0,T1=6
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR ARM - Commissions payable
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9]
0315 REM "IOLIST FOR AR3 - Salesperson Masterfile
0320 IOLIST B$
0325 REM "IOLIST FOR AR1 - Customer Masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0335 REM "IOLIST FOR AR6 - Open Invoice Header File
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13]
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6]
0360 IOLIST F$,F[0],F[1]
0365 REM "For sort G(0)=sales, G(1)=Gross profit
0370 IOLIST G$,G[0],G[1]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0509 REM "Slots 7&8 reserved for use by sort file TMP
0510 Y$="01O ARM...  02O AR3...  03O AR1...  04O AR6...  05O AR8...  06O AS"+"0...  09O ARI...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0550 FIND (Z[13],KEY=X3$(9,3)+"AR1",DOM=9800)P1$,P[0],P[1],P[2],P[3],P[4],P[5],P[6],P[7],P[8],P[9],P[10],P[11],P[12],P[13],P[14],P[15],P[16],P[17],P[18],P[19],P[20],P[21],P[22],P[23],P[24],P[25],P[26],P[27],P[28],P[29]
0555 GOSUB 7600
0560 FIND (Z[13],KEY=X3$(9,3)+"A/R")P0$
0570 FIND (Z[13],KEY=X3$(9,3)+"A/P")AP_PARMS$
0580 DIM OP_SEC$(255); FIND (Z[13],KEY="osec"+X3$(40,3),DOM=0581)OP_SEC$
0600 REM 
0605 READ (Z[13],KEY="STATAR3")V8$
0610 V7$=""; IF POS("U"=V8$(49),17)<>0 AND POS("V"=V8$(49),17)<>0 THEN V7$="*"
0620 IF V7$="*" THEN GOSUB 8500
0700 REM "Open Printer
0710 GOSUB 8300
0720 REM 
0725 REM "If they have the Accounts Payable Gateway, we will create a text file (PA1) and write to it each time a salesperson totals if they have a vendor code set up in the AR3 file to be used in the gateway to create A/P invoices,wo#86692
0730 CALL "ZZ2PRP","PA",RESULT$,TF_DATE$; IF RESULT$<>"Y" OR V3$(2,1)="T" THEN GAP$="N"; GOTO 0800
0735 GAP$="Y",GAP_FILE$="PA1."+X3$(23,4),GAP_UPDATE$="N",PA1_WRITE$="N"
0740 ERASE GAP_FILE$,ERR=0741
0745 SERIAL GAP_FILE$,0,0
0750 OPEN LOCK (32500,OPT="TEXT")GAP_FILE$
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[6]; U=33,U0=6,U[0]=0,U[1]=4,U[2]=2,U[3]=4,U[4]=10,U[5]=8,U[6]=5,T0=3,V1=6,V2=4,V3=10,U0$="YY  ",T5$="S   AAR3AA/R    ",U5$="Fiscal Year",U6$="Salesperson",U7$="Customer"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0981 REM "If charge backs only then set V4$ and V2$ to only look at keys wi"+ "th ~'s in the last 3 key fields
0982 IF V0$(73,1)="Y" THEN DIM X$(30,"~"); V2$(20,10)=X$,V2$(41,8)=X$,V2$(57,5)=X$,V4$(11)=X$
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1155 IF POS(" "<>A$(46,6))=0 THEN A$(46,6)=A$(34,6)
1160 IF POS($7E$<>A$(11,10))<>0 THEN IF A$(46,6)<V0$(75,6) OR A$(46,6)>V0$(81,6) THEN GOTO 1000
1165 IF Q5=1 THEN PRINT @(25,21),U$,; REMOVE (Z[1],KEY=U$,DOM=1000); IF Q0$<>U$(1,10) THEN GOSUB 8400; GOTO 1000
1168 IF Q5=1 THEN GOTO 1000
1175 IF A[6]=0 AND V0$(72,1)<>"Y" AND POS("~"=A$(11,10))=0 THEN GOTO 1000
1300 REM "Read other data
1320 DIM B$(LEN(B$)); FIND (Z[2],DOM=1321,KEY=A$(7,4))IOL=0320
1325 IF F0$="*" THEN F0$=""; GOSUB 6200
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(11,10))IOL=0330
1340 DIM D$(LEN(D$)); FIND (Z[4],DOM=1341,KEY=A$(11,10)+A$(21,8))IOL=0340; GOTO 1342
1341 DIM E$(LEN(E$)); FIND (Z[5],DOM=1342,KEY=A$(11,10)+A$(21,8))IOL=0350; D$(66,8)=E$(53,8)
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 REM 
1440 REM GOSUB 6200
1450 GOSUB 6300
1490 T=0
1500 REM "Print Data
1504 REM "Print chargebacks?
1505 IF POS("~"<>A$(11,10))=0 THEN GOSUB 8100; GOTO 1000
1510 IF W+1>W0 THEN GOSUB 6000
1511 IF GAP$="Y" THEN IF PA1$="" THEN IF CHECKED$<>"Y" THEN GOSUB 8800
1512 GOSUB 7700
1515 CALL "ZZDISP","AX",A$(21,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1520 IF X3$(69,1)<>"Y" THEN Y5$(11)=FND$(A$(34,6)) ELSE CALL "ZZDISP","DX",A$(34,6),"   ",X3$,X$,"",0,0,X4$; Y5$(11)=X$
1525 Y5$(22)=D$(66,8)
1530 IF X3$(69,1)<>"Y" THEN Y5$(32)=FND$(A$(46,6)) ELSE CALL "ZZDISP","DX",A$(46,6),"   ",X3$,X$,"",0,0,X4$; Y5$(32)=X$
1532 Y5$(43)=A$(40,4)+"."+A$(44,2)
1534 IF A[5]<>100 AND A[5]<>0 THEN GOTO 2000
1535 Y5$(51)=STR(A[0]:Z1$),Y5$(64)=STR(A[1]:Z1$); IF V0$(59,1)<>"B" THEN Y5$(77)=STR(A[2]:Z2$)
1545 Y5$(90)=STR(A[6]:Z2$); IF P1$(32,1)="Y" THEN IF A[7]-A[0]=0 THEN Y5$(118)=STR(A[8]:Z2$) ELSE Y5$(102)=STR(A[0]-A[7]:Z1$); GOSUB 8200; Y5$(118)=STR(A[8]:Z2$)
1550 IF P1$(32,1)="Y" THEN Y5$(130)=C9$ ELSE Y5$(106)=C9$; REM "C9$ set in 7700's
1555 GOSUB 7300
1800 REM 'PRINT GP & COMM %S IF NEEDED
1810 X=POS(V0$(74,1)="YG"); IF X=0 AND (V0$(87,1)<>"Y" OR V0$(59,1)="B") THEN GOTO 1900
1820 IF A[1]<>0 THEN Y5$(81)=STR(A[2]*100/A[1]:Z3$)
1830 IF A[X]<>0 THEN Y5$(94)=STR(A[6]*100/A[X]:Z3$); IF P1$(32,1)="Y" THEN Y5$(122)=STR(A[8]*100/A[X]:Z3$)
1840 IF V0$(87,1)="Y" THEN Y5$(64,LEN(Z1$))=STR(A[1]-A[2]:Z1$)
1860 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[0],T[T0,2]=T[T0,2]+A[1],T[T0,3]=T[T0,3]+A[2],T[T0,4]=T[T0,4]+A[6],T[T0,5]=T[T0,5]+A[0]-A[7],T[T0,6]=T[T0,6]+A[8]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print a split invoice
2005 DIM D$(139),D[13]
2010 FIND (Z[4],KEY=A$(11,18),DOM=2011)IOL=0340
2020 Y5$(51)=STR(D[2]:Z1$),Y5$(64)=STR(D[9]:Z1$),Y5$(90)=STR(D[11]:Z2$); REM IF P1$(32,1)="Y" IF A(7)-(D(2)-D(3))=0 Y5$(102)=STR(D(2)-D(3):Z1$) ELSE   Y5$(102)=STR(D(2)-D(3)-A(7):Z1$); GOSUB 8200
2021 IF V0$(59,1)<>"B" THEN Y5$(77,LEN(Z2$))=STR(D[10]:Z2$)
2025 GOSUB 7300
2030 Y5$(31)=STR(A[5]:Z3$)+"% Split"
2035 Y5$(51)=STR(A[0]:Z1$),Y5$(64)=STR(A[1]:Z1$); IF V0$(59,1)<>"B" THEN Y5$(77)=STR(A[2]:Z2$)
2045 Y5$(90)=STR(A[6]:Z2$); IF P1$(32,1)="Y" THEN IF A[7]-A[0]=0 THEN Y5$(118)=STR(A[8]:Z2$) ELSE Y5$(102)=STR(A[0]-A[7]:Z1$); GOSUB 8200; Y5$(118)=STR(A[8]:Z2$)
2050 IF P1$(32,1)="Y" THEN Y5$(130)=C9$ ELSE Y5$(106)=C9$
2080 GOSUB 7300
2090 GOTO 1800
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$(1,4)+" Period "+U1$(5,2); GOSUB 3050; GOSUB 7000
3153 IF T0$="END" THEN W0$="",W1$=""
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$+" "+U2$,T6$=U2$; GOSUB 3050; GOSUB 7000
3253 IF U1$<>U9$(1,V1) OR T0$="END" THEN W1$="",W0$="" ELSE GOSUB 6200
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3256 GOSUB 6200
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5010 IF Q5=1 THEN GOTO 9920
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5210 T9=1
5290 GOTO 9900
5300 REM "Purge records
5304 REM "Skip unless full date range done
5305 IF POS(" "<>V0$(75,6))<>0 OR POS($7E$<>V0$(81,6))<>0 THEN GOTO 9920
5307 IF OP_SEC$(66,1)="Y" THEN GOTO 9920; REM "WO102518, don't ask clear ?
5310 CALL "ZZPROM","X0AR2RHA",X3$,Z,"","","",0; ON Z GOTO 5311,9920
5312 IF GAP$="Y" THEN CALL "ZZPROM",".Y",X3$,Z,"Update Commissions to A/P Invoice Data Entry File?","","",0; IF Z=0 THEN GAP_UPDATE$="Y"
5315 IF V7$="*" THEN GOSUB 8600
5317 PRINT @(0,20),'CE',@(23,20),"Now clearing:",
5360 Q5=1; GOTO 0900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6090 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6110 GOSUB 8000
6115 Y5$(1)="Invoice",Y5$(12)="Invoice",Y5$(24)="Order",Y5$(33)="Payment",Y5$(44)="Audit",Y5$(54)="Invoice",Y5$(69)="Net",Y5$(80)="Gross"; IF P1$(32,1)="Y" THEN Y5$(118)="Commission"
6116 IF V0$(59,1)="B" THEN Y5$(80,6)="      "; REM 'BLOCK PRINTIGN OF GROSS PROFIT IF B IN FIRST POSITION OF OPTIONS FIELD
6120 GOSUB 7300
6125 Y5$(2)="Number",Y5$(14)="Date",Y5$(24)="Number",Y5$(35)="Date",Y5$(43)="Control",Y5$(55)="Total",Y5$(67)="Amount",Y5$(80)="Profit",Y5$(89)="Commission"; IF P1$(32,1)="Y" THEN Y5$(104)="Write offs",Y5$(121)="Due"
6126 IF V0$(59,1)="B" THEN Y5$(80,6)="      "; REM 'BLOCK PRINTING OF GROSS PROFIT IF B IN 1ST POSITION OF OPTIONS FIELD 
6127 IF P0$(113,1)="Y" THEN Y5$(80,6)="Margin"
6130 GOSUB 7300
6140 IF POS(V0$(74,1)="YG")<>0 THEN Y5$(80)="/ GP %",Y5$(91)="/ Comm %   "; IF P1$(32,1)="Y" THEN Y5$(120)="/ Comm %"
6142 IF V0$(87,1)="Y" AND V0$(59,1)<>"B" THEN Y5$(67,13)="/ Cost"
6145 IF V0$(74,1)="Y" AND P0$(113,1)="Y" THEN Y5$(83,1)="M"
6150 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6220 W0$=A$(7,4)+" - "+FNS$(B$(5,35))
6230 GOSUB 7650
6299 RETURN 
6300 REM "Break #2 header
6305 IF POS("~"=A$(11,10))<>0 THEN GOTO 6399
6310 IF W+2>W0 THEN GOSUB 6000
6320 CALL "ZZDISP","AX",A$(11,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(6)=X$
6325 Y5$(19)=C$(11,35)
6330 GOSUB 7300
6399 RETURN 
6400 REM "Break #3 header
6499 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(50)=T1$(1,13),Y5$(63)=T1$(1,13),Y5$(76)=T1$(1,12),Y5$(89)=T1$(1,12); IF P1$(32,1)="Y" THEN Y5$(101)=T1$(1,13),Y5$(117)=T1$(1,12)
7006 IF V0$(59,1)="B" THEN Y5$(76,12)=Z8$(1,12); REM 'BLOCK PRINTING OF GROSS PROFIT IF OPTIONS FIELD SET TO B
7009 GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(50)=STR(T[T,1]:Z0$),Y5$(63)=STR(T[T,2]:Z0$),Y5$(76)=STR(T[T,3]:Z1$),Y5$(89)=STR(T[T,4]:Z1$),Y5$(101)=STR(T[T,5]:Z0$),Y5$(117)=STR(T[T,6]:Z1$)
7021 IF V0$(59,1)="B" THEN Y5$(117,LEN(Z1$))=Z8$(1,LEN(Z1$))
7029 GOSUB 7300
7030 IF V0$(87,1)="Y" AND V0$(59,1)<>"B" THEN Y5$(63,LEN(Z0$))=STR(T[T,2]-T[T,3]:Z0$)
7040 IF POS(V0$(74,1)="YG")<>0 THEN IF T[T,2]<>0 THEN Y5$(81)=STR(T[T,3]*100/T[T,2]:Z3$),Y5$(94)=STR(T[T,4]*100/T[T,2]:Z3$); IF P1$(32,1)="Y   " THEN Y5$(122)=STR(T[T,6]*100/T[T,2]:Z3$); GOSUB 7300 ELSE GOSUB 7300
7065 IF T$="END" THEN Y5$(50)=T2$(1,13),Y5$(63)=T2$(1,13),Y5$(76)=T2$(1,12),Y5$(89)=T2$(1,12),Y5$(101)=T2$(1,13),Y5$(117)=T2$(1,12)
7066 IF V0$(59,1)="B" THEN Y5$(76,12)=Z8$(1,12)
7069 GOSUB 7300
7085 IF T=2 THEN IF V7$="*" THEN GOSUB 8550
7087 IF T=2 THEN IF GAP$="Y" THEN GOSUB 8850
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7330 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "SET TITLE W0$ BASED ON DECLINING COMMISSION PARAMETER
7605 IF POS(P1$(36,1)="YRG")=0 THEN GOTO 7640
7609 REM "If using declining commission, then set P1$(32,1) to Y to use tha"+ "t report format.
7610 P1$(32,1)="Y"
7614 REM "L is array of days, R is array of days late x # of types (increas"+ "e 2 if more types), L0 is number of days late entries
7615 DIM L[4],R[4,4]; L0=-1
7620 FOR I=0 TO 2
7625 IF P[I]<>0 THEN L0=L0+1,L[L0]=P[I],R[L0,0]=P[I+3],R[L0,1]=P[I+6]
7630 NEXT I
7631 IF P[9]<>0 THEN L0=L0+1,L[3]=P[9],R[L0,0]=P[11],R[L0,1]=P[13],R[3,2]=P[24],R[3,3]=P[25],R[3,4]=P[26]
7632 IF P[10]<>0 THEN L0=L0+1,L[4]=P[10],R[L0,0]=P[12],R[L0,1]=P[14],R[4,2]=P[27],R[4,3]=P[28],R[4,4]=P[29]
7633 IF P[0]<>0 THEN R[0,2]=P[15],R[0,3]=P[16],R[0,4]=P[17]
7634 IF P[1]<>0 THEN R[1,2]=P[18],R[1,3]=P[19],R[1,4]=P[20]
7635 IF P[2]<>0 THEN R[2,2]=P[21],R[2,3]=P[22],R[2,4]=P[23]
7649 RETURN 
7650 REM "SET TITLE W1$
7652 IF POS(P1$(36,1)="YRG")=0 THEN GOTO 7690
7655 IF L0<0 THEN W1$=""; GOTO 7690
7660 W1$="Commission reduced to:"
7662 I0=NUM(B$(224,1)); REM " SSP# 151967
7663 IF POS(P1$(36,1)="RG")>0 THEN W1$="R = Commission rate reduced by "+STR(R[0,I0])+"% after invoice"+" is "+STR(L[0])+" days old and "+STR(R[1,I0])+"% every "+STR(L[1])+" days later"; GOTO 7690
7664 REM "IF POS(P1$(36,1)="RG")>0 THEN W1$="R = Commission rate reduced by "+STR(R[0,I0])+"% after invoice"+" is "+STR(L[0])+" days old and "+STR(R[1,I0])+"% every "+STR(L[1])+" days later"; GOTO 7690
7665 FOR I=0 TO L0
7670 J$=CHR(ASC("A")+I)
7675 W1$=W1$+" "+J$+"="+STR(R[I,I0])+"% @ "+STR(L[I])+" days"
7677 IF I<L0 THEN W1$=W1$+","
7680 NEXT I
7690 RETURN 
7700 REM "Calculate payment code and refigure commission amount
7705 IF POS(P1$(36,1)="YRG")=0 THEN GOTO 7790
7706 REM "Get # days between dates into D0
7707 D0$=A$(34,6),D1$=A$(46,6)
7710 CALL "ZZDATE",X3$,"",D0$,D1$,D0,1,0,0,0,0,0,"","",""
7711 REM "Check if # days < smallest category, C9$="" , then just exit
7712 C9$=" "; IF D0<L[0] OR L[0]=0 THEN GOTO 7790
7713 IF POS(P1$(36,1)="RG")>0 THEN IF P1$(32,1)="Y" AND (A[8]>0 OR POS(X3$(9,3)="136")=0) THEN GOTO 7800
7715 FOR I=L0 TO 0 STEP -1
7720 IF D0>=L[I] THEN C9=I,C9$=CHR(ASC("A")+I); EXITTO 7730
7725 NEXT I
7730 REM "Adjust commission payable amount, based on #days category and sal"+ "esperson type (B$(224,1))
7731 REM "Adjust A(8) if comm when payed or A(6) if comm when completed see"+ " P1$(32,1)
7735 C8=0,C8=NUM(B$(224,1),ERR=7736)
7740 C7=R[C9,C8]
7744 REM "Don't do calculation if commission amount is negative
7745 IF P1$(32,1)="Y" THEN IF A[8]>0 THEN A[8]=A[8]*C7/100
7750 IF P1$(32,1)<>"Y" THEN IF A[6]>0 THEN A[6]=A[6]*C7/100
7790 RETURN 
7800 REM "do Declining commissions based on Reduction in Rate
7805 I0=0,I0=NUM(B$(224,1),ERR=7806)
7810 R9=R[0,I0]+INT((D0-L[0])/L[1])*R[1,I0]
7815 REM "Figure commission % based onInvoice NET AMOUNT, if an R, G means "+ "on Gross profit
7818 IF P1$(36,1)="R" THEN R5=A[1] ELSE R5=A[2]
7820 IF R5<>0 THEN R8=A[8]*100/R5 ELSE R8=0
7824 REM " subtract r9, then refigure A(8)
7825 R7=R8-R9; IF P1$(36,1)="R" AND A[1]<0 THEN GOTO 7845; REM " IF R7<0 THEN LET R7=0
7826 IF A[1]=0 AND A[8]<0 AND P1$(36,1)="R" THEN GOTO 7845
7828 IF A[8]<0 AND A[1]>0 AND P1$(36,1)="R" AND R7<0 THEN R7=(ABS(R8)-R7+R8)*SGN(R8)
7830 A[8]=R5*R7/100,C9$="R"
7845 RETURN 
8000 REM "PRINT P/E DATE"
8005 DIM A0$(6); IF T<2 THEN A0$(1,6)=A$(1,6) ELSE A0$(1,6)=U1$(1,6)
8010 IF P2$(9,4)<>A0$(1,4) THEN DIM P2$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A0$(1,4),DOM=8060)P2$
8020 D1$=P2$(NUM(A0$(5,2))*6+15,6),D0$=D1$
8030 J5$="For Fiscal year "+A0$(1,4)+", period "+A0$(5,2)+" ending "+FND$(D0$)
8040 Y5$(INT(W3/2)-INT(LEN(J5$)/2))=J5$
8050 GOSUB 7300
8060 RETURN 
8100 REM "Print Salesperson's chargebacks
8104 F6$="*",F3=0,F4=0
8105 READ (Z[6],KEY=A$(1,10),DOM=8106)
8110 READ (Z[6],END=8190)IOL=0360
8115 IF F$(1,10)<>A$(1,10) THEN GOTO 8190
8116 IF F$(17,6)<V0$(75,6) OR F$(17,6)>V0$(81,6) THEN GOTO 8110; REM "Check dates before printing chargebacks"
8117 IF GAP$="Y" THEN IF PA1$="" THEN IF CHECKED$<>"Y" THEN GOSUB 8800
8119 IF F6$<>"*" THEN GOTO 8125
8120 IF W+4>W0 THEN GOSUB 6000
8121 GOSUB 7300; GOSUB 7300; Y5$(1)="Salesperson charge backs "; F6$=""
8122 Y5$(64)="Commission",Y5$(77)="Gross Profit",Y5$(119)="Commission"; GOSUB 7300
8123 Y5$(1)="CB Number",Y5$(14)="Date",Y5$(25)="Description",Y5$(67)="Code",Y5$(78)="Adjustment",Y5$(119)="Adjustment"; GOSUB 7300
8125 IF W+2>W0 THEN GOSUB 6000
8130 Y5$(1)=F$(11,6),Y5$(11)=FND$(F$(17,6)),Y5$(24)=F$(23,40),Y5$(67)=F$(63,5)
8135 Y5$(77)=STR(F[0]:Z2$),Y5$(118)=STR(F[1]:Z2$); IF V0$(59,1)="B" THEN Y5$(77,LEN(Z2$))=Z8$(1,LEN(Z2$))
8140 GOSUB 7300
8145 T[2,3]=T[2,3]+F[0],T[2,6]=T[2,6]+F[1]
8146 F3=F3+F[0],F4=F4+F[1]
8160 GOTO 8110
8190 IF F6$="*" THEN GOTO 8193
8191 IF V0$(59,1)<>"B" THEN Y5$(76)=T1$(1,12),Y5$(117)=T1$(1,12); GOSUB 7300; Y5$(77)=STR(F3:Z2$),Y5$(118)=STR(F4:Z2$); GOSUB 7300; GOSUB 7300; GOTO 8193
8192 IF V0$(59,1)="B" THEN Y5$(117)=T1$(1,12); GOSUB 7300; Y5$(118)=STR(F4:Z2$); GOSUB 7300; GOSUB 7300
8193 REM REMOVE (Z(1),KEY=A$(1,33),DOM=08194)
8195 RETURN 
8200 REM "Print D, F or W flags if A$ is long enough to have them
8205 IF LEN(A$)<54 THEN GOTO 8245
8210 J5$=""
8215 FOR I=52 TO 54
8220 IF A$(I,1)<>" " THEN J5$=J5$+A$(I,1)
8225 NEXT I
8230 Y5$(114)=J5$
8245 RETURN 
8300 REM "Put "FLAG" records into ARM file, These are FY AP and Salesp, wit"+ "h an cust# of ~'s
8305 READ (Z[6],KEY="",DOM=8306)
8307 PRINT @(20,20),"Now Sorting: ",
8310 READ (Z[6],END=8380)J$
8315 PRINT @(33,20),J$(1,4)," ",J$(5,2)," ",J$(7,4)," ",J$(11,6),
8320 DIM K$(54,"~")
8325 K$(1,10)=J$(1,10)
8330 WRITE (Z[1],KEY=K$(1,33))K$
8340 GOTO 8310
8380 REM "Finish
8385 PRINT @(20,20),'CL',
8395 RETURN 
8400 REM "Remove chargebacks
8420 READ (Z[6],KEY=U$(1,10),DOM=8430)
8430 READ (Z[6],END=8480)IOL=0360
8440 IF F$(1,10)<>U$(1,10) THEN GOTO 8480
8450 REMOVE (Z[6],KEY=F$(1,16))
8460 GOTO 8430
8480 Q0$=U$(1,10)
8490 RETURN 
8500 REM "Define TMP Sort file
8510 Y$="[32]Commission Payable Report"
8520 CALL "ZZ2SRT",X3$,X4$,Z[13],Z[2],10,Y$,Z,"NY"; IF Z=2 THEN GOTO 9920
8530 Y$="07O "+Y$+"08O ARA...  "
8535 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
8540 RETURN 
8550 REM "Write totals to temp file
8555 G$=U1$+U2$,G[0]=T[T,2],G[1]=T[T,3]-T[T,5]
8560 WRITE (Z[7],KEY=G$)IOL=0370
8595 RETURN 
8600 REM "Read TMP records in and write out to stats file
8604 S9$=V8$,F0=Z[8]
8605 READ (Z[7],KEY="",DOM=8606)
8610 READ (Z[7],END=8640)IOL=0370
8615 PRINT @(23,20),G$(1,4)," ",G$(5,2)," ",G$(7),
8620 Q1$=G$(7,4)+G$(1,4),Q1=NUM(G$(5,2))
8623 Q0$="U",Q0=G[0]; GOSUB 8700
8625 Q0$="V",Q0=G[1]; GOSUB 8700
8635 GOTO 8610
8645 RETURN 
8700 REM "Update stats q0$ in period q1 with data q0
8705 IF Q0=0 THEN GOTO 8730
8710 DIM K[14]; P9$=Q1$+Q0$
8715 GOSUB 8735
8720 K[Q1]=K[Q1]+Q0
8725 GOSUB 8755
8730 RETURN 
8735 REM "Read stats (Packed/Unpacked)
8740 Q=POS(Q0$=S9$(49),17); IF Q=0 THEN EXITTO 8730 ELSE M0$=S9$(Q+48,17)
8745 CALL "ZZPACK",X3$,"E",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
8750 GOTO 8765
8755 REM "Write stats
8760 CALL "ZZPACK",X3$,"W",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
8765 RETURN 
8800 REM "Check salesperson for vendor code and if there start PA1 record
8810 CHECKED$="Y"
8815 IF STP(B$(225,10),3," ")="" THEN GOTO 8845; REM "This salesperson does not have a vendor code in their AR3 record
8820 DIM PA1$(332); PA1$(1,6)="CPR100",PA1$(7,6)=AP_PARMS$(7,6),PA1$(13,10)=B$(225,10),PA1$(23,10)=A$(1,6)+"00",PA1$(33,6)=X3$(21,6),PA1$(110,3)=X3$(9,3); REM "Source, version, fiscal/ap, vendor code, invoice #, invoice date, source company code
8825 DIV$=A$(11,2); REM "get division # of first record for this salesperson to get g/l account numbers
8830 DIM ARI$(214); READ (Z[9],KEY=DIV$+DIM(7),DOM=8831)ARI$; GOTO 8835
8831 DIV$="00"; GOTO 8830
8835 PA1$(285,12)=ARI$(190,12),PA1$(309,12)=ARI$(166,12); REM "Comm pay and comm exp g/l account numbers
8845 RETURN 
8850 REM "Add totals to PA1 record and write out if doing this salesperson
8855 CHECKED$="N"
8860 IF PA1$="" OR STP(PA1$,3," ")="" OR T[T,6]=0 THEN PA1$=""; GOTO 8895
8865 PA1$(129,12)=STR(T[T,6]:Z4$),PA1$(297,12)=STR(T[T,4]:Z4$)
8870 IF T[T,6]-T[T,4]=0 THEN PA1$=PA1$(1,308) ELSE PA1$(321,12)=STR(T[T,6]-T[T,4]:Z4$)
8880 WRITE (32500)PA1$; PA1$="",PA1_WRITE$="Y"
8895 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF T9=1 AND V3$(2,1)<>"T" THEN GOTO 5300
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF GAP$="Y" THEN IF GAP_UPDATE$="N" OR PA1_WRITE$="N" THEN CLOSE (32500); ERASE GAP_FILE$,ERR=9926 ELSE CALL "PA2UBA",X3$,X4$,"","CPR"; CALL "PA2UAA",X3$,X4$,"","CPR"
9930 ! IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 EXIT 
9999 END 
56000 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
