0010 REM "Sales Tax Payable <AR2RHB>
0035 REM "5.4 - 04/28/05 - 13.36 - tma - SSP# 183350
0040 REM "Copyright 2005 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 REM "Alternate Sales Tax Payable Report <AR2RHB>
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2RHB",X1$="Detail Sales Tax Payable"
0120 DIM A$(51),A[25],B$(37),B[2],C$(37),C[2],D$(37),D[2],A0$(51),A0[6]
0140 Z0$="##,###,###.00-",Z1$="###.000%"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0166 DEF FNE$(Z9$)=Z9$(3,2)+X3$(59,1)+STR(MOD(ASC(Z9$(1,1))-65,10)*10+NUM(Z9$(2,1)):"00"); REM " NOT USED BUT CONVERTED FOR 2000
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0210 T=1,T0=0,T1=9
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR ARL - Sales tax payable
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25]
0315 IOLIST A0$,A0[0],A0[1],A0[2],A0[3],A0[4],A0[5],A0[6]
0320 IOLIST B$,B[0],B[1],B[2]
0330 IOLIST C$,C[0],C[1],C[2]
0340 IOLIST D$,D[0],D[1],D[2]
0350 IOLIST E$,E[0],E[1],E[2],E[3]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O AS3...  02O AR5...  05O AR6...  06O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 READ (Z[6],KEY=X3$(9,3)+"A/R")P0$
0600 REM "Create Sort file
0605 K0$="[192]Detailed Sales Tax Work File"
0606 Q=0; CALL "ZZINFO",Z[1],Q,X3$,R0,0,0,0,0,0,0,Q$+""; R0=(-R0)*3
0610 CALL "ZZ2SRT",X3$,X4$,Z[6],R0,26,K0$,K1,"YN"
0615 IF K1=2 THEN GOTO 9900
0620 Y$="03O "+K0$
0625 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0640 GOSUB 7500
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[4]; U=24,U0=4,U[0]=0,U[1]=4,U[2]=2,U[3]=10,U[4]=8,T0=2,V1=6,V2=10,V3=0,V4=0,U0$="Y   ",T5$="S   S   S   S   ",U5$="Fiscal year",U6$="",U7$="",U8$=""; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 U1=Z[3]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[3],KEY=U$,DOM=1000)IOL=0310
1165 IF Q5=1 THEN CALL "ZZDISP","A",U$(7,2)+"-"+U$(9,4)+"-"+U$(13,4),"",X3$,"","",34,21,X4$; REMOVE (Z[1],KEY=U$,DOM=1000); GOTO 1000
1300 REM "Read other data
1310 B0$=B$,C0$=C$,D0$=D$
1315 IF A$(7,2)+"        "=B$(1,10) THEN GOTO 1321
1320 DIM B$(LEN(B$)); FIND (Z[2],DOM=1321,KEY=A$(7,2)+"        ")IOL=0320
1330 DIM C$(LEN(C$)); IF A$(9,4)<>"    " THEN FIND (Z[2],DOM=1331,KEY=A$(7,6)+"    ")IOL=0330
1340 DIM D$(LEN(D$)); IF A$(13,4)<>"    " THEN FIND (Z[2],DOM=1341,KEY=A$(7,10))IOL=0340
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 GOSUB 6200
1440 GOSUB 6300
1450 GOSUB 6400
1460 GOSUB 6500
1490 T=0
1500 REM "Print Data
1510 IF W+2>W0 THEN GOSUB 6000
1515 CALL "ZZDISP","AX",A$(17,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1520 CALL "ZZDISP","AX",A$(27,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(12)=X$
1530 Y5$(25)=STR(A[0]:Z0$),Y5$(41)=STR(A[1]:Z0$)
1535 Y5$(61)=STR(A[2]:Z0$)
1540 Y5$(77)=STR(A[3]:Z0$)
1543 IF A[2]/2+A[3]<>A[6] THEN Y5$(94)="*",F0=1
1545 Y5$(97)=STR(A[4]:Z0$),Y5$(113)=STR(A[5]:Z0$)
1550 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[0]
1915 T[T0,2]=T[T0,2]+A[1]
1920 T[T0,3]=T[T0,3]+A[2]
1925 T[T0,4]=T[T0,4]+A[3]
1930 T[T0,5]=T[T0,5]+A[4]
1935 T[T0,6]=T[T0,6]+A[5]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$="FY:"+U1$(1,4)+" PD:"+U1$(5,2),T6$=""; GOSUB 3050; GOSUB 7000
3155 IF T0$<>"END" THEN IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T6$="",T$=U2$+"-"+U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "lowest order break"
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T6$="",T$=U2$+"-"+U3$+"-"+U4$; GOSUB 3050; IF POS(" "<>U4$)=0 THEN GOSUB 7100 ELSE GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
5000 REM "End of Print
5010 IF Q5=1 THEN GOTO 9920
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 REM LET T=0,T$="Report"; GOSUB 07000
5200 REM "
5210 T9=1
5290 GOTO 9900
6000 REM "Page header
6010 IF F0=1 THEN F0=0,J5$=Y5$,Y5$(1)="* Taxable sale on invoice total screen was overridden by operator"; GOSUB 7300; Y5$=J5$
6020 W0$="Fiscal Year: "+A$(1,4)+"  "+"Accounting Period: "+A$(5,2)
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6120 Y5$(32)="Non Taxable Sale",Y5$(69)="Taxable Sale",Y5$(100)="Sales Tax",Y5$(115)="Total S.T."
6125 GOSUB 7300
6135 Y5$(1)="Invoice",Y5$(12)="Customer",Y5$(28)="Freight",Y5$(46)="Other",Y5$(65)="Custom",Y5$(82)="Stock",Y5$(101)="billed",Y5$(115)="on invoice"
6140 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6299 RETURN 
6300 REM "Break #2 header
6399 RETURN 
6400 REM "Break #3 header
6499 RETURN 
6500 REM "Break #4 header
6510 IF W+5>W0 THEN GOSUB 6000
6515 GOSUB 7300
6520 X5$=A$(7,2)
6525 J5$=FNS$(B$(11,25)),J5=B[0]
6530 IF POS(" "<>A$(9,4))<>0 THEN X5$=X5$+"-"+A$(9,4),J5$=J5$+", "+FNS$(C$(11,25)),J5=C[0]
6535 IF POS(" "<>A$(13,4))<>0 THEN X5$=X5$+"-"+A$(13,4),J5$=J5$+", "+FNS$(D$(11,25)),J5=D[0]
6545 Y5$(1)=X5$+"   "+J5$+"  "+STR(J5:Z1$)
6550 GOSUB 7300
6599 RETURN 
7000 REM "Totals logic
7001 IF T=1 THEN GOSUB 7600; GOTO 7100; REM IF T=T0 THEN IF T(T0,0)<2 THEN GOSUB 07300; GOTO 07100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(25)=T1$(1,LEN(Z0$)),Y5$(41)=T1$(1,LEN(Z0$)),Y5$(61)=T1$(1,LEN(Z0$)),Y5$(77)=T1$(1,LEN(Z0$)),Y5$(97)=T1$(1,LEN(Z0$)),Y5$(113)=T1$(1,LEN(Z0$))
7007 GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7011 IF T<>2 THEN GOTO 7015 ELSE J5$=T$
7012 T$=J5$(1,2)
7013 IF POS(" "<>J5$(3,4))<>0 THEN T$=T$+"-"+J5$(3,4)
7014 IF POS(" "<>J5$(7,4))<>0 THEN T$=T$+"-"+J5$(7,4)
7015 IF T$<>"" THEN IF T=1 THEN Y5$(T0+2-T)=T$+" ("+STR(T[T,0]:"0")+"):" ELSE Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7025 Y5$(25)=STR(T[T,1]:Z0$),Y5$(41)=STR(T[T,2]:Z0$)
7030 Y5$(61)=STR(T[T,3]:Z0$),Y5$(77)=STR(T[T,4]:Z0$)
7035 Y5$(97)=STR(T[T,5]:Z0$),Y5$(113)=STR(T[T,6]:Z0$)
7040 GOSUB 7300
7065 IF T$="END" THEN Y5$(25)=T2$(1,LEN(Z0$)),Y5$(41)=T2$(1,LEN(Z0$)),Y5$(61)=T2$(1,LEN(Z0$)),Y5$(77)=T2$(1,LEN(Z0$)),Y5$(97)=T2$(1,LEN(Z0$)),Y5$(113)=T2$(1,LEN(Z0$))
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7484 V1$(1,3)="AS3"
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Create sort
7503 REM "Changed logic for ssp#90808 to find an existing temp file record if there is one then add to the buckets since we can now have more than one AS3 record per invoice
7505 READ (Z[1],KEY="",DOM=7506)
7510 GOSUB 8100
7520 READ (Z[1],END=7590)IOL=0310
7522 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
7523 GOSUB 7800; IF Q0=1 THEN GOTO 7520
7524 REM "Write out state record
7525 DIM A0$(51),A0[6]; A0$=A$,A0$(9,8)=""
7526 EXTRACT (Z[3],KEY=A0$(1,26),DOM=7527)IOL=0315
7528 A0[0]=A0[0]+A[10],A0[1]=A0[1]+A[13],A0[2]=A0[2]+A[1],A0[3]=A0[3]+A[4]+A[7],A0[4]=A0[4]+A[16],A0[5]=A0[5]+A[16]+A[17]+A[18],A0[6]=A0[6]+A[19]
7530 WRITE (Z[3],KEY=A0$(1,26))IOL=0315
7534 REM "Write out county, if needed
7535 IF POS(" "<>A$(9,4))=0 THEN GOTO 7544 ELSE DIM A0$(51),A0[6]; A0$=A$,A0$(13,4)=""
7536 EXTRACT (Z[3],KEY=A0$(1,26),DOM=7537)IOL=0315
7538 A0[0]=A0[0]+A[11],A0[1]=A0[1]+A[14],A0[2]=A0[2]+A[2],A0[3]=A0[3]+A[5]+A[8],A0[4]=A0[4]+A[17],A0[5]=A0[5]+A[16]+A[17]+A[18],A0[6]=A0[6]+A[20]
7540 WRITE (Z[3],KEY=A0$(1,26))IOL=0315
7544 REM "Write out local, if needed
7545 IF POS(" "<>A$(13,4))=0 THEN GOTO 7560 ELSE DIM A0$(51),A0[6]; A0$=A$
7546 EXTRACT (Z[3],KEY=A0$(1,26),DOM=7547)IOL=0315
7548 A0[0]=A0[0]+A[12],A0[1]=A0[1]+A[15],A0[2]=A0[2]+A[3],A0[3]=A0[3]+A[6]+A[9],A0[4]=A0[4]+A[18],A0[5]=A0[5]+A[16]+A[17]+A[18],A0[6]=A0[6]+A[21]
7550 WRITE (Z[3],KEY=A0$(1,26))IOL=0315
7560 GOTO 7520
7590 PRINT @(0,19),'CE',
7595 RETURN 
7600 REM "Print end of acct period
7605 GOSUB 7300; GOSUB 7300
7610 Y5$(1)="***** End of Fiscal Year: "+U1$(1,4)+" Accounting Period: "+U1$(5,2)+" *****"
7615 GOSUB 7300
7700 REM "PRINT P/E DATE"
7710 FIND (Z[6],DOM=7711,KEY=X3$(9,3)+"G/LYE"+A$(1,4))P1$
7720 Y5$(81)="Ending "+FND$(P1$(NUM(A$(5,2))*6+15,6))
7749 RETURN 
7800 REM "Determine if ST payed on invoice payment, skip AS3 records with invoices not paid off; Q0=1 means skip it
7805 Q0=0
7810 IF P0$(88,1)<>"Y" THEN GOTO 7845
7815 DIM E[3]; READ (Z[5],KEY=A$(27,10)+A$(17,8),DOM=7816)IOL=0350; GOTO 7820
7816 Q0=0; GOTO 7840; REM " If Open invoice rec not here, consider it paid off
7820 IF E[3]<>0 THEN Q0=1
7845 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T4,T0,K,B,D,S0,S1,F$
8130 T0=INT(T4*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T4,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF W5<1 THEN RUN "ARGRHA"
9930 ! IF Y4$<>"" THEN RUN Y4$,ERR=9931
9935 ERASE "TMP"+FID(0),ERR=9936
9940 EXIT 
9999 END 
