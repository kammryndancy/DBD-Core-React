0010 REM "Cash Receipts Update <AR2CUA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 12/23/02 - 12.029722 - tma - SSP# 155810
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 SETERR 9000
0110 X0$="ARGCUA",X1$="Cash Receipts Update"
0120 DIM Z0$(80,"-"),C$(200),P2$(20),B[3]
0145 GOSUB 8950
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0180 DEF FNV$(Z9$)=CHR(INT(NUM(Z9$)/10)-125)+STR(MOD(NUM(Z9$),10))
0200 REM "
0230 X0=-1; X2=-1
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7]
0330 IOLIST C$
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15]
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14],F[15],F[16],F[17],F[18],F[19],F[20],F[21]
0370 IOLIST G$(1),G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14]
0380 IOLIST K$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9],K[10],K[11],K[12],K[13],K[14],K[15]
0390 IOLIST S$,S[0],S[1],S[2],S[3],S[4],S[5],S[6],S[7],S[8]
0400 IOLIST R$,R[0],R[1],R[2],R[3],R[4],R[5],R[6],R[7],R[8]
0410 IOLIST L$
0420 IOLIST U$,U0$,U[0]
0500 REM "FILES
0505 Z=NUM(X3$(60,3)); DIM Z[Z]
0510 Z$="01L ARP...  02O ARQ...  03O ZYB...  04O GL6...  05O AR1...  06O AR0...  07O AR6...  08O AR7...  09O ARN...  10O ARO...  11O ARI...  25O AR2...  15O AT5...  16O AT6...  17O AT7...   "
0515 Z$=Z$+"13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
0550 READ (Z[13],KEY=X3$(9,3)+"G/L")G0$
0555 READ (Z[13],KEY=X3$(9,3)+"B/R",DOM=0556)F0$
0560 IF X3$(30,1)="M" THEN READ (Z[13],KEY="PFGG/L")F3$
0580 Q9$="C"; IF G0$(40,1)="A" THEN Q9$="A" ELSE IF G0$(180,1)="Y" THEN Q9$=Q9$+"A"
0585 W9$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,W9$
0590 CALL "ZZDAPP",X3$,"DALL"
0600 REM "Read parameters
0610 DIM P[8],P0$(90),P1$(32)
0620 FIND (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$,P[0],P[1],P[2],P[3],P[4],P[5],P[6],P[7],P[8]
0630 FIND (Z[13],KEY=X3$(9,3)+"AR1",DOM=9800)P1$
0640 IF POS("Y"=P0$(88,1)+P1$(32,1))>0 THEN P3$="Y" ELSE P3$="N"
0650 FIND (Z[13],KEY=X3$(9,3)+"F/M",DOM=0651)P4$
0695 CALL "ZZPROM","U6",X3$,0,U9$,"","",0
0730 DIM A$(35),A[7]
0740 DIM B$(37),B[4]
0750 DIM E$(400),E[15]
0760 DIM F$(53),F[21]
0770 DIM G$(180),G[14]
0775 DIM L$(202)
0780 DIM K$(66),K[15],S$(47),S[8],R$(52),R[8]
0800 REM "CHECK FOR ROOM?
0802 REM "GOTO 00836
0805 CALL "ZZPROM","U1",X3$,0,"","","",0
0810 DIM X[Z,1],X$(0)
0815 FOR X=1 TO Z
0820 CALL "ZZINFO",X,0,X3$,A,B,0,0,0,0,0,""; X[X-1,0]=A,X[X-1,1]=B
0825 NEXT X
0830 PRINT @(0,22),'CL',
0835 IF X[Z[1]-1,0]=0 THEN IF Q1$<>"" THEN GOTO 9900 ELSE CALL "ZZPROM","U2",X3$,S3,"","","",0; GOTO 9900
0840 GOTO 0900
0880 REM "PROMPT OUT OF ROOM
0885 X$="There is not enough room to update this data. Press <Return>: "
0890 IF Q1$<>"" THEN GOTO 9900 ELSE CALL "ZZPROM","U3",X3$,S3,"","","",0; GOTO 9900
0900 REM "RESTART LOGIC
0905 V0$=X3$(1,6)+X3$(40,3)+DAY+STR(TIM*100:"0000")+X3$(178,6); WRITE (Z[1],KEY="",DOM=0906)V0$
0910 EXTRACT (Z[1],KEY="")V0$,K0$,K1$,K0,K1; I1=IND(Z[1])
0915 GOSUB 6000
0920 IF K0$="" THEN READ (Z[1],KEY=K0$); GOTO 0960
0930 EXTRACT (Z[1],KEY=K0$,DOM=0931)IOL=0310
0935 PRINT @(12,12),"Restarting update with record: ",A$(1,4)+"-"+A$(5,2)+"-"+A$(7,3)+"-"+A$(10,1)+"-"+A$(11,10)+"-"+A$(21,6)+"-"+A$(27,8)
0940 IF Q1$="" THEN CALL "ZZPROM","U5",X3$,0,"","","",14
0970 IF Q1$="" THEN CALL "ZZPROM","9",X3$,S3,"","","",0; ON S3 GOTO 0971,9900
0975 PRINT @(0,12),'CL',
1000 REM "READ NEXT RECORD
1020 K0$=KEY(Z[1],END=5000)
1023 K9$=K0$; IF X3$(330,1)="1" THEN K0$=K0$(3); REM "Truncate seq id off front of key if this is a franchise
1024 REM "Screen out deposit headers, but save the date
1025 IF LEN(K0$)<11 THEN READ (Z[1],KEY=K9$)A$; D0$=A$(11,6),D9$=A$,D7$=""; GOTO 1000
1030 READ (Z[1],KEY=K9$)IOL=0310
1032 IF X3$(30,1)<>"M" OR X3$(330,1)="1" THEN GOTO 1100 ELSE IF LEN(K0$)=26 THEN D8$=A$(68,3)
1035 IF D8$="   " THEN GOTO 1100
1036 IF POS(A$(68,3)="144244",3)>0 THEN GOTO 1100
1040 REM "UPDATE REMOTE LOCATION TRANSFER FILE
1050 CALL "CM2AAA",X3$,Z[1],2,D8$+K0$,"",0
1055 IF D7$<>D8$ THEN CALL "CM2AAA",X3$,Z[1],3,D8$+D9$(1,10),D9$,0; D7$=D8$; REM "Update deposit headers once per company, D7$ is last company updated, when it changes update deposit header again
1100 REM "Look for check or invoice"
1110 IF LEN(K0$)>26 THEN GOTO 1500
1200 REM "Process Check
1205 P2=0; GOSUB 2200
1210 PRINT @(12,12),'CL',U9$,A$(1,4)+"-"+A$(5,2)+"-"+A$(7,3)+"-"+A$(10,1)+"-"+A$(11,10)
1220 REM "Update cash based on bank
1230 IF A$(7,3)<>C$(1,3) THEN DIM C$(200); FIND (Z[3],KEY=A$(7,3),DOM=1231)IOL=0330
1240 Q7$=Q9$,Q8$=Q9$,Q0$=C$(69,12),Q0=A[1]
1245 GOSUB 8700
1260 REM "Get customer record for invoice records
1270 IF POS(" "<>A$(11,10))<>0 THEN GOSUB 7600
1300 REM "Update G/L if necessary
1310 GOSUB 2000
1420 C0$=A$(27,6)
1480 P2=1; GOSUB 2200
1490 GOTO 1000
1500 REM "Process Invoices
1505 P2=1; GOSUB 2200; REM "Open files for invoices
1510 PRINT @(12,12),"Now updating record: ",A$(1,4)+"-"+A$(5,2)+"-"+A$(7,3)+"-"+A$(10,1)+"-"+A$(11,10)+"-"+A$(21,6)+"-"+A$(27,8)
1515 REM "Get invoice
1520 DIM G$(180),G[14]
1525 EXTRACT (Z[7],KEY=A$(11,10)+A$(27,8),DOM=1526)IOL=0370; GOTO 1530
1526 G$(1,18)=A$(11,10)+A$(27,8)
1530 IF X3$(30,1)<>"M" OR X3$(330,1)="1" THEN GOTO 1560
1531 REM "Skip if this is not a franchise data record
1532 IF POS(D8$="144244",3)>0 THEN GOTO 1560
1533 IF X3$(30,1)="M" THEN IF A$(11,2)<"10" THEN GOTO 1560
1535 CALL "CM2UAC",X3$,"C",A$,A{ALL},G$,"",0
1540 GOSUB 7900
1555 REM "Update G/L for A/R & discounts, set T0=Proration amount & R1 total amount
1560 GOSUB 2100
1565 REM "Update sales tax and/or commission if needed
1570 IF P3$="Y" OR POS("C"=Q9$)<>0 THEN GOSUB 2300
1575 REM "Intialize for lines"
1580 GOSUB 8000
1585 GOSUB 8900
1590 H1$=FNV$(P2$(9,4))+A$(5,2)+"CR"+"C"+V0$(22,6)+"AR"
1595 IF POS("C"=Q9$)=0 THEN R9$=""; GOTO 1620
1600 R8$=G$(11,7); IF G$(18,1)<>" " THEN R8$=R8$+"-"+G$(18,1) ELSE IF G$(19,1)="C" THEN R8$=R8$+"-CM" ELSE IF G$(19,1)="D" THEN R8$=R8$+"-DM"
1605 IF P0$(13,1)="Y" THEN R7$=G$(1,2)+"-"+G$(3,NUM(P0$(14,1))) ELSE R7$=G$(1,NUM(P0$(14,1)))
1610 DIM R9$(45); R9$(1)="Ck#: "+FNS$(A$(21,6))+" I#: "+R8$+" C#: "+R7$+" "+E$(155,10)
1620 G0=G[0],G1=G[1],G2=G0
1622 R1=-R1
1624 IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" AND POS(E$(431,3)="144244",3)=0 THEN G2=-1; REM "Normal case use G2 as temp var, so we don't destroy value in G0
1625 CALL "GL2XUA",X3$,X4$,"ARO...",40,H1$,R9$,G2,3,2,T0,Z[7],I1,"","",0,Z[8],H$,H[1],G1,H[2],R2,R1,"","",G$
1630 G[1]=G1
1650 REM "Update Invoice buckets
1660 G[3]=G[3]-A[1],G[4]=G[4]-A[2],G[5]=0
1670 IF A$(1,6)>G$(133,6) THEN G$(133,6)=A$(1,6)
1680 REM "Update ARN file if needed
1681 IF X3$(330,1)="1" AND G$(11,7)="ONACCNT" THEN GOTO 1683
1682 IF G$(11,7)="ONACCNT" THEN GOTO 1687
1685 IF G[3]<>0 THEN GOTO 1690 ELSE REMOVE (Z[9],KEY=G$(1,10)+"B"+G$(20,6)+G$(11,8),DOM=1686)
1686 WRITE (Z[9],KEY=G$(1,10)+"A"+G$(20,6)+G$(11,8))
1700 REM "Update customer A/R buckets & if necc. the summary record
1710 GOSUB 7200
1715 E[6]=E[6]-A[1]
1720 IF G[3]<>0 THEN GOTO 1780
1730 IF G[2]<=P[5] THEN E0=14 ELSE IF G[2]<=P[6] THEN E0=16 ELSE IF G[2]<=P[7] THEN E0=18 ELSE E0=20
1740 CALL "ZZDATE",X3$,"",G$(20,6),C0$,E1,1,0,0,0,0,0,"","",""
1750 IF E1>=0 THEN F[E0]=F[E0]+1,F[E0+1]=F[E0+1]+E1
1780 REM "Update G/L if necessary
1785 GOSUB 2000
1800 WRITE (Z[7],KEY=G$(1,18))IOL=0370
1805 IF W9$<>"" THEN TMP$=G$(1,10); CALL "RT2WOC",ERR=1806,X3$,X4$,TMP$,"AR6...","U",G$(1,18)
1820 IF A[4]<>0 AND POS("C"=Q9$)=0 AND P0$(88,1)<>"Y" THEN GOSUB 7700
1840 IF G[3]=0 AND P4$(183,1)="Y" THEN CALL "AR2CUB",X3$,X4$,G$,A$
1850 IF G[3]=0 AND P0$(108,1)="Y" AND P1$(32,1)="Y" THEN CALL "AR2CUC",X3$,X4$,G$,A$
1950 GOTO 1000
2000 REM "Update G/L base on A(0)
2005 GOSUB 8900
2010 FOR I=1 TO LEN(Q9$)
2015 I0=A[0],H1$=FNV$(P2$(9,4))+A$(5,2)+"CR"+Q9$(I,1)+V0$(22,6)+"AR"
2020 IF P0$(13,1)="Y" THEN R7$=E$(1,2)+"-"+E$(3,NUM(P0$(14,1))) ELSE R7$=E$(1,NUM(P0$(14,1)))
2025 IF LEN(K0$)=26 THEN GOTO 2040
2026 R8$=G$(11,7); IF G$(18,1)<>" " THEN R8$=R8$+"-"+G$(18,1) ELSE IF G$(19,1)="C" THEN R8$=R8$+"-CM" ELSE IF G$(19,1)="D" THEN R8$=R8$+"-DM"
2030 DIM R9$(45); R9$(1)="I: "+R8$+" "+R7$+" "+E$(11,35)
2035 GOTO 2050
2040 DIM R9$(45)
2045 IF POS(" "<>A$(11,10))=0 THEN R9$(1)=A$(21,6)+" / "+A$(33,30) ELSE R9$(1)=A$(21,6)+" / "+FNS$(R7$)+" "+A$(33,30)
2055 REM "TEMP REM 1/5/95 TO CK OUT PROBLEM JSC.  IF A$(11,10)<>S$(1,10) THEN IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" THEN GOTO 02061
2060 CALL "GL2XUA",X3$,X4$,"ARQ...",7,H1$,R9$,I0,3,2,1,Z[1],I1,"","",0,0,"",0,0,0,0,1,"","",G$
2065 IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" THEN IF POS(D8$="144244   ",3)=0 THEN CALL "CM2AAA",X3$,Z[2],2,D8$+"","",A[0]; IF D7$<>D8$ THEN CALL "CM2AAA",X3$,Z[1],3,D8$+D9$(1,10),D9$,0; D7$=D8$; REM "Update deposit header if needed
2070 NEXT I
2090 RETURN 
2100 REM "Update G/L for A/R & discounts
2105 IF G[3]<>0 THEN R2=2; PRECISION 14; T0=A[1]/G[3]; PRECISION 2 ELSE IF G[2]<>0 THEN R2=1; PRECISION 14; T0=A[1]/G[2]; PRECISION 2 ELSE R2=1; T0=1
2106 R1=A[1]
2110 IF P0$(19,1)="Y" THEN L0$=G$(108,9) ELSE DIM L0$(9); L0$(1)=A$(11,2)
2120 IF L0$<>L$(1,LEN(L0$)) THEN DIM L$(214); FIND (Z[11],KEY=L0$,DOM=2121)IOL=0410
2125 REM "Accural postings
2130 IF POS("A"=Q9$)=0 THEN GOTO 2150 ELSE Q8$="A"
2132 Q$=L$(10,12); IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" AND E$(1,2)>="10" THEN Q$=F3$(43,12),P=POS("???"=Q$); IF P>0 THEN Q$(P,3)=E$(431,3)
2135 Q0$=Q$,Q0=-A[1]; GOSUB 8700
2138 IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" THEN GOTO 2190
2140 IF A[3]<>0 THEN Q0$=L$(34,12),Q0=A[3]; GOSUB 7500
2145 IF A[4]<>0 THEN Q0$=L$(46,12),Q0=A[4]; GOSUB 8700
2150 REM "Cash basis postings
2155 IF POS("C"=Q9$)=0 THEN GOTO 2180 ELSE Q8$="C"
2156 IF A$(27,7)="ONACCNT" OR G$(36,10)="Prepayment" THEN Q0$=L$(202,12),Q0=-A[1]; GOSUB 8700
2159 REM "Freight
2160 Q0$=L$(22,12),Q0=-(T0*(G[8]-G[13])),G[13]=G[13]-Q0,R1=R1+Q0; GOSUB 7500
2180 REM "Postings for both
2181 Q8$=Q9$
2185 IF A[2]<>0 THEN Q0$=L$(82,12),Q0=A[2]; GOSUB 8700
2195 RETURN 
2200 REM "Handle file opens & closes for sales tax & commissions
2201 REM "P2=0 means open for checks, P2=1 means open for invoices
2210 IF P2=0 THEN Z$="03C ARK...  05C ARL...  06C ARM...  03O ZYB...  05O AR1...  06O AR0...  " ELSE Z$="03C ZYB...  05C AR1...  06C AR0...  03O ARK...  05O ARL...  06O ARM...  "
2220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 2221,9900
2250 RETURN 
2300 REM "Update sales tax and/or commissions to payable including G/L
2302 TERM$=""; READ (Z[25],KEY=G$(51,2),DOM=2303)TERM$; REM " SSP# 146930
2305 REM "Sales tax
2310 EXTRACT (Z[3],KEY=G$(1,18),DOM=2490)IOL=0380
2312 IF MID(TERM$,57,1)="Y" THEN GOTO 2400; REM " SSP# 146930
2315 IF P0$(88,1)<>"Y" AND POS("C"=Q9$)=0 THEN GOTO 2400
2330 DIM S$(47),S[9]
2340 EXTRACT (Z[5],KEY=A$(1,6)+K$(25,10)+K$(11,8),DOM=2341)IOL=0390; GOTO 2360
2350 S$=A$(1,6)+K$(25,10)+K$(11,8)+K$(1,10)+K$(19,6)+C0$,S[0]=G[3],S[2]=K[2]
2365 S[1]=S[1]+A[1],S0=0
2370 FOR I=3 TO 8
2371 IF G[3]-A[1]=0 THEN S1=K[I+2] ELSE S1=K[I+2]*T0
2372 S[I]=S[I]+S1,K[I+2]=K[I+2]-S1; IF FPT(I/2)=0 THEN S0=S0+S1
2375 NEXT I
2377 ST$=""; DIM ST[60],TEMP[1]; IF P0$(119,1)="Y" THEN ST$=L$(58,12)+L$(46,12)
2378 IF P0$(88,1)="Y" THEN GOTO 2380 ELSE IF POS("C"=Q9$)=0 THEN GOTO 2400 ELSE IF P0$(119,1)="Y" THEN CALL "AR2DUT",X3$,X4$,5,"",TEMP{ALL},K$,K{ALL},T0,S0,ST$,ST{ALL}; Q8$="C",Q0$=ST$(1,12),Q0=-S0; GOSUB 8700; Q8$=Q7$; GOTO 2395 ELSE Q8$="C",Q0$=L$(58,12),Q0=-S0; GOSUB 8700; Q8$=Q7$; GOTO 2395
2380 IF G$(168,1)<>"Y" THEN WRITE (Z[5],KEY=A$(1,6)+K$(25,10)+K$(11,8))IOL=0390; IF P0$(119,1)="Y" THEN CALL "AR2DUT",X3$,X4$,5,"",TEMP{ALL},K$,K{ALL},T0,S0,ST$,ST{ALL} END_IF ELSE CALL "AR2DUT",X3$,X4$,2,A$,A{ALL},G$,G{ALL},T0,S0,ST$,ST{ALL}
2382 WRITE (Z[3],KEY=G$(1,18))IOL=0380
2383 IF P0$(88,1)="Y" AND P1$(63,1)="Y" THEN CALL "AR2CUD",X3$,G$(133,6)+K$(25,10)+K$(11,8),A$(1,6)
2385 IF P0$(119,1)<>"Y" OR ST$="" THEN GOTO 2389
2386 NUM_CODES=LEN(ST$)/24-1
2387 FOR T3=0 TO NUM_CODES; Q0$=ST$(1,12),Q0=-ST[T3]; GOSUB 8700; IF POS("A"=Q9$)<>0 THEN Q8$="A",Q0$=ST$(13,12),Q0=ST[T3]; GOSUB 8700; Q8$=Q7$ END_IF ; IF ST$>"" THEN ST$=ST$(25); NEXT T3
2388 GOTO 2395
2389 IF POS("A"=Q9$)<>0 THEN Q8$="A",Q0$=L$(46,12),Q0=S0; GOSUB 8700; Q8$=Q7$
2390 Q0$=L$(58,12),Q0=-S0; GOSUB 8700
2395 R1=R1-S0
2400 REM "Commissions
2405 IF MID(TERM$,57,1)="Y" THEN GOTO 2490; REM " SSP# 146930
2410 IF P1$(32,1)<>"Y" THEN GOTO 2490
2412 IF P1$(33,1)="Y" AND G[3]-A[1]<>0 THEN GOTO 2490
2415 DIM L[6],M[35]
2416 L[0]=K[0],L[1]=K[1],L[2]=K[2],L[3]=K[3],L[4]=K[4],L[6]=G[11]; IF P1$(33,1)="Y" THEN L[5]=K[0] ELSE L[5]=A[1]
2417 IF (POS(X3$(9,3)="020113",3)<>0 OR P0$(127,1)="Y") AND P1$(33,1)="Y" AND (A[2]<>0 OR A[3]<>0 OR A[5]<>0) THEN L[5]=L[5]-A[2]-A[3]-A[5]; IF L[3]<>0 THEN L[4]=L[4]*(L[3]-A[2]-A[3]-A[5])/L[3]
2419 CALL "AR2XAA",X3$,L{ALL},0,6,M{ALL},K{ALL},11,15
2420 FOR I0=0 TO 4
2425 IF POS(" "<>K$(46+I0*4,4))=0 THEN GOTO 2480 ELSE K3$=A$(1,6)+K$(46+I0*4,4)+K$(1,18)+K$(35,5),I1=I0*7
2426 DIM R$(66),R[8]
2430 EXTRACT (Z[6],KEY=K3$,DOM=2431)IOL=0400; GOTO 2439
2433 R$=K3$+K$(19,6)+V0$(22,6)+C0$
2434 REM "Add Disc, frt or w/o allowed flags to end of R$
2435 IF A[2]<>0 THEN R$=R$+"D" ELSE R$=R$+" "
2436 IF A[3]<>0 THEN R$=R$+"F" ELSE R$=R$+" "
2437 IF A[5]<>0 THEN R$=R$+"W" ELSE R$=R$+" "
2438 R[0]=M[I1],R[1]=M[I1+1],R[2]=M[I1+3],R[3]=M[I1+2],R[4]=K[4],R[5]=K[11+I0],R[6]=M[I1+6]
2439 R[7]=R[7]+M[I1+5]
2440 IF G[3]-A[1]=0 THEN R8=M[I1+4],R[8]=R[8]+R8 ELSE R8=M[I1+4]*T0,R[8]=R[8]+R8
2445 R$(40,6)=V0$(22,6)
2470 WRITE (Z[6],KEY=K3$)IOL=0400
2471 IF POS(X3$(9,3)="020113",3)>0 OR P0$(127,1)="Y" THEN R8=K[4]
2472 K[4]=K[4]-R8; WRITE (Z[3],KEY=G$(1,18))IOL=0380
2475 Q0$=L$(190,12),Q0=-R8; GOSUB 8700
2476 IF POS("A"=Q9$)<>0 THEN Q8$="A",Q0$=L$(178,12),Q0=R8; GOSUB 8700; Q8$=Q7$
2477 IF POS("C"=Q9$)<>0 THEN Q8$="C",Q0$=L$(166,12),Q0=R8; GOSUB 7500; Q8$=Q7$
2480 NEXT I0
2490 RETURN 
5000 REM "END OF UPDATE
5002 IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" THEN IF D7$<>D8$ THEN CALL "CM2AAA",X3$,Z[1],3,D8$+D9$(1,10),D9$,0; D7$=D8$; REM "Update deposit header if needed
5005 P2=0; GOSUB 2200
5010 GOSUB 7650
5020 GOSUB 8770
5030 IF F0$>"" THEN GOSUB 5500
5100 REM "
5200 REM "CLEAR FILES
5205 PRINT @(0,10),'CE',
5210 CALL "ZZPROM","U7",X3$,0,"","","",10
5220 CALL "ZZINTZ",X3$,"B",0,STR(Z[1]:"00")+STR(Z[2]:"00")+STR(Z[15]:"00")+STR(Z[16]:"00")+STR(Z[17]:"00")
5230 GOSUB 6700
5240 PRINT 'CF'
5400 REM 
5450 GOTO 9900
5500 REM UPDATE BANK RECON FILE WITH DEPOSITS
5510 Z$="11C ARO... 11O BR0... "
5515 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 5516,9900
5520 READ (Z[1],KEY="",DOM=5521)
5530 K$=KEY(Z[1],END=5580); READ (Z[1],END=5580)IOL=0310
5532 IF X3$(330,1)="1" THEN K$=K$(3)
5535 IF LEN(K$)<>10 THEN GOTO 5570
5540 DIM J$(127),J[2]
5545 J$(1,3)=A$(7,3),J$(4,1)="2",J$(79,1)="N"
5550 J$(5,9)=V0$(22,6)+"000",J$(49,6)=A$(11,6)
5552 J$(14,35)="Cash receipts update "+V0$(22,4)+"."+V0$(26,2)+"-"+A$(10,1)
5554 J$(100,6)=A$(1,6)
5558 J$(11,3)=STR(J0:"000"),J0=J0+1
5560 WRITE (Z[11],KEY=J$(1,13)+J$(119,2),DOM=5558)J$,A[0]
5570 READ (Z[1],KEY=A$(1,10)+$FF$,DOM=5575)
5575 GOTO 5530
5595 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 CALL "ZZPROM","U9",X3$,0,"","","",4
6025 CALL "ZZPROM","U8",X3$,0,"","","",7
6030 PRINT " ",V0$(22,4),".",V0$(26,2),
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6225 GOSUB 6450
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6490 RETURN 
6600 REM "RESTART LOGIG
6605 RETURN 
6610 WRITE (Z[1],IND=I1)V0$,K0$,K1$,K0,K1
6690 RETURN 
6700 REM "Increment Audit Control No.
6705 F9=Z[13]
6710 A2$=X3$(146,6)+"  000100",A0$=X3$(9,3)+"AUDIT",A1$=""; EXTRACT (F9,KEY=A0$,DOM=6716)A0$,A1$
6715 A0=POS(A2$(1,6)=A1$,14); IF A0=0 THEN A1$=A1$+A2$; GOTO 6715 ELSE A2=NUM(A1$(A0+8,4))+1,A2$(9,4)=STR(A2:"0000"),A1$(A0,14)=A2$
6720 WRITE (F9,KEY=A0$)A0$,A1$
6740 RETURN 
7000 REM "Set up constants for ageing
7005 IF D2$=D3$ THEN RETURN ELSE D3$=D2$
7010 IF P0$(47,1)="Y" THEN GOTO 7090
7020 REM "age by months"
7030 A0$=D2$(1,4),A2=NUM(A0$(3)),A0=4
7040 FOR I=1 TO 4
7050 A1$=A0$(1,2),A3=A2-I; IF A3<1 THEN A3=A3+12; IF A1$(2,1)<>"0" THEN A1$(2,1)=CHR(ASC(A1$(2,1))-1) ELSE A1$=CHR(ASC(A1$(1,1))-1)+"9"
7060 A0$=A0$+A1$+STR(A3:"00")
7070 NEXT I
7080 RETURN 
7090 REM "Set aging if aging is days"
7100 A0$="",A0=6,A1$="      "
7110 FOR I=0 TO 4
7120 CALL "ZZDATE",X3$,"",D2$,A1$,-P[I],0,0,0,0,0,0,"","",""
7130 A0$=A0$+A1$
7140 NEXT I
7150 RETURN 
7200 REM "Age invoice and set A2= total to update and add invoice total to A2 bucket"
7203 REM "Setup to age
7205 D2$=E$(391,6); GOSUB 7000
7210 IF P0$(46,1)="Y" THEN D1$=G$(54,6) ELSE D1$=G$(20,6)
7215 IF G$(11,7)="ONACCNT" THEN A2=8; GOTO 7270
7220 FOR I=0 TO 4
7230 IF D1$(1,A0)>A0$(I*A0+1,A0) THEN A2=I+7; EXITTO 7270
7240 NEXT I
7250 REM "if still here then maximum age
7260 A2=12
7270 E[A2]=E[A2]-A[1]
7280 RETURN 
7500 REM "New salesperson substitution
7510 IF P0$(110,2)<"01" THEN GOTO 7530
7520 Q0$(NUM(P0$(110,2),ERR=7530),2)=G$(76,2)
7530 GOSUB 8700
7540 RETURN 
7600 REM "Change customer number
7605 REM "Check for existing customer to be written out
7610 IF E$(1,10)=A$(11,10) THEN GOTO 7645
7615 IF POS(" "<>E$(1,10))<>0 THEN GOSUB 7650
7620 REM "Get new customer
7625 DIM E$(599),E[15],F$(53),F[21]
7630 EXTRACT (Z[5],KEY=A$(11,10),DOM=7631)IOL=0350; GOTO 7635
7631 E$(1,10)=A$(11,10)
7635 EXTRACT (Z[6],KEY=A$(11,10),DOM=7636)IOL=0360; GOTO 7640
7636 F$(1,10)=A$(11,10)
7645 RETURN 
7650 REM "Write out customer records
7660 WRITE (Z[5],KEY=E$(1,10))IOL=0350
7670 WRITE (Z[6],KEY=F$(1,10))IOL=0360
7675 IF W9$<>"" THEN CUST$=F$(1,10),CUST_NO$=F$(1,10); CALL "RT2WOC",ERR=7676,X3$,X4$,CUST_NO$,"AR1...","U",CUST$; CALL "RT2WOC",ERR=7676,X3$,X4$,CUST_NO$,"AR0...","U",CUST$
7690 RETURN 
7700 REM "Update Sales Tax Report for Accrual Based Reporters Only"
7701 REM "W suffix to Invoice Number means 'Write-off'
7710 DIM S$(47),S[9]
7720 S$(1)=A$(1,6)+G$(117,10)+G$(11,8)+G$(1,10)+G$(20,6)+C0$
7725 S$(9,8)="w/o"
7730 EXTRACT (Z[5],KEY=S$(1,24),DOM=7731)IOL=0390
7740 S[6]=S[6]-A[4]
7750 WRITE (Z[5],KEY=S$(1,24))IOL=0390
7790 RETURN 
7890 RETURN 
7900 REM "Additional PFG Postings
7902 REM "Get data for this RL.  Company code stored in cust. record (431,3)
7903 IF A$(11,2)<"10" THEN RETURN 
7904 CALL "CM2ZAB",X3$,Z[13],E$(431,3),F0{ALL}
7905 REM "Get job totals to determine total net sale
7906 Q$=E$(431,3)+G$(66,8); A0$=C$(1,11); CALL "CM2NCZ",X3$,0,Q$,T{ALL},T$
7909 REM "Ad fund calculations
7910 REM "Pro-rate ad fund posting based on applied to original amount
7911 Q=100; IF T[0]<>0 THEN Q=100*A[1]/T[0]
7912 Q0$=F3$(55,12),Q0=(-F0[1])*(T[0]-T[4]-T[2])*Q/100/100; GOSUB 8650
7915 Q0=-Q0,Q0$=F3$(31,12); GOSUB 8650
7919 REM "Uncollected Franchise A/R
7920 Q0=A[1],Q0$=F3$(7,12); GOSUB 8650
7925 Q0=-Q0,Q0$=F3$(31,12); GOSUB 8650
7929 REM "Debit the advance account w/Write-offs
7930 Q0=A[1]-A[6],Q0$=F3$(31,12); GOSUB 8650
7934 REM "Offset revenue account by service fee of write-offs
7935 Q0=Q0*F0[0]/100,Q0$=F3$(19,12); GOSUB 8650
7940 Q0=-Q0,Q0$=F3$(31,12); GOSUB 8650
7949 REM "Offset DA fees for write offs
7952 Q0=Q0*F0[2]/100,Q0$=F3$(67,12),Q0=-Q0; GOSUB 8650
7955 Q0=-Q0,Q0$=F3$(79,12); GOSUB 8650
7960 IF A[5]<>0 THEN Q0=-A[5],Q0$=F3$(31,12); GOSUB 8650
7995 RETURN 
8000 REM "Setup entry for open invoice transaction
8010 DIM H$(46),H[2]
8020 H$(1)=G$(1,18)+"P"+C0$+A$(21,6)+"  "+V0$(22,6)+A$(1,6),H[1]=-A[1],H[2]=-A[2]
8025 IF MID(TERM$,47,1)="Y" THEN GOTO 8045; REM " SSP# 146930
8027 IF P1$(32,1)<>"Y" THEN GOTO 8045
8028 IF P1$(33,1)="Y" AND G[3]-A[1]<>0 THEN GOTO 8045
8029 IF E[3]-B[1]=0 THEN GOTO 8045
8030 FOR I0=0 TO 4
8035 M[I0*7+4]=M[I0*7+4]*T0
8040 NEXT I0
8090 RETURN 
8650 REM "Replace Source Company Code if needed
8660 P=POS("???"=Q0$); IF P>0 THEN Q0$(P,3)=E$(431,3)
8700 REM "G/L control postings accumulation
8710 REM "q0$=G/L account number, q0=amount
8715 FOR T2=1 TO LEN(Q8$)
8720 IF A$(1,6)<>T0$ THEN GOSUB 8770; T0$=A$(1,6)
8730 P=POS(Q8$(T2,1)+Q0$=T9$,13); IF P>0 THEN P=INT(P/13),O[P]=O[P]+Q0; GOTO 8755
8740 IF LEN(T9$)<T9*13 THEN T9$=T9$+Q8$(T2,1)+Q0$; GOTO 8730
8750 GOSUB 8770; GOTO 8730
8755 NEXT T2
8760 RETURN 
8770 REM "FLUSH G/L ACCUM - SUMMARY POST
8780 IF T9$="" THEN GOTO 8880
8790 FOR X=1 TO LEN(T9$) STEP 13
8800 P=INT(X/13)
8810 IF O[P]=0 THEN GOTO 8870
8815 GOSUB 8900
8820 U$=T9$(X,1)+FNV$(P2$(9,4))+T0$(5,2)+"CR"+V0$(22,6)+T9$(X+1,12)+"~~"+X3$(15,6)+"AR"
8830 U0$="Cash Receipts data entry"; DIM U[1]
8840 EXTRACT (Z[4],KEY=U$(1,27),DOM=8841)IOL=0420
8850 U[0]=U[0]+O[P]
8860 WRITE (Z[4],KEY=U$(1,27))IOL=0420
8870 NEXT X
8880 T9=14,T9$=""; DIM O[T9]
8890 RETURN 
8900 REM "Set end of period marker Q
8910 IF P2$(9,4)<>T0$(1,4) THEN P2$(1,13)=X3$(9,3)+"G/LYE"+T0$(1,4); CLOSE (Z); OPEN (Z)"ZZPARM"; READ (Z,KEY=P2$(1,12))P2$; CLOSE (Z)
8920 Q=NUM(P2$(13,2)),Q=15+Q*6
8945 RETURN 
8950 REM "Program flow logic
8960 DIM U2$(6); U2$(1)=FID(0); U2$=U2$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U2$,DOM=8990)U2$,U3$
8980 Q1=NUM(U3$(7,2)),Q1$=U3$(9,Q1),U3$=U3$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9700 REM "PROG FLOW
9710 IF U3$="" THEN REMOVE (Z[13],KEY=U2$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U2$)U2$,U3$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U3$(1,6)
9900 REM "END PROGRAM
9907 CALL "ZZDAPP",X3$,"AALL"
9908 IF Q1$<>"" THEN GOTO 9700
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 EXIT 
9999 END 
