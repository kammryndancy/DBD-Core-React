0010 ! ARGATI - Maintain Customer Multiple Finder Fee Setup
0035 REM "5.7 - 01/20/16 - 11.838611 - jvv - SSP# 282386
0037 REM "282386-Issue with Finder Fees setup.  When the value is modified   
0040 REM "Copyright 2016 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0052 IF TCB(88)=0 THEN {
0055 MSGBOX "You must be using Windx to access this program" } ELSE {
0060 CLEAR 
0065 SETERR 0070; ENTER X3$,X4$,MFF_KEY$
0070 ARG_1$=MFF_KEY$
0075 PROCESS "ARGATI","../ARG/AR.EN",ARG_1$
0078  }
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 X0=-1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0190 DIM Z[NUM(X3$(60,3))]
0200 ! Files
0210 Z$="01O ATI...  02O AR1...  03O AP4...  13O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOL_ATI:IOLIST ATI$,ATI[0],ATI[1],ATI[2]
0320 IOL_AR1:IOLIST AR1$
0350 DIM ATI[2]
0400 ! 
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0420 CTAB$=DIM(2)
0450 A$=MSG("SGCF"),B$=MSG("SGCF_R"),TLN=LEN(B$)
0455 DIM TP$[TLN],TPS$[TLN]
0460 FOR X=1 TO TLN
0465 P=POS("|"=A$),TP$[X]=A$(1,P-1),A$=A$(P+1)
0468 TPS$[X]=B$(X,1)
0470 NEXT X
0490 RETURN 
0499 ! 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 GOSUB CLEAR_REC
0530 IF NOT(NUL(ARG_1$)) THEN CUSTOMER$=ARG_1$; DISABLE CONTROL CUSTOMER.CTL; HIDE CONTROL BUTTON_8.CTL; GOTO FIND_CUSTOMER ELSE SHOW CONTROL BUTTON_8.CTL
0570 RETURN 
0599 ! 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
0999 ! 
1000 ! 1000 - Start of maintenance only code - Find/Add/Delete/Clear record
1010 FIND_CUSTOMER:
1015 IF NUL(CUSTOMER$) THEN GOTO CLEAR_REC
1020 CALL "ZZWLKU;Parse_cust",CUSTOMER$,CUST_DIV$,CUST_CODE$
1080 FIND (Z[2],KEY=CUST_DIV$+CUST_CODE$,DOM=NO_SUCH)IOL=IOL_AR1
1085 CUSTOMER$=CUST_DIV$+CUST_CODE$
1088 CUST_NAME$=AR1$(11,35)
1090 GOSUB LOAD_GRID
1110 EXIT 
1120 ! 
1150 NO_SUCH: MSGBOX "Client Code not found: "+CUSTOMER$+SEP+SEP+MSG("REC_NOTFND"),_FYI$,"!"; GOTO CLEAR_REC ! EXIT 
1199 ! 
1300 ! Delete selected entries
1304 DELETE_FF:
1308 MSGBOX MSG("TKGLMS_03"),MSG("CONFIRM"),"YESNO,2",ANS$
1312 IF ANS$<>"YES" THEN GOTO END_DELETE_ENTRIES
1315 DEL_CNT=0
1316 FOR I=1 TO FF_CNT
1320 FF_GRID.CTL'ROW=I
1324 READ DATA FROM FF_GRID.CTL'ROWDATA$ TO IOL=FF_GRID.CTL'LOADIOLIST$
1328 IF C_DELETE$="1" THEN {
1330 ! SGBOX "Deleting entry "+STR(I)+", key="+CUSTOMER$+C_SEQ$
1335 FFDKEY$=CUSTOMER$+C_SEQ$
1344 REMOVE (Z[1],KEY=FFDKEY$)
1350 DEL_CNT=DEL_CNT+1
1356  }
1360 NEXT I
1370 IF DEL_CNT=0 THEN MSGBOX ("No Entries selected to delete"); RETURN 
1380 END_DELETE_ENTRIES:
1385 GOSUB FIND_CUSTOMER
1395 RETURN 
1399 ! 
1400 WRITE_REC:
1495 RETURN 
1499 ! 
1600 CLEAR_FF_FIELDS:
1610 FIND_FEE_TYPE$="",VENDOR$="",VEND_NAME$=""
1620 FIND_FEE_RATE=0
1630 DROP_BOX WRITE FIND_FEE_TYPE.CTL,""
1640 REFRESH_FLG=1
1650 RETURN 
1659 ! 
1800 ! 1800 - Clear record
1810 CLEAR_REC:
1820 CUSTOMER$="",CUST_NAME$=""
1825 GOSUB CLEAR_FF_FIELDS
1830 GRID LOAD FF_GRID.CTL,0,0,""; FF_CNT=0,FF_TIME=0
1840 BT_SAVE.CTL'ENABLED=0,BT_DELETE.CTL'ENABLED=0
1850 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1,CHANGE_FLG=0
1860 RETURN 
1899 ! 
1900 CHECK_CHANGES:
1910 IF NOT(CHANGE_FLG) OR NOT(FF_CNT) THEN RETURN 
1920 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
1930 IF ANS$="YES" THEN GOSUB WRITE_REC
1945 RETURN 
1949 ! 
2000 LOAD_GRID:
2004 FF_GRID.CTL'VISIBLE=0
2005 GRID LOAD FF_GRID.CTL,0,0,""; FF_CNT=0,FF_TIME=0
2010 GR=FF_GRID.CTL
2015 READ (Z[1],KEY=CUSTOMER$,DOM=*NEXT)
2020 NEXT_REC:
2030 FFK$=KEY(Z[1],END=END_LOAD_CUSTOMER); READ (Z[1],KEY=FFK$)IOL=0310
2045 IF MID(FFK$,1,10)<>CUSTOMER$ THEN GOTO END_LOAD_CUSTOMER
2050 FFT$=MID(ATI$,14,1),GR_TYPE$=TBL(POS(FFT$=B$),"",TP$[1],TP$[2],TP$[3],TP$[4],TP$[5],TP$[6])
2055 FIND (Z[3],KEY=MID(ATI$,15,10),DOM=*NEXT)FF_VEND$
2060 VEND_NAME$=MID(FF_VEND$,11,35)
2065 GR_VEND$=FN%ZZDISP$(MID(ATI$,15,10),"A/P")
2070 GRID LOAD FF_GRID.CTL,1,0,SEP+GR_TYPE$+SEP+GR_VEND$+SEP+VEND_NAME$+SEP+STR(ATI[0]:"####0.00")+SEP+MID(ATI$,11,3)+SEP+MID(ATI$,15,10)+SEP; ++FF_CNT
2100 GOTO NEXT_REC
2120 END_LOAD_CUSTOMER:
2125 DIM ROWCHANGED[FF_CNT]
2130 ! F FF_CNT THEN GRID LOAD FF_GRID.CTL,1,0,SEP
2135 FF_GRID.CTL'VISIBLE=1
2140 REFRESH_FLG=1
2150 RETURN 
2199 ! 
3000 ! Add new Finder Fee record
3010 ADD_NEW_FF:
3020 DIM ATI$(125); FF_SEQ=0
3030 ATI$(1,10)=CUSTOMER$
3035 ATI$(14,1)=FIND_FEE_TYPE$
3040 ATI$(15,10)=VENDOR$
3050 ATI[0]=FIND_FEE_RATE
3060 ADD_SEQ_NUM:
3065 FF_SEQ=FF_SEQ+1; FF_SEQ$=STR(FF_SEQ:"000")
3070 ATI_KEY$=CUSTOMER$+FF_SEQ$
3075 ATI$(11,3)=FF_SEQ$
3080 WRITE (Z[1],KEY=ATI_KEY$,DOM=ADD_SEQ_NUM)IOL=IOL_ATI
3092 GOSUB LOAD_GRID
3093 GOSUB CLEAR_FF_FIELDS
3095 RETURN 
3099 ERR_EXISTS: MSGBOX MSG("DRGQRF_01"),_FYI$,"!,TIM=3"; RETURN 
3199 ! 
5000 REWRITE_FF_SEQ:
5010 ! EAD DATA FROM GR'ROWDATA$ TO IOL=GR'LOADIOLIST$
5030 WR_KEY$=CUSTOMER$+C_SEQ$
5040 ATI$(1,13)=WR_KEY$
5050 ATI$(14,1)=GR_TYPE$
5060 ATI$(15,10)=VEND_DIV$+VEND_CODE$
5070 ATI[0]=NUM(C_RATE$)
5100 WRITE (Z[1],KEY=WR_KEY$)IOL=IOL_ATI; RETURN 
5120 RETURN 
5999 ! 
8100 ROW_CHANGED:
8110 CURROW=TKGRID.CTL'ROW
8120 IF CURROW>0 AND CURROW<=TK_CNT THEN ROWCHANGED[CURROW]=1
8125 TKGRID.CTL'COLUMN=-1,TKGRID.CTL'VALUE$="X",BT_SAVE.CTL'ENABLED=1
8140 NEXT_ID=TKGRID.CTL; REFRESH_FLG=1
8145 RETURN 
8149 ! 
9999 END 
10099 ! 
11000 CONV_TYPEO:
11010 FFT$=C_TYPE$
11020 SWITCH FFT$
11030 CASE "Sales"
11040 GR_TYPE$="S"; BREAK
11050 CASE "Gross Profit"
11055 GR_TYPE$="G"; BREAK
11060 CASE "Cost"
11065 GR_TYPE$="C"; BREAK
11070 ! ASE "Adj GP"
11075 ! GR_TYPE$="A"; BREAK
11080 CASE "Fixed Amount"
11085 GR_TYPE$="F"; BREAK
11090 END SWITCH 
11095 RETURN 
11099 ! 
11100 CONV_TYPE:
11110 FFT$=C_TYPE$
11120 SWITCH FFT$
11130 CASE TP$[1] ! "Sales"
11140 GR_TYPE$=TPS$[1]; BREAK
11150 CASE TP$[2] ! "Gross Profit"
11155 GR_TYPE$=TPS$[2]; BREAK
11160 CASE TP$[3] ! "Cost"
11165 GR_TYPE$=TPS$[3]; BREAK
11170 ! CASE TP$[4] ! "Adj GP"
11175 ! R_TYPE$=TPS$[4]; BREAK
11180 CASE TP$[4] ! "Fixed Amount"
11185 GR_TYPE$=TPS$[4]; BREAK
11188 DEFAULT 
11189 GR_TYPE$=" "
11190 END SWITCH 
11195 RETURN 
11199 ! 
12000 PRE_FFEE_TYPE:
12005 GR'ROW=GR'CURRENTROW,GR'COLUMN=GR'CURRENTCOLUMN
12010 GR'TEXT$=MSG("SGCF")
12090 RETURN 
12099 ! 
12100 SELECT_FFEE_TYPE:
12110 CUR_ROW=GR'ROW
12115 CUR_COL=GR'COLUMN
12150 GR'CURRENTROW=CUR_ROW,GR'CURRENTCOLUMN=CUR_COL ! NEXT_ID=GR
12155 READ DATA FROM GR'ROWDATA$ TO IOL=GR'LOADIOLIST$
12159 TMP_ROW=CUR_ROW
12170 GOSUB UPD_ROW
12190 RETURN 
12198 ! 
12199 ! 
12200 PRE_FFEE_VEND:
12210 ! R'BITMAP$="!lookup"
12250 RETURN 
12299 ! 
12300 SELECT_FFEE_VEND:
12305 CUR_ROW=GR'ROW ! SSP 282386
12310 CUR_COL=GR'COLUMN ! SSP 282386
12320 QRY_VEND$=""; PROCESS "QRY_AP4.1","../APG/AP.EN",QRY_VEND$
12330 IF NUL(QRY_VEND$) THEN RETURN 
12340 C_FFVEND$=MID(QRY_VEND$,1,10)
12350 C_VENDOR_NAME$=MID(QRY_VEND$,11,35)
12370 GR'VALUE$=C_FFVEND$
12380 C_VENDOR$=C_FFVEND$
12381 GR'COLUMN=7,GR'VALUE$=C_VENDOR$,GR'COLUMN=CUR_COL ! SSP 282386
12382 C_VEND$=C_VENDOR$ ! SSP 282386
12385 GOSUB UPD_ROW
12390 RETURN 
12399 ! 
12500 SELECT_FFEE_RATE:
12510 CUR_ROW=GR'ROW
12515 CUR_COL=GR'COLUMN
12520 GR'CURRENTROW=CUR_ROW,GR'CURRENTCOLUMN=CUR_COL; NEXT_ID=GR
12530 READ DATA FROM GR'ROWDATA$ TO IOL=GR'LOADIOLIST$
12540 GOSUB UPD_ROW
12550 RETURN 
12590 ! 
15000 UPD_ROW:
15010 GR'ROW=GR'CURRENTROW,GR'COLUMN=GR'CURRENTCOLUMN
15020 CUR_ROW=GR'ROW,CUR_COL=GR'COLUMN
15030 READ DATA FROM GR'ROWDATA$ TO IOL=GR'LOADIOLIST$
15035 GOSUB CONV_TYPE
15036 CALL "ZZWLKU;Parse_VEND",C_VEND$,VEND_DIV$,VEND_CODE$ ! SSP 282386
15040 GOSUB REWRITE_FF_SEQ
15050 GOSUB LOAD_GRID
15080 GR'CURRENTROW=GR'ROW,GR'CURRENTCOLUMN=CUR_COL+1; NEXT_ID=GR
15090 RETURN 
56000 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
56002 REM "282386-Issue with Finder Fees setup.  When the value is modified   
