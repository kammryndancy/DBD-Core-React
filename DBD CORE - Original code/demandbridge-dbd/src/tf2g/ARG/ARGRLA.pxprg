0010 REM "Commissions booking report <AR2RLA>
0035 REM "4.4 - 02/03/99 - 9.18 - tma - SSP# 108555
0040 REM "Copyright 1999 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2RLA",X1$="Commissions Booking Report"
0120 DIM A$(52),A[20],B$(244),C$(397),C[15],D$(139),D[14],E$(124),E[6],P2$(50),F$(66),F[16]
0140 Z0$="#####,###.00-",Z1$="####,###.00-",Z2$="###,###.00-",Z3$="###.00-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0210 T=1,T0=0,T1=6
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR ARM - Commissions payable
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9]
0315 REM "IOLIST FOR AR3 - Salesperson Masterfile
0320 IOLIST B$
0325 REM "IOLIST FOR AR1 - Customer Masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0335 REM "IOLIST FOR AR6 - Open Invoice Header File
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14]
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6]
0355 REM "IOLIST FOR ARK"
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14],F[15]
0400 IOLIST K$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O ARY...  02O AR3...  03O AR1...  04O AR6...  05O AR8...  06O ARK...  13O ZZPARM  "
0515 V1$="ARY..."
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0550 FIND (Z[13],KEY=X3$(9,3)+"AR1",DOM=9800)P1$
0560 FIND (Z[13],KEY=X3$(9,3)+"A/R")P0$
0570 DIM OP_SEC$(255); FIND (Z[13],KEY="osec"+X3$(40,3),DOM=0571)OP_SEC$
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[6]; U=33,U0=6,U[0]=0,U[1]=4,U[2]=2,U[3]=4,U[4]=10,U[5]=8,U[6]=5,T0=3,V1=6,V2=4,V3=10,U0$="YY  ",T5$="S   AAR3AA/R    ",U5$="Fiscal Year",U6$="Salesperson",U7$="Customer"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(Z[1],END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1165 IF Q5=1 THEN PRINT @(25,21),U$,; REMOVE (Z[1],KEY=U$,DOM=1000); GOTO 1000
1300 REM "Read other data
1320 DIM B$(LEN(B$)); FIND (Z[2],DOM=1321,KEY=A$(7,4))IOL=0320
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(11,10))IOL=0330
1340 DIM D$(LEN(D$)); FIND (Z[4],DOM=1341,KEY=A$(11,10)+A$(21,8))IOL=0340; GOTO 1342
1341 DIM E$(LEN(E$)); FIND (Z[5],DOM=1342,KEY=A$(11,10)+A$(21,8))IOL=0350; D$(66,8)=E$(53,8)
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 REM 
1440 GOSUB 6200
1450 GOSUB 6300
1490 T=0
1500 REM "Print Data
1510 IF W+1>W0 THEN GOSUB 6000
1515 CALL "ZZDISP","AX",A$(21,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1520 IF X3$(69,1)<>"Y" THEN Y5$(11)=FND$(A$(34,6)) ELSE CALL "ZZDISP","DX",A$(34,6),"   ",X3$,X$,"",0,0,X4$; Y5$(11)=X$
1525 Y5$(22)=D$(66,8)
1532 Y5$(43)=A$(40,4)+"."+A$(44,2)
1533 IF LEN(A$)<52 OR A$(52,1)<>"Y" THEN IF P1$(62,1)="Y" THEN A[2]=A[2]+D[14]
1534 IF A[5]<>100 AND A[5]<>0 THEN GOTO 2000
1535 Y5$(51)=STR(A[0]:Z1$),Y5$(64)=STR(A[1]:Z1$),Y5$(77)=STR(A[2]:Z2$)
1540 IF A[1]<>0 THEN Y5$(89)=STR(A[2]*100/A[1]:Z3$)
1545 Y5$(107)=STR(A[6]:Z2$); IF P1$(32,1)="Y" THEN IF A[7]=1 THEN Y5$(120)=STR(A[8]:Z2$)+"*" ELSE Y5$(120)=STR(A[8]:Z2$)
1550 IF V0$(75,1)="Y" THEN Y5$(77,12)="",Y5$(89+LEN(Z3$))=""
1555 GOSUB 7300
1800 REM "Print Commission % if desired
1810 IF V0$(74,1)="Y" THEN IF A[1]<>0 THEN Y5$(111)=STR(A[6]*100/A[1]:Z3$),Y5$(124)=STR(A[8]*100/A[1]:Z3$); GOSUB 7300
1820 IF V0$(74,1)="G" THEN IF A[2]<>0 THEN Y5$(111)=STR(A[6]*100/A[2]:Z3$),Y5$(124)=STR(A[8]*100/A[2]:Z3$); GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[0],T[T0,2]=T[T0,2]+A[1],T[T0,3]=T[T0,3]+A[2],T[T0,4]=T[T0,4]+A[6],T[T0,5]=T[T0,5]+A[7],T[T0,6]=T[T0,6]+A[8]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print a split invoice
2005 DIM D$(139),D[14]
2010 FIND (Z[4],KEY=A$(11,18),DOM=2011)IOL=0340
2020 Y5$(51)=STR(D[2]:Z1$),Y5$(64)=STR(D[9]:Z1$),Y5$(77)=STR(D[10]:Z2$),Y5$(107)=STR(D[11]:Z2$)
2023 IF D[9]<>0 THEN Y5$(89)=STR(D[10]*100/D[9]:Z3$)
2024 Y5$(107)=STR(A[6]:Z2$)
2025 GOSUB 7300
2035 Y5$(51)=STR(A[0]:Z1$),Y5$(64)=STR(A[1]:Z1$),Y5$(77)=STR(A[2]:Z2$)
2040 Y5$(99)=STR(A[5]:Z3$)
2045 IF P1$(32,1)="Y" THEN IF A[7]=1 THEN Y5$(120)=STR(A[8]:Z2$)+"*" ELSE Y5$(120)=STR(A[8]:Z2$)
2080 GOSUB 7300
2090 GOTO 1800
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$(1,4)+" Period "+U1$(5,2); GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5010 IF Q5=1 THEN GOTO 9920
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5210 T9=1
5290 GOTO 9900
5300 REM "Purge records
5305 IF OP_SEC$(66,1)="Y" THEN GOTO 9920; REM "WO102518, don't ask clear ?
5310 CALL "ZZPROM","X0AR2RHA",X3$,Z,"","","",0; ON Z GOTO 5311,9920
5317 PRINT @(0,20),'CE',@(23,20),"Now clearing:",
5360 Q5=1; GOTO 0900
6000 REM "Page header
6020 GOSUB 8000
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(1)="Invoice",Y5$(12)="Invoice",Y5$(24)="Order",Y5$(44)="Audit",Y5$(54)="Invoice",Y5$(69)="Net",Y5$(80)="Gross",Y5$(105)="Total Invoice"; IF P1$(32,1)="Y" THEN Y5$(120)="Salespersons"
6117 IF V0$(75,1)="Y" THEN Y5$(80)=""
6120 GOSUB 7300
6125 Y5$(2)="Number",Y5$(14)="Date",Y5$(24)="Number",Y5$(43)="Control",Y5$(55)="Total",Y5$(67)="Amount",Y5$(80)="Profit",Y5$(90)="G.P.%",Y5$(100)="Split  Commission",Y5$(121)="Commission"; IF P0$(113,1)="Y" THEN Y5$(80,6)="Margin",Y5$(92,1)="M"
6127 IF V0$(75,1)="Y" THEN Y5$(80)="",Y5$(90)="G.P.%"; IF P0$(113,1)="Y" THEN Y5$(92,1)="M"
6130 GOSUB 7300
6140 IF V0$(74,1)="Y" THEN Y5$(108)="/ Comm %"; IF P1$(32,1)="Y" THEN Y5$(122)="/ Comm %"; GOSUB 7300 ELSE GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6210 IF W+2>W0 THEN GOSUB 6000
6220 Y5$(1)=A$(7,4),Y5$(7)=B$(5,35)
6225 GOSUB 7300
6299 RETURN 
6300 REM "Break #2 header
6310 IF W+2>W0 THEN GOSUB 6000
6320 CALL "ZZDISP","AX",A$(11,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(6)=X$
6325 Y5$(19)=C$(11,35)
6330 GOSUB 7300
6399 RETURN 
6400 REM "Break #3 header
6499 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(50)=T1$(1,13),Y5$(63)=T1$(1,13),Y5$(76)=T1$(1,12),Y5$(106)=T1$(1,12); IF P1$(32,1)="Y" THEN Y5$(119)=T1$(1,13)
7006 IF V0$(75,1)="Y" THEN Y5$(76)=""
7008 GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(50)=STR(T[T,1]:Z0$),Y5$(63)=STR(T[T,2]:Z0$),Y5$(76)=STR(T[T,3]:Z1$),Y5$(106)=STR(T[T,4]:Z1$); IF P1$(32,1)="Y" THEN Y5$(119)=STR(T[T,6]:Z1$)
7025 IF T[T,2]<>0 THEN Y5$(89,LEN(Z3$))=STR(T[T,3]*100/T[T,2]:Z3$)
7028 IF V0$(75,1)="Y" THEN Y5$(76,13)="",Y5$(89+LEN(Z3$))=""
7030 GOSUB 7300
7040 IF V0$(74,1)="Y" THEN IF T[T,2]<>0 THEN Y5$(111)=STR(T[T,4]*100/T[T,2]:Z3$),Y5$(124)=STR(T[T,6]*100/T[T,2]:Z3$)
7065 IF T$="END" THEN Y5$(50)=T2$(1,13),Y5$(63)=T2$(1,13),Y5$(76)=T2$(1,12),Y5$(106)=T2$(1,12); IF P1$(32,1)="Y" THEN Y5$(119)=T2$(1,13); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1),W1$=W3$
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
8000 REM "PRINT P/E DATE"
8005 DIM A0$(6); IF T<2 THEN A0$(1,6)=A$(1,6) ELSE A0$(1,6)=U1$(1,6)
8010 IF P2$(9,4)<>A0$(1,4) THEN DIM P2$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A0$(1,4),DOM=8060)P2$
8020 D1$=P2$(NUM(A0$(5,2))*6+15,6),D0$=D1$
8030 W0$="For Fiscal year "+A0$(1,4)+", period "+A0$(5,2)+" ending "+FND$(D0$)
8040 REM "LET Y5$(INT(W3/2)-INT(LEN(J5$)/2))=J5$
8050 REM "GOSUB 7300
8060 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF T9=1 AND V3$(2,1)<>"T" THEN GOTO 5300
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 ! IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 EXIT 
9999 END 
