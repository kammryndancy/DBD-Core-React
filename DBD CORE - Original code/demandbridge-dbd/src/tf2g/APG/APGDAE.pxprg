0010 REM "Get amounts from PO information <APGDAE>
0020 SETERR 9000
0035 REM "5.4 - 03/10/06 - 18.676111 - kmc - SSP# 190474
0037 REM "190474-Additional Services - Enhance TF to provide for tax codes   
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 SETERR 9000
0110 ENTER X3$,A$,A{ALL},T9$,T{ALL},Q3$,%G8,LINE_FRT
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0370 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24]
0380 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8]
0390 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12]
0503 GOSUB SETUP_MESSAGES
0510 ! Z$="01O FS1...  02O APE...  06O ZZPARM  09O PO7...  10O POA...  13O PO1...  15O PO2...  "
0525 _FILE_NAME$="FS1"+%C$,_FS1=HFN; OPEN (_FS1,ERR=OPEN_ERR)_FILE_NAME$
0530 _FILE_NAME$="APL"+%C$,_APL=HFN; OPEN (_APL,ERR=OPEN_ERR)_FILE_NAME$
0535 _FILE_NAME$="ZZPARM",_ZZPARM=HFN; OPEN (_ZZPARM,ERR=OPEN_ERR)_FILE_NAME$
0540 _FILE_NAME$="PO7"+%C$,_PO7=HFN; OPEN (_PO7,ERR=OPEN_ERR)_FILE_NAME$
0545 _FILE_NAME$="POA"+%C$,_POA=HFN; OPEN (_POA,ERR=OPEN_ERR)_FILE_NAME$
0550 _FILE_NAME$="PO1"+%C$,_PO1=HFN; OPEN (_PO1,ERR=OPEN_ERR)_FILE_NAME$
0555 _FILE_NAME$="PO2"+%C$,_PO2=HFN; OPEN (_PO2,ERR=OPEN_ERR)_FILE_NAME$
0600 READ (_ZZPARM,KEY=%C$+"A/R",DOM=0605)F0$
0610 READ (_ZZPARM,KEY=%C$+"A/P")F1$
1000 REM "
1005 DIM Y[4]
1010 GOSUB 7200; GOTO 9900
5900 REM "REMOVE LINES
5910 IF A0=0 THEN GOTO 5990
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0290
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0290
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0290
5955 GOTO 5990
5960 IF ERR=0 THEN RETRY ELSE GOTO 9000
5990 RETURN 
7200 REM "Get amount if P/O receipt
7205 IF Q3$="" THEN RETURN ELSE DIM H[12],I[9]
7206 T9$="",H9$="",T5$="",T9=1; DIM T[20]
7207 A0=A[0],F=_APL; GOSUB 5900; A0=0,A[0]=0
7208 A[1]=0,A[9]=0,A[2]=0,A[11]=0,LINE_FRT=0
7209 READ (_PO1,KEY=Q3$(1,9),DOM=7240)W8$
7210 GOSUB 8000
7212 READ (_POA,KEY=Q3$(10,10),DOM=7213)
7215 K$=KEY(_POA,END=7240); READ (_POA)
7220 IF K$(1,10)<>Q3$(10,10) THEN GOTO 7240
7223 IF W9$="" OR K$(11,9)<>W9$(297,9) THEN T5$=""; READ (_PO1,KEY=K$(11,9),DOM=7235)W9$; REM " IF W9$(7,10)<>W8$(7,10) THEN GOTO 07235
7224 IF W9$(7,10)<>W8$(7,10) THEN GOTO 7235
7225 READ (_PO7,KEY=K$(11,9)+K$(1,10)+K$(20,4),DOM=7215)IOL=0390
7226 IF K$(23,1)=" " THEN FOR X=0 TO 8; I[X]=H[X]; NEXT X; REC=H[0],INV=H[4]; REM "SSP117713
7227 IF STP(H$(69,24),3," ")="" THEN GOTO 7228 ELSE GOSUB 7300; REM "Process alt g/l codes ssp#111482
7228 IF H$(1,9)<>W8$(297,9) THEN READ (_PO1,KEY=H$(1,9),DOM=7229)W8$; GOSUB 8000
7229 IF K$(23,1)<>" " THEN GOSUB 8500; GOTO 7235
7230 A[1]=A[1]+H[6],A[9]=A[9]+H[6],LINE_FRT=LINE_FRT+H[9]
7231 IF T5$<>"*" THEN T5=NUM(H$(51,11),ERR=7232); IF T5<>0 THEN T5$="*",A[2]=A[2]+T5,A[1]=A[1]+T5
7232 GOSUB 8400
7233 A[3]=H[10],A[4]=H[11],A[5]=H[12]
7235 GOTO 7215
7240 REM DIM Q$(11); IF POS(" "<>Q3$(1,9))<>0 THEN CALL "ZZDISP","AX",Q3$(1,9),"P/O",X3$,Q$,"","","",X4$; LET A$(47,15)="P/O "+Q$
7241 A$(62,10)=Q3$(1,9)
7243 IF F1$(76,1)="Y" OR POS(%C$="118246338",3)>0 THEN A$(75,9)=Q3$(1,2)+"0000000"
7244 IF POS(%C$="112",3)>0 THEN IF Q3$(9,1)=" " THEN A$(75,9)="999000000",A$(72,3)="S"
7245 RETURN 
7300 REM "Post Alternate G/L Amounts, the offsetting posting will be done in 8400's
7305 FOR GLX=69 TO 81 STEP 12
7310 IF STP(H$(GLX,12),3," ")="" THEN GOTO 7335 ELSE H9$=H$(GLX,12),GL_AMT=H[(GLX-57)/12+6]
7320 P=POS(H9$=T9$); IF P=0 THEN T9$=T9$+H9$,T[T9]=GL_AMT,T9=T9+1 ELSE P=INT(P/12)+1,T[P]=T[P]+GL_AMT
7335 NEXT GLX
7345 RETURN 
8000 REM "GET SALESPERSON
8010 IF POS(" "<>W8$(306,8))=0 THEN GOTO 8050
8015 IF F1$(70,1)<>"Y" AND F0$(13,1)<>"S" AND F0$(110,2)<"01" THEN GOTO 8050
8020 DIM A5$(97,"0"); FIND (_FS1,KEY=W8$(306,8),DOM=8050)A5$
8030 IF F1$(70,1)="Y" THEN A$(100,4)=A5$(93,4)
8050 REM BUILD COMMENT
8060 IF POS(" "<>W8$(248,12))>0 THEN IF POS("    "=W8$(248,12))>0 THEN A$(47,15)="Job "+W8$(248,12) ELSE A$(47,15)="Jb "+W8$(248,12)
8090 RETURN 
8400 REM "Put COS acct # on T9$ and into T(), if exists in T9$ use it else use T(T9) and increment T9
8405 H9$=H$(27,12)
8406 IF STP(H9$,3," ")="" THEN GOTO 8440
8409 REM "Do A/R division substitution - or Salesperson
8410 IF W9$(173,1)<>"C" OR POS(W9$(351,1)="BS")>0 OR (H$(62,1)="Y" AND F0$(121,1)<>"Y") THEN GOTO 8420 ELSE IF %G8<1 THEN GOTO 8417
8412 IF F0$(13,1)="Y" AND Q3$(42,2)<>"  " THEN H9$(%G8,2)=Q3$(42,2)
8415 IF F0$(13,1)="S" THEN H9$(%G8,2)=STR(NUM(A5$(95,2),ERR=8416):"00")
8417 IF F0$(110,2)>"00" THEN H9$(NUM(F0$(110,2)),2)=STR(NUM(A5$(95,2),ERR=8418):"00")
8420 P=POS(H9$=T9$)
8425 IF P=0 THEN T9$=T9$+H9$,T[T9]=H[6]-H[7]-H[8],T9=T9+1; IF H$(23,1)=" " THEN P8=T9-1,P9=H[6]-H[7]-H[8],P9$=H9$; GOTO 8440 ELSE GOTO 8440
8430 P=INT(P/12)+1,T[P]=T[P]+H[6]-H[7]-H[8]; IF H$(23,1)=" " THEN P8=P,P9=H[6]-H[7]-H[8],P9$=H9$
8440 RETURN 
8500 REM "Subtract off old amount P9 in P9$,first time only (P9$<>""), and run through special shipping, adding amounts in
8505 IF P9$<>"" THEN T[P8]=T[P8]-P9,P9$=""
8509 REM "LET H[6]=H[2]
8510 DIM B[24]; FIND (_PO2,KEY=K$(11,9)+K$(20,3),DOM=8511)IOL=0370
8520 IF REC<>0 AND REC<>INV AND H$(93,1)<>"Y" THEN H[6]=P9; GOTO 8526 ELSE IF H$(93,1)<>"Y" THEN H[6]=H[0]*I[5]/B[2]; REM "SSP117713, add 93,1 check for SSP132195, CIG Plus, need to use already calculated amounts
8525 H[7]=I[7],H[8]=I[8]; REM "Sub from header records, there should be only 1 ship to for B type order
8585 GOSUB 8400
8590 RETURN 
9000 REM "Error processing
9005 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9010 EXIT ERR
9200 SETUP_MESSAGES:
9210 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9220 _MSG_DIRECTORY$=MSG("DIRECTORY")
9230 _MSG_PREFIX$=MSG("PREFIX")
9240 SETUP_MESSAGES_END:RETURN 
9245 ! 
9250 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
9255 CMD_STR$="E"
9260 EXIT 
9265 ! 
9800 CLOSE_FILES:
9825 IF _FS1 THEN CLOSE (_FS1)
9830 IF _APL THEN CLOSE (_APL)
9835 IF _ZZPARM THEN CLOSE (_ZZPARM)
9840 IF _PO7 THEN CLOSE (_PO7)
9845 IF _POA THEN CLOSE (_POA)
9850 IF _PO1 THEN CLOSE (_PO1)
9855 IF _PO2 THEN CLOSE (_PO2)
9890 CLOSE_FILES_END:RETURN 
9895 ! 
9900 REM "END PROGRAM
9910 GOSUB CLOSE_FILES
9950 EXIT 
9999 END 
