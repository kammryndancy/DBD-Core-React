0010 REM "Update OS1 RECORDS  <OS2UAA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 03/06/07 - 14.890555 - jvv - SSP# 203458
0037 REM "203458-Addition of S.P. Richards to TOPS module                    
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0095 F$="CALLED",FF$=F$
0100 IF %GUI=0 THEN SETERR 9000
0110 X0$="OS2UAA",X1$="Price Update - Office Supplies"
0120 DIM Z0$(80,"-")
0125 M0$="#######.00"
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 IF %GUI=0 THEN IF X3$="" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0299 IF %GUI=0 THEN CALL "ZZ2PRP","OS",RESULT$,DATE$; IF RESULT$<>"Y" THEN CALL "ZZPROM",".4",X3$,Z,"ACCESS DENIED! The Office Supply Interface System is not an i n stalled module.","","",0; RUN "ZMENU"
0300 REM "IOLISTS
0320 IOLIST OS0$
0321 IOLIST OS1$
0322 IOLIST OS5$,OS5[0],OS5[1],OS5[2],OS5[3],OS5[4],OS5[5],OS5[6],OS5[7],OS5[8],OS5[9]
0325 IOLIST OS7$
0400 REM "
0410 DIM OS5$(65),OS5[10]
0500 REM "FILES
0505 IF %GUI=0 THEN DIM Z[NUM(X3$(60,3))]
0510 Z$="02O OS0...  03O OS1...  04O OS5...  05O OS7...  06OSOS5...  13O ZZPARM  " ! ssp# 197544  
0520 IF %GUI=0 THEN CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],"",M6{ALL},"",0,"",0
0555 IF %GUI THEN GOSUB 8100; GOTO 0700
0560 IF FF$="CALLED" THEN GOSUB 8100; GOTO 0700
0585 RT$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT$
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 I$=Q0$
0750 READ (Z[13],KEY=X3$(9,3)+"OSP")OSP$
0760 SPFX$=OSP$(218,1); IF SPFX$="" THEN SPFX$="U"
1000 REM "
1010 GOSUB 4000; REM " LOAD THE OS5 ARRAY
1015 X_MAX=LEN(OS5_ARRAY$)/OS5_CHUNK-1 ! ssp# 198019
1020 IF X_MAX<0 THEN GOTO 5000 ! ssp# 198019
1040 OS0_KEY$=KEY(Z[2],END=5000)
1050 READ (Z[2],KEY=OS0_KEY$)IOL=0320
1051 C=C+1
1055 FOR X=0 TO X_MAX ! ssp# 198019
1056 OS5$=OS5_ARRAY$(X*OS5_CHUNK+1,OS5_CHUNK) ! ssp# 198019
1075 DIM OS1$(250); READ (Z[3],KEY=OS0$(1,10)+OS5$(1,6),DOM=1076)IOL=0321; GOTO 1200
1080 OS1$(1,10)=OS0$(1,10),OS1$(11,6)=OS5$(1,6),OS1$(25,12)=OS5$(18,12)
1200 REM " FOUND THE RECORD SO UPDATE ONLY PRICE
1201 REM "IF OS1$(17,1)<>"L" THEN GOTO 2000
1203 OS1$(25,12)=OS5$(18,12); REM SSP 204616
1205 IF OS5$(31,6)<>DIM(6) THEN GOSUB 3000 ELSE GOSUB 3500
1210 BASE_PRICE=NUM(OS0$(204,10))
1215 RATE=NUM(OS5$(8,10))/100
1216 FLOOR=NUM(OS5$(37,10))/100
1220 IF OS5$(7,1)="M" THEN ADJUSTMENT=COST*RATE; PRICE=COST+ADJUSTMENT
1221 IF OS5$(7,1)="M" AND PRICE>BASE_PRICE THEN PRICE=BASE_PRICE; REM SSP 199949
1225 IF OS5$(7,1)="D" THEN ADJUSTMENT=BASE_PRICE*RATE; PRICE=BASE_PRICE-ADJUSTMENT
1226 IF FLOOR=0 THEN GOTO 1234
1227 IF PRICE=0 THEN GOTO 1234
1228 GP=1-(COST/PRICE); IF GP<FLOOR THEN PRICE=COST*100/(100-FLOOR*100)
1233 IF PRICE>BASE_PRICE THEN PRICE=BASE_PRICE; REM SSP
1234 IF OS5$(7,1)="G" THEN PRICE=COST*100/(100-RATE*100)
1235 OS1$(57,10)=STR(COST:"#######.00")
1237 IF OS5$(7,1)="L" THEN PRICE=BASE_PRICE
1240 OS1$(47,10)=STR(PRICE:"#######.00")
1241 OS1$(237,10)=OS0$(204,10)
1245 OS1$(17,1)="C"; REM " CALCULATED PRICE
1246 READ (Z[6],KEY=OS1$(11,6),DOM=*NEXT)OS5_SECOND$; REM SSP 197544
1247 OS1$(18,1)=OS5_SECOND$(59,1); REM SSP 197544
1250 IF OSP$(459,1)="N" AND NUM(OS1$(47,10))<>0 AND NUM(OS1$(57,10))<>0 THEN WRITE (Z[3],KEY=OS1$(1,16))IOL=0321 ELSE IF OSP$(459,1)="Y" THEN WRITE (Z[3],KEY=OS1$(1,16))IOL=0321
1251 BASE_PRICE=0,COST=0,RATE1=0,RATE2=0,RATE3=0,RATE4=0,PRICE1=0,PRICE2=0,PRICE3=0,PRICE4=0
1260 NEXT X
1500 IF MOD(C,T0)=1 THEN GOSUB 8150
2000 GOTO 1040
3000 REM "FIGURE OUT THE COST TO USE
3001 COST=0
3005 DIM OS7$(125); READ (Z[5],KEY=OS0$(1,10)+OS5$(31,6),DOM=3199)IOL=0325
3007 IF OS7$=DIM(125) THEN GOTO 3199
3008 IF OS5$(30,1)=" " THEN OS5$(30,1)="3"
3010 IF OS5$(30,1)="3" THEN COST=NUM(OS7$(67,10))
3020 IF OS5$(30,1)="2" THEN COST=NUM(OS7$(47,10))
3030 IF OS5$(30,1)="1" THEN COST=NUM(OS7$(27,10))
3040 IF COST<>0 THEN GOTO 3199
3050 IF OS5$(30,1)="3" THEN OS5$(30,1)="2" ELSE IF OS5$(30,1)="2" THEN OS5$(30,1)="1"
3060 GOTO 3007
3199 RETURN 
3500 REM " GET LOWEST COST
3525 READ (Z[5],KEY=OS0$(1,10),DOM=*NEXT)
3530 OS7_KEY$=KEY(Z[5],END=3599)
3535 IF OS7_KEY$(1,10)<>OS0$(1,10) THEN GOTO 3599
3540 READ (Z[5],KEY=OS7_KEY$)OS7$
3545 IF OSP$(225,1)="1" THEN COST$=OS7$(27,10) ELSE IF OSP$(225,1)="2" THEN COST$=OS7$(47,10) ELSE IF OSP$(225,1)="3" THEN COST$=OS7$(67,10)
3550 IF COST=0 THEN COST=NUM(COST$)
3555 IF NUM(COST$)<COST THEN COST=NUM(COST$)
3565 GOTO 3530
3599 RETURN 
4000 REM "LOAD THE OS5 ARRAY - FOR EACH CONSUMER PRICE PLAN
4010 OS5_ARRAY$=""
4020 OS5_CHUNK=46 ! ssp# 198019 
4021 REM OS5_CHUNK=36 ! ssp# 198019 
4060 READ (Z[4],KEY="",DOM=4061)
4061 OS5_KEY$=KEY(Z[4],END=4199)
4065 READ (Z[4],KEY=OS5_KEY$)IOL=0322
4067 IF OS5$(38,1)<>SPFX$ THEN GOTO 4061; REM " PRICE NOT LOADED FROM TAPE
4068 IF OS5$(37,1)="C" THEN GOTO 4061; REM " CONTRACT PRICE
4069 REM "IF OS5$(39,1)<>"L" THEN GOTO 4061; REM "MANUALLY ENTERED PRICE
4070 OS5_ARRAY$=OS5_ARRAY$+OS5$(1,6)+OS5$(37,1)+STR(OS5[0]:M0$)+OS5$(40,12)+OS5$(52,1)+OS5$(53,6)+STR(OS5[1]:M0$)
4080 GOTO 4061
4199 RETURN 
5000 REM "EOJ"
5020 IF %GUI THEN GOTO PROCESS_END
5030 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program will read through the Office Supply Inventory File (OS0)           and create a new OS1 record for each Pricing Matrix record in the OS5 file.     If the Pricing Matrix Record indicates the price is for a contract price or for an item that has not been loaded from a tape, it will be skipped."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[2],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 IF I$="" AND %GUI=0 THEN PRINT @(0,9),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8151 REM "line reset to 21 from 19 for ssp#089855 cwj 7/15/98
8155 CALL "ZZBARG",X3$,"H",21,10,50,T1,T,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
20000 ! 100 - Initialization
20010 INIT:
20015 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN CMD_STR$="END"; GOSUB WRAPUP; GOTO *RETURN
20020 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
20027 IF POS("MS"=UCS(SYS)) THEN ISWIN=1 ELSE ISWIN=0
20030 GOSUB SETUP_LIBRARY; IF %FLMAINT_MSG$<>"" THEN MESSAGE_LIB %FLMAINT_MSG$
20055 CALL "ZZ2PRP","OS",RESULT$,DATE$; IF RESULT$<>"Y" THEN MSGBOX "","TOPS Module is not installed"; CMD_STR$="END"; GOSUB WRAPUP; GOTO *RETURN
20082 DIM Z[NUM(X3$(60,3))]
20085 Z$="02O OS0...  03O OS1...  04O OS5...  05O OS7...  06OSOS5...  13O ZZPARM  "
20087 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z0>0 THEN GOTO OPEN_ERR
20090 RETURN 
20100 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
20110 CMD_STR$="END"
20120 RETURN 
20130 ! 300 - Set up the message library
20140 SETUP_LIBRARY:
20150 _SV_MSGLIB$=MSG(*)
20160 _LANG$=LCS(%NOMAD_DEF_SFX$); IF _LANG$="" THEN _LANG$=LCS(ENV("PVXLANG")); IF _LANG$="" THEN _LANG$=LCS(ENV("LANG")); IF _LANG$="" THEN _LANG$="en"
20170 MESSAGE_LIB "*tfmsglib."+_LANG$,ERR=*NEXT; GOTO 20190
20180 MESSAGE_LIB "*tfmsglib.en"
20200 RETURN 
20350 ! 900 - Wrapup
20360 WRAPUP:
20370 %NUM_OS1_REC$=""
20400 MESSAGE_LIB _SV_MSGLIB$
20410 SET_PARAM 'KR'=_SV_KR
20415 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
20417 CMD_STR$="END"
20420 RETURN 
34000 ! 14000 - Enable/Disable groups - _ENABLE_FLG: 0=don't!, >1=corresponding key segment on, others off, -1=buttons and non-key fields on
34010 ENABLE_GROUPS:
34030 CALL "*wingrp;Disable",BUTTONS.GRP$
34040 CALL "*wingrp;Enable",FIELDS.GRP$
34050 RETURN 
36100 GET_RECORDS:
36110 CALL "ZZINFO",Z[2],T9,X3$,T,T0,K,B,D,S0,S1,F$
36120 %NUM_OS1_REC$=STR(T)
36190 RETURN 
36200 PROCESS:
36220 GOTO 0100
36250 PROCESS_END:
36260 GOSUB WRAPUP
36290 RETURN 
56000 REM "5.3 - 11/10/04 - 11.348888 - jme - SSP# 177423
56001 REM SSP# 177423 - Convert TOPS for GUI  
56002 REM "5.5 - 07/10/06 - 8.047777 - lms - SSP# 197544
56003 REM SSP 197544 - modified to update Plan type to OS1 record during update
56004 REM "5.5 - 07/24/06 - 13.052777 - bam - SSP# 198019
56005 REM "198019-All client price plans entered in the plan setup don't      
56006 REM "199949 Add GP % Floor to calculations
