0010 REM "3.8 Lot Inventory Status Report <FM2CLA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/09/12 - 12.881944 - tma - SSP# 257273
0037 REM "257273-Change the Lot Inventory Status Rpt 3.8 heading to          
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0110 X0$="FM2CLA",X1$="Lot Inventory Status Report"
0120 DIM A$(50),A[20],C$(187),C[30],F$(200),S$(20),R[2,10],G$(200),K$(200)
0140 Z0$="-###,###,##0",Z1$="#####,##0-",Z2$="-####.00",Z3$="-####,###.00"
0210 T=1,T0=0,T1=8
0215 F0=0
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,"FM2CGA","",V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR TMP - Temp Sort File(s)
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20]
0325 REM "IOLIST FOR IC0 - Inventory masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28],C[29],C[30]
0360 IOLIST F$
0370 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14],G[15],G[16],G[17],G[18],G[19],G[20],G[21],G[22],G[23],G[24],G[25],G[26],G[27],G[28],G[29],G[30],G[31]
0400 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0410 IOLIST K$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01X ICM...  03O IC0... 04O FMZ...  05O ICF... 06O AR1...  07O FM1."+".. 08O FS6...  09O FMP...  10O ART... 11O FM0...  20O FS1...  21O FS2...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02X "+"ICM"+"..."
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 CALL "ZZRSEQ",X3$,"FM2CGA","ICM...",U,U0,U1,U{ALL},Y{ALL},T0,V0,U0$,T5$,U5$,S9$,X9; ON X9 GOTO 0821,9920
0822 Q$=""; IF V2$(1,10)=V2$(11,10) THEN Q$=V2$(1,10)
0823 IF V$(59,1)="0" THEN Q$="!"+Q$; REM "Allow 0 sales price on lots
0825 D0$=V$(72,6),D1$=V$(78,6); CALL "FM2CGZ",X3$,X4$,Q$,Z[5],Z[4],Z[1],Z[3],Z[8],D0$,D1$,V$; REM " 156917
0850 DIM T[T0,T1]; U5$(1,20)=Q4$
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0910 REM "Warehouse added to range, not on screen
0915 IF V$(71,1)="1" THEN DIM Q$(4,$7E$); V2$=V2$(1,58)+S$(1,LEN(Q$))+Q$
0950 REM " Position read"
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$; IF U1=Z[1] THEN GOTO 1150 ELSE U$="",X=0
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1200 REM "Reporting criteria
1205 IF POS("I"=V$(59,12))=0 THEN IF A[0]=0 AND A[2]=0 AND A[3]=0 AND A[4]=0 THEN GOTO 1000
1220 FIND (Z[3],KEY=A$(1,20),DOM=1000)IOL=0330
1240 DIM G$(240),G[31]; FIND (Z[7],KEY=A$(1,20),DOM=1241)IOL=0370
1245 IF G$(195,1)="Y" THEN GOTO 1000
1250 IF V$(85,1)="Y" THEN IF A$(30,1)<>"9" THEN GOTO 1000
1252 IF MID(V$,87,1)="Y" AND A$(30,1)="9" AND A$(21,8)<>DIM(8) THEN GOSUB 4000; REM " SSP# 167214
1300 REM 
1320 DIM K$(200); READ (Z[11],KEY="D"+S$(1,10)+A$(30,4),DOM=1321)IOL=0410; IF K$(234,1)="Y" THEN IF V$(86,1)<>"Y" THEN GOTO 1000
1400 REM "Subtotals"
1410 IF U2$="" THEN GOTO 1490 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1490 T=0,U2$=U9$
1495 IF F$(1,10)<>A$(1,10) THEN GOSUB 7700
1496 GOSUB 7800
1510 IF W+3>W0 THEN GOSUB 6000
1512 IF T9<>0 THEN GOTO 1521
1515 Y5$(1)=A$(11,10)
1520 Y5$(12)=C$(21,40)
1521 IF FLAG$<>"" THEN Y5$(20)="**Received Not Billed**"; FLAG$=""
1523 IF A$(21,9)<>S$(1,9) THEN CALL "ZZDISP","AXS",A$(21,9),"P/O",X3$,Y5$,"",53,0,X4$ ELSE IF LEN(FNS$(A$(21,9)))=0 THEN Y5$(53)=A$(51,12)
1524 A0=A[0]-A[4]+A[3]-A[2],A1=A[8]
1525 IF A$(30,1)<>"9" THEN Y5$(67,1)="*"
1526 IF V$(71,1)="2" THEN Y5$(65,4)=A$(30,4)
1527 REM LET Y5$(74)=STR(A1:Z2$)+" "+FNS$(A$(69,4))
1530 Y5$(69)=STR(A0:Z0$)+STR(A[2]:Z0$)+STR(A[3]:Z0$)+STR(A[4]:Z0$)+STR(A[0]:Z0$)
1580 GOSUB 7300
1600 REM "UPDATE RECON FILE
1605 IF A[9]<>0 THEN A[10]=A0*A[8]/A[9]; FOR X=11 TO 14; A[X]=A[X-9]*A[8]/A[9]; NEXT X
1606 GOTO 1650
1610 DIM J[20]; J$=A$(1,20)
1615 READ (Z[10],KEY=J$(1,20),DOM=1616)IOL=0400
1620 J[5]=J[5]+A[3]
1622 J[6]=J[6]+A[12]
1630 WRITE (Z[10],KEY=J$(1,20))IOL=0400
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A0,T[T0,2]=T[T0,2]+A[2],T[T0,3]=T[T0,3]+A[3],T[T0,4]=T[T0,4]+A[4],T[T0,5]=T[T0,5]+A[0]
1925 IF A[9]<>0 THEN A[10]=A0*A[8]/A[9]; FOR X=11 TO 14; A[X]=A[X-9]*A[8]/A[9]; NEXT X
1935 GOSUB 7600
1940 X=0; IF A$(30,1)="9" THEN X=1
1945 FOR Q=10 TO 20; R[X,Q-10]=R[X,Q-10]+A[Q]; NEXT Q
1990 T[T0,0]=T[T0,0]+1
1992 T9=T9+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$; T6$=X$ ! SSP#252145
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
4000 REM "Check to see if Customer owned inventory that has been received but not billed ssp# 166262
4005 FLAG$=""
4010 READ (Z[20],KEY=A$(21,8),DOM=4199)
4020 READ (Z[21],KEY=A$(21,8),DOM=*NEXT)
4030 FS2_KEY$=KEY(Z[21],END=4199)
4040 IF FS2_KEY$(1,8)<>A$(21,8) THEN GOTO 4199
4050 READ (Z[21],KEY=FS2_KEY$)FS2$
4060 IF FS2$(161,10)+FS2$(19,10)<>A$(1,10)+A$(11,10) THEN GOTO 4030
4070 FLAG$="FOUND"
4199 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5050 T=0; GOSUB 3100
5200 REM "
5295 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6090 ELSE ON W5 GOTO 6041,9910
6050 GOSUB 6200
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(53)="Job",Y5$(63)="Distr",Y5$(72)="Beginning",Y5$(121)="Ending"; IF V$(85,1)="Y" THEN Y5$(63,5)="" ! ssp#230361
6120 GOSUB 7300
6125 Y5$(1)="Item code",Y5$(14)="Item description",Y5$(53)="Number",Y5$(63)="Owned",Y5$(72)="Inventory",Y5$(85)="Receipts",Y5$(99)="Issues      Adjust.",Y5$(121)="Inventory"; IF V$(85,1)="Y" THEN Y5$(63,5)=""
6130 GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6200 REM "CUSTOMER HEADER
6210 Y5$(1)="Client: "
6215 CALL "ZZDISP","AX",F$(1,10),"A/R",X3$,Q$,"",10,0,X4$
6220 Y5$(10)=FNS$(F$(11,35))+" ("+Q$+")"
6230 GOSUB 7300
6235 GOSUB 7300
6290 RETURN 
6900 REM "SORT
6955 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6970 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 6971,9920
6990 RETURN 
7000 REM "Totals logic
7001 IF T=2 AND T[T,0]<2 THEN F0=0
7002 IF T[T,0]<2 AND T<>1 THEN GOSUB 7300; GOTO 7100
7003 IF W+4>W0 AND POS("$"=V$(59,12))>0 OR W+3>W0 THEN GOSUB 6000
7005 Y5$(69)=T1$(1,12)+" "+T1$(1,10)+" "+T1$(1,10)+" "+T1$(1,10)+" "+T1$(1,12); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7020 Y5$(69)=STR(T[T,1]:Z0$)+STR(T[T,2]:Z0$)+STR(T[T,3]:Z0$)+STR(T[T,4]:Z0$)+STR(T[T,5]:Z0$); GOSUB 7300
7030 IF T=2 AND POS("$"=V$(59,12))>0 THEN Y5$(117)=STR(F0:Z3$); GOSUB 7300; F0=0
7065 IF T$="END" THEN Y5$(67)=T2$(1,12),Y5$(81)=T2$(1,10),Y5$(93)=T2$(1,10),Y5$(104)=T2$(1,10),Y5$(115)=T2$(1,12); GOSUB 7300
7070 IF T=1 THEN GOSUB 7500
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7105 IF T=T0-2 THEN T9=0
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7202 IF POS("S"=V$(59,12))>0 THEN RETURN 
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 W0$="Period Beginning: "+FND$(V$(72,6))+", Ending: "+FND$(V$(78,6)) ! ssp#230361
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W3$=V$(19,POS("   "=V$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Dollar totals
7510 GOSUB 7300; GOSUB 7300
7520 FOR Q=0 TO 2; IF V$(85,1)="Y" THEN IF Q=0 OR Q=2 THEN GOTO 7580
7521 IF Q<2 THEN A0=R[Q,10]-R[Q,1]+R[Q,2]-R[Q,3],R[Q,4]=R[Q,10],R[Q,0]=A0
7522 IF Q=0 THEN Y5$(42)="DISTRIBUTOR OWNED:" ELSE IF Q=1 THEN Y5$(45)="CUSTOMER OWNED:" ELSE GOSUB 7300; Y5$(54)="TOTAL:"
7525 FOR X=0 TO 4
7530 IF Q<2 THEN R[2,X]=R[2,X]+R[Q,X]
7540 Y5$(69+X*LEN(Z3$))=STR(R[Q,X]:Z3$)
7550 NEXT X
7560 GOSUB 7300
7580 NEXT Q
7590 DIM R[2,10]
7595 RETURN 
7600 REM "PRINT DOLLARS INSTEAD OF QTY
7605 IF A[9]=0 THEN A[9]=1
7610 FOR X=11 TO 13
7611 A[X]=A[X-11+2]*A[8]/A[9]
7612 NEXT X
7615 IF A[8]=0 THEN Y5$(47)=STR(A[8]:Z2$) ELSE Y5$(47)=STR(A[8]:Z2$)+" "+FNS$(A$(69,4))+"/"+STR(A[9])
7620 FOR X=0 TO 4
7622 A0=A[11]-A[12]+A[13]; IF POS("I"=V$(59,12))>0 OR POS(MID(I$,237,1)="Bb1")<>0 OR A(0)-A(4)+A(3)-A(2)=0 THEN A[20]=A[10]+A0,A[14]=A[20] ELSE A[14]=A[20],A[10]=A[20]-A0 ! SSP#232828
7625 Y5$(69+X*LEN(Z3$))=STR(A[X+10]:Z3$)
7630 NEXT X
7680 IF V$(84,1)="Y" THEN GOSUB 7300 ELSE Y5$(1)=""
7685 F0=F0+A[14]
7690 RETURN 
7700 REM "Customer change
7720 IF F$(1,10)<>A$(1,10) THEN FIND (Z[6],KEY=A$(1,10))IOL=0360; W=999
7730 READ (Z[9],KEY="D"+F$(1,10),DOM=*NEXT)I$
7745 RETURN 
7800 REM "Get Sell Value
7805 Q$=A$(21,9),X$=""; IF A$(30,1)="0" THEN X$="D"
7810 IF POS(MID(I$,237,1)="Bb1")>0 THEN CALL "FM2UZG",X3$,0,I$(237,1),A$(1,20),Q$,C0$,C0,C1; IF C1<>0 THEN GOTO 7835
7830 CALL "FM2UZE",X3$,Z[8],0,A$(1,20),Q$,X$,C0$,C1,C0
7835 IF V$(59,1)<>"0" THEN IF C0<>0 THEN IF C0<>A[8] THEN A[8]=C1,A[9]=C0
7840 IF V$(59,1)<>"0" THEN IF A[8]=0 AND C0=0 THEN A[8]=G[3],A[9]=G[4]
7845 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
