0010 REM "Carton Label Printing <PO2PCA>
0035 REM "5.0 - 03/06/02 - 10.026111 - dmm - SSP# 146548
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 BEGIN ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="PO2PCA",X1$="Carton Label Printing"
0120 DIM A$(0),B$(82),B[19],C$(297),G[13],E[1]
0140 M0$="###,###",M1$="###,###.00"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0180 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0220 W3=60
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR PO1 - purchase order header
0310 IOLIST A$
0315 REM "IOLIST FOR PO2 - Purchase order lines
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19]
0325 REM "IOLIST FOR FS1 - Sales order Header
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7]
0335 REM "IOLIST FOR AR1 - Customer Masterfile
0340 IOLIST D$
0345 REM "IOLIST FOR FM0 - Location Masterfile
0350 IOLIST E$,E[0],E[1]
0365 REM "IOLIST FOR FS2 - Sales order line
0370 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13]
0375 REM "IOLISTS FOR AR3 - Salesperson Masterfile
0380 IOLIST H$
0400 IOLIST I$
0490 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0504 DIM P[1]
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O PO6...  02O PO1...  03O PO2...  04O FS1...  05O AR1...  06O PO3...  07O FS2...  09O AR3...  10O AP4...  11O FM0...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0580 READ (Z[13],KEY=X3$(9,3)+"F/M")P0$,*,*,*,*,*,*,P[0]
0590 READ (Z[13],KEY="CMP"+X3$(9,3))P5$
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0730 GOSUB 7600
0740 REM GOSUB 7700
0800 REM "Alternate Sort Info & total dim
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[2]; U=10,U0=2,U[0]=0,U[1]=1,U[2]=9,T0=0,U0$="    ",T5$="          "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM "Position read
0905 REM "set key file to read from
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0920 V2$=V2$(1,20)
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
0995 IF Q5=0 THEN GOSUB 6000
1000 REM "Read next record
1005 U$=KEY(U1,END=5000); READ (U1)
1007 IF U$(1,1)<>"C" THEN GOTO 5000
1008 REM "check key against range info
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1045 IF Q5=1 THEN IF U$(14,1)<>"P" THEN GOTO 1000 ELSE CALL "ZZDISP","A",U$(2,9),C9$,X3$,"","",34,21,X4$; GOSUB 8300; REMOVE (Z[1],KEY=U$,DOM=1000); GOTO 1000
1050 REM IF U$(2,9)<>U9$(2,9) THEN GOTO 01100
1060 DIM U1$(14); U1$(1,13)=U$; U1$(14,1)="P"; REMOVE (Z[1],KEY=U$); WRITE (Z[1],KEY=U1$)
1100 REM "Get record
1110 U9$=U$
1150 C9=0,C9$="P/O"; READ (Z[2],KEY=U$(2,9),DOM=1155)IOL=0310; GOTO 1160
1155 C9=1,C9$="O/P"; DIM C$(168),C[7]; READ (Z[4],KEY=U$(2,8),DOM=1156)IOL=0330; GOTO 1160
1156 IF U$(1,1)="C" THEN REMOVE (U1,KEY=U$,DOM=1000); GOTO 1000 ELSE GOTO 1000
1300 REM "Read other data
1305 P0=1
1320 IF C9=0 THEN DIM C$(168),C[7]; FIND (Z[4],KEY=A$(306,8),DOM=1321)IOL=0330
1330 IF C9=0 THEN DIM D$(300); FIND (Z[5],KEY=A$(179,10),DOM=1331)IOL=0340 ELSE FIND (Z[5],KEY=C$(6,10),DOM=1331)IOL=0340
1350 DIM H$(100); FIND (Z[9],KEY=C$(93,4),DOM=1351)IOL=0380; GOTO 1360
1355 FIND (Z[9],KEY=D$(278,4),DOM=1356)IOL=0380
1500 REM "Print Data
1505 ON C9 GOTO 1506,1700
1506 REM "P/O based
1510 FIND (Z[3],KEY=U$(2,12),DOM=1590)IOL=0320
1530 N0=B[3],N1=B[11],C6$=U$(2,8)+" "+B$(7,3); REM "was C6$=U$(2,9)+B$(7,3)
1535 L1$=U$(2,9),L2$=A$(248,12),L5$=B$(15,2),L6$=B$(68,9),L0=B[5],L8$=""
1536 GOSUB 8100
1540 IF B$(10,1)="C" THEN L4$=B$(101,10) ELSE CALL "ZZDISP","AX",B$(101,10),"ICE",X3$,L4$,"",0,0,X4$
1550 GOSUB 4000
1590 GOTO 1900
1700 REM "S/O based
1705 REM "same as 1500-1699 but use FS2 file
1710 FIND (Z[7],KEY=U$(2,8)+U$(11,3),DOM=1900)IOL=0370
1730 N0=G[9],N1=G[0],C6$=U$(2,9)+G$(6,3)
1735 L1$=U$(2,9),L2$="",L4$=G$(19,10),L5$=G$(48,2),L0=G[12],L6$=G$(104,9),L8$=""; GOSUB 8150
1740 IF G$(14,1)="Y" THEN L4$=G$(19,10) ELSE CALL "ZZDISP","AX",G$(19,10),"ICE",X3$,L4$,"",0,0,X4$
1750 GOSUB 4000
1995 GOTO 1000
2000 REM "Print labels for a location
2002 REM "Assumes proper sales person is in H$
2005 L3=9,L4=10
2006 REM "Set P1$
2007 IF R5$="TP" THEN DIM P1$(200,"X"); R5$=""; GOTO 2009
2008 IF C9=0 THEN GOSUB 8400 ELSE GOSUB 8450
2010 FOR L2=1 TO L1
2015 W=1
2020 GOSUB 2100
2030 GOSUB 2200
2040 GOSUB 2300
2070 IF W<21 THEN GOSUB 7300; GOTO 2070
2075 GOSUB 7330
2080 NEXT L2
2090 RETURN 
2100 REM "Print company address & salesperson
2102 L9=14
2105 GOSUB 7650; FOR I=1 TO L9-P[0]; GOSUB 7330; NEXT I
2120 IF P0$(21,1)<>"Y" THEN P1$(1)=""
2122 IF P0$(22,1)="Y" THEN Y5$(L3,40)=X3$(86,40); IF C$(43,1)="Y" AND C$(8,1)="8" THEN Y5$(L3,40)=D$(11,35)
2125 R9=0
2130 FOR I=47 TO 127 STEP 40
2135 Y5$(L3)=P1$(I,40); GOSUB 7250
2140 NEXT I
2145 IF R9>0 THEN FOR I=1 TO R9; GOSUB 7330; NEXT I
2147 IF P[0]>0 THEN FOR I=1 TO P[0]; GOSUB 7330; NEXT I
2155 GOSUB 7700
2160 W=8
2165 P9$=P1$(167,14); GOSUB 7500
2167 REM "If this is a work order and the customer is a dealer then skip theprinting of the salesperson's name
2168 IF C$(43,1)="Y" AND C$(8,1)="8" THEN GOTO 2180
2169 IF POS(" "<>H$(5,35))=0 THEN GOTO 2180
2170 X$=FNS$(H$(5,35)); IF LEN(X$)+LEN(P8$)+3>58 THEN X$=X$(1,55-LEN(P8$))+" - "+P8$ ELSE X$=X$+" - "+P8$
2172 Y5$(30-INT(LEN(X$)/2))=X$
2175 GOSUB 7300
2190 RETURN 
2200 REM "Print customer address
2210 IF W<7 THEN GOSUB 7300; GOTO 2210
2215 Y5$(L3)=L7$; GOSUB 7200
2220 IF POS(" "<>L$)<>0 THEN Y5$(L3)="Attn: "+L$; GOSUB 7200
2230 Y5$(L3)=L0$(1,30); GOSUB 7300
2240 Y5$(L3)=L0$(31,30); GOSUB 7200
2245 CALL "ZZDISP","AX",L0$(79,9),"ZIP",X3$,X$,"",0,0,X4$
2250 Y5$(L3)=FNS$(L0$(61,16))+", "+L0$(77,2)+"  "+X$; GOSUB 7300
2265 Y5$(L3)=L9$
2270 GOSUB 7300
2290 RETURN 
2300 REM "Print item info
2305 IF W<16 THEN GOSUB 7300; GOTO 2305
2306 REM "if this is a work order and the cust. is a dealer then print the customer order # in our order number field and the P/0 number in the factory Job # field.
2310 X$=FNS$(C$(28,15))
2311 IF C$(43,1)="Y" AND C$(8,1)="8" THEN Y5$(33-INT(LEN(X$)/2))=X$ ELSE Y5$(15-INT(LEN(X$)/2))=X$
2315 CALL "ZZDISP","AX",L1$,"P/O",X3$,X$,"",0,0,X4$
2318 IF C$(43,1)="Y" AND C$(8,1)="8" THEN Y5$(49-INT(LEN(X$)/2))=X$ ELSE Y5$(33-INT(LEN(X$)/2))=X$
2319 IF C$(43,1)="Y" AND C$(8,1)="8" THEN GOTO 2325
2320 X$=FNS$(L2$); Y5$(49-INT(LEN(X$)/2))=X$
2325 GOSUB 7300; GOSUB 7300
2329 IF LEN(L3$)>58 THEN L3$=L3$(1,58)
2330 Y5$(30-INT(LEN(L3$)/2))=L3$
2335 GOSUB 7300; GOSUB 7300
2340 Y5$(L4)=L4$,Y5$(33)=L5$; IF FPT(L0)=0 THEN Y5$(45)=STR(L0:M0$) ELSE Y5$(43)=STR(L0:M1$)
2390 RETURN 
4000 REM "Given C6$ (p/o or s/o# + line#) & Lx$ stuff set,N0=#labels on line,N1=Quantity on line, run through special stuff, then if any left over dogeneral stuff
4003 IF C9=0 AND B$(14,1)<>"Y" OR (C9=1 AND G$(18,1)<>"Y") THEN GOTO 4100
4005 READ (Z[6],KEY=C6$,DOM=4006)
4010 READ (Z[6],END=4190)IOL=0350
4020 IF E$(166,9)+E$(7,3)<>C6$ THEN GOTO 4190
4030 L$=E$(16,30),L0$=E$(46,89),L9$=E$(175,35),L1=E[1]; IF LEN(E$)>=259 THEN L7$=E$(225,35) ELSE IF E$(11,1)="C" THEN L7$=D$(11,35) ELSE L7$=P1$(7,40)
4040 GOSUB 2000
4070 GOTO 4010
4100 REM "Do general inst if necc.
4105 IF C9=0 AND B$(11,3)<>"   " THEN FIND (Z[6],KEY=C6$(1,8)+"     ",DOM=4121)IOL=0350; GOTO 4140
4120 READ (Z[6],KEY=C6$(1,9)+"    ",DOM=4121)IOL=0350; GOTO 4140
4121 REM " if no general use customer address
4130 L7$=D$(11,35),L$="",L0$=D$(56,89); DIM L9$(35)
4135 GOTO 4150
4140 REM "Normal case
4145 L7$=E$(225,35),L$=E$(16,30),L0$=E$(46,89),L9$=E$(175,35)
4150 IF N1=N2 OR N2=0 THEN L1=N0 ELSE L1=INT(N0*N1/N2)+1
4160 GOSUB 2000
4190 RETURN 
5000 REM "End of Print
5010 IF Q5=1 THEN GOTO 9920
5020 IF W8=0 THEN GOTO 5200
5200 REM "
5291 GOTO 9900
5300 REM "Remove printed P/O's
5310 CALL "ZZPROM",".Y",X3$,Z,"Did all of the carton labels print correctly?","","",0
5320 ON Z GOTO 5330,5340
5330 GOSUB 5350; GOTO 5345
5345 RETURN 
5350 REM "Do remove
5354 PRINT @(0,19),'CE',
5355 CALL "ZZPROM","X0PO2PAA",X3$,Z,"","","",19
5360 Q5=1; GOTO 0900
5390 RETURN 
6000 REM "Print alignment pattern?
6005 GOSUB 6100
6010 IF V3$(2,2)="A " THEN PRINT 'EA',
6015 CALL "ZZPROM","R4",X3$,Z,"","","",X0
6017 IF Z<2 AND V3$(2,2)="A " THEN PRINT 'BA',
6020 ON Z GOTO 6090,6021,9920
6030 R5$="TP"
6040 GOSUB 2000
6050 GOTO 6010
6090 L$="",L0$="",L1$="",L2$="",L3$="",L4$="",L5$="",L6$="",L7$="",L8$="",L9$=""
6095 RETURN 
6100 REM "Setup test data"
6105 X$="X"
6110 DIM C$(100,X$),H$(100,X$),L$(30,X$),L0$(89,X$),L1$(9,X$),L2$(12,X$),L3$(40,X$),L4$(10,X$),L5$(2,X$),L6$(9,X$),L7$(35,X$),L9$(35,X$)
6115 L0=99999,L1=1
6190 RETURN 
7200 REM "If Y5$ is all blanks skip else print
7210 IF POS(" "<>Y5$)=0 THEN GOTO 7395 ELSE GOTO 7310
7250 REM "Special print, single high in double high mode
7260 IF POS(" "<>Y5$)=0 THEN R9=R9+1,W=W+1; GOTO 7395 ELSE W=W+1; GOTO 7330
7300 REM "Output line Y5$ to output device
7310 W=W+1
7324 REM "For oki 293 double high printing type printers
7325 PRINT (W9)""
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7405 CLOSE (13); OPEN (13)"ZZP"; Z[12]=13; GOTO 7430
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0490
7460 V2$(1,2)="CC"
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7486 CLOSE (14)
7490 RETURN 
7500 REM "Take phone #  P9$ and reformat into P8$ for printing
7505 IF POS(" "<>P9$(1,10))=0 THEN P8$=""; GOTO 7540
7510 IF P9$(1,3)<>"   " THEN P8$="("+P9$(1,3)+") "
7515 P8$=P8$+P9$(4,3)+"-"+P9$(7,4)
7520 IF LEN(P9$)>=14 AND POS(" "<>P9$(11,4))<>0 THEN P8$=P8$+" Ext: "+P9$(11,4)
7540 RETURN 
7600 REM "Set D1$=normal, D2$=double high printing
7601 REM "Assumes special mode 5 is double & special mode six is un-double
7602 REM "This program assumes that double high is on until turned off, changes will be needed if double high goes off auto. at end of a line see lines 740 & 9905
7605 D1$="",D2$=""
7610 READ (Z[13],KEY="PRINTERS",DOM=7640)D1$,D2$
7615 P=POS(FID(W9)=D2$,2); IF P=0 THEN GOTO 7640
7620 READ (Z[13],KEY="ptr"+D1$(INT(P/2)*8+9,8),DOM=7621)D3$; GOTO 7625
7621 D1$="",D2$=""; GOTO 7640
7625 D1$=D3$(282,16),D2$=D3$(266,16)
7629 REM "Assumes that FNS$ is looking for 2 or more spaces not just one
7630 D1$=FNS$(D1$),D2$=FNS$(D2$)
7640 RETURN 
7650 REM "Return to normal
7653 IF D1$="" THEN GOTO *RETURN
7655 PRINT (W9)'BO',D1$,'EO',
7690 RETURN 
7700 REM REM "Goto double high
7703 IF D2$="" THEN GOTO *RETURN
7705 PRINT (W9)'BO',D2$,'EO',
7740 RETURN 
8000 REM "Build desc from H8$
8010 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(80,2))=0 THEN GOTO 8040 ELSE IF H8$(80,1)<>"0" THEN H9$=H9$+H8$(80,1)
8035 H9$=H9$+H8$(81,1)+" Ply "
8040 H9$=H9$+FNS$(H8$(21,40)); H8$=""
8050 RETURN 
8100 REM "For P/O Set up H8$ and set L3$
8110 DIM H8$(150)
8115 H8$(64,8)=B$(17,8),H8$(72,8)=B$(111,8),H8$(80,2)=B$(15,2),H8$(21,40)=B$(28,40)
8120 GOSUB 8000
8130 L3$=H9$
8140 RETURN 
8150 REM "For S/O set up H8$ ans set L3$
8160 DIM H8$(150)
8165 H8$(64,18)=G$(32,18),H8$(21,40)=G$(50,40)
8170 GOSUB 8000
8180 L3$=H9$
8190 RETURN 
8300 REM "Set flag
8310 DIM C[7]; EXTRACT RECORD (Z[4],KEY=U$(2,8),DOM=8340)C$
8320 C$(127,1)="Y"; WRITE RECORD (Z[4],KEY=U$(2,8))C$
8340 RETURN 
8350 REM "Given I$ and I9 starting pos, fill P1$
8360 P1$(7)=C0$,P1$(47)=I$(I9+35,30),P1$(87)=I$(I9+65,30)
8365 IF POS(" "<>I$(I9+113,9))=0 THEN I9$="" ELSE CALL "ZZDISP","AX",I$(I9+113,9),"ZIP",X3$,I9$,"",0,0,X4$
8370 IF POS(" "<>I$(I9+95,16))=0 THEN P1$(127,40)=I$(I9+111,2)+"  "+I9$ ELSE P1$(127,40)=FNS$(I$(I9+95,16))+", "+I$(I9+111,2)+" "+I9$
8390 RETURN 
8400 REM "Get I$ for P/O carton label
8405 DIM P1$(180); I9=11
8407 IF LEN(A$)<17 THEN DIM A$(17)
8410 FIND (Z[10],KEY=A$(7,10),DOM=8440)IOL=0400
8415 C0$=P1$(7,39)
8420 GOSUB 8350
8425 P1$(167,14)=P5$(167,14)
8440 RETURN 
8450 REM "Get I$ for O/P carton label
8455 DIM P1$(180); I9=16
8460 FIND (Z[11],KEY="D"+P1$(1,10)+G$(10,4),DOM=8490)IOL=0400
8465 C0$=I$(16,35)
8470 GOSUB 8350
8474 REM "Give phone # if dist. loc.
8475 P1$(167,14)=I$(200,14)
8490 RETURN 
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 GOSUB 7650
9906 IF FID(0)<>FID(W9) THEN W9=-1; REM "To prevent FF action by ZZERPT
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF Y4$<>X0$ AND V3$(2,1)<>"T" THEN GOSUB 5300
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
