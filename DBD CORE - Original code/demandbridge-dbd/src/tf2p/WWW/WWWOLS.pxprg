0010 REM "WWW Electronic Commerce Services (WWWOLS)
0020 SETESC 9300; SETERR 9000
0030 ESCAPE 
0035 REM "4.1 - 01/21/97 - 18.04 - kmc
0040 REM "Copyright 1997 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER PARAM$,DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="WWWOLS",X1$="WWW Electronic Commerce Services"
0120 EOL$=STP(PARAM$(1,4),1)
0310 ZW2_IOLIST:IOLIST ZW2$(1),ZW2[0],ZW2[1],ZW2[2]
0500 REM "Open files
0505 DIM Z[20]
0511 Z[1]=UNT; OPEN (Z[1])"ZW2"
0512 Z[2]=UNT; OPEN (Z[2])"ZWF"
0513 Z[3]=UNT; OPEN (Z[3])"SS1"
0525 Z[15]=UNT; OPEN (Z[15])"ZW4"
0600 REM "Set date to show closeds after
0605 NUM_DAYS_CLOSED=14
0610 TEMP$=FN%NTD$(JUL(0,0,0)-NUM_DAYS_CLOSED,"YYMMDD"); TEMP$(1,1)=CHR(65+NUM(TEMP$(1,1))),SHOW_CLOSEDS_AFTER$=TEMP$
0615 THIS_DAY$=FN%NTD$(JUL(0,0,0),"YYMMDD"); THIS_DAY$(1,1)=CHR(65+NUM(THIS_DAY$(1,1)))
1000 REM "Process DATA array
1006 GOSUB DEFAULT_TAG_LIST
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1020 TAG_POS=POS(PAD(TAG$,TAG_LENGTH)=TAG_LIST$,TAG_LENGTH); IF TAG_POS>0 THEN ON INT(TAG_POS/TAG_LENGTH) GOSUB 7601,7602,7603,7604,7605,NO_MATCH
1090 NEXT I
1100 REM "Done with record
1110 READ (Z[3],KEY=CLIENT$,ERR=1111)SSI$; GOTO 1114
1112 MESSAGE_CODE=1; GOTO EXIT_WITH_MESSAGE
1114 IF STP(SSI$(621,10),1)<>PASSWORD$ THEN MESSAGE_CODE=1; GOTO EXIT_WITH_MESSAGE
1115 IF SSI$(223,1)="Y" THEN MESSAGE_CODE=2; IF SSI$(9,2)<>"00" THEN CLIENT_SHOW$=STR(NUM(SSI$(1,8)))+"-"+SSI$(9,2) ELSE CLIENT_SHOW$=STR(NUM(SSI$(1,8))) END_IF ; GOTO EXIT_WITH_MESSAGE
1120 ISSUES_FOUND=0; IF ISSUE$="" THEN MATCH_KEY$=CLIENT$ ELSE MATCH_KEY$=CLIENT$+ISSUE$ END_IF ; EXTRACT (Z[2],KEY=MATCH_KEY$,DOM=1121); REM "Extract because we may have the full key
1125 KEY_2$=KEY(Z[2],END=1190); READ (Z[2],END=1190); IF KEY_2$(1,LEN(MATCH_KEY$))<>MATCH_KEY$ THEN GOTO 1190
1130 DIM ZW2$(300),ZW2[2]; FIND (Z[1],KEY=KEY_2$(11,6),DOM=1125)IOL=ZW2_IOLIST; IF ISSUE$="" THEN IF (ZW2$(195,1)="C" AND ZW2$(196,6)<SHOW_CLOSEDS_AFTER$) OR POS(ZW2$(195,1)="EFLVYI")<>0 THEN GOTO 1125 END_IF END_IF ; ISSUES_FOUND=ISSUES_FOUND+1
1135 GOSUB DISPLAY_ISSUE
1185 GOTO 1125
1190 IF ISSUES_FOUND=0 THEN GOSUB DO_HEADER; GOSUB DO_NONE_FOUND
1200 IF HEADER_DONE$="Y" THEN GOSUB DO_TRAILER
1895 REM " GOSUB 2000
1995 GOTO 9900
2000 REM "Debug loginc
2005 PRINT (OUTPUT)"Content-type: text/html",EOL$,EOL$,
2010 FILE_NAME$=PARAM$(5,8); CALL "WWWSND",ERR=2011,PARAM$,FILE_NAME$,OUTPUT; REM "Send file header
2050 REM "PRINT (OUTPUT) "CLIENT$=",CLIENT$,"<BR>","ISSUE$=",ISSUE$,"<BR>","MATCH_KEY$=",MATCH_KEY$,"<BR>","KEY_2$=",KEY_2$,"<BR>",EOL$,
2090 FILE_NAME$=PARAM$(13,8); CALL "WWWSND",ERR=2091,PARAM$,FILE_NAME$,OUTPUT; REM "Send file trailer
2095 RETURN 
2100 EXIT_WITH_MESSAGE:REM "Send back standard error page with message dep"+      "endent on MESSAGE_CODE
2105 PRINT (OUTPUT)"Content-type: text/html",EOL$,EOL$,
2110 FILE_NAME$=PARAM$(5,8); CALL "WWWSND",ERR=2111,PARAM$,FILE_NAME$,OUTPUT; REM "Send file header
2115 ON MESSAGE_CODE GOTO 2190,2121,2122,2190
2121 PRINT (OUTPUT)"<BR><BR><H2>","Client code or password is invalid, ple"+"ase correct and re-submit.","</H2><BR><BR>",EOL$,; GOTO 2190
2122 PRINT (OUTPUT)"<BR><BR><H2>","Your support request for client ",CLIENT_SHOW$," cannot be processed at this time.  Please contact the"+" TopForm Accounting Department to resolve this matter.","</H2><BR><BR>",EOL$,; GOTO 2190
2190 FILE_NAME$=PARAM$(13,8); CALL "WWWSND",ERR=2191,PARAM$,FILE_NAME$,OUTPUT; REM "Send file trailer
2195 EXIT_WITH_MESSAGE_END:GOTO 9900
2200 DISPLAY_ISSUE:REM "Display issue in ZW2$
2205 IF HEADER_DONE$<>"Y" THEN GOSUB DO_HEADER
2208 DESC_1$="",DESC_2$=""; IF STP(ZW2$(53,60),3," ")="" THEN DESC_1$=STP(ZW2$(113,60),1) ELSE DESC_1$=STP(ZW2$(53,60),1),DESC_2$=STP(ZW2$(113,60),1)
2210 PRINT (OUTPUT)"<PRE><B><FONT SIZE=+1>",EOL$,
2215 PRINT (OUTPUT)"<FONT COLOR="+QUO+"Red"+QUO+">",PAD(ZW2$(1,6),8,"R"),DIM(4),"</FONT>",DESC_1$,EOL$,
2218 IF POS(" "<>DESC_2$)>0 THEN PRINT (OUTPUT)DIM(12),DESC_2$,EOL$,
2220 PRINT (OUTPUT)DIM(12),"Entered on: ",FND$(ZW2$(7,6))," at ",FNT$(ZW2$(13,4)),"  by: ",STP(ZW2$(23,16),1),EOL$,
2222 IF STP(ZW2$(261,30),1)<>"" THEN PRINT (OUTPUT)DIM(12),"Email responses to: ",STP(ZW2$(261,30),1),EOL$,
2223 PRINT (OUTPUT)"<FONT COLOR="+QUO+"Green"+QUO+">",
2225 S_MESS$=""; IF ZW2$(195,1)="K" THEN STATUS$="Open",S_MESS$="Waiting for Customer Approval" ELSE IF ZW2$(195,1)="Z" AND ZW2$(191,3)<>"TBB" THEN LETSTATUS$="Open",S_MESS$="Programming Complete" ELSE IF ZW2$(195,1)="J" THEN STATUS$="Open",S_MESS$="Submitted to Quotation Process" ELSE IF ZW2$(195,1)="C" THEN STATUS$="Closed"; IF POS(" "<>ZW2$(196,6))<>0 THEN S_MESS$="Received notification to close on: "+FND$(ZW2$(196,6))+" at "+FNT$(ZW2$(202,4)) END_IF ELSE IF ZW2$(195,1)="U" THEN STATUS$="Resolved",S_MESS$="Issue resolved, but requires software update." ELSE IF ZW2$(195,1)="G" THEN STATUS$="Complete"; IF STP(ZW2$(222,6),3," ")="" THEN S_MESS$="Issue will be closed unless we are notified." ELSE S_MESS$="Issue will be closed on "+FND$(ZW2$(222,6))+" unless we are notified." END_IF ELSE IF ZW2$(195,1)="W" THEN STATUS$="Waiting",S_MESS$="Customer input needed to resolve issue." ELSE STATUS$="Open"; IF POS(" "<>ZW2$(228,6))<>0 THEN IF ZW2$(228,6)>THIS_DAY$ THEN S_MESS$="Estimated Completion Date: "+FND$(ZW2$(228,6)) ELSE S_MESS$="Estimated Completion Date is being reviewed."
2230 PRINT (OUTPUT)PAD(STATUS$,8,"R"),DIM(4),S_MESS$,"</FONT>",EOL$,
2240 GOSUB DISPLAY_NOTES
2250 PRINT (OUTPUT)"</FONT></B></PRE>",EOL$,
2295 DISPLAY_ISSUE_END:RETURN 
2300 DO_HEADER:REM "Start the display of issues
2305 PRINT (OUTPUT)"Content-type: text/html",EOL$,EOL$,
2310 FILE_NAME$=PARAM$(5,8); CALL "WWWSND",ERR=2311,PARAM$,FILE_NAME$,OUTPUT; REM "Send file header
2312 COMP$=STR(NUM(SSI$(1,8)):"000")+" - "+STP(SSI$(11,35),1)
2315 IF CONF$<>"Y" THEN PRINT (OUTPUT)"<FONT SIZE=+2><B> Thank you "+STP(SSI$(11,35),1)+" ("+STR(NUM(SSI$(1,8)):"000")+") for your request.<HR></B></FONT>",EOL$,; IF ISSUE$="" THEN PRINT (OUTPUT)"These are your open support issues and support i"+"ssues closed in the last ",STR(NUM_DAYS_CLOSED)," days:<BR>",EOL$, END_IF ; GOTO 2330
2320 IF STP(ZW2$(23,16),3," ")="" THEN TEMP$=STP(SSI$(11,35),1)+" ("+STR(NUM(SSI$(1,8)):"000")+")" ELSE TEMP$=STP(ZW2$(23,16),1)+" of "+STP(SSI$(11,35),1)+" ("+STR(NUM(SSI$(1,8)):"000")+")"
2325 PRINT (OUTPUT)"<BR><FONT SIZE=+2><B> Thank you "+TEMP$+",<BR> for your electronic request for supp"+"ort. <BR></B></FONT>",EOL$,
2327 IF CHANGE$="Y" THEN TEMP$="change " ELSE TEMP$="" END_IF ; PRINT (OUTPUT)"<BR><FONT SIZE=+1><B>This ",TEMP$,"request was logged in o"+"ur support system on "+FND$(ZW2$(7,6))," at ",FNT$(ZW2$(13,4))+"<BR><"+"/B></FONT><BR>",
2329 IF CHANGE$<>"Y" THEN PRINT (OUTPUT)"<FONT SIZE=+2><B> Please make note of the reference"+" number assigned: <FONT COLOR="+QUO+"Red"+QUO+">",ZW2$(1,6)+"</FON"+"T>. <BR></B></FONT>",EOL$,
2340 HEADER_DONE$="Y"
2345 DO_HEADER_END:RETURN 
2350 DO_TRAILER:REM "display the trailer
2370 FILE_NAME$=PARAM$(13,8); CALL "WWWSND",ERR=2371,PARAM$,FILE_NAME$,OUTPUT; REM "Send file trailer
2395 DO_TRAILER_END:RETURN 
2400 DISPLAY_NOTES:REM " Display the notes for the call
2405 NOTES$=""; READ (Z[15],KEY=ZW2$(1,6),DOM=2406)
2407 PRINT (OUTPUT)"<FONT SIZE=+0>",EOL$,
2410 KEY_15$=KEY(Z[15],END=2411); IF KEY_15$(1,6)=ZW2$(1,6) THEN READ (Z[15],KEY=KEY_15$,DOM=2410)*,TEMP$; NOTES$=NOTES$+TEMP$; GOTO 2410
2415 P_TO_SHOW=0,P_IN=POS("*clientin*"=NOTES$),P_OUT=POS("*clientout*"=NOTES$); IF P_IN<>0 AND (P_OUT=0 OR P_IN<P_OUT) THEN P_TO_SHOW=P_IN ELSE P_TO_SHOW=P_OUT; REM "If clientin there and it is before client out then show it els"+             "e show client out
2418 IF P_TO_SHOW=0 THEN GOTO DISPLAY_NOTES_END
2420 NOTES$=NOTES$(P_TO_SHOW),P_EOL=POS($0A$=NOTES$),N_INFO$=NOTES$(1,P_EOL),NOTES$=NOTES$(P_EOL+1); P_NEXT=POS($0A$+"*"=NOTES$)
2423 IF P_NEXT=0 THEN SECTION$=NOTES$,NOTES$="" ELSE SECTION$=NOTES$(1,P_NEXT),NOTES$=NOTES$(P_NEXT+1); REM " Throw away uneeded notes, pull info line off, then get just t"+             "his section into Section$, Notes left as they are, or none left
2425 GOSUB PARSE_N_INFO
2430 IF INFO$[1]="clientin" THEN MESS$=STP(INFO$[5],1)+" sent: " ELSE MESS$="We said: "
2435 SECTION_HEADER$="On "+INFO$[3]+" at "+INFO$[4]+" "+MESS$
2440 GOSUB DISPLAY_SECTION
2445 GOTO 2415
2495 DISPLAY_NOTES_END:PRINT (OUTPUT)"</FONT>",EOL$,; RETURN 
2500 DISPLAY_SECTION:REM "Display a section of notes in SECTION$
2505 FIRST_DN=0
2520 P=POS($0A$=SECTION$); IF P=0 THEN LINE$=SECTION$,SECTION$="" ELSE LINE$=SECTION$(1,P-1),SECTION$=SECTION$(P+1) END_IF ; IF LEN(LINE$)>1 THEN IF FIRST_DN=0 THEN FIRST_DN=1; PRINT (OUTPUT)"<I>",EOL$,DIM(19),SECTION_HEADER$,"</"+"I>",EOL$, END_IF ; PRINT (OUTPUT)DIM(21),LINE$,EOL$, END_IF ; IF P<>0 THEN GOTO 2520
2545 DISPLAY_SECTION_END:RETURN 
2550 PARSE_N_INFO:REM "Take '*' seperated fields in N_INFO$ and parse into"+      " INFO$ array starting at INFO$[1]. INFO$[0] will hold original string
2555 DIM INFO$[10]; INFO_INDEX=0,INFO$[0]=N_INFO$; IF N_INFO$="" THEN GOTO PARSE_N_INFO_END ELSE N_INFO$=N_INFO$(2); IF N_INFO$(LEN(N_INFO$),1)<" " THEN N_INFO$=N_INFO$(1,LEN(N_INFO$)-1); REM "Remove leading '*' and trailing LF
2560 P_STAR=POS("*"=N_INFO$),INFO_INDEX=INFO_INDEX+1; IF P_STAR=0 THEN INFO$[INFO_INDEX]=N_INFO$; GOTO 2565 ELSE INFO$[INFO_INDEX]=N_INFO$(1,P_STAR-1); N_INFO$=N_INFO$(P_STAR+1); GOTO 2560
2595 PARSE_N_INFO_END:RETURN 
2600 DO_NONE_FOUND:REM "If ISSUES_FOUND is 0 then give message
2605 IF ISSUE$<>"" THEN PRINT (OUTPUT)"<FONT SIZE=+1><BR><BR>Could not find any information about Reference Number ",ISSUE$,". Please check your input and re-submit if needed.<BR><BR></FONT>",EOL$,; GOTO DO_NONE_FOUND_END
2610 PRINT (OUTPUT)"<FONT SIZE=+1><BR><BR> You have no open requests and there are no closed requests in the last ",STR(NUM_DAYS_CLOSED)," days.<BR><BR></FONT>",EOL$,
2645 DO_NONE_FOUND_END:RETURN 
3000 SET_CLIENT:REM "Set client number based on value
3003 TEMP$=FN%CVT4$(VALUE$); P_DASH=POS("-"=TEMP$)
3004 IF P_DASH>0 THEN IF P_DASH=1 THEN TEMP_2$=TEMP$(P_DASH+1),TEMP$="" ELSE IF P_DASH=LEN(TEMP$) THEN TEMP_2$="",TEMP$=TEMP$(1,P_DASH-1) ELSE TEMP_2$=TEMP$(P_DASH+1),TEMP$=TEMP$(1,P_DASH-1)
3005 CLIENT$=STR(NUM(TEMP$):"00000000")+STR(NUM(TEMP_2$):"00")
3045 SET_CLIENT_END:RETURN 
7550 DEFAULT_TAG_LIST:REM "Setup the Tag list with entries that are TAG_LE"+      "NGTH long: start at 7601 corresponding to tag 1. IF setup ="Y" then s"+      "etup else perform action
7555 TAG_LENGTH=10
7590 TAG_LIST$="",CLIENT$="",ISSUE$=""
7595 SETUP$="Y"
7600 REM "TAG LIST for setup or action
7601 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("client",TAG_LENGTH) ELSE IF LEN(VALUE$)=10 THEN CLIENT$=VALUE$ ELSE GOSUB SET_CLIENT; RETURN 
7602 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("issue",TAG_LENGTH) ELSE IF VALUE$="" OR STP(VALUE$,3," ")="" THEN ISSUE$="" ELSE ISSUE$=STR(NUM(VALUE$):"000000") END_IF ; RETURN 
7603 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("conf",TAG_LENGTH) ELSE CONF$=STP(VALUE$,1); RETURN 
7604 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("password",TAG_LENGTH) ELSE PASSWORD$=VALUE$; RETURN 
7605 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("change",TAG_LENGTH) ELSE CHANGE$="Y"; RETURN 
7699 DEFAULT_TAG_LIST_END: SETUP$="N"; RETURN 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9001 J=ERR,K=TCB(5)
9002 SETERR 0000; ENDTRACE 
9003 PRINT (OUTPUT)"Content-type: text/html",EOL$,EOL$,
9004 FILE_NAME$=PARAM$(5,8); CALL "WWWSND",ERR=9005,PARAM$,FILE_NAME$,OUTPUT; REM "Send file header
9005 PRINT (OUTPUT,ERR=9010)"ERR=",STR(J)," LINE = ",STR(K),"<BR>",EOL$,
9006 PRINT (OUTPUT)"VALUE$=",VALUE$,EOL$,
9007 PRINT (OUTPUT)"HTA(VALUE$)=",HTA(VALUE$),EOL$,
9010 GOTO 9900
9900 REM "End
9905 FOR I=1 TO 20; IF Z[I]<>0 THEN CLOSE (Z[I]) END_IF ; NEXT I
9950 EXIT 
9999 END 
