0010 REM "<ED2IN4> EDI Invoice Export (Custom 074/DoItBest)
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 06/18/24 - 16.57749 - jvv - SSP# 307489
0037 REM "307489-IMPL1032 DBD=462 Change to EDI 810 ED2IN4 program           
0040 REM "Copyright 2024 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CUST$,INV$
0100 SETERR 9000
0110 X0$="ED2IN4",X1$="EDI Invoice Order Export (074/DoItBest)"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1,C=1
0135 C9=-1
0140 DIM A[30],C[20],Z1$(15," ")
0145 DTT$=FN%NTD$(CDN,"YYYYMMDD")
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0166 DEF FNF$(Z9$)=Z9$(7,4)+Z9$(1,2)+Z9$(4,2)
0170 IF POS("MS"=UCS(SYS)) THEN ISWIN=1 ELSE ISWIN=0
0290 PARM$=X3$(9,3)+"E/M"
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0330 IOLIST C$,C1$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0370 IOLIST G$
0410 IOLIST Q$
0420 IOLIST K$
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O EM1... 07O AR1...  08O EM2... 09O ARB...  10O ART...  11O IC0...  12O FS1...  13O ZZPARM  14O EM8...  15O EM9...  16O FM1...  17O SM4... 30O AR2... 31O AT1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0561,9920
0580 DEVICE=W9
0590 READ (Z[13],KEY=PARM$,DOM=9900)IOL=0410; REM "Get Senders information
0592 IF STP(Q$(304,2),3," ")="" THEN Q$(304,2)="ZZ"
1000 REM "Main Process
1010 READ (Z[9],KEY=INV$,DOM=9900)IOL=0310; REM "Initial read from orders file (ARB)
1017 IF POS(A$(65,2)="02030506070809",2) THEN GOTO 9900; REM " Check for COD terms - 02,03,05,06,07,08,09 - do not process if COD
1020 GOSUB 1900; REM "Get Customer & Trading Partner Information
1030 IF FOUND_TP$="N" THEN GOTO 9900
1035 IF TRANS_TYPE$="CN" THEN READ (Z[31],KEY=CUST$+A$(7,8),DOM=1036)CREDIT$
1040 GOSUB 2000; REM "Set up channel for printing
1050 GOSUB 6700; REM "Write new control number back to ZZPARM file
1060 GOSUB 2100; REM "Heading portion of EDI file
1070 GOSUB 2200; CTT=CTT+1; REM "Write line items out to EDI file
1080 GOSUB 2300; REM "Bottom portion of EDI file
1090 GOSUB 3000; REM "Archive file to editmp in case of problems
1600 GOTO 9900
1900 REM "Get Customer & Trading Partner Info
1910 FOUND_TP$="N"
1920 READ (Z[8],KEY=CUST$,DOM=1921)
1925 DIM EM1$(255),G$(599)
1930 CUST_TRADE_CODE$=KEY(Z[8],END=1995); READ (Z[8],END=1995)
1940 IF CUST_TRADE_CODE$(1,10)<>CUST$ THEN GOTO 1995
1950 REC_ID$=CUST_TRADE_CODE$(11,15)
1960 READ (Z[7],KEY=CUST_TRADE_CODE$(1,10),DOM=1961)IOL=0370
1970 FIND (Z[1],KEY=REC_ID$,DOM=1971)EM1$
1980 IF EM1$(73,1)="Y" THEN FOUND_TP$="Y" ELSE GOTO 1925
1985 IF STP(EM1$(152,2),3," ")="" THEN EM1$(152,2)="ZZ"
1995 RETURN 
2000 REM "Set the channel up for printing
2001 SERIAL "EDILCK",0,0,ERR=2002; GOTO 2003
2002 WAIT 5; GOTO 2001
2005 PRINTER_CFG$="512,1,3,,"; REM "Line width 512, don't lock others out, build in /tmp then send, no timeout
2010 FILE_NAME$=STR(NUM(A$(7,8)):"00000000")+".out"
2015 PRINTER_CFG$=STP(Q$(152,30),1)+FILE_NAME$
2020 DEV_CHANNEL=DEVICE,DEVICE_NAME$=Q$(212,2)
2025 REM "Determine correct channel to close
2030 CLOSE (DEV_CHANNEL); OPEN (DEV_CHANNEL,OPT=PRINTER_CFG$)DEVICE_NAME$
2095 RETURN 
2100 REM "Build top part of EDI file
2101 DIM FS_HEAD$(350); FIND (Z[12],KEY=A$(92,8),DOM=2102)FS_HEAD$
2110 DT$=FND$(A$(86,6)); DT$=FNF$(DT$)
2115 TRANS_TYPE$=""
2116 IF A$(77,1)="C" THEN TRANS_TYPE$="CN" ELSE TRANS_TYPE$="DO"
2117 IF TRANS_TYPE$="CN" THEN READ (Z[31],KEY=CUST$+A$(7,8),DOM=2118)CREDIT$
2118 IF A$(77,1)="C" THEN TRANS_TYPE$="CN" ELSE IF CUST$=EM1$(51,10) THEN TRANS_TYPE$="WH" ELSE TRANS_TYPE$="DO"; REM SSP 212942
2120 PRINT (DEV_CHANNEL)"ISA*00*          *00*          *"+Q$(304,2)+"*",
2125 IF EM1$(71,1)="N" THEN TEST$="P" ELSE TEST$="T"
2126 SID$="35543"+DIM(10)
2129 OUT$=DTE(0:"%Hz%mz"); PRECISION 2
2130 PRINT (DEV_CHANNEL)SID$+"*"+EM1$(152,2)+"*"+REC_ID$+"*"+DTT$(3)+"*"+OUT$+"*U*"+"00401*"+Q$(143,9)+"*0*"+TEST$+"*>"; REM " HARDCODED BECAUSE WHAT HARBINGER AND WHAT DOITBEST WANTS ARE DIFFERENT ! DBD-400-DBSPT-164641-SSP307452 SSP 307489
2140 PRINT (DEV_CHANNEL)"GS*IN*"+STP(SID$,1)+"*"+STP(REC_ID$,1)+"*"+DTT$+"*"+OUT$+"*"+Q$(143,9)+"*X*"+"0"+STP(EM1$(157,10),1); REM " ADDED EXTRA 0 TO FRONT...SHOULD HAVE 04010 IN FILE FOR HARBINGER, BUT DOITBEST WANTS 004010 SSP307489
2150 PRINT (DEV_CHANNEL)"ST*810*"+Q$(143,9); CTT=CTT+1
2155 IF LEN(FS_HEAD$)>0 THEN ORD_DT$=FND$(FS_HEAD$(16,6)); ORD_DT$=FNF$(ORD_DT$)
2158 IF ORD_DT$=DIM(8) THEN ORD_DT$=DT$; REM " IF NO ORDER DATE USE THE INVOICE DATE
2160 PRINT (DEV_CHANNEL)"BIG*"+DT$+"*"+STP(A$(7,8),1)+"*"+ORD_DT$+"*"+STP(A$(100,15),1)+"*"; CTT=CTT+1
2161 IF TRANS_TYPE$="CN" THEN PRINT (DEV_CHANNEL)"REF*CM*"+STP(CREDIT$(25,8),1); CTT=CTT+1
2165 REM "PRINT (DEV_CHANNEL)"REF*CD*"+STP(G$(185,8),1); CTT=CTT+1; REM " RECEIVING DEPARTMENT- LOOKS LIKE WHERE THEY PUT CREDIT AUTH #
2166 REM "IF TRACKING_NO$<>"" THEN PRINT (DEV_CHANNEL)"REF*CN*"+STP(TRACKING_NO$,1); CTT=CTT+1; ELSE IF TRACKING_NO$="" THEN PRINT (DEV_CHANNEL)"REF*CN*NONE"; CTT=CTT+1
2168 TRADE_PART$=EM1$(51,10); PRINT (DEV_CHANNEL)"REF*IT*"+STP(TRADE_PART$,1); CTT=CTT+1
2170 REM "PRINT (DEV_CHANNEL)"REF*VN*"+STP(A$(92,8),1); CTT=CTT+1
2171 IF TRANS_TYPE$="DO" THEN PRINT (DEV_CHANNEL)"N1*ST*"+STP(G$(11,35),2)+"*92*"+STP(G$(185,20),2)+$0A$+"N3*"+STP(G$(56,30),2)+$0A$+"N4*"+STP(G$(116,16),2)+"*"+G$(132,2)+"*"+STP(G$(134,9),2); CTT=CTT+3 ! DBD-400-DBSPT-164641-SSP307452
2172 HOLD$=STP(A$(290,35),1); WHSE$=HOLD$(LEN(HOLD$)-3)
2173 IF TRANS_TYPE$="WH" THEN PRINT (DEV_CHANNEL)"N1*ST*"+STP(G$(11,35),2)+"*92*"+WHSE$+$0A$+"N3*"+STP(G$(56,30),2)+$0A$+"N4*"+STP(G$(116,16),2)+"*"+G$(132,2)+"*"+STP(G$(134,9),2); CTT=CTT+3 ! DBD-400-DBSPT-164641-SSP307452
2174 IF A$(191,30)<>G$(56,30) THEN PRINT (DEV_CHANNEL)"N1*RC*"+STP(G$(11,35),2)+$0A$+"N3*"+STP(A$(191,30),2)+$0A$+"N4*"+STP(A$(251,16),2)+"*"+A$(267,2)+"*"+STP(A$(269,9),2); CTT=CTT+3 ! DBD-400-DBSPT-164641-SSP307452
2175 FIND (Z[30],KEY=A$(65,2),DOM=2172)TERM$,TERM_PERCENT
2176 DISC_DUE_DT$=FND$(A$(122,6)); DISC_DUE_DT$=FNF$(DISC_DUE_DT$)
2177 DISC_AMT=A[10]*100; DUE_DAYS$=TERM$(34,3); DISC_DUE_DAYS$=TERM$(44,3); TERM_DESC$=TERM$(3,30)
2178 DUE_DT$=FND$(A$(116,6)); DUE_DT$=FNF$(DUE_DT$)
2179 IF TRANS_TYPE$<>"CN" AND TERM_PERCENT=0 THEN PRINT (DEV_CHANNEL)"ITD*14*3**"+STP(DISC_DUE_DT$,1)+"**"+STP(DUE_DT$,1)+"******"+STP(TERM$(3,30),1); CTT=CTT+1; REM " SSP# 149193      
2180 IF TRANS_TYPE$<>"CN" AND TERM_PERCENT>0 THEN PRINT (DEV_CHANNEL)"ITD*14*3*"+STR(TERM_PERCENT)+"*"+STP(DISC_DUE_DT$,1)+"*"+DISC_DUE_DAYS$+"*"+STP(DUE_DT$,1)+"*"+DUE_DAYS$+"*"; CTT=CTT+1; REM " SSP# 149193/205074
2181 IF TRANS_TYPE$="CN" THEN PRINT (DEV_CHANNEL)"ITD*14*3***"+STP(DT$,1)+"**"; CTT=CTT+1; REM " SSP# 149193      
2185 PRINT (DEV_CHANNEL)"DTM*011*"+FNF$(FND$(A$(180,6))); CTT=CTT+1
2190 RETURN 
2200 REM "Write line items out to EDI file
2210 KT=A[20],TRACK_NO$=""
2220 IF KT<=0 THEN GOTO 2290 ELSE READ (Z[10],IND=KT,END=2290)IOL=0330
2230 IF C$="" THEN EXITTO 1080 ELSE KT=NUM(C$)
2235 FIND (Z[14],KEY=C1$(50,4),DOM=2236)IOL=0420
2236 IF LEN(K$)=0 THEN DIM K$(10); K$(5,2)="EA"
2240 IF C1$(1,1)="S" THEN GOSUB 2500; GOTO 2220
2245 IF C1$(6,1)="N" OR C1$(1,1)="M" OR C[1]=0 THEN GOTO 2220
2255 IF C[6]<>0 THEN QTY=C[1]/C[6] ELSE QTY=C[1]
2260 GOSUB 2400 ! Check for CIC
2265 IF TRACK_NO$="" THEN GOSUB 2600
2266 UPC$=C1$(7,40); IF TRANS_TYPE$="CN" AND STP(C1$(65,10),3," ")="" THEN C1$(65,10)="CREDIT"
2267 IF POS("UPC"=UPC$) THEN UPC$=UPC$(POS("UPC"=UPC$)+3) ELSE UPC$=""
2269 IF UPC$<>"" THEN PRINT (DEV_CHANNEL)"IT1**"+STR(QTY)+"*"+STP(K$(5,2),2)+"*"+STP(STR(C[2]:"#######.00"),2)+"**IN*"+STP(ITEM$,2)+"*VN*"+STP(C1$(7,6),2)+"*UP*"+STP(UPC$,2)+"****"; CTT=CTT+1 ! WO228481, use ITEM$ in place of C1$(65,10)
2270 IF UPC$="" THEN PRINT (DEV_CHANNEL)"IT1**"+STR(QTY)+"*"+STP(K$(5,2),2)+"*"+STP(STR(C[2]:"#######.00"),2)+"**IN*"+STP(ITEM$,2)+"*VN*"+STP(C1$(7,6),2); CTT=CTT+1 ! WO228481, use ITEM$ in place of C1$(65,10)
2272 PRINT (DEV_CHANNEL)"PID*F****"+STP(C1$(7,40),1); REM "CTT=CTT+1
2275 C=C+1,CTT=CTT+1,TTL_UNITS=TTL_UNITS+QTY
2280 GOTO 2220
2290 RETURN 
2300 REM " Bottom Portion of EDI file
2310 TAX_TOT=(A[4]+A[6]+A[8])*100; SAC_F=(A[2]+A[17])*100 ! SSP 307489
2315 IF TRANS_TYPE$<>"CN" THEN TDS$=STP(STR(A[9]*100:"00000000"),2); TDS2$=STP(STR(A[1]*100:"0000000"),2)
2316 IF TRANS_TYPE$="CN" THEN TDS$=STP(STR(A[9]*100:"00000000"),2); TDS2$=STP(STR(A[1]*100:"0000000"),2),TDS$=STP(TDS$,0,"0"),TDS$=STR(NUM(TDS$)*-1),TDS2$=STP(TDS2$,0,"0"),TDS2$=STR(NUM(TDS2$)*-1)
2320 IF TRANS_TYPE$<>"CN" THEN PRINT (DEV_CHANNEL)"TDS*"+STP(TDS$,0,"0")+"*"+STP(TDS2$,0,"0"); CTT=CTT+1
2321 IF TRANS_TYPE$="CN" THEN PRINT (DEV_CHANNEL)"TDS*"+STP(TDS$,0,"0")+"*"+STP(TDS2$,0,"0"); CTT=CTT+1
2325 IF TRACK_NO$="" THEN TRACK_NO$="NA"
2343 ! IF TRANS_TYPE$<>"CN" AND SAC_C<>0 THEN PRINT (DEV_CHANNEL)"SAC*"+"C***ZZ*"+STR(SAC_C)+"**********"+"OTHER/MISC"; CTT=CTT+1; REM "  SSP# 149193 SSP307489      
2344 ! IF TRANS_TYPE$="CN" AND SAC_C<>0 THEN PRINT (DEV_CHANNEL)"SAC*"+"A*F050*FA*ZZ*"+STP(STR(SAC_C:"########"),2)+"**********"+"OTHER/MISC"; CTT=CTT+1; REM " SSP# 149193 SSP 307489                                                         
2345 IF SAC_F<>0 AND TRANS_TYPE$<>"CN" THEN PRINT (DEV_CHANNEL)"SAC*C*D240***"+STR(SAC_F)+"**********FREIGHT"; CTT=CTT+1 ! SSP 307489
2346 IF SAC_F<>0 AND TRANS_TYPE$="CN" THEN PRINT (DEV_CHANNEL)"SAC*A*D240***"+STP(STR(SAC_F:"########"),2)+"**********FREIGHT"; CTT=CTT+1 ! SSP 307489
2347 IF TAX_TOT<>0 AND TRANS_TYPE$<>"CN" THEN PRINT (DEV_CHANNEL)"SAC*"+"C*H750***"+STR(TAX_TOT)+"*******06***TAX_SALES TAX(STATE AND LOCAL)"; CTT=CTT+1
2348 IF TAX_TOT<>0 AND TRANS_TYPE$="CN" THEN PRINT (DEV_CHANNEL)"SAC*"+"A*H750***"+STP(STR(TAX_TOT:"########"),2)+"*******06***TAX_SALES TAX(STATE AND LO CAL)"; CTT=CTT+1
2375 C=C-1,CT=1+CTT
2380 PRINT (DEV_CHANNEL)"CTT*"+STR(C)+$0A$+"SE*"+STR(CT)+"*"+Q$(143,9)+$0A$+"GE*1*"+Q$(143,9)+$0A$+"IEA*1*"+Q$(143,9) ! DBD-400-DBSPT-164641-SSP307452
2390 RETURN 
2400 REM "Check for CIC - start using this on WO228481
2410 ITEM$=C1$(65,10)
2420 DIM FM1$(448); READ (Z[16],KEY=CUST$+ITEM$,DOM=2440)FM1$(1)
2430 IF FM1$(350,20)<>DIM(20) THEN ITEM$=STP(FM1$(350,20),1)
2450 RETURN 
2500 REM "Allowances & Charges
2520 ! F STP(C0$(65,10),1)="FREIGHT" THEN SAC_F=SAC_F+C[4]*100; !SSP 307489
2545 IF C[4]>0 THEN SAC_C=SAC_C+C[4]*100
2546 IF C[4]<0 THEN SAC_A=SAC_A+C[4]*100
2590 RETURN 
2600 REM "Get Tracking Number
2605 GET_KEY$=A$(92,8)+C1$(135,1)+C1$(47,3)
2610 READ (Z[17],KEY=GET_KEY$,DOM=2620)
2620 READ (Z[17],END=2690)SM4$
2640 IF SM4$(1,12)<>GET_KEY$ THEN GOTO 2690
2660 IF SM4$(101,8)=A$(7,8) THEN TRACK_NO$=MID(SM4$,383,60) ELSE GOTO 2620 ! ssp 257952
2670 CARRIER_NAME$=SM4$(37,12),CARRIER_ID$=SM4$(31,6),TRACKING_NO$=MID(SM4$,383,60) ! SSP 257952
2690 RETURN 
3000 REM "Write file to the editmp directory
3010 DOC_NUM$=A$(7,8)+Z1$(1,15-LEN(A$(7,8)))
3020 WRITE (Z[15],KEY="810"+DTT$+A$(15,10)+A$(7,8))FILE_NAME$
3030 CLOSE (DEV_CHANNEL)
3035 IF ISWIN THEN S2$="copy "+HWD+DLM+"export"+DLM+FILE_NAME$+" "+HWD+DLM+"editmp"+DLM; CALL "ZZ2CMD",X3$,X4$,S2$,"","IN",COMM_CODE,COMM_CODE$; GOTO 3050; REM "SSP146619, If NT call command processor so command will happen on server
3040 S2$="cp "+HWD+DLM+"export"+DLM+FILE_NAME$+" "+HWD+DLM+"editmp"+DLM; INVOKE S2$
3090 RETURN 
6700 REM "Write new control number out to ZZPARM file
6710 Q=NUM(Q$(143,9))+1; Q$(143,9)=STR(Q:"000000000")
6720 REMOVE (Z[13],KEY=PARM$)
6730 WRITE (Z[13],KEY=PARM$)IOL=0410
6790 RETURN 
9000 REM "Error
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "Ctrl logic
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End Program
9905 CLOSE (DEV_CHANNEL,ERR=9906)
9906 ERASE "EDILCK",ERR=9907
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "205074-Email from Barbara regarding EDI for Do It Best 
56001 REM "205640-We're having problems with invoices with discount terms
56002 REM "212942-Voicemail from Nancy regarding DIB EDI 810's  
56004 REM "228481-Modify four EDI Invoice programs to output the CIC in place
56005 REM "257952-Need larger tracking number field to replace 20 character   
56006 REM "307452-DBD-400-DBSPT-164641;Modify EDI810 programs ISA/GS Version  
56007 REM "307489-IMPL1032 DBD=462 Change to EDI 810 ED2IN4 program           
