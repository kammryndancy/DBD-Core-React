0010 REM "Accounts Payable Gateway Journal <PA2RAA>
0035 REM "5.7 - 04/21/13 - 12.9525 - jvv - SSP# 262361
0037 REM "262361-GUI conversion of Accounts Payable Gateway - Module GW_A/P  
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "Copied from AP2DBA, WO101028
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="PA2RAA",X1$="Accounts Payable Gateway Journal"
0120 DIM A$(119),A[13],B$(41),B[1],C$(227),C[2],D$(0)
0140 Z0$="###,###,###.00-",Z1$="#,###.00"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0220 W3=129,W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR PA0 - Accounts Payable GW Header file
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13]
0315 REM "IOLIST FOR PA2 - Accounts payable GW Lines File
0320 IOLIST B$,B[0]
0325 REM "IOLIST FOR AP4 - Vendor Masterfile
0330 IOLIST C$,C[0],C[1],C[2]
0335 REM "IOLIST FOR GL1 - G/L Masterfile
0340 IOLIST D$(1,LEN(D$))
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01L PA0...  02O PA2...  03O AP4...  04O GL1...  "
0515 Y$=Y$+"06O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 DIM F0$(100); READ (Z[6],KEY=X3$(9,3)+"A/P",DOM=0561)F0$
0565 DIM F1$(200); READ (Z[6],KEY=X3$(9,3)+"AP2A",DOM=0566)F1$
0590 J0=0; IF POS("J/C"=X4$(7),12)>0 THEN J0=-10
0605 REM "LET N0$="AP2DUA"; GOSUB 06900
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0750 REM "Check for restart record"
0755 IF K0$>"" THEN X3$(184,1)="1" ELSE GOTO 0800
0765 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W4,W4$,W5$
0770 CALL "ZZREST",X3$,Z[6],Z[1],W9,W3,W
0775 GOSUB 6000
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[4]; U=26,U0=4,U[0]=0,U[1]=4,U[2]=2,U[3]=10,U[4]=10,T0=4,T1=9,V1=4,V2=2,V3=2,V4=8,U0$="NY  ",T5$="Z   Z   Z   S   ",U5$="Fiscal Year",U6$="Accounting Period",U7$="Division",U8$="Vendor"; GOTO 0850
0850 DIM T[T0,T1]
0855 U8$=""
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1200 REM "If update then process records
1205 IF Q5<>1 THEN GOTO 1300
1210 CALL "ZZDISP","A",A$(7,10),"A/P",X3$,"","",25,20,X4$; PRINT @(32,20),A$(17,10),
1250 IF A$(111,2)="  " AND A$(119,1)="Y" THEN GOSUB 8000; REMOVE (U1,KEY=U$,DOM=1251)
1295 GOTO 1000
1300 REM "Read other data
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(7,10))IOL=0330
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 GOSUB 6200
1440 GOSUB 6300
1450 GOSUB 6400
1460 GOSUB 6500
1490 T=0
1500 REM "Print Data
1510 IF W+4>W0 THEN GOSUB 6000
1515 CALL "ZZDISP","AX",A$(7,10),"A/P",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1520 Y5$(14)=C$(11,30),Y5$(45)=A$(17,10)
1525 IF X3$(69,1)<>"Y" THEN Y5$(56)=FND$(A$(27,6)) ELSE CALL "ZZDISP","DX",A$(27,6),"   ",X3$,X$,"",0,0,X4$; Y5$(56)=X$
1530 Y5$(67)=A$(33,2)
1535 IF X3$(69,1)<>"Y" THEN Y5$(71)=FND$(A$(41,6)) ELSE CALL "ZZDISP","DX",A$(41,6),"   ",X3$,X$,"",0,0,X4$; Y5$(71)=X$
1540 Y5$(82)=STR(A[1]:Z0$),Y5$(98)=STR(A[7]:Z0$),Y5$(114)=STR(A[9]:Z0$)
1545 GOSUB 7300
1547 IF F0$(70,1)="Y" THEN Y5$(3)=A$(100,4)
1550 Y5$(14)=A$(47,15),Y5$(33)=A$(62,10),Y5$(46)="Hold?",Y5$(53)=A$(87,1)
1551 IF X3$(69,1)<>"Y" THEN Y5$(56)=FND$(A$(35,6)) ELSE CALL "ZZDISP","DX",A$(35,6),"   ",X3$,X$,"",0,0,X4$; Y5$(56)=X$
1552 CALL "ZZDISP","AX",A$(75,9),"AP2",X3$,X$,"",0,0,X4$; Y5$(67)=A$(72,3),Y5$(71)="Category: "+X$
1554 IF A$(107,1)="1" THEN Y5$(93)="Source company: "+A$(104,3)
1555 GOSUB 7300
1556 IF POS(" "<>A$(108,3))<>0 THEN GOSUB 7800
1558 IF F0$(68,1)<>"Y" THEN GOTO 1600 ELSE T9=0; FOR X=2 TO 5; T9=T9+ABS(A[X]); NEXT X; IF T9=0 THEN GOTO 1600
1560 FOR X=0 TO 3; Y5$(12+X*16,16)=F1$(8+X*20,16); NEXT X
1562 IF "J/C"="JOB COST" THEN Y5$(78)="Workmans Comp."
1565 GOSUB 7300
1570 FOR X=2 TO 5; IF A[X]<>0 THEN Y5$(14+(X-2)*16)=STR(A[X]:Z0$); NEXT X ELSE NEXT X
1575 IF A[8]<>0 THEN Y5$(78)=STR(A[8]:Z0$)
1580 IF X3$(330,1)="1" OR X3$(30,1)="M" THEN GOSUB 7300
1585 GOSUB 7300 ! SSP262361, special amounts need to print, works differently in AP2DBA which was used to create this report initially
1600 IF A$(119,1)=" " THEN A$(119,1)="N"
1605 Y5$(1)="Invoice created? "+A$(119,1)+" Import Source: "+A$(113,3)
1610 IF A$(111,2)>"  " THEN CALL "PA2SDA",X3$,X4$,A$(111,2),REASON$; Y5$(40)="*ON HOLD* "+REASON$
1620 GOSUB 7300
1800 REM "
1820 GOSUB 4000
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[1],T[T0,2]=T[T0,2]+A[7],T[T0,3]=T[T0,3]+A[9]
1915 FOR X=4 TO 7; T[T0,X]=T[T0,X]+A[X-2]; NEXT X
1920 T[T0,8]=T[T0,8]+A[8]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOSUB 7700; GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000; IF T0$<>"END" THEN GOSUB 7700
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOSUB 7700; GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000; IF T0$<>"END" THEN GOSUB 7700
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "lowest order break"
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
4000 REM "Print G/L Distribution
4005 DIM V[1]; C9=0,T4=0
4007 READ (Z[2],KEY=A$(1,26),DOM=4008)
4010 IF W>W0 THEN C9=2; GOSUB 6000
4020 K$=KEY(Z[2],END=4100); READ (Z[2],KEY=K$)IOL=0320
4025 IF K$(1,26)<>A$(1,26) THEN GOTO 4100
4030 DIM D$(100); READ (Z[4],KEY=B$(30,12),DOM=4031)IOL=0340
4035 IF B[0]=0 THEN GOTO 4020
4040 IF T4=0 OR C9>=2 THEN GOSUB 4200
4045 T4=T4+1
4050 Y5$(70)=B$(27,3); REM "Line number
4055 CALL "ZZDISP","AX",B$(30,12),"G/L",X3$,X$,"",0,0,X4$; Y5$(75)=X$,G0=LEN(X$)+1; REM "G/L account number
4060 Y5$(75+G0)=D$(13,35)
4065 Y5$(114)=STR(B[0]:Z0$)
4070 GOSUB 7300
4075 V[1]=V[1]+B[0]
4080 GOTO 4010
4100 REM "Print totals if more than one line
4110 IF T4<=1 THEN GOSUB 7300; GOTO 4195
4120 IF W+2>W0 THEN GOSUB 6000; GOSUB 4200
4130 Y5$(114)=T1$(1,LEN(Z0$)); GOSUB 7300
4140 Y5$(114)=STR(V[1]:Z0$); GOSUB 7300
4150 GOSUB 7300
4195 RETURN 
4200 REM "Line header
4205 GOSUB 7300
4210 Y5$(70)="Ln#",Y5$(75)="G/L Account",Y5$(117)="Distribution"
4220 GOSUB 7300
4245 RETURN 
5000 REM "End of Print
5005 IF Q5=1 THEN GOTO 5390
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END",W0$=""
5040 IF V1>0 THEN GOSUB 3120
5045 IF W+4>W0 THEN GOSUB 6000
5050 T=0,T$="Report"; GOSUB 7000
5060 REM "IF X3$(184,1)="1" THEN PRINT (W9) @(INT(W3/2)-39),"** THE UPDATE WAS INTERRUPTED AND WILL BE RESTARTED! SEE PAGE 1 FOR DETAILS **"; LET W=W+1
5200 REM "
5205 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
5210 IF V3$(2,1)<>"T" THEN GOSUB 5300
5220 Y4$=""
5290 GOTO 9900
5300 REM "Remove records from PA0/PA2 files if invoice created
5310 CALL "ZZPROM",".Y",X3$,Z,"Should these items be cleared from the GAP files if invoice created?","","",0
5320 ON Z GOTO 5330,5390
5330 Y$="03C AP4... 04C GL1...  06C ZZPARM  12C ZZP     "
5335 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
5350 REM "Do remove
5354 IF NOT(%GUI) THEN PRINT @(0,19),'CE', ! SSP262361
5355 IF NOT(%GUI) THEN CALL "ZZPROM","X0PA2RAA",X3$,Z,"","","",19 ! SSP262361 
5360 Q5=1; GOTO 0990
5390 RETURN 
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(14)="Vendor Name",Y5$(34)="Our",Y5$(47)="Invoice",Y5$(56)="Inv. Date",Y5$(67)="Trms",Y5$(74)="Disc.",Y5$(88)="Invoice",Y5$(101)="Available",Y5$(117)="G/L"
6120 GOSUB 7300
6125 Y5$(1)="Vendor Code",Y5$(14)="\Inv Comment",Y5$(32)="Reference",Y5$(47)="Number",Y5$(56)="Inv. Due",Y5$(67)="Bank",Y5$(73)="Expires",Y5$(89)="Amount",Y5$(102)="Discount",Y5$(115)="Distribution"
6130 GOSUB 7300
6140 Y5$(1)=T1$; GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6210 IF W+3>W0 THEN GOSUB 6000
6299 RETURN 
6300 REM "Break #2 header
6399 RETURN 
6400 REM "Break #3 header
6499 RETURN 
6500 REM "Break #4 header
6599 RETURN 
6900 REM 
6905 IF N0$="" OR V3$(1,1)<>"I" THEN GOTO 6990
6910 EXTRACT (Z[1],KEY="",DOM=6990)V0$,K0$
6915 IF K0$>"" THEN GOTO 6990
6930 Q$="+("+V0$(22,4)+"."+V0$(26,2)+")"
6935 CALL "ZZPROM","X0AUDITS",X3$,S3,Q$,"","",0; ON S3 GOTO 6940,5200,9900
6950 REMOVE (Z[1],KEY="")
6990 RETURN 
7000 REM "Total Login
7005 IF T<>0 AND T<>2 OR T$="" THEN GOTO 7100
7008 Y5$(82)=T1$(1,LEN(Z0$)),Y5$(98)=T1$(1,LEN(Z0$)),Y5$(114)=T1$(1,LEN(Z0$)); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(82)=STR(T[T,1]:Z0$),Y5$(98)=STR(T[T,2]:Z0$),Y5$(114)=STR(T[T,3]:Z0$)
7030 GOSUB 7300; FOR X=4 TO 8; T9=T9+ABS(T[T,X]); NEXT X; IF T9=0 THEN GOTO 7090
7035 FOR X=0 TO 3; Y5$(12+X*16,16)=F1$(8+X*20,16); NEXT X
7036 IF "J/C"="JOB COST" THEN Y5$(78)="Workmans Comp."
7040 GOSUB 7300
7050 FOR X=0 TO 4; IF T[T,4+X]<>0 THEN Y5$(14+X*16)=STR(T[T,4+X]:Z0$)
7055 NEXT X
7060 GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 IF X=0 THEN T[T-1,X]=T[T-1,X]+1,T[T,X]=0 ELSE T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.00")+" Minutes"
7280 PRECISION 2
7285 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300; Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)'LF', ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7700 REM "PRINT P/E DATE"
7710 IF A$(1,4)<>MID(P1$,9,4) THEN DIM P1$(105),W0$(0); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A$(1,4),DOM=7711)P1$
7720 D1$=P1$(NUM(A$(5,2))*6+15,6),D0$=D1$
7730 W0$="For Fiscal year "+A$(1,4)+", period "+A$(5,2)+" ending "+FND$(D0$)
7740 RETURN 
7800 REM "Print alternat currency & amounts
7805 FIND (Z[6],KEY="CUR"+A$(108,3),DOM=7845)C9$
7810 C8$="Amounts in "+FNS$(C9$(7,20))+":"
7820 Y5$(80-LEN(C8$))=C8$,Y5$(82)=STR(A[12]:Z0$),Y5$(98)=STR(A[13]:Z0$)
7825 GOSUB 7300
7845 RETURN 
8000 REM "Remove corresponding records from FO2 & FO3
8020 CALL "ZZLCPY",X3$,"D",Z[2],0,A$(1,26),0,0
8045 RETURN 
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9800 REM "
9810 Y4$="ZMENU"
9900 REM "End program
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU" ! SSP262361
9999 END 
56000 REM "262361-GUI conversion of Accounts Payable Gateway - Module GW_A/P  
