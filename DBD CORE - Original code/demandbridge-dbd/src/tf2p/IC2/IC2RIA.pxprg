0010 REM "I/C Status and History Report  <IC2RIA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 01/06/12 - 9.31 - tma - SSP# 251643
0037 REM "251643-Changes to Inventory Status and History report. Panel is    
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0110 X0$="IC2RIA",X1$="Inventory Status & History Report"
0120 DIM A$(187),A[40],C$(200),D[20],F$(100),S$(20),F[1]
0140 Z0$="##,##0-",Z1$="####,##0-",Z2$="###,##0-",Z3$="###.0-"
0210 T=1,T0=1,T1=1
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,"IC2RGA","",V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR IC0 - Inventory masterfile
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35],A[36],A[37],A[38],A[39],A[40]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15],D[16],D[17],D[18],D[19],D[20]
0360 IOLIST F$,F[0],F[1]
0399 IOLIST X$,X[0],X[1],X[2],X[3],X[4],X[5],X[6],X[7],X[8],X[9],X[10],X[11],X[12],X[13],X[14],X[15],X[16],X[17],X[18],X[19],X[20],X[21],X[22],X[23],X[24],X[25],X[26],X[27],X[28],X[29],X[30],X[31],X[32],X[33],X[34],X[35],X[36],X[37],X[38],X[39],X[40]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01X IC0... 03O AR1... 04O IC1... 05O IC9... 06O FMP... 07O ZY2... "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02X "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0540 READ (Z[13],KEY="STATI/C")F9$
0550 GOSUB 7900
0565 FOR X=0 TO 9; F1$=F1$+F9$(49+X*17,5); NEXT X
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 CALL "ZZRSEQ",X3$,"IC2RGA",V1$,U,U0,U1,U{ALL},Y{ALL},T0,V0,U0$,T5$,U5$,S9$,X9; ON X9 GOTO 0821,9920
0825 IF V1$(1,3)="IC5" THEN V0=2
0850 DIM T[T0,T1]; U5$(1,20)=Q4$
0860 IF V0=1 THEN V2$=V2$(1,10)+V2$(21,10)+V2$(11,10)+V2$(31,10)
0865 IF V0=2 THEN V2$=V2$(1,6)+V2$(7,10)+V2$(27,10)+V2$(17,10)+V2$(37,10)
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0950 REM " Position read"
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$; IF U1=Z[1] THEN GOTO 1150 ELSE U$="",X=0
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1200 REM "Reporting criteria
1210 P7=1; IF V$(91,1)="Y" AND A[19]<>0 THEN P7=A[19]
1220 IF R0$>"" THEN IF A$(61,3)<R0$(1,3) OR A$(61,3)>R0$(4,3) THEN GOTO 1000
1280 GOSUB 7600; IF X[0]=0 THEN GOTO 1000
1290 IF V$(92,1)="Y" THEN IF X[4]>0 AND X[1]-X[2]+X[3]>X[4] THEN GOTO 1000
1294 REM "P5 IS MINIMUM DAYS O/H FOR REPORT, P6 IS DAYS O/H FOR ITEM
1295 IF P5>0 THEN IF P6>P5+F[0] THEN GOTO 1000
1310 IF A$(1,10)<>C$(1,10) THEN GOSUB 7500
1320 IF A$(61,3)<>F$(2,3) THEN DIM F[1]; READ (Z[6],KEY="T"+A$(61,3),DOM=1321)IOL=0360
1400 REM "Subtotals"
1410 IF U2$="" THEN GOTO 1490 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1490 IF U2$="" AND V0=2 THEN GOSUB 6200 END_IF ; T=0,U2$=U9$ ! SSP#251643
1505 REM "X(1) IS ON HAND, X(2) IS COMMITTED
1510 IF W+3>W0 THEN GOSUB 6000
1515 Y5$(1)=A$(11,10)+C0$
1520 Y5$(29)=" "+A$(61,3),Y5$(35)=STR(A[19]:Z0$)+"/"+A$(120,4)
1530 FOR X=0 TO 5; Y5$(49+X*LEN(Z2$),LEN(Z2$))=STR(X[16-X]:Z2$); NEXT X
1540 Y5$(98)=STR(X[5]:Z2$)
1550 Y5$(107)=STR(X[1]-X[2]:Z2$)
1555 Y5$(116)=STR(X[8]/P4:"##0-")
1560 Y5$(122)=STR(X[3]:Z2$)
1595 GOSUB 7300
1600 REM "SECOND LINE STUFF
1620 Y5$(1)=A$(21,40)
1625 Y5$(30)=" "+A$(80,2)
1630 IF X[4]<>0 THEN Y5$(107)=STR(X[4]:Z2$)
1632 Y5$(116)=STR((P8+F[0])/P4:"##0-")
1635 Y5$(122)=STR(X[9]:Z2$)
1660 IF O1$>"" THEN Y5$(122)=FND$(O1$)
1690 GOSUB 7300
1695 GOSUB 7300
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3135 IF V0=2 AND POS(U0$(T+1,1)="23")>0 THEN GOSUB 6200 ! SSP#251643
3150 IF POS(U0$(T+1,1)="13")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5050 T=0; GOSUB 3100
5200 REM "
5295 GOTO 9900
6040 CALL "ZZHEAD","!"+X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6090 ELSE ON W5 GOTO 6041,9910
6050 IF WHSE$>"" THEN Y5$(52)="Warehouse Location: "+WHSE$; GOSUB 7300
6055 GOSUB 7300
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6105 ! SSP#251643 IF V0=2 THEN IF U9$(1,3)<>F$(2,3) THEN F$(1)="T"+U9$(1,3); READ (Z[6],KEY="T"+U9$(1,3),DOM=6106)IOL=0360
6110 ! SSP#251643 IF V0=2 THEN Y5$(10)="Product code: "+U9$(1,3)+" "+F$(5,15); GOSUB 7300; GOSUB 7300
6112 Y5$(98)=P9$,Y5$(110)="Qty   "+P4$; GOSUB 7300
6115 Y5$(1)="Item",Y5$(30)="P/C",Y5$(40)="Carton",Y5$(50)="+----------------- Sales History ------------+",Y5$(98)="Average  On hand  O/H   On Order"
6120 GOSUB 7300
6125 Y5$(1)="Code / Description",Y5$(30)="Plys",Y5$(42)="Pack",Y5$(53)=P8$,Y5$(98)="Mo Usage   RO Pt  RO pt   / B/O"
6130 GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6200 REM "Product code break
6210 IF U9$(1,3)<>F$(2,3) THEN F$(1)="T"+U9$(1,3); READ (Z[6],KEY="T"+U9$(1,3),DOM=6106)IOL=0360 ! SSP#251643
6220 GOSUB 7300; Y5$(4)="Product code: "+U9$(1,3)+" "+F$(5,15); GOSUB 7300 ! SSP#251643
6290 RETURN ! SSP#251643
6900 REM "SORT
6955 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6970 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 6971,9920
6990 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 IF V$(71,1)="1" THEN V2$=V2$(1,10)+V2$(21,10)+V2$(11,10)+V2$(31)
7460 READ (Z[13],KEY=X3$(9,3)+"G/LYE"+V$(72,4))F8$; P0=NUM(F8$(13,2))
7465 IF V$(91,1)="Y" THEN Z2$="####0.0-",W1$="Units converted to number of cartons"
7467 P4=1,P4$="Days"; IF V$(102,1)="W" THEN P4=7,P4$="Wks" ELSE IF V$(102,1)="M" THEN P4=30,P4$="Mos"
7470 P9$=""; FOR X=0 TO 0; P9$=P9$+STR(30*NUM(V$(84+X*2,2))/P4:"##0")+" "+P4$; NEXT X
7472 W0$="Printed as of period "+V$(72,4)+"/"+V$(76,2)+" ending "+FND$(V$(78,6))
7475 P5=NUM(V$(96,3)); IF P5<>0 THEN W2$="Only items with less than a "+STR(INT(P5/P4))+" "+P4$+" supply on hand"
7476 P8=NUM(V$(99,3)); IF P8>0 THEN W1$=W1$+"   Lead time of "+STR(P8/P4)+" "+P4$
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7486 WHSE$=""; IF V$(103,4)<>"    " THEN WHSE$=V$(103,4)
7488 Q=POS("P"=V$(59,12)); IF Q>0 THEN R0$=V$(59+Q,6)
7490 RETURN 
7500 REM "GET CUSTOMER CODE FORMATTED
7505 C0$=""; IF A$(1,10)=S$(1,10) THEN GOTO 7530 ELSE READ (Z[3],KEY=A$(1,10),DOM=7506)IOL=0330
7510 CALL "ZZDISP","AX",A$(1,10),"A/R",X3$,C0$,"",0,0,X4$; C0$="/ "+C0$+" "+C$(11,30)
7545 RETURN 
7550 REM "GET PRODUCT CODE DESCR
7600 REM "GATHER ON HAND, ETC FROM IC1
7601 REM "X(4) IS RO POINT IN DAYS
7605 DIM X[40]
7610 READ (Z[4],KEY=A$(1,20),DOM=7611)
7615 READ (Z[4],END=7645)IOL=0340; IF D$(1,20)<>A$(1,20) THEN GOTO 7645
7617 IF WHSE$>"" THEN IF D$(21,4)<>WHSE$ THEN GOTO 7615
7620 X[0]=X[0]+1,X[1]=X[1]+(D[3]+D[4]-D[5]+D[6]),X[2]=X[2]+D[7],X[3]=X[3]+D[8],X[4]=X[4]+D[0]
7621 X[9]=X[9]+D[11]
7625 GOSUB 7700
7640 GOTO 7615
7680 REM 
7682 IF P7<>1 THEN FOR X=1 TO 40; X[X]=X[X]/P7; NEXT X
7685 GOSUB 7800
7695 RETURN 
7700 REM "Read current year usage
7710 X9=POS("U"=F1$,5),Q$="R",X9$="U"
7715 D0$=V$(72,4)
7720 DIM G[14]; CALL "ZZPACK",X3$,Q$,F1$(X9+2,3),"",0,0,G{ALL},Z[5],X9$,D$(1,24)+D0$,F9$
7722 P9=NUM(V$(76,2)),Z=0
7725 Z=0; FOR X=P9 TO 1 STEP -1; X[11+Z]=X[11+Z]+G[X],Z=Z+1; NEXT X
7740 REM "PRIOR YEAR
7745 D0$=STR(NUM(D0$)-1:"0000")
7750 DIM G[14]; CALL "ZZPACK",X3$,Q$,F1$(X9+2,3),"",0,0,G{ALL},Z[5],X9$,D$(1,24)+D0$,F9$
7755 FOR X=P0 TO 1 STEP -1; X[11+Z]=X[11+Z]+G[X],Z=Z+1; NEXT X
7790 RETURN 
7800 REM "CALCULATE AVERAGES
7805 P6=999
7810 FOR X=0 TO 0
7815 Z=NUM(V$(84+X*2,2)); IF Z>12+P9 THEN Z=12+P9
7816 IF Z=0 THEN GOTO 7845
7820 Q=0; FOR Y=0 TO Z-1
7830 Q=Q+X[11+Y]
7835 NEXT Y
7840 X[5+X]=Q/Z
7845 NEXT X
7850 REM "CALC DAYS ON HAND
7855 FOR X=0 TO 0
7858 Q=X[1]-X[2]+X[3]; IF Q=0 AND X[5+X]<>0 THEN P6=0
7860 IF X[5+X]<>0 AND Q<>0 THEN X[8+X]=30*Q/X[5+X]; IF X[8+X]<P6 THEN P6=X[8+X]
7865 NEXT X
7890 RETURN 
7900 REM "GET CALENDAR
7910 Q$=F8$,P8$="",P9=NUM(V$(76,2))
7915 FOR X=0 TO 5
7920 Q=P9-X
7925 IF Q<1 THEN Q$(9,4)=STR(NUM(F8$(9,4))-1:"0000"); READ (Z[13],KEY=Q$(1,12),DOM=7928)Q$; Q=Q+12; GOTO 7930
7926 GOTO 7930
7928 CALL "ZZPROM",".4",X3$,0,"This report needs the G/L fiscal year for "+Q$(9,4)+" to be defined","","",0; GOTO 9900
7930 X$=Q$(15+Q*6,6)
7935 READ (Z[7],KEY=X$(3,2))X$
7940 P8$=X$(18,3)+"     "+P8$
7945 NEXT X
7949 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI<>0 AND Y4$<>"" THEN CALL Y4$,ERR=*NEXT
9928 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
