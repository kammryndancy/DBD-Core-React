0010 REM "Inventory Lot Setup <IC2LAA>
0015 REM "Prog Type: I -3.0A
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 03/25/16 - 11.301111 - tma - SSP# 278898
0037 REM "278898-WMS Bin Inquiry and Change:  Did a bin transfer and the qty 
0040 REM "Copyright 2016 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000; SETESC 9300
0110 X0$="IC2LAA",X1$="Inventory Lot Setup",K9$="",K9=0
0120 DIM Z0$(80,"-")
0130 K0=32,K1=5
0135 C9=-1
0140 M0$="###,###.00-",M1$="####,###.00-",M2$="###,##0-",M3$="###,##0-",M4$="#,###,##0-"
0145 GOSUB 8950
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12]
0311 IOLIST AA$(1),AA[0],AA[1],AA[2],AA[3],AA[4],AA[5],AA[6],AA[7],AA[8],AA[9],AA[10],AA[11],AA[12]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28],C[29],C[30]
0340 IOLIST D$
0345 IOLIST D1$
0360 IOLIST F$
0370 IOLIST G$
0375 IOLIST G5$,G4,G5 ! SSP#227493
0380 IOLIST G6$,G6,G7
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ICF...  02O AR1...  03O IC0...  04O FM0...  06O PO1...  07O AP4...  05O FS1...  08O FS2...  09O ZZPARM  13O ZZPARM  10O FMP...  11O AR4...  15O AR3...  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0535 DIM SEC$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=0536)SEC$
0540 READ (Z[13],KEY=X3$(9,3)+"F/M",DOM=*NEXT)FM_PARMS$; REM " SSP# 157344
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0620 IF POS(X3$(9,3)="066500",3)>0 THEN IF X3$(188,3)<>"066" THEN GOTO 9900
0640 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
0680 GOSUB 6000
0700 REM "Q1 PROCESSING
0704 IF Q1$="" THEN GOTO 0799
0705 IF Q1$(LEN(Q1$)-1)="**" THEN Q1$=Q1$(1,LEN(Q1$)-2) ELSE GOTO 0799
0706 DIM B$(400),B[15],C$(128),C[30],D$(224),E$(61),E[7],F$(314),G$(310),G[2],E$(168),E[7],F$(197),F[23],S$(50),D1$(200)
0707 DIM A$(269),A[12],AA$(269),AA[12]; REM "Keep AA$ & AA[ same as A$ & AA[ - WO-104689
0708 A$(1)=Q1$
0710 GOSUB 6000; Q9=1; GOSUB 1500; GOSUB 6200
0720 CALL "ZZPROM","4",X3$,Z,"","","V",X0
0730 GOTO 9700
1000 REM "
1010 Y8=1; CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1015 DIM V7$(28); V7$(1,1)="1",V7$(2,1)="1",V7$(5,1)="1",V7$(6,1)="1",V7$(12,1)="1",V7$(13,1)="1",V7$(20,1)="1"
1030 DIM B$(400),B[15],C$(128),C[30],D$(224),E$(61),E[7],F$(314),G$(310),G[2],E$(168),E[7],F$(197),F[23],S$(50),D1$(200)
1035 DIM A$(269),A[12],AA$(269),AA[12]; REM "Keep AA$ & AA[ same as A$ & A[
1045 A$(1,LEN(A1$))=A1$; IF A1$="" THEN A$(1,LEN(K9$))=K9$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1300,1350,1400,1500
1200 REM "Customer number
1202 IF Q1$>"" THEN A$(1)=Q1$; GOTO 1220
1205 IF LEN(A1$)>0 THEN A$(1,10)=A1$(1,10); GOTO 1220
1206 IF ABS(C0)<>2 THEN GOTO 1220
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,18,3,1,10,C0,"A/R","{1"+X$,K9$,"IC2LAA00","AR1","AR2MAB",""; IF C0=6 THEN GOSUB 6400; C0=1
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,10),DOM=1213)
1214 IF C0=-1 AND POS(" "<>A$(1,10))=0 THEN GOTO 1220
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1219 IF POS(" "<>A$(1,10))=0 THEN DIM B$(100); GOTO 1225
1220 IF V9<>-2 THEN FIND (Z[2],KEY=A$(1,10),DOM=1210)IOL=0320
1225 PRINT @(31,3),B$(11,30),
1240 GOTO 1120
1250 REM "Item number
1252 IF Q1$>"" THEN A$(1)=Q1$; GOSUB 6450; GOTO 1270
1253 CUST$=A$(1,10)
1255 IF LEN(A1$)>10 THEN A$(11,10)=A1$(11,10); GOTO 1270
1260 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,18,4,11,10,C0,"ICE","{2"+X$,CUST$,"IC2LAA02","IC0","IC2MAB",""; IF C0=6 THEN GOSUB 6400; C0=1
1261 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1262 READ (Z[1],KEY=A$(1,20),DOM=1263)
1263 IF ABS(C0)=4 THEN GOTO 9800
1264 IF ABS(INT(C0))=3 THEN GOTO 6800
1265 IF ABS(C0)=2 THEN GOTO 1290
1270 IF V9<>-2 THEN FIND (Z[3],KEY=A$(1,20),DOM=1260)IOL=0330
1272 IF A$(267,3)=DIM(3) THEN A$(267,3)=C$(109,3)
1275 PRINT @(31,4),C$(21,40),
1290 GOTO 1120
1300 REM "Location code
1305 IF LEN(A1$)>20 THEN A$(21,4)=A1$(21,4); GOTO 1320
1306 Q$="D"+S$(1,10)
1310 CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,18,5,21,4,C0,"","{2"+X$,Q$,"IC2LAA04","FM0D","FM2SKA",""; IF C0=6 THEN GOSUB 6400; C0=1
1311 IF ABS(C0)=2 AND LEN(A1$)>10 THEN A1$=A1$(1,10)
1312 READ (Z[1],KEY=A$(1,24),DOM=1313)
1320 IF V9<>-2 THEN FIND (Z[4],KEY="D"+S$(1,10)+A$(21,4),DOM=1310)IOL=0340
1325 GOSUB 7500
1340 GOTO 1120
1350 REM "Receipt date
1355 IF LEN(A1$)>24 THEN A$(25,6)=A1$(25,6); GOTO 1370
1357 Q$=A$(1,24)
1360 CALL "ZZENTR","DUX",A{ALL},A$,X4$,X3$,18,6,25,6,C0,"","{2"+X$,Q$,"IC2LAA06","ICF","",""; IF C0=6 THEN GOSUB 6400; C0=1
1361 IF ABS(C0)=2 AND LEN(A1$)>20 THEN A1$=A1$(1,20) ! [SSP-209199] SSP 222933
1362 READ (Z[1],KEY=A$(1,30),DOM=*NEXT) ! [SSP-209199] SSP 222933
1390 GOTO 1120
1400 REM "Sequence no
1407 Q$=A$(1,30)
1410 CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,44,6,31,2,C0,"","{2"+X$,Q$,"IC2LAA08","ICF","",""; IF C0=6 THEN GOSUB 6400; C0=1
1411 IF ABS(C0)=2 AND LEN(A1$)>24 THEN A1$=A1$(1,24)
1412 READ (Z[1],KEY=A$(1,32),DOM=1413)
1415 IF INT(ABS(C0))=3 THEN GOTO 6800 ELSE IF ABS(C0)=4 THEN GOTO 9900
1440 GOTO 1120
1500 REM "
1505 Z9=-1,C9=0,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,32),AA1$=""
1520 READ (Z[1],ERR=7200,KEY=A1$,DOM=1600)IOL=0310
1522 DIM AA$(269),AA[12]; AA1$=A$(1,32); READ (Z[1],KEY=AA1$)IOL=0311
1525 Z9=0,V7$=""; GOSUB 5800
1526 OLD_BIN_TYPE$="BK",OLD_KEY$=AA1$,BIN_LOC$=AA$(100,10),LOC_CODE$=AA$(21,4); CALL "UPDRFB;READBYKEY",ERR=*NEXT,0,0,BIN_LOC$,LOC_CODE$,SEG3$,SEG4$,SEG5$,RFB$,RFB{ALL},FOUNDRFB; IF NOT(FOUNDRFB) THEN CALL "UPDRFB;READBYKEY",0,0,BIN_LOC$,DIM(4),SEG3$,SEG4$,SEG5$,RFB$,RFB{ALL},FOUNDRFB ! SSP 277572
1528 IF FOUNDRFB THEN OLD_BIN_TYPE$=RFB$(67,2) ! SSP 277572
1530 IF Q9=1 THEN Q9=0; RETURN 
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1680 GOSUB 7450
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1805 IF X3$(185,1)="I" AND A$(1,109)<>AA$(1,109) AND STP(AA$(1,109),3)<>"" THEN GOSUB 7800; GOTO 6700
1810 IF X3$(185,1)="I" THEN CALL "ZZPROM","6A",X3$,Z,"","","",0; IF Z=1 THEN Z9=1; GOSUB 7400; GOTO 2000 ELSE GOTO 6700 ! SSP#278898
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700,1821
1825 GOTO 6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 GOSUB 7400
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 SETERR 9000; X$="",Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2038 IF X0<LEN(V7$) THEN V7$(X0+1,1)=""
2039 IF X3$(185,1)="I" THEN ON X0 GOTO 2600,3350,3600,3650,3700,3750,1800
2040 ON X0 GOTO 2100,3250,3300,3800,2150,2200,2250,2300,2350,2400,2450,2500,2550,2600,2650,2700,2750,2800,2850,2900,3850,2950,3000,3050,3100,3150,4000,3200,4050,3350,3400,3450,3500,3550,3600,3650,3700,3750,1800
2100 REM "Receiving report
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,8,33,8,C0,"","{3"+X$,"","IC2LAA10","PO7","PO2MAC",""; IF C0=6 THEN GOSUB 6400; C0=1
2140 GOTO 2020
2150 REM "P/O number
2155 P9$=A$(41,9)
2160 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,18,9,41,9,C0,"P/O","{3"+X$,"","IC2LAA12","PO1","PO2MAA",""; IF C0=6 THEN GOSUB 6400; C0=1
2175 IF P9$<>A$(41,9) THEN GOSUB 7550
2190 GOTO 2020
2200 REM "P/O date
2210 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,41,9,50,6,C0,"","{3"+X$,"","IC2LAA14","","",""
2240 GOTO 2020
2250 REM "Factory Job
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,66,9,56,12,C0,"","{3"+X$,"","IC2LAA16","","",""
2290 GOTO 2020
2300 REM "Vendor Number
2310 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,18,10,68,10,C0,"A/P","{3"+X$,"","IC2LAA18","AP4","AP2MAB",""; IF C0=6 THEN GOSUB 6400; C0=1
2320 IF V9<>-2 THEN FIND (Z[7],KEY=A$(68,10),DOM=2310)IOL=0370
2325 DIM D1$(200); FIND (Z[4],KEY="P"+A$(68,10)+A$(78,4),DOM=2326)IOL=0345
2330 GOSUB 7600
2340 GOTO 2020
2350 REM "Plant number
2360 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,30,10,78,4,C0,"","{3"+X$,"","IC2LAA20","FM0","FM2SKA",""; IF C0=6 THEN GOSUB 6400; C0=1
2365 IF A$(78,4)="    " THEN GOTO 2371
2370 IF V9<>-2 THEN FIND (Z[4],KEY="P"+A$(68,10)+A$(78,4),DOM=2360)IOL=0345
2375 GOSUB 7600
2390 GOTO 2020
2400 REM "Starting number
2410 CALL "ZZENTR","R",A{ALL},A$,X4$,X3$,18,11,82,9,C0,"","{3"+X$,"","IC2LAA22","","",""
2440 GOTO 2020
2450 REM "Ending number
2460 CALL "ZZENTR","R",A{ALL},A$,X4$,X3$,18,12,91,9,C0,"","{3"+X$,"","IC2LAA24","","",""
2490 GOTO 2020
2500 REM "Total cost
2510 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,41,11,0,621,C0,M0$,"{3"+X$,"","IC2LAA26","","",""
2540 GOTO 2020
2550 REM "Total Freight
2560 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,66,11,1,621,C0,M0$,"{3"+X$,"","IC2LAA28","","",""
2590 GOTO 2020
2600 REM "Bin number
2610 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,41,12,100,10,C0,"","{3"+X$,"","IC2LAA30","","",""
2635 IF X3$(185,1)<>"I" THEN IF ABS(C0)<2 THEN X0=X0+2; REM "WO114389
2640 GOTO 2020
2650 REM "Order number
2660 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,66,12,110,8,C0,"O/P","{3"+X$,"","IC2LAA32","FS1","FM2ODA",""; IF C0=6 THEN GOSUB 6400; C0=1
2670 IF V9<>-2 THEN FIND (Z[5],KEY=A$(110,8),DOM=2671)
2690 GOTO 2020
2700 REM "Line number on order
2710 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,76,12,118,3,C0,"","{3"+X$,"","IC2LAA34","FS2","FM2ODB",""; IF C0=6 THEN GOSUB 6400; C0=1
2720 IF V9<>-2 THEN FIND (Z[8],KEY=A$(118,3),DOM=2721)IOL=0360
2740 GOTO 2020
2750 REM "Primary unit
2760 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,14,121,4,C0,"","{3"+X$,"","IC2LAA36","","",""
2765 D2=1; GOSUB 7650
2790 GOTO 2020
2800 REM "Packaging unit
2810 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,4,15,125,4,C0,"","{3"+X$,"","IC2LAA38","","",""
2815 IF A$(125,4)="    " THEN A[2]=0
2820 GOSUB 7650
2840 GOTO 2020
2850 REM "Units per package
2855 IF A$(125,4)="    " THEN GOTO 2020
2860 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,18,15,2,601,C0,M3$,"{3"+X$,"","IC2LAA40","","",""
2865 GOSUB 7700
2890 GOTO 2020
2900 REM "Units per primary/package unit
2910 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,18,16,3,601,C0,M3$,"{3"+X$,"","IC2LAA42","","",""
2915 GOSUB 7700
2940 GOTO 2020
2950 REM "Quantity
2960 CALL "ZZENTR","NM",A{ALL},A$,X4$,X3$,6,19,4,702,C0,M4$,"{3"+X$,"","IC2LAA44","","TPALAA",""; IF C0=6 THEN GOSUB 6400; C0=1
2970 GOSUB 7700
2990 GOTO 2020
3000 REM "Sales Price
3010 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,46,14,5,621,C0,M0$,"{3"+X$,"","IC2LAA46","","",""
3020 IF A[5]=0 THEN PRINT @(66,14),'CL',; A$(129,4)="",A[6]=0; GOTO 2020
3040 GOTO 2020
3050 REM "U/M
3055 IF A[5]=0 THEN GOTO 2020
3060 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,66,14,129,4,C0,"","{3"+X$,"","IC2LAA48","ZZPARM","",""; IF C0=6 THEN GOSUB 6400; C0=1
3070 IF POS(" "<>A$(129,4))>0 THEN FIND (Z[9],KEY="U/M"+A$(129,4),DOM=3060)IOL=0375 ELSE IF A[5]<>0 THEN GOTO 3060
3075 GOTO 2020
3080 X0=X0-1; GOTO 3050
3100 REM "Units per u/m
3102 IF POS(" "<>A$(129,4))=0 THEN A[6]=0; PRINT @(72,14),'CL',; GOTO 2020 ELSE FIND (Z[9],KEY="U/M"+A$(129,4),DOM=3080)IOL=0375
3105 IF G5$(20,1)="Y" AND G4<>0 THEN A[6]=G4 ELSE IF G5$(20,1)="Y" AND G4=0 THEN A[6]=G5 ! SSP#227493
3107 IF G5$(20,1)="Y" THEN PRINT @(72,14),A[6]:M2$,; GOTO 2020 ! SSP#227493
3110 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,72,14,6,501,C0,M2$,"{3"+X$,"","IC2LAA50","","",""
3120 IF A[6]<=0 AND A[5]<>0 THEN A[6]=1; GOTO 3110
3140 GOTO 2020
3150 REM "Customer PO
3160 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,46,15,133,15,C0,"","{3"+X$,"","IC2LAA52","","",""
3190 GOTO 2020
3200 REM "Invoice number
3210 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,71,15,148,7,C0,"AR6","{3"+X$,"","IC2LAA54","","",""
3240 GOTO 2020
3250 REM "SHORT CARTON
3260 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,41,8,155,1,C0,"","{3"+X$,"","IC2LAA67","","",""
3290 GOTO 2020
3300 REM "Audit Control #
3310 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,61,8,196,6,C0,"","{3"+X$,"","IC2LAA69","","",""
3340 GOTO 2020
3350 REM "Comments, open for I/C,S3 on WO114389
3360 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,38,17,156,40,C0,"","{3"+X$,"","IC2LAA71","","",""
3390 GOTO 2020
3400 REM "Lot Number
3402 REM CALL "ZZ2PRP","WW",RESULT$,DATE$; IF RESULT$="Y" THEN GOTO 2020
3405 X$="Lot Number assigned during receiving - normally not changed"
3410 CALL "ZZENTR","AZS",A{ALL},A$,X4$,X3$,37,18,234,8,C0,"","{3"+X$,"","IC2LAA73","","",""
3440 GOTO 2020
3450 REM "Storage Expiration Date
3455 X$="Storage Expiration Date"
3460 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,51,18,250,6,C0,"","{3"+X$,"","IC2LAA75","","",""
3490 GOTO 2020
3500 REM "Product Code
3502 IF P0$(64,1)<>"Y" THEN GOTO 2020
3505 X$="Product Code"
3510 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,66,18,242,3,C0,"","{3"+X$,"","IC2LAA77","","",""
3520 IF POS(" "<>A$(242,3))>0 THEN FIND (Z[10],KEY="T"+A$(242,3),DOM=3510)
3540 GOTO 2020
3550 REM "Commission Code
3552 IF P0$(64,1)<>"Y" THEN GOTO 2020
3555 X$="Commission Code"
3560 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,74,18,245,5,C0,"","{3"+X$,"","IC2LAA79","","",""
3570 IF POS(" "<>A$(245,5))>0 THEN FIND (Z[11],KEY=A$(245,5),DOM=3560)
3590 GOTO 2020
3600 REM "Standard Unit Cost"
3601 IF MID(SEC$,85,1)<>"Y" AND X3$(185,1)="I" THEN GOTO 2020 ! SSP#220366
3602 IF P0$(64,1)<>"Y" THEN GOTO 2020
3605 X$="Standard Unit Cost"
3610 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,5,21,7,621,C0,M0$,"{3"+X$,"","IC2LAA81","","",""
3620 IF A[7]=0 THEN A[8]=0,A$(256,4)=""; PRINT @(22,21),"    ",@(32,21),A[8]:M2$,; GOTO 2020
3640 GOTO 2020
3650 REM 
3651 IF MID(SEC$,85,1)<>"Y" AND X3$(185,1)="I" THEN GOTO 2020 ! SSP#220366
3652 IF P0$(64,1)<>"Y" OR A[7]=0 THEN GOTO 2020
3653 IF A[7]=0 THEN GOTO 3680
3655 X$="Standard Unit Cost Unit of Measure"
3660 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,22,21,256,4,C0,"","{3"+X$,"","IC2LAA83","","",""
3670 IF POS(" "<>A$(256,4))>0 THEN FIND (Z[13],KEY="U/M"+A$(256,4),DOM=3660)IOL=0380 ELSE IF A[7]<>0 THEN GOTO 3660
3680 GOTO 2020
3690 X0=X0-1; GOTO 3650
3700 REM 
3701 IF MID(SEC$,85,1)<>"Y" AND X3$(185,1)="I" THEN GOTO 2020 ! SSP#220366
3702 IF P0$(64,1)<>"Y" THEN GOTO 2020
3703 IF A[7]=0 THEN GOTO 3740
3704 FIND (Z[13],KEY="U/M"+A$(256,4),DOM=3690)IOL=0380
3705 IF MID(G6$,20,1)<>"Y" THEN GOTO 3710
3706 IF G6<>0 THEN A[8]=G6 ELSE A[8]=G7
3707 PRINT @(32,21),A[8]:M2$,; GOTO 2020
3708 X$="Standard Unit Cost Quantity per Unit of Measure"
3710 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,32,21,8,501,C0,M2$,"{3"+X$,"","IC2LAA85","","",""
3720 IF A[8]<=0 AND A[1]<>0 THEN A[8]=1; GOTO 3710
3740 GOTO 2020
3750 REM " FMS PRICE - SSP# 157344
3752 IF MID(FM_PARMS$,306,1)<>"Y" THEN GOTO 3790
3755 X$="This FMS Price will update to FMZ if entered here"
3760 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,52,21,9,621,C0,M0$,"{3"+X$,"","IC2LAA81","","",""
3790 GOTO 2020
3800 REM "Primary Salesperson
3801 IF A$(1,10)=DIM(10) THEN GOTO 3840
3805 X$="You Must Enter a Valid Salesperson Code"
3810 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,74,8,263,4,C0,"","{3"+X$,"","AR2MAB46","AR3","AR2MBB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3810,3811
3815 READ (Z[15],KEY=A$(263,4),DOM=3800)
3840 GOTO 2020
3850 REM price class 190538
3860 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,18,18,267,3,C0,"","{3"+X$,"","IC2LAA36","FMPI","IC2MEA",""; IF ABS(C0)>THEN THEN GOSUB 6400; ON C0 GOTO 3860,3861; REM SSP 204015
3870 IF A$(267,3)<>"   " THEN FIND (Z[10],KEY="I"+A$(267,3),DOM=3850)PRICE_CLASS$
3890 GOTO 2020
4000 REM Storage Amount ssp 202394
4010 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,41,16,11,621,C0,M0$,"{3"+X$,"","IC2LAA46","","",""
4040 GOTO 2020
4050 REM Finance Amount ssp 202394
4060 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,68,16,12,621,C0,M0$,"{3"+X$,"","IC2LAA46","","",""
4090 GOTO 2020
5800 REM "Alt sort keys
5815 FIND (Z[6],KEY=A$(41,9),DOM=5816)IOL=0360
5820 FIND (Z[7],KEY=A$(68,10),DOM=5821)IOL=0370
5825 FIND (Z[4],KEY="P"+A$(68,10)+A$(78,4),DOM=5826)IOL=0345
5835 FIND (Z[8],KEY=A$(118,3),DOM=5836)IOL=0360
5840 FIND (Z[9],KEY="U/M"+A$(129,4),DOM=5841)IOL=0375
5890 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(1,3+V0),"Customer number:",@(5,4+V0),"Item number:",@(3,5+V0),"Location code:",@(4,6+V0),"Receipt date:",@(31,6+V0),"Sequence no:",@(0,7+V0),Z0$,
6070 PRINT @(0,8+V0),"Receiving report:",@(6,9+V0),"P/O number:",@(31,9+V0),"P/O date:",@(53,9+V0),"Factory Job:",@(4,10+V0),"Vendor/Plant:",@(1,11+V0),"Starting number:",@(29,11+V0),"Total cost:",@(55,11+V0),"Total Frt:",@(3,12+V0),"Ending number:",@(29,12+V0),"Bin number:",@(56,12+V0),"On Order:",@(0,13+V0),"--------------------------------+-----------------------------------------------",@(4,14+V0),"Primary unit:",@(32,14+V0),"|Sales Price:",@(61,14+V0),"U/M:",@(9,15+V0),"per",@(32,15+V0),"|Customer PO:",@(62,15+V0),"Invoice:",@(4,16+V0),"Items in",@(7,18+V0),"Quantity",
6071 PRINT @(4,18+V0),"Price Class:"
6072 PRINT @(33,16+V0),"Storage:",@(60,16+V0),"Finance:",; REM ssp 202394
6080 PRINT @(27,8),"Short Carton?",@(49,8),"Audit Con#:",@(68,8),"SLSP:",; REM " ssp186762
6085 PRINT @(31,17),"Cmts:",
6090 PRINT @(31,18),"Lot#:",@(46),"SED:",
6100 IF P0$(64,1)="Y" THEN PRINT @(62),"PC:",@(70),"CC:",@(0,21),"SUC:",@(17),"U/M:",@(27),"QTY:",
6110 IF MID(FM_PARMS$,306,1)="Y" THEN PRINT @(41,21),"FMS Price: ",; REM " ssp# 157344
6111 PRINT @(63,21),"Cst Supp PO:",; REM SSP 198988"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6205 REM "D9 controls returns for displays of pieces of the screen
6215 GOSUB 6450
6220 PRINT @(18,8),A$(33,8),@(41,8),A$(155,1),@(61,8),A$(196,6)
6224 CALL "ZZDISP","A   ",A$(41,9),"P/O",X3$,"","",18,9,X4$
6228 PRINT @(41,9),FND$(A$(50,6)),@(66,9),A$(56,12),
6229 GOSUB 7600
6232 CALL "ZZDISP","A   ",A$(68,10),"A/P",X3$,"","",18,10,X4$
6236 PRINT @(30,10),A$(78,4),@(18,11),A$(82,9),@(18,12),A$(91,9),
6237 IF D9=1 THEN D9=0; RETURN ; REM "P/O stuff
6240 CALL "ZZDISP","NM  M",STR(A[0]:M0$),"",X3$,"","",41,11,X4$
6244 CALL "ZZDISP","NM  M",STR(A[1]:M0$),"",X3$,"","",66,11,X4$
6248 PRINT @(41,12),A$(100,10),
6252 CALL "ZZDISP","A   ",A$(110,8),"O/P",X3$,"","",66,12,X4$
6256 PRINT @(76,12),A$(118,3),@(18,14),A$(121,4),@(4,15),A$(125,4),
6257 GOSUB 7650
6260 CALL "ZZDISP","NM  M",STR(A[2]:M3$),"",X3$,"","",18,15,X4$
6264 CALL "ZZDISP","NM  M",STR(A[3]:M3$),"",X3$,"","",18,16,X4$
6268 CALL "ZZDISP","NM  M",STR(A[4]:M4$),"",X3$,"","",6,19,X4$
6272 CALL "ZZDISP","NM  M",STR(A[5]:M0$),"",X3$,"","",46,14,X4$
6276 PRINT @(66,14),A$(129,4),
6280 CALL "ZZDISP","NM  M",STR(A[6]:M2$),"",X3$,"","",72,14,X4$
6284 PRINT @(46,15),A$(133,15),
6288 CALL "ZZDISP","A   ",A$(148,7),"AR6",X3$,"","",71,15,X4$
6289 IF A[10]=1 THEN PRINT @(75,21),"Yes" ELSE PRINT @(75,21),"No "; REM SSP 198988
6292 GOSUB 7700
6293 PRINT @(38,17),A$(156,40),
6295 PRINT @(37,18),A$(234,8),@(51,18),FND$(A$(250,6)),@(18,18),A$(267,3)
6296 PRINT @(74,8),A$(263,4),; REM " SSP 186762
6300 IF P0$(64,1)="Y" THEN PRINT @(66,18),A$(242,3),@(74,18),A$(245,5),@(5,21),A[7]:M0$,@(22,21),A$(256,4),@(32,21),A[8]:M2$,
6305 PRINT @(41,16),A[11]:M0$,@(68,16),A[12]:M0$; REM SSP 202394
6310 IF MID(FM_PARMS$,306,1)="Y" THEN PRINT @(52,21),A[9]:M0$,; REM " SSP# 157344
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A   ",A$(1,10),"A/R",X3$,"","",18,3,X4$
6464 PRINT @(31,3),B$(11,30),
6468 CALL "ZZDISP","A   ",A$(11,10),"I/C",X3$,"","",18,4,X4$
6472 PRINT @(31,4),C$(21,40),@(18,5),A$(21,4),
6474 GOSUB 7500
6475 PRINT @(18,6),FND$(A$(25,6)),@(44,6),A$(31,2),
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 A1$=""; GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[2],KEY=A$(1,10),DOM=6606)IOL=0320
6610 FIND (Z[3],KEY=A$(11,10),DOM=6611)IOL=0330
6615 FIND (Z[4],KEY=A$(21,4),DOM=6616)IOL=0340
6680 IF A$(267,3)=DIM(3) THEN A$(267,3)=C$(109,3)
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 IF CHG$="Y" AND AA$(1,109)<>A$(1,109) THEN GOSUB 7900; GOTO 6790; REM RESET FRT & COSTS AND WRITE NEW RECORDS
6780 WRITE (Z[1],KEY=A1$)IOL=0310
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 PRINT 'RB',; CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6865 GOTO 1100
6870 GOTO 6895
6890 GOTO 1515
6895 IF A1$="" THEN GOTO 6860 ELSE PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=6896)
6898 ON S9 GOTO 6810,1100
7200 REM "Forced read
7210 IF ERR<>0 THEN GOTO 9000
7220 CALL "ZZREAD",Z[1],A1$,A$,A{ALL},S8; ON S8 GOTO 7221,1630
7230 GOSUB 6200
7240 CALL "ZZPROM","E0",X3$,0,"","","",0; GOTO 7290
7250 CALL "ZZPROM","E0A",X3$,0,"","","",0; GOTO 7290
7290 GOTO 1630
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 IF X0>0 THEN X0=X0-0
7395 C0=3; RETURN 
7400 REM "EXTRACT
7410 EXTRACT (Z[1],ERR=7200,KEY=A1$,DOM=7440)IOL=0310
7420 C9=1,Z9=1; GOSUB 5800
7440 RETURN 
7450 REM "EXTRACT NEW ENTRY
7460 WRITE (Z[1],KEY=A1$,DOM=7250)IOL=0310; GOTO 7400
7490 RETURN 
7500 REM "Print location
7505 L9$=FNS$(D$(16,35))
7510 IF POS(" "<>D$(111,16))<>0 THEN L9$=L9$+" "+FNS$(D$(111,16))+", "+D$(127,2)
7512 IF LEN(L9$)>37 THEN L9$=D$(16,35)
7515 PRINT @(31,5),'CL',L9$,
7545 RETURN 
7550 REM "DEFAULT P/O STUFF IF P/O NUMBER CHANGES
7555 FIND (Z[6],KEY=A$(41,9),DOM=7595)IOL=0360
7560 A$(50,6)=F$(192,6),A$(56,12)=F$(248,12),A$(68,14)=F$(7,14)
7565 FIND (Z[7],KEY=A$(68,10),DOM=7566)IOL=0370
7570 DIM D1$(200); FIND (Z[4],KEY="P"+A$(68,10)+A$(78,4),DOM=7571)IOL=0345
7580 D9=1; GOSUB 6228
7595 RETURN 
7600 REM "Display Vendor, Plant name
7610 DIM G1$(40)
7615 IF POS(" "<>D1$(111,16))=0 THEN D1$(111,18)=G$(106,18)
7620 IF POS(" "<>D1$(111,16))=0 THEN G1$(1)=G$(11,35) ELSE G1$(1)=FNS$(G$(11,35))+"-"+FNS$(D1$(111,16))+", "+D1$(127,2)
7635 PRINT @(36,10+V0),G1$,; G1$=""
7645 RETURN 
7650 REM "Display units properly
7655 PRINT @(13,15+V0),'SB',A$(121,4),'SF',
7660 IF A$(125,4)="    " THEN PRINT @(18,15),S$(1,LEN(M3$)),@(13,16+V0),A$(121,4), ELSE PRINT @(13,16+V0),A$(125,4),
7664 REM "Handle case prior to entry of A$(125,4)
7680 GOSUB 7700
7695 RETURN 
7700 REM "Print quantity explantion line
7710 IF A[4]=1 THEN Q$=FNC$(A$(121,4))+"  of  " ELSE Q$=FNC$(A$(121,4))+"s  of  "
7715 IF A$(125,4)="    " THEN Q$=Q$+FNR$(STR(A[3]:M3$)) ELSE Q$=Q$+FNR$(STR(A[2]:M3$))+" "+FNC$(A$(125,4))+"s  of  "+FNR$(STR(A[3]:M3$))
7725 IF A$(125,4)="    " THEN Q=A[4]*A[3] ELSE Q=A[4]*A[2]*A[3]
7740 PRINT @(17,19+V0),'CL',Q$,@(54),"  =  "+STR(Q:M4$),
7795 RETURN 
7800 REM "Transfer qty
7810 CHG$="",X$="Do you want to transfer part of the quantity?"
7820 CALL "ZZPROM","Y",X3$,Z,X$,"","",0; IF Z=1 THEN GOTO 7890
7830 CALL "ZZENTR","NM",A{ALL},A$,X4$,X3$,6,19,4,702,C0,M4$,"{3"+X$,"","IC2LAA44","","TPALAA",""; IF C0=4 THEN GOTO 1000 ELSE IF C0=6 THEN GOSUB 6400; C0=1
7840 IF A[4]=0 OR A[4]>=AA[4] THEN GOTO 7830
7850 GOSUB 7700
7860 CHG$="Y"
7870 X$="A quantity of "+STR(AA[4]-A[4])+" is left in old location ("+AA$(100,10)+")"
7880 CALL "ZZPROM","0",X3$,Z,X$,"","",0; IF Z<>0 THEN A[4]=AA[4]; GOSUB 6200; GOTO 7800
7890 RETURN 
7900 REM "Write out new records from transfer
7903 ! IF AA$(125,4)="    " THEN P5=AA[4]*AA[3] ELSE P5=AA[4]*AA[2]*AA[3] ! SSP#250787 SSP 274589
7905 ! CALL "FM2EXT",Z(13),1,AA$(121,4),AA(3),P5,E,AA(0),4; A[0]=A[4]*E ! SSP#250787 274589
7908 SAVE_PRC=PRC; PRECISION 14 ! SSP 274589
7910 CHG=A[4]/AA[4],A[0]=A[0]*CHG,A[1]=A[1]*CHG ! SSP#250787 274589
7915 A[11]=A[11]*CHG,A[12]=A[12]*CHG; REM "200525
7916 PRECISION SAVE_PRC ! SSP 274589
7918 GOSUB CHECK_NUMBERING; IF NUM_OK THEN A$(82,9)=STR(TR_ST),A$(91,9)=STR(TR_END)
7920 WRITE (Z[1],KEY=A1$,DOM=7930)IOL=0310; GOTO 7940 ! WRITE NEW RECORD
7930 KEY$=A1$(1,32); CALL "IC2LCC",X3$,X4$,Z[1],31,2,LAST_ONE$,KEY$; A1$(1,32)=KEY$,A$(1,32)=KEY$; GOTO 7920
7950 NEW_KEY$=A$(1,32); NEW_BIN_TYPE$="BK",BIN_LOC$=A$(100,10),LOC_CODE$=A$(21,4); CALL "UPDRFB;READBYKEY",ERR=*NEXT,0,0,BIN_LOC$,LOC_CODE$,SEG3$,SEG4$,SEG5$,RFB$,RFB{ALL},FOUNDRFB; IF NOT(FOUNDRFB) THEN CALL "UPDRFB;READBYKEY",0,0,BIN_LOC$,DIM(4),SEG3$,SEG4$,SEG5$,RFB$,RFB{ALL},FOUNDRFB ! SSP 277572
7955 IF FOUNDRFB THEN NEW_BIN_TYPE$=RFB$(67,2) ! SSP 277572
7960 A[4]=AA[4]-A[4],A[0]=AA[0]-A[0],A[1]=AA[1]-A[1],A$(100,10)=AA$(100,10),A$(156,40)=AA$(156,40)
7965 A[11]=AA[11]-A[11],A[12]=AA[12]-A[12]; REM "200525
7968 IF NUM_OK THEN A$(82,8)=STR(ORG_ST),A$(91,9)=STR(ORG_END); NUM_OK=0
7970 A1$=AA$(1,32),A$(1,32)=AA$(1,32)
7980 WRITE (Z[1],KEY=A1$)IOL=0310 ! Write OLD record
7985 IF NEW_BIN_TYPE$="PK" AND OLD_BIN_TYPE$<>"PK" THEN CALL "UPDICF;PRIO_PICK_BIN",Z[1],OLD_KEY$,NEW_KEY$,P_OK; IF P_OK THEN A1$(1,32)=NEW_KEY$ ! 277572
7990 RETURN 
8500 CHECK_NUMBERING:
8505 NUM_OK=0
8510 IF POS(" "<>AA$(82,9))=0 THEN RETURN 
8520 SN=NUM(AA$(82,9),ERR=*RETURN),EN=NUM(AA$(91,9),ERR=*NEXT) ! SSP 229144
8530 IF AA$(125,4)="    " THEN P5=A[4]*AA[3] ELSE P5=A[4]*AA[2]*AA[3]
8540 TR_ST=SN,TR_END=SN+P5-1
8550 ORG_ST=TR_END+1,ORG_END=EN
8560 NUM_OK=1
8695 RETURN 
8699 ! 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8925 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8930 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8949 DEF FNC$(Z9$)=Z9$(1,POS(" "=Z9$+" ")-1)
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"; CLOSE (14); OPEN (14)"ZZPARM"
8970 FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN Q1$=""; GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 Y8=2
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "190538-Item price class pricing by bin identifier.                 
56001 REM "190538-Item price class pricing by bin identifier.        
56002 REM "202394-Invoice storage and finance charges                         
56004 REM "209199-ICF table column name mismatch between Oracle and TopForm   
56005 REM "220366-SUC field in IC2LAA(SIC-L1) is being triggered by old 3271  
56010 REM "204015-IC utility for SIC not setup in GUI
56011 REM "222933-Since we received our upgrade this morning, we've noticed    
56012 REM "236031-Issue w/ quick Lot Bin Change for numbered items. If you    
56014 REM "229144-WMS - Bin Transfers.                                        
56015 REM "274589-Quick lot bin change.  Does not split value correctly for   
56016 REM "277572-CHUI Quick lot bin change - cater for picking between       
56017 REM "278898-WMS Bin Inquiry and Change:  Did a bin transfer and the qty 
