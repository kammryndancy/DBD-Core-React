0010 REM "Area Code/Exchange Utility <AR2UTZ>
0020 SETESC 9300; SETERR 9000
0035 REM "4.1 - 07/24/97 - 10.02 - dmm - SSP# 088141
0040 REM "Copyright 1997 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AR2UTZ",X1$="Area Code/Exchange Utility",K9$="",K9=0
0120 DIM Z0$(80,"-")
0130 K0=9,K1=1
0135 C9=-1,V9=-2
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ASK...  02O AR1...  03O FM0...  04O AP4...  05O ZP4...  06O AR3...  07O FEA...  08O FMP...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0620 GOSUB 6000
0700 REM "Check for existing entries
0710 K$=KEY(Z[1],END=0800)
0720 CALL "ZZPROM","X0AP2CBA",X3$,Z,"Entries exist in the Data Enry File.","","",0; ON Z GOTO 0800,0721,9900
0730 CALL "ZZINIT",STR(Z[1]:"00")
0800 REM 
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(100),B0$(100),C$(100),C0$(100),D$(100),D0$(100),D8$(40),D9$(40)
1035 DIM A$(12)
1040 IF V9<>-1 THEN GOSUB 6900
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 5000
1140 ON X0 GOTO 1200,1250,1500
1200 REM "From Area Code
1205 X$="Use F1-F3 report option to obtain printout prior to updating"
1210 CALL "ZZENTR","SUZX",A{ALL},A$,X4$,X3$,20,3,1,3,C0,"","{1"+X$,"","AR2UTZ00","ASK","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,5000,6970
1240 GOTO 1120
1250 REM "From Exchange
1255 X$="To change entire area code to a new one, leave blank"
1260 CALL "ZZENTR","SUZX",A{ALL},A$,X4$,X3$,20,4,4,3,C0,"","{1"+X$,"","AR2UTZ02","","",""; IF ABS(C0)>4 THEN GOSUB 6400
1262 IF A$(4,3)="   " THEN PRINT @(26,4),"All Unspecfied Exchanges", ELSE PRINT @(26,4),'CL',
1265 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1266,6800,5000,6970
1290 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=A$(1,6)
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1530 GOSUB 6600
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 GOTO 1000
1650 A$(7,3)=A$(1,3); GOSUB 6220
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700,6800
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2100,2150,1800
2100 REM "To area code
2110 CALL "ZZENTR","SUZX",A{ALL},A$,X4$,X3$,20,6,7,3,C0,"","{3"+X$,"","AR2UTZ04","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2140 GOTO 2020
2150 REM "To Exchange
2155 IF A$(4,3)="   " THEN A$(10,3)=""; GOSUB 6200; GOTO 2020 ELSE IF A$(10,3)="   " THEN A$(10,3)=A$(4,3)
2160 CALL "ZZENTR","SUZX",A{ALL},A$,X4$,X3$,20,7,10,3,C0,"","{3"+X$,"","AR2UTZ06","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2170 IF C0=2 THEN GOTO 2020
2190 GOTO 2020
5000 REM "End of entry
5020 CALL "ZZPROM","9",X3$,Z,"Change Telephone Numbers Now?","","",0; IF Z=1 THEN GOTO 9900
5090 GOTO 7500
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(4,3),"From Area Code: ",@(10,4),"Exchange:",@(0,5),Z0$,
6065 PRINT @(6,6),"To Area Code:",@(10,7),"Exchange:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(20,6),A$(7,3),@(20,7),A$(10,3),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(20,3),A$(1,3),@(20,4),A$(4,3),
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1140
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6900 REM "GET DEFAULTS
6902 V9$="2203  2205  "
6910 IF V9>=0 THEN READ (Z[13],IND=V9)V9$,IOL=0310; GOTO 6930 ELSE V9=-1
6920 EXTRACT (Z[13],KEY=X0$+"DFLT",DOM=6921)V9$,IOL=0310; V9=IND(Z[13]); READ (Z[13])
6930 RETURN 
6950 REM "WRITE DEFAULT
6955 WRITE (Z[13],KEY=X0$+"DFLT")V9$,IOL=0310
6960 GOSUB 6920; GOTO 1630
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1)Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 6982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
7500 REM "Update
7505 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
7510 FOR F=2 TO 8
7520 DIM B[10],B0$(0)
7530 IF F=2 THEN B[0]=143,B[1]=205,B[2]=338,B=2,H$="Customer File"
7540 IF F=3 THEN B[0]=138,B[1]=200,B=1,H$="Ship-To & Location File"
7550 IF F=4 THEN B[0]=133,B[1]=195,B=1,H$="Vendor Master File"
7560 IF F=5 THEN B[0]=138,B[1]=200,B=1,H$="Telemarketing Contact File"
7570 IF F=6 THEN B[0]=127,B[1]=189,B=1,H$="Salesperson File"
7580 IF F=7 THEN B[0]=143,B[1]=205,B[2]=338,B=2,H$="E/Q Prospect File"
7590 IF F=8 THEN B[0]=132,B[1]=194,B=1,B0$="a",H$="Item Contact File"
7610 PRINT @(0,6),'CE',"File being processed: ",H$,; C=0; GOSUB 8100
7620 READ (Z[F],KEY=B0$,DOM=7621)
7630 K$=KEY(Z[F],END=7890); EXTRACT RECORD (Z[F],KEY=K$)B$
7635 IF B0$>"" THEN IF B0$<>K$(1,LEN(B0$)) THEN GOTO 7890
7640 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
7645 FOR I=0 TO B
7650 IF B$(B[I]+3,3)="   " THEN GOTO 7790; REM "No action if exchange blank"
7660 READ (Z[1],KEY=B$(B[I],6),DOM=7661)A$; GOTO 7680
7670 READ (Z[1],KEY=B$(B[I],3)+"   ",DOM=7790)A$
7680 IF A$(10,3)<>"   " THEN B$(B[I],6)=A$(7,6) ELSE B$(B[I],3)=A$(7,3)
7790 NEXT I
7800 REM "
7820 WRITE RECORD (Z[F])B$
7880 GOTO 7630
7890 NEXT F
7895 CALL "ZZINIT",STR(Z[1]:"00")
7900 REM 
7910 CALL "AR2UT8",X3$,X4$,"","*"
7920 CALL "AP2UT5",X3$,X4$,"","*"
7930 CALL "ZP2UT0",ERR=7931,X3$,X4$,"","*"
7940 CALL "UT2ZIP",X3$,X4$,"","*"
7990 GOTO 9900
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[F],T9,X3$,T,T0,K,B9,D,S0,S1,F$
8115 PRINT @(0,7),"There are "+STR(T)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8900 REM 
8910 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
