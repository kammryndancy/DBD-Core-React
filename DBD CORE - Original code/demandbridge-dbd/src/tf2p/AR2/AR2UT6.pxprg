0010 REM "A/R Reage Customer Master Balances <AR2UT6>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 08/20/03 - 14.047777 - dmm - SSP# 133604
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,V2$,V0$
0100 SETERR 9000
0110 X0$="AR2UT6",X1$="A/R Reage Customer Master Balances"
0120 DIM Z0$(80,"-"),P[8],A[15],B[14]
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14]
0360 IOLIST P$,P[0],P[1],P[2],P[3],P[4],P[5],P[6],P[7],P[8]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AR1...  02O AR6...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0600 REM "
0610 READ (Z[13],KEY=X3$(9,3)+"A/R",DOM=9900)IOL=0360
0620 IF LEN(V2$)>=2 AND V2$(1,2)="JS" THEN Q0$=V2$,Q1$=V0$; DIM V0$(100),V2$(20,"~"); V2$(1,10)="",DATE$=FN%NTD$(FN%CDN,"YYYYMMDD"),DATE$=CHR(NUM(DATE$(1,3))-125)+DATE$(4),V0$(79,6)=DATE$; GOTO 0900
0630 IF LEN(V0$)>=77 THEN GOTO 0721
0640 IF Q1$>"" THEN GOTO 0721
0690 GOSUB 6000
0695 GOSUB 8100
0700 DIM V0$(100),V2$(20,"~"); V2$(1,10)=""
0710 CALL "ZZENTR","D",A{ALL},V0$,X4$,X3$,40,4,79,6,C0,"",X$,"","AR2UT600","","",""
0720 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0721,9900
0900 GOSUB 4100
0990 EXTRACT (Z[1],KEY=V2$(1,10),DOM=0991)
1000 REM 
1005 GOSUB 8100
1010 IF Q1$="" THEN PRINT @(10,15),"Clearing Aged Balances in Customer File:",
1020 EXTRACT (Z[1],END=1100)IOL=0310
1022 IF A$(1,10)>V2$(11,10) THEN GOTO 1100
1025 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1030 A[6]=0,A[7]=0,A[8]=0,A[9]=0,A[10]=0,A[11]=0,A[12]=0,A$(391,6)=V0$(79,6)
1040 WRITE (Z[1])IOL=0310
1041 IF RT_PARM$<>"" THEN CUST$=A$(1,10); CALL "RT2WOC",ERR=1042,X3$,X4$,CUST$,"AR1...","U",A$(1,10)
1050 GOTO 1020
1100 REM "PROCESS OPEN INVOICES
1104 GOSUB 8200
1105 EXTRACT (Z[2],KEY=V2$(1,10),DOM=1106)
1110 L$=""
1120 IF Q1$="" THEN PRINT @(8,15),'CL',"Rebuilding Aged Total from Open Invoices:",
1200 READ (Z[2],END=5000)IOL=0320
1210 IF B$(1,10)>V2$(11,10) THEN GOTO 5000
1220 C=C+1; IF MOD(C,T0)=1 AND Q1$<>"" THEN GOSUB 8150
1300 IF L$<>B$(1,10) THEN GOSUB 3000
1310 IF POS(V0$(59,1)="12")>0 AND B$(19,1)="F" THEN GOTO 1340
1315 GOSUB 4500
1320 T[0]=T[0]+B[3],T[A2]=T[A2]+B[3]
1340 GOTO 1200
3000 REM "Customer Change
3020 IF L$="" THEN GOTO 3080
3030 DIM A$(400),A[15]
3040 EXTRACT (Z[1],KEY=L$,DOM=3041)IOL=0310
3045 A$(1,10)=L$
3050 FOR I=0 TO 6; A[I+6]=T[I]
3055 NEXT I
3060 WRITE (Z[1],KEY=A$(1,10))IOL=0310
3061 IF RT_PARM$<>"" THEN CUST$=A$(1,10); CALL "RT2WOC",ERR=3062,X3$,X4$,CUST$,"AR1...","U",A$(1,10)
3080 L$=B$(1,10); DIM T[6]
3090 RETURN 
4100 REM "Set up constants for ageing
4110 IF P$(47,1)="Y" THEN GOTO 4200
4115 REM "age by months"
4120 A0$=V0$(79,4),A2=NUM(A0$(3)),A0=4
4130 FOR I=1 TO 4
4135 A1$=A0$(1,2),A3=A2-I; IF A3<1 THEN A3=A3+12; IF A1$(2,1)<>"0" THEN A1$(2,1)=CHR(ASC(A1$(2,1))-1) ELSE A1$=CHR(ASC(A1$(1,1))-1)+"9"
4140 A0$=A0$+A1$+STR(A3:"00")
4145 NEXT I
4190 RETURN 
4200 REM "Set aging if aging is days"
4210 A0$="",A0=6,A1$="      "
4220 FOR I=0 TO 4
4230 CALL "ZZDATE",X3$,"",V0$(79,6),A1$,-P[I],0,0,0,0,0,0,"","",""
4240 A0$=A0$+A1$
4250 NEXT I
4290 RETURN 
4500 REM "Age invoice
4505 IF P$(46,1)<>"Y" THEN D0$=B$(20,6) ELSE D0$=B$(54,6)
4506 IF B$(11,7)="ONACCNT" OR (B$(19,1)="C" AND P$(96,1)<>"Y") THEN A2=2; GOTO 4590
4510 FOR I=0 TO 4
4520 IF D0$(1,A0)>A0$(I*A0+1,A0) THEN A2=I+1; EXITTO 4590
4530 NEXT I
4539 REM "if still here then maximum age
4540 A2=6
4590 RETURN 
5000 REM "EOJ
5005 PRINT @(0,19),'CL',
5010 GOSUB 3000
5012 PRINT @(0,15),'CL',
5015 IF V0$(1,6)="AR2RCD" THEN GOTO 9900
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"Enter date to use when aging invoices:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8200 REM "Same as 8100 but for Z(2)
8210 CALL "ZZINFO",Z[2],T9,X3$,T,T0,K,B,D,S0,S1,F$
8225 C=0,T1=0
8230 T0=INT(T*.02); IF T0<=1 THEN T0=2
8245 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9011 IF MID(Q0$,1,2)="JS" THEN A1$=$FF$+X0$+"|Error|"+STR(Y5)+"|Line|"+STR(Y6)+"|"; GOTO 9900
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
