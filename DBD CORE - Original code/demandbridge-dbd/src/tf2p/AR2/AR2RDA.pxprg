0010 REM "A/R Trial Balance Report <AR2RDA>
0035 REM "5.7 - 04/08/14 - 13.24 - tma - SSP# 269421
0037 REM "269421-On Account Balances transactions not showing on the AR      
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2RDA",X1$="A/R Trial Balance Report"
0120 DIM A$(100),A[14],B$(227),B[2],J[5],DET[1]
0140 Z0$="###,###,###.00-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0175 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
0210 T=1,T0=0,T1=5
0220 W3=125
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR ARI - A/R Open Invoice Header
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14]
0315 REM "IOLIST FOR AR1 - Vendor Masterfile
0320 IOLIST B$,B[0],B[1],B[2]
0330 IOLIST C0,C$,DET[0],DET[1]
0340 IOLIST D$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O AR6...  02O AR1...  03O AR7...  04O ARH...  13O ZZPARM  25O AR2...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0540 IF POS(" "<>V0$(59,12))>0 THEN X1$=X1$+" - "+FNS$(V0$(59,12))
0590 READ (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[1]; U=10,U0=1,U[0]=0,U[1]=10,T0=2,V1=2,V2=8,U0$="Y   ",T5$="S   AA/R        ",U5$="Division",U6$="Customer"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0956 REM LET V2$(1,12)=V0$(72,6)+V0$(72,6)
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1155 IF A[3]=0 THEN IF A$(127,6)<V0$(72,6) THEN IF A$(133,6)<V0$(72,6) THEN GOTO 1000
1157 IF POS("F"=V0$(59,12))<>0 AND A$(19,1)="F" THEN GOTO 1000
1160 GOSUB 7500
1200 FOR I=1 TO 5
1205 IF C[I]<>0 THEN EXITTO 1300
1210 NEXT I
1215 GOTO 1000
1300 REM "Read other data
1305 TERM$=""; REM " SSP# 146930
1320 IF B$(1,10)<>A$(1,10) THEN DIM B$(LEN(B$)); FIND (Z[2],DOM=1321,KEY=A$(1,10))IOL=0320
1330 IF V0$(78,1)="O" THEN READ (Z[25],KEY=A$(51,2),DOM=1331)TERM$; REM " SSP# 146930
1331 IF V0$(78,1)="O" AND MID(TERM$,57,1)="Y" THEN GOTO 1332 ELSE IF V0$(78,1)="O" AND MID(TERM$,57,1)<>"Y" THEN GOTO 1000; REM " SSP# 146930
1340 IF V0$(78,1)="N" THEN READ (Z[25],KEY=A$(51,2),DOM=1341)TERM$; IF TERM$(57,1)="Y" THEN GOTO 1000; REM " SSP# 146930
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 IF P0$(13,1)="Y" THEN GOSUB 6300
1440 GOSUB 6200
1490 T=0
1500 REM "Print Data
1510 IF W+1>W0 THEN GOSUB 6000
1515 CALL "ZZDISP","AX",A$(11,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1520 IF X3$(69,1)<>"Y" THEN Y5$(13)=FND$(A$(20,6)) ELSE CALL "ZZDISP","DX",A$(20,6),"   ",X3$,X$,"",0,0,X4$; Y5$(13)=X$
1525 IF POS(" "<>A$(102,6))=0 THEN Y5$(34)="Unavail" ELSE Y5$(34)=A$(102,4)+"."+A$(106,2)
1530 FOR X=0 TO 4; Y5$(42+X*17)=STR(C[X+1]:Z0$); NEXT X
1550 GOSUB 7300
1555 IF DET[1]<>0 THEN GOSUB 7800
1560 GOSUB 7600
1900 REM "Accumulate Totals"
1905 REM "IF X3$(9,3)="020" THEN IF A$(11,7)="9999999" AND A$(19,1)="F" THEN LET J9=J9+A(3)
1910 FOR X=1 TO 5; T[T0,X]=T[T0,X]+C[X]
1920 IF X3$(9,3)="020" THEN IF A$(11,7)="9999999" AND A$(19,1)="F" THEN J[X]=J[X]+C[X]
1925 IF POS(X3$(9,3)="091095")<>0 THEN IF A$(19,1)="F" THEN J[X]=J[X]+C[X]
1930 NEXT X
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X6$,"",0,0,X4$
3071 REM " Was IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X6$,"",0,0,X4$
3075 T6$=X6$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; IF P0$(13,1)<>"Y" THEN GOSUB 7100 ELSE GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3240 U2$=U1$(1,V1)+U2$; REM "IF P0$(13,1)<>"Y" THEN LET U2$="00"+U2$ !SSP#227477
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6035 ! IF T0$="END" THEN W0$=""  !SSP#237458
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6130 Y5$(14)="Invoice",Y5$(29)="Orig. Audit",Y5$(48)="Opening",Y5$(117)="Ending"
6135 GOSUB 7300
6140 Y5$(1)="Invoice",Y5$(15)="Date",Y5$(29)="Control No.",Y5$(49)="Balance",Y5$(64)="Sales Jrl",Y5$(76)="Cash Receipts Jrl",Y5$(95)="Net activity",Y5$(117)="Balance"
6145 GOSUB 7300
6150 Y5$(1)="------------------------------------------------------------------------------------------------------------------------------------"
6155 GOSUB 7300
6199 RETURN 
6200 REM "Break #2 header
6210 IF W+2>W0 THEN GOSUB 6000
6220 Y5$(1)="Customer Code:"
6225 CALL "ZZDISP","AX",A$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(16)=X$
6230 Y5$(18+LEN(X$))=B$(11,35)
6235 GOSUB 7300
6299 RETURN 
6300 REM "Break #1 header
6310 IF W+3>W0 THEN GOSUB 6000
6315 GOSUB 7300
6320 DIM D$(34); FIND (Z[4],KEY=A$(1,2),DOM=6321)IOL=0340
6340 Y5$(1)=A$(1,2)+"  "+D$(3,30)
6350 GOSUB 7300
6355 GOSUB 7300
6390 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(42)=T1$(1,15),Y5$(59)=T1$(1,15),Y5$(76)=T1$(1,15),Y5$(93)=T1$(1,15),Y5$(110)=T1$(1,15); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(42)=STR(T[T,1]:Z0$),Y5$(59)=STR(T[T,2]:Z0$),Y5$(76)=STR(T[T,3]:Z0$),Y5$(93)=STR(T[T,4]:Z0$),Y5$(110)=STR(T[T,5]:Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(42)=T2$(1,15),Y5$(59)=T2$(1,15),Y5$(76)=T2$(1,15),Y5$(93)=T2$(1,15),Y5$(110)=T2$(1,15); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7134 REM "IF X3$(9,3)="020" THEN IF X<>2 AND X<>4 AND X<>5 THEN GOTO 07136
7135 IF POS(X3$(9,3)="020091095")<>0 THEN IF T=2 THEN T[T-1,X]=T[T-1,X]-J[X],J[X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7460 GOSUB 7700
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Get transaction data
7510 DIM C[5],DET[1]; C0=A[1],C2=0,FLAG$="",SEQ$="",C$=""
7515 IF A$(127,6)<V0$(72,6) THEN C[1]=A[6] ELSE IF A$(127,6)=V0$(72,6) THEN C[2]=A[6]
7520 IF FLAG$="END" THEN C$=""; GOTO 7590
7525 CALL "AR2XAB",X3$,X4$,Z[3],A$,C0,C$,DET{ALL},FLAG$,SEQ$,"R"
7526 IF FLAG$="NONE" THEN C$=""; GOTO 7590
7530 IF C$(40,6)=V0$(72,6) THEN IF POS(C$(19,1)="P")>0 THEN C[3]=C[3]+DET[0] ELSE C[2]=C[2]+DET[0]
7535 IF C$(40,6)<V0$(72,6) THEN C[1]=C[1]+DET[0]
7536 IF C$(40,6)>V0$(72,6) THEN DET[1]=DET[1]+DET[0]
7540 GOTO 7520
7590 C[4]=C[3]+C[2],C[5]=C[1]+C[4]
7592 REM "IF A(3)<>C(4)+C(1)+C2 THEN LET C2=A(3)-C(1)-C(2)-C2,C(5)=C(5)+C2,C(4)=C(4)+C2
7595 RETURN 
7600 REM "PRINT TRANSACTIONS"
7601 DIM DET[1]; C0=A[1],FLAG$="",SEQ$="",C$=""
7602 IF FLAG$="END" THEN C$=""; GOTO 7690
7603 CALL "AR2XAB",X3$,X4$,Z[3],A$,C0,C$,DET{ALL},FLAG$,SEQ$,"R"; IF FLAG$="NONE" THEN C$=""; GOTO 7690
7605 IF W+1>W0 THEN GOSUB 6000
7607 IF C$(11,8)<>A$(11,8) THEN Y5$(1)="*"; IF LEN(C$)=0 THEN GOTO 7650
7608 CALL "ZZDISP","AX",C$(11,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(2)=X$
7609 CALL "ZZDISP","DX",C$(20,6),"   ",X3$,X$,"",0,0,X4$; Y5$(13)=X$
7610 Y5$(26)=C$(44,2)+"/"+C$(40,4)+" "+C$(34,4)+"."+C$(38,2)+" ("+C$(19,1)+")"
7640 IF C$(40,6)>V0$(72,6) THEN Y5$(76)="**Future"
7650 Y5$(93)=STR(DET(0):Z0$) ! SSP#269421
7660 Y5$(110)=C$(26,8)
7680 IF V0$(59,1)="A" AND A$(19,1)="F" AND C$(40,6)<V0$(72,6) THEN DIM Y5$(W3) ELSE GOSUB 7300
7685 GOTO 7602
7690 RETURN 
7700 REM "PRINT P/E DATE"
7710 DIM P1$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+V0$(72,4),DOM=7740)P1$
7720 D1$=P1$(NUM(V0$(76,2))*6+15,6),D0$=D1$
7730 W0$="For fiscal year "+V0$(72,4)+", period "+V0$(76,2)+" ending "+FND$(D0$)
7745 RETURN 
7800 REM "Print Cleared Detail
7801 RETURN 
7810 Y5$(1)="*"
7820 Y5$(13)="*** Cleared Detail *** ("+V0$(76,2)+" "+V0$(72,4)+")"
7860 Y5$(93)=STR(DET[1]:Z0$)
7870 GOSUB 7300
7890 RETURN 
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 REM "237458-The Fiscal Year and Period does not print on the last page
