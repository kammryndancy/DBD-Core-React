0010 REM "Invoice Printing <AR2EBC>
0035 REM "5.2 - 03/02/04 - 10.650833 - lms - SSP# 167214
0036 REM "TopForm Standard/Costed Version/FarWest Version"
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "Option code 24 means Farwest with remittance
0090 IF %GUI THEN CLEAR ; SETERR 9000 ELSE BEGIN ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 0000; SETESC 0000
0110 X0$="AR2EBC",X1$="Invoice Printing"
0120 DIM A$(689),A[30],B$(289),B[20],M$(20),M[1]
0140 M0$="####.00-",M1$="###,##0-",M2$="###,###.00-",M3$="####"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0210 T=1,T0=1,T1=1
0220 W3=110
0225 W=0,W8=1
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR ARB - A/R Invoice Data Entry Header
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0315 REM "IOLIST FOR ART - Invoice Entry Print Detail
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20]
0330 IOLIST C$
0340 IOLIST D$
0350 IOLIST E$
0360 IOLIST F$
0370 IOLIST G$
0390 IOLIST M$,M[0],M[1]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01L ARB...  02O ART...  03O AR2...  04O AR3...  05O FMP...  06O FM0...  07O AR1...  "
0515 Y$=Y$+"13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 M5$="####.00#"
0590 GOTO 0700
0600 REM "TEST PATTERN
0610 CALL "ZZPROM","R4",X3$,A,"","","",0; ON A GOTO 0695,0611,9900
0615 DIM C$(120,"X"),D$(200,"X"),F$(40,"X"),G$(400,"X")
0620 DIM A$(400,"X"); A$(180,6)=""; GOSUB 2500
0630 GOSUB 6000
0690 GOTO 0600
0695 RETURN 
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0730 IF FID(0)<>FID(W9) THEN W0=34
0740 W8=1
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[3]; U=14,U0=3,U[0]=0,U[1]=4,U[2]=2,U[3]=8,T0=0,V1$="ARB...",U0$="    ",T5$="                "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
0995 GOSUB 0600
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1200 REM 
1205 REM "IF print only unprinted, then check print flag
1210 IF V0$(72,1)="Y" THEN IF A$(64,1)="P" THEN GOTO 1000
1300 REM "Read other data
1302 P0=0
1305 IF A$(60,1)="Y" THEN GOTO 1000
1306 IF Q5$<>"*" AND A$(345,1)<>"Y" THEN GOTO 1000
1307 IF Q5$="*" THEN GOSUB 5800; GOTO 1000
1310 DIM C$(120); FIND (Z[3],KEY=A$(65,2),DOM=1311)IOL=0330
1320 DIM D$(200); FIND (Z[4],KEY=A$(133,4),DOM=1321)IOL=0340
1330 REM DIM E$(300); FIND (Z(6),KEY="C"+A$(15,10)+A$(187,4),DOM=1331) IOL=0350
1340 REM DIM F$(40); FIND (Z(5),KEY="S"+A$(186,1),DOM=1341) IOL=0360
1350 FIND (Z[7],KEY=A$(15,10))IOL=0370
1400 REM "Subtotals"
1415 GOSUB 3100
1490 T=0
1500 REM "Print Data
1510 W=0
1810 GOSUB 2000
1820 GOSUB 6000
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print Lines or Detail"
2010 X3=A[20],T4=0,T9=0
2015 GOSUB 2500
2020 IF X3<1 THEN GOSUB 2700; RETURN 
2030 READ (Z[2],IND=X3)X3,IOL=0320
2035 IF B$(2,1)="Y" THEN T9=T9+B[4]; GOTO 2020
2036 IF B[4]=0 AND B$(6,1)<>"Y" THEN IF B[5]<>0 THEN GOSUB 7900; GOTO 2020 ELSE GOTO 2020
2040 REM "Read Secondary Information"
2200 REM "Print Each Line
2205 T4=T4+1,B9=B[0]
2210 IF W>28 OR (POS(B$(1,1)="IC")<>0 AND W>26) OR (B[6]<>0 AND B[6]<>1 AND M$(20,1)<>"Y" AND W>27) THEN T6=1; GOSUB 6000; T4=0; GOSUB 2500
2211 IF B[0]=0 AND B[1]=0 THEN GOTO 2215
2212 IF B$(1,1)<>"M" THEN GOSUB 7700 ELSE IF B[1]<>0 AND M9<>0 THEN B[1]=B[1]/M9
2213 IF B9<>0 THEN Y5$(1)=STR(B9:M1$); IF FPT(B9)<>0 THEN Y5$(1)=STR(B9:M0$)
2214 Y5$(10)=STR(B[1]:M1$); IF FPT(B[1])<>0 THEN Y5$(10)=STR(B[1]:M0$)
2215 IF B$(1,1)="M" THEN Y5$(19)=B$(7,40) ELSE GOSUB 7800
2216 IF B$(1,1)<>"M" AND A$(115,1)<>"Y" AND B$(54,1)<>"Y" THEN Y5$(59)="*"
2220 IF B$(1,1)="M" THEN GOTO 2230
2222 IF B[0]=0 AND B[1]=0 THEN GOTO 2227
2224 IF B[2]<>0 THEN Y5$(60)=STR(B[2]:M5$)
2225 Y5$(69)=B$(50,4)
2227 Y5$(73)=STR(B[4]:M2$)
2230 GOSUB 7900
2300 IF B$(1,1)<>"M" AND B[6]<>0 AND B[6]<>1 AND M$(20,1)<>"Y" THEN P1$=" Pack: "+STR(B[6]:M3$) ELSE P1$=""
2301 IF V0$(282,1)="Y" THEN P1$=""
2303 IF P$<>"" THEN Y5$(19)=P$,J5=1 ELSE J5=0
2305 IF J5=1 THEN IF P1$<>"" THEN Y5$(59-LEN(P1$))=P1$
2306 IF J5=0 AND (POS(B$(1,1)="IC")=0 OR POS(" "<>B$(65,10))=0) THEN Y5$(59-LEN(P1$))=P1$
2307 IF POS(" "<>Y5$)<>0 THEN GOSUB 7300
2310 IF V0$(73,1)<>"Y" AND POS(B$(1,1)="IC")<>0 AND POS(" "<>B$(65,10))<>0 THEN CALL "ZZDISP","AX",B$(65,10),"ICE",X3$,X$,"",0,0,X4$; Y5$(19)="Item code: "+X$; IF J5=0 THEN Y5$(59-LEN(P1$))=P1$; GOSUB 7300 ELSE GOSUB 7300
2320 P$="",P1$=""
2400 REM "Accumulate Line Totals"
2410 T9=T9+B[4]
2499 GOTO 2020
2500 REM "Line Header
2505 GOSUB 7300
2507 IF V0$(59,2)="24" THEN GOSUB 8000
2510 IF A$(77,1)="C" THEN Y5$(38)="Credit memo"
2515 GOSUB 7300
2520 P0=P0+1; IF P0>1 THEN Y5$(43)="Page: "+STR(P0:"##0"); DIM H5$(LEN(H5$))
2525 CALL "ZZDISP","DX",A$(86,6),"",X3$,X$,"",0,0,X4$; Y5$(58)=X$
2530 CALL "ZZDISP","AX",A$(7,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(71)=X$
2540 FOR X=0 TO 2; GOSUB 7300; NEXT X
2560 CALL "ZZDISP","AX",A$(15,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(62-INT(LEN(X$)/2))=X$
2565 Y5$(76-INT(LEN(FNS$(A$(100,15)))/2),15)=A$(100,15)
2570 FOR X=0 TO 3; GOSUB 7300; NEXT X
2575 Y5$(9)=A$(25,35); Y5$(49)=A$(290,35); GOSUB 7300
2580 FOR X=0 TO 1; Y5$(9)=G$(56+X*30,30),Y5$(49)=A$(191+X*30,30); GOSUB 7300; NEXT X
2584 G0$=FNS$(G$(116,16))+", "+G$(132,2),A5$=FNS$(A$(251,16))+", "+A$(267,2)
2585 Y5$(9)=G0$
2586 CALL "ZZDISP","AX",G$(134,9),"ZIP",X3$,X$,"",0,0,X4$; Y5$(11+LEN(G0$))=X$
2587 Y5$(49)=A5$
2588 CALL "ZZDISP","AX",A$(269,9),"ZIP",X3$,X$,"",0,0,X4$; Y5$(51+LEN(A5$))=X$
2595 FOR X=0 TO 5; GOSUB 7300; NEXT X
2600 REM "
2605 CALL "ZZDISP","AX",A$(92,8),"O/P",X3$,X$,"",0,0,X4$
2610 Y5$(1)=X$,Y5$(11)=FND$(A$(180,6)),Y5$(22)=A$(162,15)
2615 Y5$(38)=D$(149,20)
2620 Y5$(60)=C$(3,25)
2630 GOSUB 7300
2635 FOR X=0 TO 1; GOSUB 7300; NEXT X
2690 RETURN 
2700 REM "Line Totals"
2710 IF W>29 THEN T6=1; GOSUB 6000; GOSUB 2500
2715 IF W<30 THEN GOSUB 7300; GOTO 2715
2720 IF A[10]<>0 AND POS(" "<>A$(122,6))<>0 THEN X$="IF PAID BY "+FND$(A$(122,6))+" YOU MAY DEDUCT "+STR(A[10]:"$"+M0$),Y5$(7)=X$
2795 RETURN 
3100 REM "Subtotal logic goes here, see -ZREPS
3180 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
4000 REM "Print bottom
4010 IF W<32 THEN GOSUB 7300; GOTO 4010
4015 G0=A[1]-A[11],G1=0; IF A[1]<>0 THEN G1=G0*100/A[1]
4020 FOR X=0 TO 4; IF X>0 THEN Y5$(1)=V0$(80+(X-1)*50,50)
4023 IF T6=1 THEN IF X=4 THEN Y5$(73)="*Continued*"; GOTO 4040 ELSE GOTO 4040
4025 IF X=0 THEN Y5$(73)=STR(A[2]+A[17]:M2$) ELSE IF X=2 THEN Y5$(73)=STR(A[4]+A[6]+A[8]:M2$) ELSE IF X=4 THEN Y5$(73)=STR(A[1]+A[2]+A[4]+A[6]+A[8]:M2$)
4030 IF V0$(59,2)="2 " THEN IF X=0 THEN Y5$(88)="Net Sale",Y5$(98)="Total Cost" ELSE IF X=1 THEN Y5$(86)=STR(A[1]:M2$),Y5$(98)=STR(A[11]:M2$) ELSE IF X=2 THEN Y5$(93)="GP$",Y5$(105)="GP%" ELSE IF X=3 THEN Y5$(86)=STR(G0:M2$),Y5$(98)=STR(G1:M2$) ELSE Y5$(86)="Commission:",Y5$(98)=STR(A[18]:M2$)
4040 GOSUB 7300; NEXT X
4045 IF A$(77,1)="C" THEN GOSUB 7300; Y5$(73)="Credit memo"; GOSUB 7300
4085 IF T6<>1 THEN IF T9<>A[1] THEN PRINT 'RB','RB','RB',; CALL "ZZPROM",".4",X3$,Z,"Invoice "+A$(7,7)+" total does not match it's lines!! Contact system administrator","","",0; GOTO 9900
4086 T6=0
4090 RETURN 
5000 REM "End of Print
5020 IF Q5$="*" THEN GOTO 9900
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5200 REM "
5255 IF V3$(2,2)="A " THEN PRINT 'EA'
5260 CALL "ZZPROM",".Y",X3$,Z,"Did all of the invocies print correctly?","","",0
5270 ON Z GOTO 5271,9900
5280 READ (U1,KEY="",DOM=5281)
5285 PRINT @(0,20),'CL',@(15,20),"Setting the print flag on invoice: ",
5290 Q5$="*"; GOTO 1000
5800 REM "Set the printed flag in the invoice
5805 READ RECORD (Z[1],KEY=U$,DOM=5840)A1$
5810 A1$(64,1)="P"
5815 CALL "ZZDISP","A",A1$(7,8),"AR6",X3$,"","",50,20,X4$
5820 WRITE RECORD (Z[1],KEY=U$)A1$
5840 RETURN 
6000 REM "Page header
6010 GOSUB 4000
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,"X",W5$
6045 ON W5 GOTO 6046,9900
6090 RETURN 
6100 REM "
6199 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7320 IF V0$(59,2)="24" THEN IF W>3 AND W<22 THEN Y5$(86)=H5$(1+(W-4)*H,H)
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7452 V1$="ARB   "
7455 READ (Z[13],KEY="CMP"+X3$(9,3))C0$
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7700 REM "Convert B(0) and B(1) to U/M quantities
7705 DIM M$(20),M[1]; READ (Z[13],KEY="U/M"+B$(50,4),DOM=7706)IOL=0390
7710 IF M$(20,1)="Y" THEN M9=M[1] ELSE M9=B[6]
7715 IF M9=0 THEN M9=1
7720 B[0]=B[0]/M9,B[1]=B[1]/M9
7730 B9=B[0]; IF V0$(280,1)="B" THEN B9=0 ELSE IF V0$(280,1)="S" THEN B9=B[1]
7740 RETURN 
7750 REM "Build desc from H8$
7760 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
7770 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7775 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
7779 H7$=H9$
7780 H9$=H9$+FNS$(H8$(21,40)); H8$=""
7790 RETURN 
7800 REM "Print Descr. Y5$(19) set to first part, P$=second line part
7810 DIM H8$(81); H8$(21,40)=B$(7,40),H8$(64,18)=B$(93,18)
7815 IF V0$(281,1)="Y" THEN H8$(64,18)=""
7820 GOSUB 7750
7830 IF LEN(H9$)>40 THEN Y5$(19)=B$(7,40),P$=H7$ ELSE Y5$(19)=H9$,P$=""
7840 RETURN 
7900 REM "Costed Line?"
7920 IF V0$(59,2)="2 " THEN IF B$(1,1)<>"M" THEN Y5$(86)=STR(B[3]:"###"+M5$),Y5$(98)=STR(B[5]:M2$)
7930 GOSUB 7300
7940 RETURN 
8000 REM "BUILD Remittance Stub
8005 H=23
8010 DIM H5$(H*18)
8020 H5$(1,H)="Please return this"
8030 H5$(1+H,H)="portion with"
8040 H5$(1+2*H,H)="remmitance to:"
8050 ON POS(X3$(9,3)="019",3) GOSUB 8200,8150
8075 CALL "ZZDISP","AX",A$(15,10),"A/R",X3$,X$,"",0,0,X4$
8080 H5$(1+8*H,H)="Account No: "+X$
8085 CALL "ZZDISP","AX",A$(7,8),"AR6",X3$,X$,"",0,0,X4$
8090 H5$(1+10*H,H)="Invoice No: "+X$
8095 CALL "ZZDISP","DX",A$(86,6),"",X3$,X$,"",0,0,X4$
8100 H5$(1+12*H,H)="      Date: "+X$
8110 H5$(1+14*H,H)="   Invoice Total"
8120 H5$(5+15*H,H)=STR(A[1]+A[2]+A[4]+A[6]+A[8]:M2$)
8140 RETURN 
8150 H5$(1+4*H,H)="Farwest Paper Flow Inc."
8160 H5$(1+5*H,H)="3706 Airport Way South"
8170 H5$(1+6*H,H)="Seattle, WA 98134-2299"
8180 RETURN 
8200 H5$(1+4*H,H)="Computer Software Inc."
8210 H5$(1+5*H,H)="3091 Governors Lake Dr"
8220 H5$(1+6*H,H)="Norcross, GA 30071"
8230 RETURN 
8910 DEF FNS$(X$)=X$(1,POS("      "=X$+"      ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF STP(Y4$)<>"" THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
