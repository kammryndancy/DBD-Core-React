0010 REM "Rebuild Commission by Line Sort <AR2UTK>
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 11/14/07 - 17.6075 - dmm - SSP# 212146
0037 REM "212146-New company name. Menu, documentation, etc.                 
0040 REM "Copyright 2007 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AR2UTK",X1$="Rebuild Commissions by Line Sort"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AS9...  02L ASA...  03O AR6...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 CALL "ZZINIT",STR(Z[2]:"00")
0980 READ (Z[1],KEY="",DOM=0981)
1000 REM 
1020 READ RECORD (Z[1],END=5000)A$
1025 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1050 IF A$(107,1)<>"Y" THEN GOSUB 7000
1100 K$=A$(22,10)+A$(11,8)+A$(1,10)+A$(19,3)
1120 WRITE (Z[2],KEY=K$)
1990 GOTO 1000
5000 REM "EOJ
5010 C=T0; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This DBI utility program will process the Commission by Line Item File, AS9, and",@(0,5),"rebuild the Sort File ASA. If the corresponding invoice has a 0 balance or is",@(0,6),"not in the Open Invoice File, then the record will be marked as paid in the",@(0,7),"Commission by Line File."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7000 REM "If this is not flagged as paid, verify that by checking AR6. If AR6 is missing or the invoice balance is 0, then set the paid flag and write  out the AS9 record
7005 DIM C$(150),C[14]
7009 REM "If we find the record,and the balance is not zero, then just leave, else set fy/ap to greatest of AS9's or Greatest in AR6, then write new record, remove old AS9 record
7010 FIND (Z[3],KEY=A$(22,10)+A$(11,8),DOM=7011)IOL=0330; IF C[3]<>0 THEN GOTO 7045
7015 A$(107,1)="Y"
7020 IF A$(1,6)<C$(133,6) THEN K$=A$(1,21),A$(1,6)=C$(133,6); WRITE RECORD (Z[1],KEY=A$(1,21))A$; REMOVE (Z[1],KEY=K$,DOM=7021) ELSE WRITE RECORD (Z[1],KEY=A$(1,21))A$
7045 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(0,9),"There are "+STR(T)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look fora result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "212146-New company name. Menu, documentation, etc.
