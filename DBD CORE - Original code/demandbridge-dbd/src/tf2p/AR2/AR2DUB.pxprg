0010 REM " A/R Invoice Update - Phase II <AR2DUB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 11/15/11 - 17.765555 - jvv - SSP# 249775
0037 REM "249775-Changes to sales tax parameters and tax calculations to     
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 IF %GUI THEN CLEAR ELSE BEGIN ! ELSE Y0$=""
0100 SETERR 9000
0110 X0$="AR2DUB",X1$=" A/R Invoice Update - Phase II"
0120 DIM Z0$(80,"-")
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0320 IOLIST K0,B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29],B[30],B[31],B[32],B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40],B[41],B[42],B[43],B[44],B[45],B[46],B[47],B[48] ! SSP 249775
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0331 IOLIST C$,C[0],STR(C[1]),STR(C[2]),C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0390 IOLIST J$,J[0],J[1]
0400 IOLIST P$,P[0],P[1]
0405 IOLIST K$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9],K[10]
0410 IOLIST S$,S[0],S[1],S[2],S[3],S[4],S[5]
0420 IOLIST S0$
0500 REM "FILES
0505 Z=NUM(X3$(60,3)); DIM Z[Z]
0510 Z$="01L ARB...  02O ART...  03O IC1...  04O IC9...  05O AR9...  06O ARA...  07O ASF...  08O ARX...  09O IC8... 10O ZE0... 12O IC0...  13O ZZPARM  14O FT7...  15O FTB...  16O BM1... 17O AST... 18O FS2... 19O FS1... "
0511 REM "AST added by 107205,FS1 added by 120626
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
0550 REM "Read in stat info"
0555 READ (Z[13],KEY="STATARX")S4$
0560 READ (Z[13],KEY="STATI/C")S5$
0565 READ (Z[13],KEY="STATARW")S6$
0575 READ (Z[13],KEY="STATA/R")S7$
0580 READ (Z[13],KEY="STATAR3")S8$
0585 READ (Z[13],KEY="STATASF",DOM=0586)S3$
0586 READ (Z[13],KEY="STATAST",DOM=0587)AST$; REM "ssp#107205
0587 RT$=""; CALL "RT2PRM",ERR=0588,X3$,X4$,RT$
0600 REM "Read parameters
0610 DIM P[8],P0$(90),P1$(32)
0620 FIND (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$,P[0],P[1],P[2],P[3],P[4],P[5],P[6],P[7],P[8]
0630 FIND (Z[13],KEY=X3$(9,3)+"AR1",DOM=9800)P1$
0640 FIND (Z[13],KEY=X3$(9,3)+"I/C")P2$
0645 GB$=""; FIND (Z[13],KEY=X3$(9,3)+"G/B",DOM=0646)GB$
0695 CALL "ZZPROM","U6",X3$,0,U9$,"","",0
0730 DIM A$(689),A[30]
0740 DIM B$(289),B[48] ! SSP 249775
0800 REM "CHECK FOR ROOM?
0801 GOTO 0900 ! 169383
0805 CALL "ZZPROM","U1",X3$,0,"","","",0
0810 DIM X[Z,1],X$(0)
0815 FOR X=1 TO Z
0820 CALL "ZZINFO",X,0,X3$,A,B,0,0,0,0,0,""; X[X-1,0]=A,X[X-1,1]=B
0825 NEXT X
0830 PRINT @(0,22),'CL',
0835 IF X[Z[1]-1,0]=0 THEN CALL "ZZPROM","U2",X3$,S3,"","","",0; GOTO 9900
0840 GOTO 0900
0880 REM "PROMPT OUT OF ROOM
0890 CALL "ZZPROM","U3",X3$,S3,"","","",0; GOTO 9900
0900 REM "RESTART LOGIC
0905 V0$=X3$(1,6)+X3$(40,3)+DAY+STR(TIM*100:"0000")+X3$(178,6); WRITE (Z[1],KEY="",DOM=0906)V0$
0910 EXTRACT (Z[1],KEY="")V0$,K0$,K1$,K0,K1; I1=IND(Z[1])
0915 GOSUB 6000
0920 IF K0$="" THEN READ (Z[1],KEY=K0$); GOTO 0960
0930 EXTRACT (Z[1],KEY=K0$,DOM=0931)IOL=0310
0935 CALL "ZZPROM","U4",X3$,10,"","","",0; PRINT " ",A$(1,4)+"-"+A$(5,2)+"-"+A$(7,8)
0940 CALL "ZZPROM","U5",X3$,0,"","","",14
0975 PRINT @(0,12),'CL',
1000 REM "READ NEXT RECORD
1020 K0$=KEY(Z[1],END=5000)
1030 READ (Z[1],KEY=K0$)IOL=0310
1035 K0$=K0$(1,14); REM "needed for conversions!
1090 PRINT @(7,12),U9$," ",K0$,
1092 DIM FT7$(121); EXTRACT (Z[14],KEY=A$(15,10)+A$(92,8),DOM=1093)FT7$; FT7$(69,8)=A$(7,8); WRITE (Z[14],KEY=FT7$(1,18))FT7$; WRITE (Z[15],KEY=FT7$(1,10)+FT7$(69,8)+FT7$(11,8)); REM "WO105287, write invoice number to repeat order record, also write FTB sort record
1096 REM "Summary bill moved to AR2DUC 3.0.3
1100 REM "
1105 IF POS(" "<>A$(92,8))<>0 THEN O0=O0+1
1109 REM "F9$ GETS RESET FOR EACH LINE - SEE 2030
1110 F9$=""; IF A$(343,1)<>"R" THEN GOTO 1200
1120 FOR X=1 TO 11; IF A[X]<>0 THEN EXITTO 1200
1125 NEXT X
1130 F9$="*"
1200 GOSUB 3200
1500 REM "UPDATE LINES
1505 DIM B[48] ! SSP 249775
1510 IF A[20]<=0 THEN GOTO 1800 ELSE K0=A[20]
1520 DIM B[48]; READ (Z[2],IND=K0)IOL=0320 ! SSP 249775
1525 IF B$(1,1)="T" THEN GOTO 1750
1530 GOSUB 2000
1535 IF B$(1,1)="S" THEN GOSUB 3900
1550 REM "
1750 IF K0>0 THEN GOTO 1520
1800 REM "RESTART LOGIC
1810 GOSUB 6600
1950 GOTO 1000
2000 REM "UPDATE A LINE 
2020 IF B$(1,1)="M" THEN RETURN 
2030 F9$=""; IF B$(1,1)="C" AND B$(89,1)="9" AND B[4]=0 AND B[5]=0 THEN F9$="*"
2040 IF POS(B$(1,1)="IC")=0 THEN GOTO 2180
2045 IF GB$<>"" THEN GOSUB 4100; REM "ssp# 106472 - update quote data
2060 IF POS(B$(89,4)="    DS  ",4)>0 THEN GOTO 2180
2100 REM "
2110 GOSUB 2900
2120 GOSUB 2800
2130 GOSUB 4000
2180 GOSUB 3000
2195 RETURN 
2800 REM "Update warehouse location data (IC1)
2810 DIM C$(58),C[20]
2812 Q$=B$(89,4)
2815 C$(1,20)=B$(55,20),C$(21,4)=Q$
2820 EXTRACT (Z[3],KEY=C$(1,24),DOM=2821)IOL=0330
2830 C[5]=C[5]+B[1]
2860 WRITE (Z[3],KEY=C$(1,24))IOL=0331
2865 IF GB$<>"" THEN ORDER_LINE$=A$(92,8)+B$(47,3),UPDATE_QTY=B[1]; CALL "BM2ICU",X3$,X4$,2,ORDER_LINE$,UPDATE_QTY,"",0
2870 EXTRACT RECORD (Z[12],KEY=C$(1,20),DOM=2890)I$
2875 IF A$(86,6)>I$(96,6) THEN I$(96,6)=A$(86,6)
2880 WRITE RECORD (Z[12],KEY=C$(1,20))I$
2885 IF RT$>"" THEN IF RT$(7,1)="H" THEN CUST$=C$(1,10); CALL "RT2WOC",ERR=2886,X3$,X4$,CUST$,"IC0...","U",C$(1,20)
2890 RETURN 
2900 REM "Statistics data
2905 F0=Z[4],S9$=S5$; Q$=B$(89,4)
2910 Q1$=B$(55,20)+Q$+A$(1,4),Q1=NUM(A$(5,2))
2920 Q0$="S",Q0=B[4]; GOSUB 7000
2925 Q1$=B$(55,20)+Q$+A$(1,4),Q1=NUM(A$(5,2))
2930 Q0$="U",Q0=B[1]
2940 GOSUB 7000
2943 IF GB$<>"" THEN ORDER_LINE$=A$(92,8)+B$(47,3),UPDATE_QTY=B[1],STAT_INFO$=B$(89,4)+A$(1,4)+"U"+A$(5,2); CALL "BM2ICU",X3$,X4$,4,ORDER_LINE$,UPDATE_QTY,STAT_INFO$,GB_COST
2945 Q1$=B$(55,20)+Q$+A$(1,4),Q1=NUM(A$(5,2))
2950 IF P0$(123,1)="Y" THEN Q0$="P",Q0=B[4]-(B[5]-B[8]) ELSE Q0$="P",Q0=B[4]-B[5]; REM "ssp#107357 If param set, then update adjusted GP
2955 GOSUB 7000
2960 REM "Update AST stats if neede ssp#107205
2961 IF P0$(124,1)<>"Y" THEN GOTO 2990
2965 F0=Z[17],S9$=AST$
2970 Q1$=A$(15,10)+B$(55,20)+A$(1,4),Q1=NUM(A$(5,2)),Q0$="S",Q0=B[4]; GOSUB 7000
2972 Q1$=A$(15,10)+B$(55,20)+A$(1,4),Q1=NUM(A$(5,2)),Q0$="U",Q0=B[1]; GOSUB 7000
2974 Q1$=A$(15,10)+B$(55,20)+A$(1,4),Q1=NUM(A$(5,2))
2975 IF P0$(123,1)="Y" THEN Q0$="P",Q0=B[4]-(B[5]-B[8]) ELSE Q0$="P",Q0=B[4]-B[5]; REM "ssp#107357 If param set, then update adjusted GP
2976 GOSUB 7000
2995 RETURN 
3000 REM "Product by div, salesperson, category
3005 T0=POS("    "=A$(133,20)+"    "); IF T0=1 THEN T0=0 ELSE T0=INT(T0/5)
3010 F0=Z[8],S9$=S4$
3015 IF T0>NUM(P1$(8,1)) THEN T0=NUM(P1$(8,1))
3020 DIM H$(9),H[14],T[30]
3025 DIM S[5]; FOR I0=1 TO 5; S[I0]=A[I0+11]; NEXT I0
3028 IF P1$(62,1)="Y" THEN B[5]=B[5]-B[9]
3030 CALL "AR2XAA",X3$,B{ALL},1,5,T{ALL},S{ALL},1,5
3032 IF P1$(62,1)="Y" THEN B[5]=B[5]+B[9]
3035 FOR I=9 TO POS(" "=P1$(9,10)+" ")+7
3040 FOR J=0 TO T0
3045 J0=J*5,Q1$=A$(15,2)+A$(133+J*4,4)+A$(153,9)+B$(3,3)+B$(111,1)+" "+A$(1,4),Q1=NUM(A$(5,2)),Q0$=P1$(I,1); IF A$(133+J*4,4)="    " THEN GOTO 3100; REM " Eliminate updating of blank slsps
3046 Q1$(7,9)=""; REM "ELIMINTATE CUST CAT 7/10/89
3050 ON POS(Q0$="SPCN") GOTO 3091,3055,3060,3065,3070
3055 Q0=T[J0+3]; GOTO 3090
3060 Q0=T[J0+3]-T[J0+4]; GOTO 3090
3065 GOTO 3100
3070 Q0=1; GOTO 3090
3090 IF F9$<>"*" THEN GOSUB 7000
3100 NEXT J
3110 NEXT I
3115 REM "Update Salesperson, customer, product code stats (ASF) open on Z(7)
3117 IF P0$(112,1)<>"Y" THEN GOTO 3190
3120 F0=Z[7],S9$=S3$
3135 FOR I=9 TO POS(" "=P1$(9,10)+" ")+7
3140 FOR J=0 TO T0
3145 J0=J*5,Q1$=A$(133+J*4,4)+A$(15,10)+B$(3,3)+" "+A$(1,4),Q1=NUM(A$(5,2)),Q0$=P1$(I,1); REM "Replace blank after B$(3,3) with B$(111,1) to enable new/repeat flag
3150 ON POS(Q0$="SPCN") GOTO 3180,3155,3160,3165,3170
3155 Q0=T[J0+3]; GOTO 3175
3160 Q0=T[J0+3]-T[J0+4]; GOTO 3175
3165 GOTO 3180
3170 Q0=1; GOTO 3175
3175 IF F9$<>"*" THEN GOSUB 7000
3180 NEXT J
3185 NEXT I
3190 DIM T[0]
3195 RETURN 
3200 REM "Update Customer Statistics
3201 REM "Update stats to invoice customer not bill-to customer
3205 S9$=S7$,F0=Z[5]
3210 FOR I=21 TO POS(" "=P0$(21,10)+" ")+19
3220 Q1$=A$(15,10)+A$(1,4),Q1=NUM(A$(5,2))
3240 Q0$=P0$(I,1)
3250 ON POS(Q0$="SPNE") GOTO 3301,3260,3265,3280,3270; REM "SSP 120526
3260 Q0=A[1]; GOTO 3300
3265 IF P0$(123,1)="Y" THEN Q0=A[1]-(A[11]-A[19]); GOTO 3300 ELSE Q0=A[1]-A[11]; GOTO 3300; REM "SSP#107357, if Y include SP's GP adj in profit
3270 Q0=A[1]; GOSUB 4200; IF Q0<>0 THEN GOTO 3300 ELSE GOTO 3301; REM "SSP 120526
3280 Q0=1; GOTO 3300
3300 IF F9$<>"*" THEN GOSUB 7000
3480 NEXT I
3500 REM "Update Salesperson Statistics
3505 T0=POS("    "=A$(133,20)+"    "); IF T0=1 THEN T0=0 ELSE T0=INT(T0/5)
3507 F0=Z[6],S9$=S8$
3508 IF T0>NUM(P1$(8,1)) THEN T0=NUM(P1$(8,1))
3510 DIM H$(9),H[14],T[100],L[100]
3515 DIM S[5]; FOR I0=1 TO 5; S[I0]=A[I0+11]; NEXT I0
3516 DIM L1[5]; FOR I0=1 TO 5; L1[I0]=A[I0+11]; NEXT I0
3517 CALL "AR2XAA",X3$,A{ALL},1,20,L{ALL},L1{ALL},1,5
3519 IF P1$(62,1)="Y" THEN A[11]=A[11]-A[19]
3520 CALL "AR2XAA",X3$,A{ALL},1,20,T{ALL},S{ALL},1,5
3522 IF A[20]<=0 THEN GOTO 3535 ELSE K0=A[20]
3523 IF K0<=0 THEN GOTO 3535
3524 DIM B[48]; READ (Z[2],IND=K0)IOL=0320 ! SSP249775
3525 IF B[11]<>0 THEN T[17]=0,T[37]=0,T[57]=0,T[77]=0,T[97]=0,L[17]=0,L[37]=0,L[57]=0,L[77]=0,L[97]=0
3526 T[57]=T[57]+B[13],L[57]=L[57]+B[13]
3527 T[77]=T[77]+B[14],L[77]=L[77]+B[14]
3528 T[97]=T[97]+B[15],L[97]=L[97]+B[15]
3530 T[37]=T[37]+B[12],L[37]=L[37]+B[12]
3531 T[17]=T[17]+B[11],L[17]=L[17]+B[11]
3532 GOTO 3523
3539 IF P1$(62,1)="Y" THEN A[11]=A[11]+A[19]
3540 FOR I=9 TO POS(" "=P1$(9,10)+" ")+7
3550 FOR J=0 TO T0
3600 J0=J*20,Q1$=A$(133+J*4,4)+A$(1,4),Q1=NUM(A$(5,2)),Q0$=P1$(I,1)
3610 ON POS(Q0$="SPCLN") GOTO 3701,3630,3640,3650,3660,3680
3630 Q0=T[J0]; GOTO 3700
3640 Q0=T[J0]-T[J0+10]; GOTO 3700
3650 Q0=T[J0+17]; GOTO 3700
3660 Q0=L[J0]-L[J0+10]; GOTO 3700
3680 Q0=1; GOTO 3700
3700 IF F9$<>"*" THEN GOSUB 7000
3775 NEXT J
3780 NEXT I
3785 DIM T[0]
3810 RETURN 
3900 REM "EXTERNAL CHARGES
3905 READ (Z[10],KEY=A$(92,8)+B$(65,10)+B$(47,3),DOM=3906)IOL=0405; GOTO 3911
3910 READ (Z[10],KEY=A$(92,8)+B$(65,10)+"   ",DOM=3980)IOL=0405
3915 K[3]=K[3]+B[5],K[4]=K[4]+B[4]
3920 K$(117,8)=A$(7,8),K$(111,6)=A$(86,6)
3940 WRITE (Z[10],KEY=K$(1,21))IOL=0405
3995 RETURN 
4000 REM "Update I/C detail transactions
4005 DIM J$(73),J[2]
4007 J1$="Inv#: "+A$(7,7); IF A$(14,1)<>" " THEN J1$=J1$+"-"+A$(14,1)
4010 J$(1,20)=B$(55,20),J$(21,4)=B$(89,4),J$(25,4)=A$(1,4),J$(29,2)=A$(5,2),J$(31,6)=A$(86,6),J$(39,1)="S",J$(40,10)=A$(15,10),J$(50,15)=J1$,J$(65,2)="SJ",J$(67,6)=V0$(22,6),J[0]=-B[1],J[1]=-B[5]
4011 IF B$(89,1)="9" THEN J$(39,1)="I"
4025 WRITE (Z[9],KEY=J$(1,38),DOM=4026)IOL=0390; GOTO 4035
4030 IC8_KEY$=J$(1,38); CALL "IC2LCC",X3$,X4$,Z[9],37,2,LAST_ONE$,IC8_KEY$; J$(1,38)=IC8_KEY$
4031 REM "K=FND(J$(37,2)),J$(37,2)=FNC$(K+1); GOTO 4025
4032 GOTO 4025
4035 IF GB$<>"" THEN ORDER_LINE$=A$(92,8)+B$(47,3),UPDATE_QTY=J[0],GB_COST=J[1],IC8_INFO$=J$; CALL "BM2ICU",X3$,X4$,5,ORDER_LINE$,UPDATE_QTY,IC8_INFO$,GB_COST
4040 RETURN 
4100 REM "For GB, update quote info
4104 REM "Get FS2$ to get quote #
4105 DIM FS2$(229); READ (Z[18],KEY=A$(92,8)+B$(47,3),DOM=4145)FS2$(1)
4110 IF STP(FS2$(221,8),3," ")="" THEN GOTO 4145; REM "No quote
4115 QUOTE$=STP(FS2$(221,8),1),CUST$=A$(15,10),PROD$=B$(65,10),SALE=B[4],COST=B[5]
4130 CALL "GB2QUP",X3$,X4$,QUOTE$,CUST$,PROD$,SALE,COST,""
4145 RETURN 
4200 REM "Check order to see if this sale amount Q0 should be excluded sales, if not, then set Q0 to 0 - ssp 120526
4210 FIND (Z[19],KEY=A$(92,8),DOM=4211)FS1$; GOTO 4215
4211 Q0=0; GOTO 4245; REM " if no order found then assume no exclude
4215 IF FS1$(193,1)<>"Y" THEN Q0=0
4245 RETURN 
5000 REM "END OF UPDATE
5100 REM "
5105 REM IF O0=0 THEN GOTO 05200
5110 READ (Z[13],KEY=X3$(9,3)+"F/M",DOM=5200)
5120 IF %GUI THEN CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0; RUN "AR2DUC" ELSE RUN "AR2DUC"
5200 REM "CLEAR FILES
5205 PRINT @(0,12),'CE',
5210 CALL "ZZPROM","U7",X3$,0,"","","",10
5220 Q$=STR(Z[1]:"00")+STR(Z[2]:"00"); CALL "ZZINTZ",X3$,"B",0,Q$
5230 GOSUB 6700
5240 PRINT 'CF'
5400 REM 
5450 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 CALL "ZZPROM","U9",X3$,0,"","","",4
6025 CALL "ZZPROM","U8",X3$,0,"","","",7
6030 PRINT V0$(22,4),".",V0$(26,2),
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6225 GOSUB 6450
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6490 RETURN 
6600 REM "RESTART LOGIG
6610 REM WRITE (Z(1),IND=I1) V0$,K0$,K1$,K0,K1
6690 RETURN 
6700 REM "Increment Audit Control No.
6705 F9=Z[13]
6710 A2$=X3$(146,6)+"  000100",A0$=X3$(9,3)+"AUDIT",A1$=""; EXTRACT (F9,KEY=A0$,DOM=6716)A0$,A1$
6715 A0=POS(A2$(1,6)=A1$,14); IF A0=0 THEN A1$=A1$+A2$; GOTO 6715 ELSE A2=NUM(A1$(A0+8,4))+1,A2$(9,4)=STR(A2:"0000"),A1$(A0,14)=A2$
6720 WRITE (F9,KEY=A0$)A0$,A1$
6740 RETURN 
7000 REM "Update stats q0$ in period q1 with data q0
7005 IF (F0<>Z[7] AND F0<>Z[8]) AND Q0=0 THEN GOTO 7090; REM "156835
7010 DIM K[14]; P9$=Q1$+Q0$
7020 GOSUB 7100
7040 K[Q1]=K[Q1]+Q0
7060 GOSUB 7150
7090 RETURN 
7100 REM "Read stats (Packed/Unpacked)
7105 Q=POS(Q0$=S9$(49),17); IF Q=0 THEN EXITTO 7090 ELSE M0$=S9$(Q+48,17)
7110 CALL "ZZPACK",X3$,"E",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
7135 GOTO 7190
7150 REM "Write stats
7160 CALL "ZZPACK",X3$,"W",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
7190 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
8930 DEF FNC$(C)=CHR(32+INT((C+32)/95))+CHR(32+MOD(C,95))
8935 DEF FND(Z9$)=(ASC(Z9$(1,1))-32)*95+ASC(Z9$(2,1))-32
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9045 REM 
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 IF NOT(%GUI) THEN BEGIN ; SETESC 9350
9945 CALL "ZZDAPP",X3$,"IPL"
9950 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 REM "249775-Changes to sales tax parameters and tax calculations to     
