0010 REM "A/R Invoice Update <AR2DUA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 04/14/22 - 16.471419 - dmm - SSP# 307309
0037 REM "307309-DBD-113 - Authorize.Net support                             
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 IF L0=1 THEN L0=0; GOTO 0511
0090 IF NOT(%GUI) THEN BEGIN 
0100 SETERR 9000
0110 X0$="AR2DUA",X1$="A/R Invoice Update"
0120 DIM Z0$(80,"-"),Y[4],L[2],P2$(20),H0$(202),I[20]
0130 Z=14
0170 DEF FNV$(Z9$)=CHR(INT(NUM(Z9$)/10)-125)+STR(MOD(NUM(Z9$),10))
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15]
0350 IOLIST E$(1),E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14]
0370 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14]
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14]
0390 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19],I[20]
0400 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9]
0410 IOLIST K$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9]
0420 IOLIST U$,U0$,U[0]
0440 IOLIST FS1$
0450 IOLIST AT1$,AT1[0],AT1[1],AT1[2],AT1[3],AT1[4]
0460 IOLIST EDD$,EDD[0],EDD[1],EDD[2],EDD[3],EDD[4],EDD[5],EDD[6]
0470 IOLIST ART$,ART[0],ART[1],ART[2],ART[3],ART[4],ART[5],ART[6],ART[7],ART[8],ART[9],ART[10],ART[11],ART[12],ART[13],ART[14],ART[15],ART[16],ART[17],ART[18],ART[19],ART[20],ART[21],ART[22],ART[23],ART[24],ART[25],ART[26],ART[27],ART[28],ART[29],ART[30],ART[31],ART[32],ART[33],ART[34],ART[35],ART[36],ART[37],ART[38],ART[39],ART[40],ART[41],ART[42],ART[43],ART[44],ART[45],ART[46],ART[47],ART[48] ! SSP 249775
0480 IOLIST GLW$,GLW[0],GLW[1]
0485 IOLIST GLV$,GLV[0],GLV[1],GLV[2],GLV[3],GLV[4],GLV[5]
0500 REM "Run initialization overlay AR2DUO
0510 RUN "AR2DUO"
0555 EXPORT_ON$="N"; READ (Z[13],KEY=X3$(9,3)+"EXPORTAR",DOM=0556)EXPORT_PARAM$; IF EXPORT_PARAM$(12,1)="Y" THEN EXPORT_ON$="Y"
0600 IF G0$(32,1)="Y" THEN GOSUB SETUP_FOR_GLW ! SSP 295218
1000 REM "READ NEXT RECORD
1020 K0$=KEY(Z[1],END=5000)
1030 DIM A[30]; READ (Z[1],KEY=K0$)IOL=0310
1032 IF K0$="" THEN READ (Z[1],KEY=K0$); GOTO 1020; REM "ssp 102992
1035 IF X3$(30,1)<>"M" AND X3$(330,1)<>"1" THEN K0$=K0$(1,14); REM "needed for conversions
1036 IF X3$(30,1)="M" THEN GOSUB 8600
1038 REM "Remote location processing
1040 IF V$="RL" THEN A[0]=0,A[20]=0
1050 PRINT @(12,12),"Now updating record: ",A$(1,4)+"-"+A$(5,2)+"-"+A$(7,8)+"  "
1059 REM "If exporting and this is not a summary bill then call export routine
1060 IF EXPORT_ON$="Y" AND A$(344,1)<>"Y" THEN CALL "AR2XIA",X3$,X4$,EXPORT_PARAM$,EXPORT_FILES{ALL},A$,A{ALL},Z{ALL},V0$
1070 IF GB$<>"" THEN GOSUB 2100
1094 REM "Don't change K0$
1095 IF POS(X3$(9,3)="020",3)>0 AND V$="FC" THEN A$(7,7)="9999999"
1096 REM "Summary bill moved to AR2DUC 3.0.3
1100 REM "
1102 IF A$(343,1)<>"R" THEN GOTO 1110
1103 IF A[18]<>0 OR A[19]<>0 THEN GOTO 1110; REM "If inv comm or gp adj is not 0 then put in open inv file
1105 FOR R0=1 TO 11; IF A[R0]<>0 THEN EXITTO 1110
1106 NEXT R0
1107 GOTO 1000
1110 REM "Check for Split terms"
1115 S0$="N",R0=1
1120 FIND (Z[3],KEY=K0$,DOM=1200)IOL=0330
1130 GOSUB 7600
1150 IF S0$="Y" THEN FOR W0=0 TO I0-1; GOSUB 7700; PRINT @(33,12),A$(1,4),"-",A$(5,2),"-",A$(7,7),"-",A$(14,1)
1200 IF G0$(32,1)="Y" THEN GOSUB CREATE_GLW ! SSP 295218
1220 GOSUB 2300
1230 GOSUB 2600
1270 GOSUB 2900
1300 REM 
1310 IF V$="RL" THEN REMOVE (Z[1],KEY=K0$) ELSE READ (Z[1],KEY=K0$)
1311 REM "mod 9/14/98 ssp 102992
1390 GOTO 1800
1400 REM "Initialize for lines"
1415 IF P2$(9,4)<>A$(1,4) THEN P2$(1,13)=X3$(9,3)+"G/LYE"+A$(1,4); CLOSE (Z); OPEN (Z)"ZZPARM"; READ (Z,KEY=P2$(1,12))P2$; CLOSE (Z)
1420 Q=15+NUM(P2$(13,2))*6
1450 IF POS("A"=Q9$)=0 THEN R9$=""; GOTO 1480 ELSE H1$=FNV$(P2$(9,4))+A$(5,2)+V$+"A"+V0$(22,6)+"AR"
1455 R8$=A$(7,8); IF POS(A$(77,1)="CD")<>0 THEN R8$=R8$+"-"+A$(77,1)+"M"
1458 IF P0$(13,1)="Y" THEN R7$=E$(1,2)+"-"+E$(3,NUM(P0$(14,1))) ELSE R7$=E$(1,NUM(P0$(14,1)))
1460 DIM R9$(45); R9$(1)=FNS$(A$(25,35))+"\I#"+R8$
1480 F2$=E$(1,18)+"  A"+A$(86,6)+A$(1,6)+V0$(22,6)
1500 REM "UPDATE LINES
1505 REM " Assumes L stuff set in 8000's
1510 A9=A[0],E0=E[0],E1=E[1]
1590 IF S0$="Y" THEN IF W0=INT(POS(" "=C$(15,72)+" ")/6)-1 THEN R0=-9
1594 REM "3.0 - IF ACCRUAL ONLY DO NOT SAVE G/L IN ARO
1595 IF G0$(40,1)="A" THEN F3$="" ELSE F3$="ARO..."
1598 IF MID(TERM$,57,1)="Y" AND POS("C"=Q9$)<>0 THEN H1$=H1$+"{"; REM "SSP#207771
1600 CALL "GL2XUA",X3$,X4$,V2$,7,H1$,R9$,A9,3,2,R0,Z[1],I1,F3$,F2$,E0,Z[7],L$,L[1],E1,L[2],0,0,"","",E$
1620 E[0]=E0,E[1]=E1
1690 RETURN 
1800 REM "RESTART LOGIC
1810 GOSUB 6600
1940 IF S0$="Y" THEN NEXT W0
1950 GOTO 1000
2100 REM "For gold bond do order tracking update
2105 DIM FS1$(350); FIND (Z[14],KEY=A$(92,8),DOM=2145)IOL=0440
2110 IF POS(" "<>A$(92,8))=0 THEN GOTO 2145
2125 ORDER$=FS1$,SHIP_FLAG$=A$(180,6),COMM$="Shipped on Invoice# "+A$(7,7),LOC$="39"
2130 CALL "GB2OTS",X3$,X4$,ORDER$,SHIP_FLAG$,COMM$,LOC$,""
2145 RETURN 
2300 REM "Update Customer Masterfile
2320 DIM D$(599),D[15]
2330 EXTRACT (Z[4],KEY=A$(15,10),DOM=2331)IOL=0340
2335 IF A$(86,6)>D$(379,6) THEN D$(379,6)=A$(86,6); WRITE (Z(4),KEY=A$(15,10))IOL=0340; EXTRACT (Z[4],KEY=A$(15,10),DOM=*NEXT)IOL=0340; REM "SSP# 201584
2340 IF P0$(15,1)="Y" THEN IF POS(" "<>D$(46,10))<>0 THEN EXTRACT (Z[4],KEY=D$(46,10),DOM=2341)IOL=0340
2345 IF POS(" "<>D$(1,10))=0 THEN D$(1,10)=A$(15,10)
2350 REM "Last active date"
2355 IF A$(86,6)>D$(379,6) THEN D$(379,6)=A$(86,6)
2360 REM "Age the invoice amt to proper bucket
2365 IF A$(77,1)="C" THEN D[8]=D[8]+A[9] ELSE GOSUB 7200
2370 REM "Add invoice amt to A/R Balance
2375 D[6]=D[6]+A[9]
2380 REM "Check A/R Balance against High Credit
2385 IF D[6]>D[14] THEN D[14]=D[6],D$(385,6)=A$(86,6)
2570 WRITE (Z[4],KEY=D$(1,10))IOL=0340; IF W9$<>"" THEN CUST$=D$(1,10); CALL "RT2WOC",ERR=2571,X3$,X4$,CUST$,"AR1...","U",D$(1,10)
2590 RETURN 
2600 REM "Update Open Invoice Header File
2605 REM "Assumes that D$ and D() contain the customer the invoice applies to
2610 L$="",L0=0
2615 IF X3$(30,1)="M" THEN IF X3$(330,1)=" " THEN CALL "CM2UAC",X3$,"B",A$,A{ALL},"","",0
2620 DIM E$(180),E[14]
2625 IF POS(A$(77,1)="DC")<>0 THEN EXTRACT (Z[5],KEY=D$(1,10)+A$(78,8),DOM=2626)IOL=0350; GOSUB 8000; GOTO 2800
2630 EXTRACT (Z[5],KEY=D$(1,10)+A$(7,8),DOM=2631)IOL=0350; GOSUB 8000; GOTO 2800
2645 REM "Fill in new invoice"
2650 E$(1)=D$(1,10)+A$(7,8)+A$(77,1)+A$(86,6)
2655 IF D$(1,10)<>A$(15,10) THEN E$(26,10)=A$(15,10)
2660 E$(36)=A$(100,15)+A$(65,2)+"N"
2665 FIND (Z[3],KEY=K0$,DOM=2666); E$(53,1)="Y"
2670 E$(54)=A$(116,6)+A$(122,6)+A$(92,8)+A$(133,20)+A$(78,8)+V0$(22,6)+A$(153,9)+A$(67,10)+A$(1,6)+A$(1,6)
2671 TERM$=""; READ (Z[25],KEY=A$(65,2),DOM=2672)TERM$; REM " SSP# 146930
2672 E$(139)=A$(180,6)+A$(162,15); IF A$(344,1)="Y" THEN E$(160,8)="PENDING"
2673 IF MID(TERM$,57,1)="Y" THEN E[3]=0 ELSE E[3]=A[9]; REM " SSP# 146930
2675 E[2]=A[9],E[4]=A[10],E[6]=A[9],E[7]=A[4]+A[6]+A[8],E[8]=A[2],E[9]=A[1],E[10]=A[1]-A[11],E[11]=A[18],E[12]=A[17],E[14]=A[19]; REM " SSP# 146930
2676 PAID_FLAG$="B"; IF MID(TERM$,57,1)="Y" THEN PAID_FLAG$="A"; REM " SSP# 146930
2680 WRITE (Z[12],KEY=E$(1,10)+PAID_FLAG$+E$(20,6)+E$(11,8)); REM " SSP# 146930
2685 IF POS(" "<>E$(66,8))>0 THEN WRITE (Z[15],KEY=E$(1,10)+E$(66,8)+E$(11,8)); REM "WO111625, invoice sort by order number, ASU file
2810 REM "Do sales tax & commissions"
2812 IF MID(TERM$,57,1)<>"Y" THEN GOTO 2828
2813 ENTRY_EXISTS=0; CALL "UPDEDD;GET_LAST_INVOICE_ENTRY",Z[26],A$(92,2),A$(94,6),A$(7,8),EDD$,EDD{ALL},ENTRY_EXISTS
2814 IF ENTRY_EXISTS=0 THEN {
2815 EDD_NEXT_SEQ=-1; VALID_SEQ=0
2816 CALL "UPDEDD;GET_NEXT_SEQ",Z[26],A$(92,2),A$(94,6),EDD_NEXT_SEQ,VALID_SEQ; IF VALID_SEQ<=0 THEN ESCAPE ! Sequence over the limit
2817 CALL "UPDEDD;GET_PREV_ENTRY",Z[26],A$(92,2),A$(94,6),EDD$,EDD{ALL},ENTRY_EXISTS
2818 IF ENTRY_EXISTS=0 THEN {DIM EDD$(476),EDD[6]; EDD$(1,8)=A$(92,8) }
2819 EDD$(9,3)=STR(EDD_NEXT_SEQ:"000")
2820  }
2825 EDD[0]=A[9],EDD[1]=A[4]+A[6]+A[8],EDD$(80,8)=A$(7,8),EDD$(120,6)=A$(86,6)
2826 WRITE (Z[26],KEY=EDD$(1,11))IOL=0460
2828 IF MID(TERM$,57,1)="Y" THEN GOTO 2830; REM " SSP# 146930
2829 IF POS("Y"=P0$(88,1)+P1$(32,1))<>0 THEN GOSUB 3800
2830 IF P0$(88,1)<>"Y" OR MID(TERM$,57,1)="Y" THEN CALL "AR2DUT",X3$,X4$,0,A$,A{ALL},D$,0,0,0,"",0
2840 GOSUB 4400
2850 REM "Update lines"
2856 IF G0$(32,1)="Y" THEN GOSUB 4800
2860 IF V$="RL" THEN A[0]=0,A[20]=0; GOSUB 1400 ELSE GOSUB 1400; REM "If doing RL's then clear line pointers before doing line processing WHICH ALSO does transaction postings
2870 IF E$(168,1)<>"Y" AND A$(178,1)="Y" THEN E$(168,1)="Y"
2872 E$(169,12)=A$(325,12)
2875 WRITE (Z[5],KEY=E$(1,18))IOL=0350
2876 IF W9$<>"" THEN CUST$=E$(1,10); CALL "RT2WOC",ERR=2877,X3$,X4$,CUST$,"AR6...","U",E$(1,18)
2880 Z$="08C ARY...  08O ASG...  "; GOSUB 9750
2885 WRITE (Z[8],KEY=E$(11,8)+E$(1,10))
2887 Z$="08C ASG...  08O ARY...  "; GOSUB 9750
2888 IF A$(77,1)="C" AND P0$(125,1)="Y" THEN GOSUB 8200; REM "WO121235, update credit memo info record with invoice total
2890 RETURN 
2900 REM "Make G/L control postings
2905 DIM Q$(9)
2910 IF P0$(19,1)="Y" THEN Q$(1)=A$(153,9) ELSE Q$(1)=D$(1,2)
2920 IF H0$(1,9)<>Q$ THEN READ (Z[6],KEY=Q$,DOM=2921)H0$; GOTO 2930
2925 READ (Z[6],KEY="00",DOM=2926)H0$
2950 REM "Post extra PFG postings
2952 IF POS(X3$(9,3)="144244",3)=0 OR D$(1,2)<"10" THEN GOTO 3000
2955 CALL "CM2ZAB",X3$,Z[13],A$(338,3),F0{ALL}
2959 REM "Gross Billings
2960 Q0=-A[9],Q0$=F0$(7,12); GOSUB 8650
2962 Q0=-Q0,Q0$=F0$(43,12); GOSUB 8650
2965 Q0$=F0$(19,12),Q0=(-F0[0])*(A[9]-A[4]-A[6]-A[8])/100; GOSUB 8650
2970 Q0$=F0$(31,12),Q0=-Q0; GOSUB 8650
2975 IF F0[2]=0 THEN GOTO 2981 ELSE Q0=Q0*F0[2]/100,Q0$=F0$(67,12); GOSUB 8650
2980 Q0=-Q0,Q0$=F0$(79,12); GOSUB 8650
2995 RETURN 
3000 REM "Post A/R"
3001 IF POS("C"=Q9$)<>0 THEN GOSUB 3100
3002 IF POS("A"=Q9$)=0 THEN GOTO 3090
3003 TERM$=""; READ (Z[25],KEY=A$(65,2),DOM=3005)TERM$; IF MID(TERM$,57,1)<>"Y" THEN GOTO 3005
3004 IF TERM$(59,12)<>DIM(12) THEN Q0$=TERM$(59,12),Q0=A[9]; GOSUB 8700; IF POS("C"=Q9$)<>0 THEN GOSUB CREDIT_CARD; GOTO 3011 ELSE GOTO 3011; REM "SSP#207771
3005 Q0$=H0$(10,12),Q0=A[9]; GOSUB 8700
3006 IF G0$(32,1)<>"Y" THEN GOTO 3009
3007 DIM GLV$(247),GLV[5]
3010 REM "POST DETAIL HERE
3011 REM "Post Freight out
3012 Q0$=H0$(22,12),Q0=-A[2]; GOSUB 7500
3013 REM "Post sales tax based on state if parm set, WO91564
3014 REM "IF MID(TERM$,57,1)="Y" THEN GOTO 3020; REM " SSP# 146930
3015 IF P0$(119,1)<>"Y" THEN GOTO 3020 ELSE ST$=""; DIM ST[60],TEMP[1]; IF P0$(88,1)<>"Y" OR MID(TERM$,57,1)="Y" THEN AR5_POS=55,ST$=H0$(58,12) ELSE AR5_POS=67,ST$=H0$(46,12)
3016 CALL "AR2DUT",X3$,X4$,4,A$,A{ALL},"",TEMP{ALL},AR5_POS,0,ST$,ST{ALL}; IF ST$="" THEN GOTO 3020 ELSE NUM_CODES=LEN(ST$)/12-1
3017 FOR T3=0 TO NUM_CODES; Q0$=ST$(1,12),Q0=-ST[T3]; GOSUB 8700; IF ST$>"" THEN ST$=ST$(13); IF MID(TERM$,57,1)="Y" AND POS("C"=Q9$)<>0 THEN GOSUB CREDIT_CARD; REM "SSP#207771
3018 NEXT T3; REM "SSP#207771
3019 GOTO 3030; REM "SSP#207771
3020 REM "Post Sales tax based on accrued or not, P0$(88,1)="Y" means sales tax pending
3021 IF MID(TERM$,57,1)="Y" OR P0$(88,1)="N" THEN Q0$=H0$(58,12) ELSE Q0$=H0$(46,12); GOTO 3025; REM " SSP# 146930
3022 IF P0$(88,1)<>"Y" THEN Q0$=H0$(58,12) ELSE Q0$=H0$(46,12)
3025 Q0=-(A[4]+A[6]+A[8]); GOSUB 8700; IF MID(TERM$,57,1)="Y" AND POS("C"=Q9$)<>0 THEN GOSUB CREDIT_CARD; REM "SSP#207771
3030 REM "Post boths sides of commissions, after checking for accrued or not
3031 REM "Post credit"
3032 REM "P1(32,1)='Y' P= pay at time invoice paid
3033 IF P1$(32,1)<>"Y" OR A[9]=0 THEN Q0$=H0$(190,12) ELSE Q0$=H0$(178,12)
3034 IF MID(TERM$,57,1)="Y" THEN Q0$=H0$(190,12); REM " SSP# 146930,157287
3035 Q0=-A[18]; GOSUB 8700; IF MID(TERM$,57,1)="Y" AND POS("C"=Q9$) THEN GOSUB CREDIT_CARD; REM "SSP#207771
3040 REM "Post Debit to commission expense
3045 Q0$=H0$(166,12),Q0=A[18]; GOSUB 7500
3049 REM "If credit or debit applied to another invoice, and commission did not go to 0 (I(4) from comission pending info, then make adjusting G/L entries. (I9$ set in 3800's -> skip if commission is paid when invoice is paid
3050 IF I9$="*" THEN IF I[4]<>0 THEN IF P1$(32,1)<>"Y" THEN Q0$=H0$(178,12),Q0=I[4]; GOSUB 8700; Q0$=H0$(190,12),Q0=-I[4]; GOSUB 8700; REM "Adjust accrued comissions,then commission payable
3090 RETURN 
3100 REM "Handle cash basis postings
3110 REM "Post commission if commission is paid at billing time
3114 IF MID(TERM$,57,1)="Y" THEN GOTO 3190; REM " SSP# 146930
3115 IF P1$(32,1)="Y" THEN GOTO 3190
3120 Q0$=H0$(190,12),Q0=-A[18]
3125 GOSUB 8700
3130 Q0$=H0$(166,12),Q0=A[18]
3135 GOSUB 7500
3190 RETURN 
3500 REM "READ DETAIL FILE FOR SPLIT COMMISSIONS
3501 IF A[12]=100 THEN I[16]=I[4]; GOTO 3599
3523 DIM ART[48]; READ (Z[27],IND=A[20])*,IOL=0470 ! SSP 249775
3526 IF ART[12]<>0 THEN T[35]=0 ELSE IF ART[13]<>0 THEN T[53]=0 ELSE IF ART[14]<>0 THEN T[71]=0 ELSE IF ART[14]<>0 THEN T[89]=0
3530 IF ART[11]<>0 OR %MSLC THEN T[17]=0,T[35]=0,T[53]=0,T[71]=0,T[89]=0 ! SSP 234674
3531 IF A[20]<1 THEN GOTO 3575 ELSE X3=A[20]
3532 IF X3<=0 THEN GOTO 3580
3533 DIM ART[48]; READ (Z[27],IND=X3)X3,IOL=0470 ! SSP 249775
3535 REM "IF ART[11]<>0 THEN T[17]=0,T[35]=0,T[53]=0,T[71]=0,T[89]=0
3550 T[17]=T[17]+ART[11]
3555 T[35]=T[35]+ART[12]
3560 T[53]=T[53]+ART[13]
3565 T[71]=T[71]+ART[14]
3570 T[89]=T[89]+ART[15]
3575 GOTO 3532
3599 RETURN 
3600 REM READ DETAIL FILE
3602 IF A[12]=100 THEN I[16]=I[4]; GOTO 3699
3631 IF A[20]<1 THEN GOTO 3675 ELSE X3=A[20]
3632 IF X3<=0 THEN GOTO 3680
3633 DIM ART[48]; READ (Z[27],IND=X3)X3,IOL=0470 ! SSP 249775
3635 REM "IF ART[11]<>0 THEN I[16]=0,I[17]=0,I[18]=0,I[19]=0,I[20]=0
3650 I[16]=I[16]+ART[11]
3655 I[17]=I[17]+ART[12]
3660 I[18]=I[18]+ART[13]
3665 I[19]=I[19]+ART[14]
3670 I[20]=I[20]+ART[15]
3675 GOTO 3632
3699 RETURN 
3800 REM "Update Sales tax & commission pending
3820 DIM I$(66),I[20]
3821 I9$=""
3825 IF POS(A$(77,1)="DC")<>0 AND POS(" "<>A$(78,8))<>0 THEN EXTRACT (Z[9],KEY=D$(1,10)+A$(78,8),DOM=3830)IOL=0390; I9$="*"; GOTO 3831
3830 EXTRACT (Z[9],KEY=D$(1,10)+A$(7,8),DOM=3831)IOL=0390
3900 REM "Set I$
3905 TERM$=""; READ (Z[25],KEY=A$(65,2),DOM=3906)TERM$; REM " SSP# 146930
3910 I$(1,10)=A$(15,10),I$(11,8)=A$(7,8),I$(19,6)=A$(86,6),I$(25,10)=A$(67,10),I$(35,5)=A$(128,5),I$(46,20)=A$(133,20)
3920 I[0]=I[0]+A[9],I[1]=I[1]+A[1],I[2]=I[2]+A[2],I[3]=I[3]+A[1]-A[11],I[4]=I[4]+A[18]
3921 REM " I[16]=A[21],I[17]=A[22],I[18]=A[23],I[19]=A[24],I[20]=A[25]; REM " SSP# 167214
3925 IF P1$(62,1)="Y" THEN I[3]=I[3]+A[19]
3927 IF MID(TERM$,57,1)="Y" THEN I[4]=0,I[5]=0,I[6]=0,I[7]=0,I[8]=0,I[9]=0,I[10]=0,I[11]=0,I[12]=0,I[13]=0,I[15]=0; GOTO 4060; REM " SSP# 146930
3930 I[5]=I[5]+A[3],I[6]=I[6]+A[4],I[7]=I[7]+A[5],I[8]=I[8]+A[6],I[9]=I[9]+A[7],I[10]=I[10]+A[8]
3940 I[11]=A[12],I[12]=A[13],I[13]=A[14],I[14]=A[15],I[15]=A[16]
4060 IF I9$="*" THEN I$(11,8)=A$(78,8); WRITE (Z[9],KEY=D$(1,10)+A$(78,8))IOL=0390; GOTO 4071
4061 GOSUB 3600
4070 WRITE (Z[9],KEY=D$(1,10)+A$(7,8))IOL=0390
4080 IF (A$(178,1)="Y" AND P0$(88,1)="Y") OR MID(TERM$,57,1)="Y" THEN CALL "AR2DUT",X3$,X4$,1,A$,A{ALL},I$,I{ALL},0,0,"",0
4090 RETURN 
4400 REM "Update Salesp. Booking and  Commissions payable
4401 REM "Prorate commissions into T()
4404 DIM S[5],T[90]; FOR I0=1 TO 5; S[I0]=A[I0+11]; NEXT I0
4405 GOSUB 7800
4406 IF P1$(62,1)="Y" THEN A0[11]=A0[11]-A0[19]
4407 CALL "AR2XAA",X3$,A0{ALL},1,18,T{ALL},S{ALL},1,5
4408 GOSUB 3500
4409 IF P1$(62,1)="Y" THEN A0[11]=A0[11]+A0[19]
4410 FOR K0=0 TO 4
4415 K1=K0*18-1,K2=133+K0*4; IF POS(" "<>A$(K2,4))=0 THEN GOTO 4690
4416 IF MID(TERM$,57,1)="Y" THEN GOTO 4430; REM " SSP# 146930,157311
4420 IF P1$(32,1)="Y" AND E[3]<>0 THEN GOTO 4600
4430 DIM K$(55),K[9]
4450 EXTRACT (Z[11],KEY=A$(1,6)+A$(K2,4)+D$(1,10)+E$(11,8)+"     ",DOM=4460)IOL=0410; GOTO 4500
4455 REM "Setup new record
4460 K$(1,6)=A$(1,6),K$(7,4)=A$(K2,4),K$(11,10)=D$(1,10),K$(21,8)=E$(11,8),K$(29,5)="     ",K$(34,6)=A$(86,6),K$(40,6)=V0$(22,6)
4500 REM "Add to totals"
4505 K[5]=A[12+K0]
4520 K[0]=K[0]+T[K1+9],K[1]=K[1]+T[K1+1],K[2]=K[2]+T[K1+1]-T[K1+11],K[3]=K[3]+T[K1+2],K[4]=K[4]+A[18],K[6]=K[6]+T[K1+18],K[8]=K[8]+T[K1+18]
4525 IF MID(TERM$,57,1)="Y" THEN K[7]=K[0],K$(46,6)=K$(34,6); REM " If CC Invoice then set trans date and trans amount to invoice date and invoice amount ssp#166441
4530 WRITE (Z[11],KEY=K$(1,33))IOL=0410; REM " ARM IOLIST K[]
4540 REM "Update Commissions by line if applicable
4544 REM "we are assuming we would not be here unless P1$(32,1)="Y" and E(3)=0, see line 4420
4545 IF P0$(108,1)="Y" THEN CALL "AR2CUC",X3$,X4$,E$,A$(1,6)
4600 REM "Sp booking part - ARY
4605 DIM K$(55),K[9]; F9$="N"; REM "F9$="Y"=found existing ARY record, "N"=building ARY record from sctrach
4610 EXTRACT (Z[8],KEY=A$(1,6)+A$(K2,4)+D$(1,10)+E$(11,8)+"     ",DOM=4611)IOL=0410; F9$="Y"; GOTO 4645
4615 REM "Setup new record
4620 K$(1,6)=A$(1,6),K$(7,4)=A$(K2,4),K$(11,10)=D$(1,10),K$(21,8)=E$(11,8),K$(29,5)="     ",K$(34,6)=A$(86,6),K$(40,6)=V0$(22,6)
4630 IF A9$="Y" AND F9$<>"Y" THEN GOSUB 7850; GOTO 4670; REM "If doing an apply to (a9$=y) and not on file ARY (f9$=y) then use adjusting values from invoice, and write out. If found record, then just adjust it using normal logic.
4645 IF A9$="Y" AND F9$="Y" THEN FOR I9=0 TO 8; K[I9]=0; NEXT I9; REM "If an apply to an invoice still here, then just clear the record of old data and write out the new data
4647 K[5]=A[12+K0]
4650 REM "Add to totals"
4660 K[0]=K[0]+T[K1+9],K[1]=K[1]+T[K1+1],K[2]=K[2]+T[K1+1]-T[K1+11],K[3]=K[3]+T[K1+2],K[4]=K[4]+A[18],K[6]=K[6]+T[K1+18],K[8]=K[8]+T[K1+18]
4665 IF LEN(K$)<52 THEN K$=K$+" "; GOTO 4665
4666 K$(52,1)="Y"; REM "This set to y to bypass the addition of gp adjustment on the booking report
4670 WRITE (Z[8],KEY=K$(1,33))IOL=0410; REM " ARY IOLIST K[]
4690 NEXT K0
4692 DIM T[0]
4695 RETURN 
4800 REM "READ THROUGH GLW FILE AND UPDATE DETAIL
4810 READ (Z[31],KEY=A$(7,8),DOM=*NEXT)
4820 GLW_K$=KEY(Z[31],END=4899)
4825 IF A$(7,8)<>GLW_K$(1,8) THEN GOTO 4899
4830 DIM GLW[1]; READ (Z[31],KEY=GLW_K$)IOL=0480
4840 DIM GLV$(247),GLV[5]
4845 GLV$(1,4)=FNV$(A$(1,4))+A$(5,2),GLV$(5,6)=A$(86,6),GLV$(12,2)=V$
4846 GLV$(125,10)=A$(15,10)
4850 GLV$(11,1)=Q9$,GLV$(14,12)=GLW$(14,12),GLV$(33,20)=GLW$(26,20)
4851 GLV$(112,6)=V0$(22,6)
4852 GLV$(118,6)=A$(86,6)
4853 GLV$(135,20)=A$(133,20); REM " SSP 178744
4855 GLV$(63,8)=GLW$(1,8),GLV$(81,12)=A$(325,12),GLV$(93,8)=A$(92,8)
4856 GLV$(101,9)=A$(92,8)+GLW$(46,1)
4860 GLV[0]=GLW[0]
4861 GLV_SEQ$=STR(1:"00000")
4865 REM "DIM GLV[5]
4870 GLV$(26,5)=GLV_SEQ$
4875 WRITE (Z[30],KEY=GLV$(1,32),DOM=*NEXT)IOL=0485; GOTO 4820
4880 IF GLV_SEQ$="99999" THEN ESCAPE ELSE GLV_SEQ$=STR(NUM(GLV_SEQ$)+1:"00000")
4885 GOTO 4870
4899 RETURN 
4900 REM "UPDATE GLV FILE FOR NON-LINE SPECIFIC POSTINGS
4901 READ (Z[31],KEY=A$(7,8),DOM=*NEXT)
4902 GLW_KEY$=KEY(Z[31],END=4999)
4903 DIM GLW[1]; READ (Z[31],KEY=GLW_KEY$)IOL=0480
4904 IF GLW$(1,8)=A$(7,8) THEN GLV$(101,9)=A$(92,8)+GLW$(46,1)
4905 DIM GLV$(247),GLV[5]
4910 GLV$(1,4)=U$(2,4)
4912 GLV$(5,6)=U$(28,6)
4914 GLV$(11,1)=U$(1,1)
4916 GLV$(12,2)=U$(6,2)
4918 GLV$(14,12)=U$(14,12)
4919 GLV$(33,10)=A$(15,10)
4920 GLV$(112,6)=U$(8,6)
4921 GLV$(63,8)=A$(7,8)
4922 GLV$(118,6)=U$(28,6)
4930 GLV_SEQ$=STR(1:"00000")
4970 GLV$(26,5)=GLV_SEQ$
4971 GLV[0]=U[0]
4972 IF U[0]=0 THEN GOTO 4999
4975 WRITE (Z[30],KEY=GLV$(1,32),DOM=*NEXT)IOL=0485; GOTO 4999
4980 IF GLV_SEQ$="99999" THEN ESCAPE ELSE GLV_SEQ$=STR(NUM(GLV_SEQ$)+1:"00000")
4985 GOTO 4970
4999 RETURN 
5000 REM "END OF UPDATE
5020 GOSUB 8770
5022 IF POS(X3$(9,3)="144244",3)<>0 THEN CALL "CM2SBA",X3$,X4$
5023 IF MID(P0$,178,1)="Y" THEN CALL "AR2DUM",X3$,X4$; REM " SSP# 164962 ! SSP286899, moved from line 994 of AR2DUO to line 5023 of AR2DUA
5025 PRINT 'CF',@(12,10),"Files now being cleared.",@(12,12),"Do not Interrupt!!",
5028 IF G0$(32,1)="Y" THEN GOSUB CLOSE_GLW_FILES ! SSP 295218
5030 IF V$="FC" OR V$="RL" THEN GOTO 5300
5100 REM "
5200 REM "CLEAR FILES
5205 Z$="02C "+V2$+"  "+"03C ARC...  "
5210 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
5214 Z$="02OR"+V2$+"  "+"03O ARC...  "
5215 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 5216,9900
5220 CALL "ZZINTZ",X3$,"B",0,STR(Z[2]:"00")+STR(Z[3]:"00")
5223 CALL "ZZDAPP",X3$,"!DGL2XUAAR2XAA"; CALL "ZZDAPP",X3$,"AALL"
5225 IF %GUI THEN CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0; RUN "AR2DUB" ELSE RUN "AR2DUB"; REM " EMAIL FROM DERRICK
5300 REM "Finance charge end
5305 IF V$="RL" THEN CALL "ZZINIT",STR(Z[1]:"00"); GOTO 5340
5310 GOSUB 6700
5313 Z$="02OR"+V2$+"  "+"03C ARK...  03O AS1...  04C AR1...  04O ASD...  "
5315 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 5316,9900
5330 CALL "ZZINTZ",X3$,"B",0,STR(Z[1]:"00")+STR(Z[2]:"00")+STR(Z[3]:"00")+STR(Z[4]:"00")
5400 REM 
5450 GOTO 9900
6000 REM "BACKGROUND
6001 RETURN 
6200 REM "DISPLAY DATA
6225 GOSUB 6450
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6490 RETURN 
6600 REM "RESTART LOGIG
6605 RETURN 
6610 WRITE (Z[1],IND=I1)V0$,K0$,K1$,K0,K1
6690 RETURN 
6700 REM "Increment Audit Control No.
6705 F9=Z; CLOSE (F9); OPEN (F9)"ZZPARM"
6710 A2$=X3$(146,6)+"  000100",A0$=X3$(9,3)+"AUDIT",A1$=""; EXTRACT (F9,KEY=A0$,DOM=6716)A0$,A1$
6715 A0=POS(A2$(1,6)=A1$,14); IF A0=0 THEN A1$=A1$+A2$; GOTO 6715 ELSE A2=NUM(A1$(A0+8,4))+1,A2$(9,4)=STR(A2:"0000"),A1$(A0,14)=A2$
6720 WRITE (F9,KEY=A0$)A0$,A1$
6730 CLOSE (F9)
6740 RETURN 
7000 REM "Set up constants for ageing
7005 IF D2$=D3$ THEN RETURN ELSE D3$=D2$
7010 IF P0$(47,1)="Y" THEN GOTO 7090
7020 REM "age by months"
7030 A0$=D2$(1,4),A2=NUM(A0$(3)),A0=4
7040 FOR I=1 TO 4
7050 A1$=A0$(1,2),A3=A2-I; IF A3<1 THEN A3=A3+12; IF A1$(2,1)<>"0" THEN A1$(2,1)=CHR(ASC(A1$(2,1))-1) ELSE A1$=CHR(ASC(A1$(1,1))-1)+"9"
7060 A0$=A0$+A1$+STR(A3:"00")
7070 NEXT I
7080 RETURN 
7090 REM "Set aging if aging is days"
7100 A0$="",A0=6,A1$="      "
7110 FOR I=0 TO 4
7120 CALL "ZZDATE",X3$,"",D2$,A1$,-P[I],0,0,0,0,0,0,"","",""
7130 A0$=A0$+A1$
7140 NEXT I
7150 RETURN 
7200 REM "Age invoice and set A2= total to update and add invoice total to A2 bucket"
7203 REM "Setup to age
7204 IF D$(11,7)="ONACCNT" THEN A2=8; GOTO 7270
7205 D2$=D$(391,6); GOSUB 7000
7210 IF P0$(46,1)="Y" THEN D1$=A$(116,6) ELSE D1$=A$(86,6)
7220 FOR I=0 TO 4
7230 IF D1$(1,A0)>A0$(I*A0+1,A0) THEN A2=I+7; EXITTO 7270
7240 NEXT I
7250 REM "if still here then maximum age
7260 A2=12
7270 D[A2]=D[A2]+A[9]
7280 RETURN 
7500 REM "New salesperson substitution
7510 IF P0$(110,2)="  " THEN GOTO 7530
7520 Q0$(NUM(P0$(110,2),ERR=7530),2)=A$(135,2)
7530 GOSUB 8700; IF MID(TERM$,57,1)="Y" AND POS("C"=Q9$)<>0 THEN GOSUB CREDIT_CARD; REM "SSP#207771
7540 RETURN 
7600 REM "Setup for split terms, set W(), I0, V(), S0$
7605 S0$="Y"
7610 I0=POS(" "=C$(15,72)+" "),I0=INT(I0/6); IF I0=0 THEN S0$="N"; RETURN ELSE DIM V[I0]
7615 W1=0
7617 PRECISION 14; IF A[9]=0 THEN A[9]=1
7620 FOR I=1 TO I0-1
7630 V[I]=C[I-1]*100/A[9],W1=W1+V[I]
7640 NEXT I
7650 V[I0]=100-W1
7655 DIM W[18*I0]
7657 PRECISION 2
7660 CALL "AR2XAA",X3$,A{ALL},1,18,W{ALL},V{ALL},1,I0
7690 RETURN 
7700 REM "Set up A() and R0 based on W0 for split terms pass
7710 W2=1,W1=W0*18
7720 FOR W3=W1 TO W1+10
7730 A[W2]=W[W3],W2=W2+1
7740 NEXT W3
7744 PRECISION 4
7745 R0=V[W0+1]/100; REM "Need to correct rounding somehow
7746 PRECISION 2
7750 A[18]=W[W1+17],A[9]=C[W0],A[10]=C[12+W0],A$(116,6)=C$(15+W0*6,6),A$(122,6)=C$(87+W0*6,6)
7760 IF W0+1>9 THEN A$(7,8)=A$(7,7)+CHR(ASC("A")+W0-9) ELSE A$(7,8)=A$(7,7)+STR(W0+1:"0")
7790 RETURN 
7800 REM "Setup A0 array to hold values similiar to ARB [A()] values, but that might take into account invoices that are being applied to
7805 DIM A0[20]
7809 REM "If no apply to or if the fiscalyear/acct pd of the invoice is different from the fiscalyear/acct pd and the inv balance is 0 or they pay commissions on invoiceing  of the cmemo or dmemo then A9$ is N, and we use the values from ARB
7810 IF POS(" "<>A$(78,8))=0 OR (A$(1,6)<>E$(133,6) AND (E[3]=0 OR P1$(32,1)="Y")) THEN A9$="N"; FOR I0=1 TO 20; A0[I0]=A[I0]; NEXT I0; GOTO 7845 ELSE A9$="Y"; REM "If not an apply to invoice, just fill in the values from ARB,A9$ (Y/N) - "Y"=apply to is in process, "N"= normal invoice
7815 A0[1]=E[9],A0[2]=E[8]; REM "net amount, freight from AR6
7820 A0[3]=I[5],A0[4]=I[6],A0[5]=I[7],A0[6]=I[8],A0[7]=I[8],A0[8]=I[10]; REM "State taxable amount, state tax billed, county ta, county tb, local ta, local tb from ARK
7825 A0[9]=E[2],A0[10]=E[4],A0[11]=E[9]-E[10]; REM "invoice total, discount amount, cost (net - gp) from AR6
7830 A0[12]=A[12],A0[13]=A[13],A0[14]=A[14],A0[15]=A[15],A0[16]=A[16]; REM "Commision splits from ARB (so commission split can be changed via invoicing?)
7835 A0[17]=E[12],A0[18]=E[11],A0[19]=E[14]; REM "Costed freight, total commission and total gp adjs b4 comm from AR6
7845 RETURN 
7850 REM "Use CM/DM invoice info to update ARY, instead of original invoice
7853 IF P1$(62,1)="Y" THEN A[11]=A[11]-A[19]
7855 DIM T0[90]; CALL "AR2XAA",X3$,A{ALL},1,18,T0{ALL},S{ALL},1,5
7857 IF P1$(62,1)="Y" THEN A[11]=A[11]+A[19]
7860 K[5]=A[12+K0]
7865 K[0]=T0[K1+9],K[1]=T0[K1+1],K[2]=T0[K1+1]-T0[K1+11],K[3]=T0[K1+2],K[4]=A[18],K[6]=T0[K1+18],K[8]=T0[K1+18]
7866 K$(52,1)="Y"; REM "This set to y to bypass the addition of gp adjustment on the booking report
7895 RETURN 
8000 REM "Handle case of existing open invoice"
8010 IF A$(1,6)>E$(133,6) THEN E$(133,6)=A$(1,6)
8020 E[2]=E[2]+A[9],E[3]=E[3]+A[9],E[4]=E[4]+A[10],E[7]=E[7]+A[4]+A[6]+A[8],E[8]=E[8]+A[2],E[9]=E[9]+A[1],E[10]=E[10]+A[1]-A[11],E[11]=E[11]+A[18],E[12]=E[12]+A[17],E[14]=E[14]+A[19]
8030 REM "Add a line to transaction lines"
8031 REM "assumes that E$ and E() holds invoice to attach transaction to
8035 DIM L$(46),L[3]
8040 L$(1)=E$(1,18)
8045 ON POS(A$(77,1)="IDC") GOTO 8046,8046,8047,8048
8046 L$(19)="A"; GOTO 8050
8047 L$(19)="D"; GOTO 8050
8048 L$(19)="C"
8049 REM "Use K0$ for summary bill
8050 L$(20)=A$(86,6)+K0$(7,8)+V0$(22,6)+A$(1,6),L[1]=A[9],L[2]=A[10]
8100 REM "Find and delete sort record if existing
8130 WRITE (Z[12],KEY=E$(1,10)+"B"+E$(20,6)+E$(11,8))
8140 REMOVE (Z[12],KEY=E$(1,10)+"A"+E$(20,6)+E$(11,8),DOM=8150)
8190 RETURN 
8200 REM "Update credit memo info record with invoice total - WO121235
8210 Z$="08C ARY...  08O AT1...  "; GOSUB 9750
8215 DIM AT1[4]; EXTRACT (Z[8],KEY=A$(15,10)+A$(7,8),DOM=8240)IOL=0450
8220 AT1[1]=ABS(A[9])
8225 WRITE (Z[8],KEY=A$(15,10)+A$(7,8))IOL=0450
8240 Z$="08C AT1...  08O ARY...  "; GOSUB 9750
8245 RETURN 
8600 REM "Find blank company code
8610 CLOSE (14); OPEN (14)"ARH"+X3$(9,3)
8620 DIM J9$(36); FIND (14,KEY=A$(15,2))J9$
8625 A$(338,3)=J9$(33,3)
8630 WRITE (Z[1],KEY=K0$)IOL=0310
8645 RETURN 
8650 REM "Replace Source Company Code if needed
8660 P=POS("???"=Q0$); IF P>0 THEN Q0$(P,3)=A$(338,3)
8700 REM "G/L control postings accumulation
8710 REM "q0$=G/L account number, q0=amount
8720 IF A$(1,6)<>T0$ THEN GOSUB 8770; T0$=A$(1,6)
8730 P=POS(Q0$=T9$,12); IF P>0 THEN P=INT(P/12),O[P]=O[P]+Q0; GOTO 8760
8740 IF LEN(T9$)<14*LEN(Q0$) THEN T9$=T9$+Q0$; GOTO 8730
8750 GOSUB 8770; GOTO 8730
8760 RETURN 
8770 REM "FLUSH G/L ACCUM - SUMMARY POST
8780 IF T9$="" THEN GOTO 8880; REM "No postings not to be update are supposed to be in here; i.e. no accrual postings on a cash basis only system
8785 Z$="08C ARY...  08O GL6...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 8787,8786
8786 CALL "ZZPROM",".4",X3$,0,"Another user is updating the G/L detail file (G/L-U1).  <Ret> when its done","","",0; GOTO 8785
8790 FOR X=1 TO LEN(T9$) STEP 12
8800 P=INT(X/12)
8810 IF O[P]=0 THEN GOTO 8870
8815 IF P2$(9,4)<>T0$(1,4) THEN P2$(1,13)=X3$(9,3)+"G/LYE"+T0$(1,4); READ (Z[13],KEY=P2$(1,12))P2$
8816 Q=NUM(P2$(13,2)),Q=15+Q*6
8820 U$="A"+FNV$(P2$(9,4))+T0$(5,2)+V$+V0$(22,6)+T9$(X,12)+$7D7D$+A$(86,6)+"AR"
8830 U0$="Sales Journal"; DIM U[1]
8840 EXTRACT (Z[8],KEY=U$(1,27),DOM=8841)IOL=0420
8850 U[0]=U[0]+O[P]
8860 WRITE (Z[8],KEY=U$(1,27))IOL=0420
8865 IF G0$(32,1)="Y" THEN GOSUB 4900
8870 NEXT X
8875 Z$="08C GL6...  08O ARY...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
8880 T9=14,T9$=""; DIM O[T9]
8890 RETURN 
8900 REM "Functions
8910 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8920 DEF FNV$(Z9$)=CHR(INT(NUM(Z9$)/10)-125)+STR(MOD(NUM(Z9$),10))
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9750 REM 
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
9770 RETURN 
9900 REM "END PROGRAM
9905 CALL "ZZDAPP",X3$,"!DGL2XUAAR2XAA"
9907 CALL "ZZDAPP",X3$,"AALL"
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 IF NOT(NUL(Y4$)) THEN IF %GUI THEN CALL Y4$ ELSE RUN Y4$
9950 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
10000 REM "POST Cash basis for credit card
10005 CREDIT_CARD:; REM "SSP# 207771
10010 Z$="08C ARY...  08O GL6...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 10015,10012
10012 CALL "ZZPROM",".4",X3$,0,"Another user is updating the G/L detail file (G/L-U1).  <Ret> when its done","","",0; GOTO 10010
10015 U$="C"+FNV$(P2$(9,4))+T0$(5,2)+V$+V0$(22,6)+Q0$+$7D7D$+A$(86,6)+"AR"
10016 U0$="Sales Journal"; DIM U[1]
10020 EXTRACT (Z[8],KEY=U$(1,27),DOM=*NEXT)IOL=0420
10030 U[0]=U[0]+Q0
10040 WRITE (Z[8],KEY=U$(1,27))IOL=0420
10060 Z$="08C GL6...  08O ARY...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
10090 RETURN 
11000 SETUP_FOR_GLW:
11010 CHL_ZZ=HFN; OPEN (CHL_ZZ)"ZZPARM"
11030 DIM I0W$(40); FIND (CHL_ZZ,KEY=X3$(9,3)+"I/C",DOM=*NEXT)I0W$ ! ZZPARM
11040 CLOSE (CHL_ZZ)
11050 CHL_ARV=HFN; OPEN (CHL_ARV)"ARV"+%C$
11060 CHL_FS1=HFN; OPEN (CHL_FS1)"FS1"+%C$
11065 CHL_FM0=HFN; OPEN (CHL_FM0)"FM0"+%C$
11070 G8W=NUM(G0$(170,2),ERR=*NEXT)
11080 ARTW_IOLIST:IOLIST X0,ARTW$,ARTW[0],ARTW[1],ARTW[2],ARTW[3],ARTW[4],ARTW[5]
11090 RETURN 
11099 ! 
12000 CREATE_GLW:! REM "G/L INTERFACE TO DETAIL SSP 295218
12005 T9W$="",JW9=0,I0W=NUM(I0W$(40,2)),I1W=NUM(I0W$(42,1)); DIM TW[30],ARTW$(104),ARTW[5],M$(223) ! I1=Starting digit of G/L sub account number to substitute
12010 IF A[20]=0 THEN GOTO END_CREATE_GLW ! ELSE IF A[0]>0 THEN GOSUB 8900
12015 X0=A[20]
12020 ! IF GL_PARM$(32,1)="Y" AND A[20]=X0 THEN GOSUB 8800; REM "CLEAR THE ENTRIES
12030 DIM FS1W$(350); FIND (CHL_FS1,KEY=A$(92,8),DOM=*NEXT)FS1W$
12045 READ_ART:
12050 IF X0<=0 THEN GOTO END_CREATE_GLW
12055 READ (Z[27],IND=X0)IOL=ARTW_IOLIST
12060 IF POS(ARTW$(1,1)="M")<>0 OR (ARTW[4]=0 AND ARTW[5]=0) THEN GOTO READ_ART
12070 DIM FS2W$(228); FIND (Z[32],KEY=A$(92,8)+ARTW$(47,3),DOM=*NEXT)FS2W$; REM "ssp90847, will check for PO code, if none then no WIP entry FS2
12080 FIND (CHL_ARV,KEY=ARTW$(75,9),DOM=*NEXT)QW$ ! **ARV
12090 QW=0; IF A$(77,1)="C" THEN QW=1
12100 DIM GLW$(99),GLW[1]; GLW$(1,8)=A$(7,8),GLW$(26,20)=ARTW$(55,20),GLW$(9,3)=ARTW$(47,3),GLW$(46,1)=FS2W$(9,1),GLW$(47,4)=FS1W$(93,4),GLW$(51,16)=FS1W$(106,16)
12110 IF A[20]=X0 THEN LINE=1 ELSE LINE=LINE+1; REM " SSP 172448
12120 QW=94+QW*36,QW0$=QW$(QW,12),QW0=-ARTW[4]; GOSUB WRITE_GLW
12130 IF POS(G0$(40,1)="AC")>0 AND POS(ARTW$(1,1)="IC")<>0 AND ARTW$(89,4)<>"DS  " AND ARTW$(89,4)<>"    " THEN QW=QW+12,QW0$=QW$(QW,12),QW0=ARTW[5]; GOSUB WRITE_GLW; QW=QW+12,QW0=-QW0,QW0$=QW$(QW,12),JW9=1; IF FS1W$(43,1)="Y" AND ARTW$(47,3)<>"   " THEN GOSUB CHECK_WO ELSE GOSUB WRITE_GLW
12140 IF FS1W$(43,1)="Y" AND ARTW$(47,1)<>"   " AND ARTW$(1,1)="N" THEN QW=QW+12,QW0$=QW$(QW,12),QW0=ARTW[5]; GOSUB WRITE_GLW; GOSUB CHECK_WO
12150 IF POS(" "<>QW$(202,12))>0 AND POS(G0$(40,1)="AC")>0 AND POS(ARTW$(89,4)="    DS  ",4)>0 AND (FS2W$(9,1)<>" " OR P0$(120,1)="Y") THEN QW=QW+12,QW0$=QW$(QW,12),QW0=ARTW[5]; GOSUB WRITE_GLW; QW0=-QW0,QW0$=QW$(202,12); IF POS(P0$(121,1)="YD")>0 THEN GOSUB WRITE_GLW ELSE GOSUB 13010; REM "SKIP DIV & WHSE SUBSTITUTION SSP 83064 unless parm set ssp98579, added FS2 PO code check on ssp90847, added a/r parm check on ssp98118
12160 GOTO READ_ART
12190 END_CREATE_GLW:
12195 DIM M$(0); RETURN 
12199 ! 
12200 GL_SUBSTITUTION:REM "Digit replacement (2 digits)
12205 IF JW9=1 THEN JW9=0; GOTO WH_SUBSTITUTE
12210 IF G8W<=0 THEN GOTO 12250
12220 REM "Division subsitition - OR - Salesperson
12230 IF POS(P0$(121,1)="DN")>0 AND QW$(202,G8W)=QW0$(G8W,2) THEN QW0$(G8W,2)="00"
12240 IF P0$(13,1)="Y" THEN QW0$(G8W,2)=A$(15,2) ELSE IF P0$(13,1)="S" AND NUM(A$(135,2),ERR=*NEXT)>=0 THEN QW0$(G8W,2)=STR(NUM(A$(135,2)):"00")
12250 IF P0$(13,1)<>"S" AND P0$(110,2)>"00" AND NUM(A$(135,2),ERR=*NEXT)>=0 THEN QW0$(NUM(P0$(110,2),ERR=*NEXT),2)=STR(NUM(A$(135,2)):"00")
12260 IF P0$(110,2)<>DIM(2) AND POS(P0$(121,1)="D")>0 AND QW$(202,G8W-1)=QW0$(1,G8W-1) THEN QW0$(NUM(P0$(110,2),ERR=12290),2)="00"
12270 IF QW$(202,G8W-1)=QW0$(1,G8W-1) AND P0$(13,1)="Y" AND P0$(121,1)="D" THEN QW0$(G8W,2)=A$(15,2); REM " SSP# 
12280 IF QW$(202,G8W-1)=QW0$(1,G8W-1) AND P0$(121,1)="Y" THEN TEST_P0$=MID(QW0$,NUM(P0$(110,2),ERR=*NEXT),2,ERR=*NEXT); QW0$(NUM(P0$(110,2),ERR=*NEXT),2)=STR(NUM(A$(135,2)):"00"); REM " SSP# TEST_P0$=mid is to see if p0$(110,2) is a valid number (ie not 0), in that case no substitution takes place
12290 GOTO 12340
12300 WH_SUBSTITUTE:REM "Warehouse substitution
12310 DIM MW$(223); READ (CHL_FM0,KEY="D"+A$(15,10)+ARTW$(89,4),DOM=*NEXT)MW$ ! FM0
12320 IF POS(" "<>MW$(214,I1W))=0 THEN GOTO *RETURN
12330 QW0$(I0W,I1W)=MW$(214,I1W)
12340 RETURN 
13000 WRITE_GLW:! REM "ACCUM
13005 IF G0$(149,1)="Y" OR (JW9=1 AND I0W$(39,1)="Y") THEN GOSUB GL_SUBSTITUTION ! i0$(39,1)=Does company postG/L inventory control accounts by location
13010 IF QW0=0 THEN GOTO *RETURN
13015 GLW$(14,12)=QW0$
13020 GLW[0]=QW0
13030 P=POS(QW0$=T9W$,LEN(QW0$)); IF P=0 THEN T9W$=T9W$+QW0$; GOTO *SAME ELSE P=INT(P/LEN(QW0$))+1
13035 TW[P]=TW[P]+QW0
13050 IF G0$(32,1)<>"Y" THEN GOTO *RETURN; REM " SSP 172448
13060 WRITE_SEQ$=STR(1:"00")
13065 GLW$(12,2)=WRITE_SEQ$
13070 WRITE (Z[31],KEY=GLW$(1,13),DOM=*NEXT)GLW$,GLW[0],GLW[1]; GOTO *RETURN
13080 WRITE_SEQ$=FN%NEXT_SEQ$(WRITE_SEQ$,1); GOTO 13065
13090 RETURN 
13099 ! 
14000 CHECK_WO:! REM "W/O?
14005 RETURN ! The parameter to check for a  Work order is longer used fs1$(43,1), so bypass this routine  (copied from AR2EAC, reading from FS3)
14010 DIM D[1]; FIND (Z[11],KEY=A$(7,8)+A$(92,8)+ARTW$(47,3)+" ",DOM=*RETURN)*,*,*,*,*,*,D[0] ! FS3 (INV# + ORDR#+LINE#)
14020 IF ARTW$(1,1)="N" THEN QW0$=QW$(190,12) ! ART rec type
14030 QW0=-D[0] ! Unit cost
14040 JW9=1; GOSUB WRITE_GLW
14050 QW0=(-ARTW[5])-QW0 ! ART Total costr
14055 REM "APPLIED OVERHEAD ACCT"
14060 QW0$=QW$(178,12); JW9=0; GOSUB WRITE_GLW
14090 RETURN 
14099 ! 
14100 CLOSE_GLW_FILES:
14110 CLOSE (CHL_ARV,ERR=*NEXT)
14120 CLOSE (CHL_FS1,ERR=*NEXT)
14130 CLOSE (CHL_FMO,ERR=*NEXT)
14190 RETURN 
14199 ! 
56000 REM "192613-Error 47 in Sales Journal Update
56001 REM "207771-Credit card invoices do not create a cash basis GL posting  
56010 REM "189822-Credit Card Processing for webec back orders filled
56012 REM "234674-Freight charge back errors with two reps on account         
56013 REM "249775-Changes to sales tax parameters and tax calculations to     
56014 REM "286899-See 282961/107, 285900/352; Timing issue with XML Output of 
56015 REM "295218-Only seeing 4 records in GLV_DETAILINVT and no record for   
56016 REM "307309-DBD-113 - Authorize.Net support                             
