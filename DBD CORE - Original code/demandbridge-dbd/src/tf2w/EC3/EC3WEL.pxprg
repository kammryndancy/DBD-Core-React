0010 REM "EC Welcome routine <EC3WEL>
0020 SETESC 9300; SETERR 9000
0035 REM "4.5 - 02/13/02 - 18.74 - wab - SSP# 146024
0040 REM "Copyright 2002 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="EC3WEL",X1$="E/C Welcome"
0120 EOL$=$0D0A$,EOL$=ATH(STP(EC_PARM$(57,8),1)); REM "Default to CR/LF, then set from parameters
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "For now assign session id based on psuedo random file name given
1000 REM "Process DATA array
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1020 IF TAG$="template" THEN TEMPLATE$=VALUE$; GOTO 1090
1021 IF TAG$="user" THEN USER$=VALUE$; GOSUB 3000; GOTO 1090
1022 IF TAG$="password" THEN PASSWORD$=VALUE$; GOTO 1090
1090 NEXT I
1100 REM "If use next $ to open file, read line by line and insert
1101 VERSION$="EV",PROGRAM$=PGN; DIM BYBINFO$[5]; BYBINFO$[1]=PROGRAM$,BYBINFO$[2]=VERSION$,BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1105 CLOSE (100); OPEN (100,OPT="TEXT")TEMPLATE$
1110 READ (100,END=1195)LINE$; GOSUB 7550
1115 INDEX=POS("?tf?"=LINE$); IF INDEX=0 THEN PRINT (OUTPUT)LINE$,EOL$,; GOTO 1110
1120 END_INDEX=POS("?"=LINE$(INDEX+4)); TAG$=LINE$(INDEX+4,END_INDEX-1),LINE1$=LINE$(1,INDEX-1),LINE2$=LINE$(INDEX+END_INDEX+4)
1130 GOSUB 2000; GOTO 1115
1995 GOTO 9900
2000 REM "Get tag value & substitute
2010 IF TAG$="cust_code" THEN LINE$=LINE1$+CUST_CODE$+LINE2$; GOTO 2095
2015 IF TAG$="ec_company" THEN LINE$=LINE1$+STP(EC_PARM$(7,50),1)+LINE2$; GOTO 2095
2020 IF TAG$="cust_name" THEN LINE$=LINE1$+STP(EC$[1](23,35),1)+LINE2$; GOTO 2095
2025 IF TAG$="session_id" THEN LINE$=LINE1$+EC$[1](1,7)+LINE2$; GOTO 2095
2027 IF TAG$="date" THEN LINE$=LINE1$+EC$[4]+LINE2$; GOTO 2095
2029 IF TAG$="time" THEN LINE$=LINE1$+EC$[5]+LINE2$; GOTO 2095
2031 IF TAG$="buyer_id" THEN LINE$=LINE1$+STP(EC$[1](8,15),1)+LINE2$; GOTO 2095
2033 IF TAG$="buyer_name" THEN LINE$=LINE1$+STP(EC$[2](16,35),1)+LINE2$; GOTO 2095
2090 LINE$=LINE1$+LINE2$; GOTO 2095; REM "If here, no matching tag, remove it so processing can continue
2095 RETURN 
3000 REM "Set formatted CUST_CODE$
3015 CALL "ZZDISP","AX",EC$[2](51,10),"A/R",X3$,CUST_CODE$,"",0,0,X4$
3045 RETURN 
7550 REM "Send LINE$ to EC3BYB for generic fields processing
7560 CALL "EC3BYB",ERR=7561,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,BYBINFO${ALL},BYBOPTIONS$
7595 RETURN 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
