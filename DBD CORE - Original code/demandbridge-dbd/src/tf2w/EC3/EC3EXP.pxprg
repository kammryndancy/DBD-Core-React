0010 REM "Expire EC Sessions <EC3EXP>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 07/11/06 - 0.281666 - crg - SSP# 191173
0037 REM "191173-Logic to show previous, 1, 2, 3, 4, etc, then next links for
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "If called, return count of records moved in q0$
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EC3EXP",X1$="Expire EC Sessions"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 IF Q1$<>"EC3SRV" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST ECB$,ECB[0],ECB[1],ECB[2]
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ECB...  02O ECC...  13O ZZPARM  03O EE5...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 DIM EC_PARM$(1275); READ (Z[13],KEY=X3$(9,3)+"E/C",DOM=0551)EC_PARM$(1)
0600 REM "
0610 IF Q1$="" THEN GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0900 REM 
0905 MOVED=0
0910 READ (Z[1],KEY="",DOM=0911)
1000 REM "BEGIN MAIN PROCESS
1020 C=C+1; IF MOD(C,T0)=1 THEN IF Q1$="" THEN GOSUB 8150
1100 REM "Proces entries
1110 DIM ECB[2]; READ (Z[1],END=5000)IOL=0310
1115 ELAPSED_TIME=INT((FN%GET_DATETIME("",0)-ECB[1])*1440)
1120 IF NUM(ECB$(94,3))>0 OR ECB$(94,3)="   " THEN PRECISION 14; IF ELAPSED_TIME>=NUM(ECB$(94,3)) THEN ECB$(93,1)="Y" END_IF END_IF ; PRECISION 2; REM "If session timer not 0 then check current time against lastactivity+timeout in minutes(1440 per day),if greater then mark as expired - IF blank then not setup correctly, remove it 0 must say 0
1122 IF EC_PARM$(1095,1)="Y" THEN TMP_SESSION_ID$=ECB$(1,7); ECB$(1,7)=ECB$(1171,7); ECB$(1171,7)=TMP_SESSION_ID$ ! SSP174619
1125 REM "if expired then remove from ECB and move to ECC
1126 IF ECB$(93,1)<>"Y" THEN GOTO 1146
1128 IF ECB$(97,10)<>DIM(10) THEN ERASE "TRO"+ECB$(97,10),ERR=*NEXT ! SSP191217, RO function file
1129 CALL "UPDEE5;DELETE_SESSION",ERR=*NEXT,Z[3],ECB$(1,7) ! SSP 191173
1130 WRITE (Z[2],KEY=ECB$(1,7))IOL=0310
1135 MOVED=MOVED+1
1140 IF EC_PARM$(1095,1)="Y" THEN REMOVE (Z[1],KEY=TMP_SESSION_ID$,DOM=1141) ELSE REMOVE (Z[1],KEY=ECB$(1,7),DOM=1141)
1145 IF EC_PARM$(1095,1)="Y" THEN ERASE "W"+ECB$(1171,7),ERR=*NEXT ELSE ERASE "W"+ECB$(1,7),ERR=*NEXT ! SSP173078, remove work file if one
1600 GOTO 1000
5000 REM "EOJ
5010 C=T; IF Q1$="" THEN GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program will check the length of sessions active in E/C and expire",@(0,5),"those sessions that have timed out and move them to the log file.",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 IF Q1$<>"EC3SRV" THEN PRINT @(0,7),"There are "+STR(T)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9905 IF Q1$<>"" THEN Q0$=STR(MOVED)
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! Program changes starting 1/5/06
56010 REM "191217-TRO+worksheet id files for EC RON email building up in data
56020 REM "191173-Logic to show previous, 1, 2, 3, 4, etc, then next links for
