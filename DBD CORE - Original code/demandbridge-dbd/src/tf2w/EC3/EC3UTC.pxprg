0010 REM "Update items warehouse group list <EC3UTC>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 02/13/23 - 9.608656 - crg - SSP# 307437
0037 REM "307437-DBD-346; Warehouse Group Rebuild utility rework             
0040 REM "Copyright 2023 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EC3UTC",X1$="Update Item Warehouse Groups"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,ECA2$
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01OSECR...  02O EDC...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0605 DIM ECPARM$(1000); FIND (Z[13],KEY=X3$(9,3)+"E/C")ECPARM$
0610 MULTI_WHSE$=ECPARM$(708,1)
0620 GOSUB 6000; GOSUB 8100
0700 IF MULTI_WHSE$<>"Y" THEN CALL "ZZPROM",".4",X3$,Z,"Feature not licensed","","",0; GOTO 9900
0710 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0711,9900
1000 REM "BEGIN MAIN PROCESS
1010 ECRKEY$=KEY(Z[1],END=1800); READ (Z[1],KEY=ECRKEY$)ECR$
1020 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1110 IF ECR$(304,21)=DIM(21) THEN GOTO 1000
1130 DELETED_GROUP$="N"; DIM W$(200); FIND (Z[2],KEY=ECR$(304,21),DOM=1140)W$; GOTO 1150
1140 DELETED_GROUP$="Y"
1200 REM "udpate ECR record
1210 EXTRACT RECORD (Z[1],KEY=ECRKEY$)ECR$
1220 ECR$(256,48)=DIM(48)
1222 IF DELETED_GROUP$="Y" THEN ECR$(304,21)=DIM(21); GOTO 1250
1230 FOR WI=0 TO 11; ECR$(WI*4+256,4)=W$(WI*4+82,4); NEXT WI
1250 WRITE RECORD (Z[1],KEY=ECRKEY$)ECR$
1290 GOTO 1000
5000 REM "EOJ
5010 C=T; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(5,5),"This utility updates the warehouse group lists to all assigned items",@(6,6),"This utility only applies if you are using the e-Logistics module", ! SSP#232252
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(20,9),"There are "+STR(T)+" records to process" ! SSP#232252
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56001 REM "232252-EC3UTC-- change spelling from update to updates             
56002 REM "307437-DBD-346; Warehouse Group Rebuild utility rework             
