0010 REM "Electronic Commerce Message file<EW3MMB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.3 - 07/12/04 - 14.068055 - tma - SSP# 165556
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EW3MMB",X1$="E C Message Maintenance"
0120 DIM Z0$(80,"-")
0130 K0=1,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$="",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$))
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ECO...  02O AR1...  06O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0620 GOSUB 6000
0990 READ (Z[1],KEY=K9$,DOM=0991)
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1035 DIM A$(500)
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1300,1500
1200 REM "Customer Code
1201 X$="<F10> to set up global message"
1205 IF LEN(A1$)>0 THEN A$(1,10)=A1$(1,10); GOTO 1240
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,18,3,1,10,C0,"A/R","{1"+X$,K9$,"EW3MMB00","AR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; IF C0=2 THEN GOTO 1217 END_IF ; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,1211
1217 IF C0=2 THEN A$(1,10)=DIM(10); C0=0; GOTO 1221
1220 FIND (Z[2],KEY=A$(1,10),DOM=1200)
1240 GOSUB 6450; GOTO 1120
1250 REM "EC Program name
1252 X$="EC3WS1=order entry messages"
1257 Q$=A$(1,10)
1258 IF LEN(A1$)>10 THEN A$(1,20)=A1$(1,20); GOTO 1270
1259 IF A$(11,10)=DIM(10) THEN A$(11,6)="EC3WS1"
1260 CALL "ZZENTR","SXU",A{ALL},A$,X4$,X3$,18,4,11,10,C0,"","{1"+X$,K9$,"EW3MMB00","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1261 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1265 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1266,6800,9700,6970
1266 IF A$(11,5)<>"EC3WS" THEN GOTO 1250
1270 GOSUB 7500
1290 GOSUB 6450; GOTO 1120
1300 REM "message number
1302 GOSUB 7600
1305 Q$=A$(1,20)
1306 X$="Valid message #'s: 1-5,60-62"
1310 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,18,5,21,5,C0,"","{1"+X$,"","EW3MMB01","","",""
1311 IF ABS(C0)=4 THEN GOTO 9700 END_IF ; IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,10)
1315 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1316,6800,9700,6970
1320 GOSUB 7700; IF DM$="" THEN GOTO 1300
1340 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,25),A$(1,LEN(K9$))=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2200,2150,2250,2300,2450,2350,2100,2100,2100,2100,1800
2100 REM "Message Lines 1-4 (X0 values 1-4)
2105 SPOS=(X0-6)*60+30,ROW=X0+6
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,ROW,SPOS,60,C0,"","{3"+X$,"","EW3MMB02","","",""
2140 GOTO 2020
2150 REM "Don't display this message
2152 X$="Prevents message from being displayed on the web"
2160 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,34,7,26,1,C0,"","{3"+X$,"","EW3MMB04","","",""
2190 GOTO 2020
2200 REM "description of the message
2202 X$="enter a description of the message"
2203 GOSUB 7700; IF A$(270,30)=DIM(30) THEN A$(270,30)=DM$; REM "148750
2210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,30,5,270,30,C0,"","{3"+X$,"","EW3MMB04","","",""
2240 GOTO 2020
2250 REM "prevent addition of line in order entry
2252 IF POS("EW3WS"=A$(11,10))=0 THEN GOTO 2290
2253 A$(300,1)="N"; GOSUB 6200; GOTO 2290; REM "disable - only causes problems - handle situations with order line handling
2260 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,34,8,300,1,C0,"","{3"+X$,"","EW3MMB04","","",""
2290 GOTO 2020
2300 REM "add line but with zero quantity?
2301 IF A$(300,1)="Y" THEN A$(27,1)="N"; GOSUB 6200; GOTO 2340
2302 XX=NUM(A$(21,5)); IF XX>=60 AND XX<=69 THEN A$(27,1)="N"; GOSUB 6200; GOTO 2340
2305 X$="Clear quantity on worksheet line but allow addition to order"
2310 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,74,7,27,1,C0,"","{3"+X$,"","EW3MMB04","","",""
2340 GOTO 2020
2350 REM "remove line at order purchase
2351 IF A$(300,1)="Y" THEN A$(28,1)="N"; GOSUB 6200; GOTO 2390
2352 XX=NUM(A$(21,5)); IF XX>=60 AND XX<=69 THEN A$(28,1)="N"; GOSUB 6200; GOTO 2390
2353 X$="Allow line on worksheet (for message display) but remove from final order"
2360 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,74,9,28,1,C0,"","{3"+X$,"","EW3MMB04","","",""
2390 GOTO 2020
2400 REM "only apply after order review
2401 IF A$(300,1)="Y" THEN A$(29,1)="N"; GOSUB 6200; GOTO 2440
2402 XX=NUM(A$(21,5)); IF XX>=60 AND XX<=69 THEN A$(29,1)="N"; GOSUB 6200; GOTO 2440
2403 X$="'order_review' tag passed by web template,DOESN'T APPLY TO LOCATION BASED BUYERS"
2410 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,74,10,29,1,C0,"","{3"+X$,"","EW3MMB04","","",""
2440 GOTO 2020
2450 REM "add line - but change qty to maximum
2451 IF A$(21,5)="00005" AND A$(27,1)="N" THEN GOTO 2452 ELSE A$(302,1)="N"; GOSUB 6200; GOTO 2490
2452 X$="Will change entered order quantity to monthly max less mtd shipments"
2460 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,74,8,302,1,C0,"","{3"+X$,"","EW3MMB04","","",""
2490 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(3,3),"Customer code:",@(1,4),"EC Program Name:",@(2,5),"Message Number:",@(0,6),Z0$,
6061 PRINT @(50,6),"Order line handling",@(44,7),"Add line - but zero quantity?",@(40,9),"Remove line when order purchased?",@(39,8),"Add line - but chg qty to maximum?",
6065 PRINT @(6,7),"Don't display this message?",@(0,8),"Prevent adding line to worksheet?",
6070 PRINT @(4,12),"     Message:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(30,5),A$(270,30),@(34,8),A$(300,1),
6223 PRINT @(34,7),A$(26,1),@(74,7),A$(27,1),@(74,9),A$(28,1),@(74,10),@(74,8),A$(302,1),
6225 FOR I=30 TO 210 STEP 60
6226 PRINT @(18,(I+30)/60+11),A$(I,60),
6229 NEXT I
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6457 DIM AR1$(1000); IF A$(1,10)<>DIM(10) THEN FIND (Z[2],KEY=A$(1,10),DOM=6458)AR1$ ELSE PRINT @(30,3),'CL',"<global system message>"; GOTO 6459
6458 PRINT @(30,3),'CL',AR1$(11,35),
6460 PRINT @(18,4),A$(11,10),@(18,5),A$(21,5),
6461 CALL "ZZDISP","A",A$(1,10),"A/R",X3$,"","",18,3,X4$
6465 GOSUB 7500
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790 ELSE IF V7$>"" THEN X0=POS(" "<>V7$); IF X0>0 THEN X0=X0-1; GOTO 2038
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7500 REM "Print Method name
7505 DIM PNAME$(35); IF STP(A$(11,10),3," ")="" THEN PRINT @(30,4),'CL',; GOTO 7545
7510 TMP$=""; CALL "ZZXPRG",TMP$,0,STP(A$(11,10),1),0,0; IF TMP$="" THEN PRINT @(30,3),'CL',; GOTO 7545
7515 P=POS("<"=TMP$); IF P>1 THEN TMP$=TMP$(1,P-1)
7520 PNAME$(1)=TMP$; PRINT @(30,4),PNAME$
7545 RETURN 
7600 REM "message number prompt
7605 IF A$(1,5)<>"EW3WS" THEN X$=""; GOTO 7640
7610 X$="1=invalid item  2=not in catalog  3=zero stock  4=inadequate stock  5=over max"
7640 RETURN 
7700 REM "defaulted descriptions
7702 N=NUM(A$(21,5)),DM$=""
7710 IF N=1 THEN DM$="Invalid item"
7712 IF N=2 THEN DM$="Item not in Catalog"
7714 IF N=3 THEN DM$="Out of Stock"
7716 IF N=4 THEN DM$="Not enough Stock"
7718 IF N=5 THEN DM$="Over Order Maximum (Quantity)"
7720 IF N=60 THEN DM$="Order Approval: ALL Orders"
7721 IF N=61 THEN DM$="Order Approval: Variable Print"
7722 IF N=62 THEN DM$="Order Approval: Over Qty Max"
7745 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
