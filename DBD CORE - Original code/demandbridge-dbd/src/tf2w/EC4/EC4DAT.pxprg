0010 REM (EC4DAT ): DATE CONVERSION ROUTINE
0015 REM "Prog Type: M -3.0A    
0035 REM "5.1 - 12/09/02 - 13.706666 - wab - SSP# 146930
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 SETERR 9000
0100 ENTER X3$,C0,Z0$,Z1$,Z2$
0101 REM 'C0 = 1=STD -> TF   2=TF -> STD, 3 = NO INPUT - RETURN TODAYS TF DATE (& TIME IN Z0$),  4= RETURN REVESED DATE  5=TF IN MM/DD/YY OUT
0102 REM 'Z0$ = DATE IN
0104 REM 'Z1$ = DATE OUT
0106 REM 'Z2$ = TF MM/DD/YYYY OF VALUE IN Z0$ (REGARDLESS OF INPUT)
0200 REM 'SET UP NUMBER OF DAYS
0210 DIM D[12]; D[1]=31,D[2]=28,D[3]=31,D[4]=30,D[5]=31,D[6]=30,D[7]=31,D[8]=31,D[9]=30,D[10]=31,D[11]=30,D[12]=31
0310 REM 'CONVERT FROM TOPFORM TO STD YYMMDD
0322 DEF FNO$(Z9$)=STR(MOD((ASC(Z9$(1,1))-65)*10,100)+NUM(Z9$(2,1)):"00")+Z9$(3,4)
0325 REM 'CONVERT FORM STD YYMMDD TO TOPFORM
0330 DEF FNA$(X$)=CHR(ASC("A")+INT(NUM(X$(1,2))/10))+STR(MOD(NUM(X$(1,2)),10))+X$(3,4)
0332 DEF FNM$(X$)=CHR(ASC("K")+INT(NUM(X$(1,2))/10))+STR(MOD(NUM(X$(1,2)),10))+X$(3,4)
0350 REM 'CONVERT TF YYMMDD TO MM/DD/YYYY
0360 DEF FNB$(Y$)=Y$(3,2)+"/"+Y$(5,2)+"/"+STR((ASC(Y$(1,1))-65)*10+1900+NUM(Y$(2,1)):"####")
0362 DEF FNN$(Y$)=Y$(3,2)+"/"+Y$(5,2)+"/"+STR((ASC(Y$(1,1))-75)*10+2000+NUM(Y$(2,1)):"####")
0365 REM 'ROUTINE TO CONVERT TF DATE INTO NUMERIC
0370 DEF FNH$(X$)=STR(ASC(X$(1,1)))+X$(2)
0375 REM 'REVERSED DATE CALCULATION
0380 DEF FNN(Z)=10000000-Z
0400 REM 'TRAP FOR OPTIONS 3 OR HIGHER
0430 ON C0-1 GOTO 0500,0500,1300,1400,1500,1600,1700,1800,1900,1900,1700,2000
0500 REM ' CK IF C0$ SET WRONG
0505 C0=1
0510 X=NUM(Z0$,ERR=0550); GOTO 0600
0550 C0=2
1000 REM 'CONVERT DATES
1020 ON C0-1 GOTO 1100,1200
1100 REM 'CONVERT STD YYMMDD TO TF YYMMDD
1110 IF Z0$(1,2)<"90" THEN GOTO 1120
1115 Z1$=FNA$(Z0$),Z2$=FNB$(Z1$); GOTO 1130; REM ' YEAR 1999 OR LESS
1120 Z1$=FNM$(Z0$),Z2$=FNB$(Z1$); REM 'Y2K
1130 IF C0=3 THEN GOTO 1330; REM 'COMPLETE TIME PROCESSING
1150 GOTO 5000
1200 REM 'CONVERT TF YYMMDD TO STD YYMMDD
1220 Z1$=FNO$(Z0$)
1225 Z2$=FNB$(Z0$)
1250 GOTO 5000
1300 REM 'OPTION 3 - NO INPUT (GET BACK TF DATE AND TIME)
1302 REM 'RETURN Z0$ WITH TF TIME
1320 Z0$=DAY,Z0$=Z0$(7,2)+Z0$(1,2)+Z0$(4,2)
1325 GOTO 1100; REM 'PROCESS STANDARD DATE
1350 Z0$=STR(INT(TIM):"00")+STR(100*FPT(TIM):"00")
1380 GOTO 5000
1400 REM 'OPTION 4:  REVERSED DATE
1402 REM 'Z0$ = TF DATE COMING IN
1404 REM 'Z1$ = REVERSED DATE GOING OUT (STRING)
1406 REM 'Z2$ = IDENTIFIES LENGTH OF RETURNING REVERSED DATE
1407 IF NUM(Z2$)=0 THEN Z2$="8"
1420 DIM ZERO$(NUM(Z2$),"0")
1440 REVDATE$=FNH$(Z0$)
1450 REVDATE=FNN(NUM(REVDATE$))
1460 Z1$=STR(REVDATE:ZERO$)
1480 GOTO 5000
1500 REM 'OPTION 5: CONVERT TF IN TO STD OUT FORMATTED AS MM/DD/YY
1520 Z1$=FNO$(Z0$)
1525 Z2$=FNB$(Z0$)
1530 Z1$=Z1$(3,2)+"/"+Z1$(5,2)+"/"+Z1$(1,2)
1590 GOTO 5000
1600 REM 'OPTION 6:  RETURN Z0$=FORMATTED TIME  Z1$=MM/DD/YY  Z2$=MM/DD/YYYY
1610 CALL "TIMECV",X3$,2,Z0$,""
1620 Z1$=DAY
1630 X$=Z1$(7,2)+Z1$(1,2)+Z1$(4,2)
1640 IF X$(1,2)<"99" THEN IN$=FNM$(X$),Z2$=FNN$(IN$) ELSE IN$=FNA$(X$),Z2$=FNB$(IN$)
1690 GOTO 5000
1700 REM 'OPTION 7/11 CALCULATES THE NUMBER OF DAYS BETWEEN 2 DATES Z0$,Z1$ RETURNS Z2$ (DAYS) - OPTION 11 CALCULATES Z1$ AS TODAY
1701 REM 'Z0$ FIRST INPUT DATE  (FROM DATE)
1702 REM 'Z1$ SECOND INPUT DATE (TO DATE)
1703 REM 'Z2$ NUMBER OF DAYS BETWEEN THE DATE RANGE
1705 DIN=1; GOSUB 7300
1710 IF Z1$(1,2)<"90" THEN Z1$=FNM$(Z1$) ELSE Z1$=FNA$(Z1$)
1711 IF C0=11 THEN Z0$=FN%NTD$(CDN,"YYMMDD")
1712 DIN=0; GOSUB 7300
1713 IF Z0$(1,2)<"90" THEN Z0$=FNM$(Z0$) ELSE Z0$=FNA$(Z0$)
1715 DATE1$=Z0$,DATE2$=Z1$; GOSUB 7400; Z2$=STR(NUMBDAYS)
1750 GOTO 5000
1800 REM OPTION 8 'CALCULATE JULIAN DATE FOR YYYY MM DD = Q2,Q0,Q1
1801 REM 'INPUT OF YYMMDD RETURNS JULIAN DATE IN YYYYJJJ FORMAT
1805 REM INPUT Z0$, RETURNS Z1$
1806 DATE$=Z0$; GOSUB 7500; Z0$=DATE$; REM 'CONVERT DTE TO YYYYMMDD
1810 Q0=NUM(Z0$(5,2)),Q1=NUM(Z0$(7,2)),Q2=NUM(Z0$(1,4))
1820 ZZZ$="000031059090120151181212243273304334"
1830 Q=NUM(ZZZ$((Q0-1)*3+1,3))+Q1; IF Q0>2 THEN Q=Q+1-SGN(FPT(Q2/4))
1840 JD=Q+Q2*1000
1850 Z1$=STR(JD)
1890 GOTO 5000
1900 REM OPTION 9 'RETURN A DATE BASED ON A STARTING DATE AND THE NUMBER OF DAYS AHEAD TO CALULATE FROM THE STARTING DATE
1901 REM 'Z0$=INPUT DATE
1902 REM 'Z1$=NUMBER OF DAYS
1903 REM 'Z2$=CALCULATED DATE
1910 DIN=0; GOSUB 7300; REM "convert to yymmdd format
1920 Z2$=FN%NTD$(FN%DTN(Z0$,"YYMMDD")+NUM(Z1$),"YYMMDD")
1930 IF C0=10 THEN IF Z2$(1,2)<"90" THEN Z2$=FNM$(Z2$) ELSE Z2$=FNA$(Z2$)
1940 GOTO 5000
2000 REM "(OPTION 12) get in tf time format and return formatted time
2010 T0=NUM(Z0$(1,2),ERR=5000)+NUM(Z0$(3,2),ERR=5000)/100
2012 IF T0>=12 THEN EXTN$=" PM" ELSE EXTN$=" AM"
2014 IF T0>=13 THEN T0=T0-12
2016 Z1$=STR(INT(T0):"00")+":"+STR(FPT(T0)*60:"00")+EXTN$
2020 GOTO 5000
5000 GOTO 9500
7000 REM 'CONVERT YYYYMMDD TO NNNNNNN
7005 REM 'USES DATE$,D
7010 D=0; IF LEN(DATE$)=6 THEN GOSUB 7500
7020 REM 'FLOATING POINT
7030 D1=NUM(DATE$(1,4),ERR=7090),D2=NUM(DATE$(5,2),ERR=7090),D3=NUM(DATE$(7,2),ERR=7090)
7040 IF D1<1752 THEN GOSUB 7100 ELSE IF D1>1752 THEN GOSUB 7200 ELSE GOSUB 7100; IF D-0 THEN GOSUB 7200
7090 PRECISION 2
7095 RETURN 
7100 REM 'FROM 01/01/0001 TO 09/02/1752
7110 GOSUB 7800
7120 IF D7=3 THEN D[2]=29
7130 GOSUB 7810
7140 IF D>0 THEN D=D4*146100+D5*36525+D6*1461+D7*365+D
7150 IF D>639798 THEN D=0
7190 RETURN 
7200 REM 'FROM 09/14/1752 TO 12/31/9999
7210 GOSUB 7800
7220 IF D7=3 AND (D6<24 OR D5=3) THEN D[2]=29
7230 GOSUB 7810
7240 RETURN 
7250 IF D>639796 THEN D=D+2 ELSE D=0
7290 RETURN 
7300 REM "automatically convert date to YYMMDD format from TF date format (nothing done if already in YYMMDD format)
7310 ON DIN GOTO 7320,7330,7340
7320 REM 'Z0$
7321 TEST=NUM(Z0$(1,1),ERR=7322); GOTO 7390
7322 Z0$=FNO$(Z0$); GOTO 7390
7330 REM 'Z1$
7331 TEST=NUM(Z1$(1,1),ERR=7332); GOTO 7390
7332 Z1$=FNO$(Z1$); GOTO 7390
7340 REM 'Z2$
7341 TEST=NUM(Z2$(1,1),ERR=7342); GOTO 7390
7342 Z2$=FNO$(Z2$); GOTO 7390
7390 RETURN 
7400 REM "@DAYS_BETWEEN: Calculate # of days between 2 dates <YDYSBT>
7401 REM 'TFMMDD FORMATS EXPECTED
7410 REM 'ENTER X3$,DATE1$,DATE2$,NUMBDAYS
7415 JIN$=DATE2$; GOSUB 7600; JD2=JNUMB
7420 JIN$=DATE1$; GOSUB 7600; JD1=JNUMB
7425 IF DATE2$(1,1)=" " OR DATE1$(1,1)=" " THEN NUMBDAYS=0 ELSE NUMBDAYS=JD2-JD1
7435 RETURN 
7500 REM 'BUILD DATE FORMAT YYYYMMDD FROM YYMMDD
7505 DATE1$="",DATE2$=""
7510 IF DATE$(1,2)<"90" THEN GOTO 7520
7515 DATE1$=FNA$(DATE$),DATE2$=FNB$(DATE1$); GOTO 7530; REM 'YEAR LESS THAN 1999
7520 DATE1$=FNM$(DATE$),DATE2$=FNB$(DATE1$); REM 'Y2K PATCH
7530 DATE$=DATE2$(7,4)+DATE2$(1,2)+DATE2$(4,2); REM RETURN YYYYMMDD
7540 RETURN 
7550 REM 'CONVERT TF DATE TO YYMMDD
7560 DATE$=FNO$(DATE1$)
7570 RETURN 
7600 REM "Calculate Julian Date <YJLIAN>
7610 REM "MOD 2/11/92 LINE 8905
7615 REM ' ENTER X3$,JIN$,B
7620 B=0; IF LEN(STP(JIN$))<6 THEN GOTO 7675
7625 C=FNA(JIN$),D=NUM(JIN$(3,2)),E=NUM(JIN$(5,2))
7630 F=C-1,G=INT(F/400),F=F-G*400,H=INT(F/100),F=F-H*100,I=INT(F/G),F=F-I*4; J$="312831303130313130313031"
7635 IF F=3 AND (I<24 OR H=3) THEN J$(3,2)="29"
7640 IF STR(E:"00")<=J$((D-1)*2+1,2) THEN K=E,L=D-1
7645 IF L>0 THEN K=K+NUM(J$((L-1)*2+1,2)),L=L-1; GOTO 7645
7650 IF K>0 THEN K=G*146097+H*36524+I*1461+F*365+K
7655 IF K>639796 THEN K=K+2 ELSE K=0
7660 JNUMB=K
7665 GOTO 7675
7670 DEF FNA(VV$)=10*(ASC(VV$(1,1))-65)+1900+NUM(VV$(2,1))-1570*POS("  "=VV$(1,2))
7675 RETURN 
7800 REM CALCUALTE D4 THRU D7
7805 D7=D1-1,D4=INT(D7/400),D7=D7-D4*400,D5=INT(D7/100),D7=D7-D5*100,D6=INT(D7/4),D7=D7-D6*4
7809 RETURN 
7810 REM '
7812 IF D3>D[D2] THEN GOTO 7819
7814 D=D3,D9=D2-1
7816 IF D9>0 THEN D=D+D[D9],D9=D9-1; GOTO 7816
7819 RETURN 
7900 REM 'GET MAX # DAYS FOR MONTH X IN Q FOR YEAR X1
7905 Q1=NUM(S0$(5,2)),Q0=NUM(S0$(3,2)),Q2=NUM(S0$(1,2)),X=Q0,X1=Q2; REM 'INPUT IN YYMMDD FORMAT
7910 Q=31-SGN(POS(STR(X:"00")="0904061102",2))
7920 IF X=2 THEN Q=28; IF MOD(X1,4)=0 AND MOD(X1,100)<>0 OR MOD(X1,400)=0 THEN Q=29
7921 REM '2 THEN LET Q=Q-1-SGN(FPT(X1/4))
7930 Z2$=STR(Q)
7940 RETURN 
8000 REM - PRINTER ERROR
8010 IF ERR<>0 THEN GOTO 8050
8020 PRINT @(0,22),'CE','RB',"Printer is offline or out of paper, 'CR' to Retry, 'F4' to Abort : ",
8030 INPUT (0,LEN=1)*
8040 IF CTL<>4 THEN PRINT @(0,22),'CE',"Retrying...",; RETRY ELSE GOTO 9500
8050 PRINT @(0,22),'CE','RB',"A printer error "+STR(ERR)+" has occurred on line "+STR(TCB(5))+" 'CR' to Retry, 'F4' to Abort : ",
8060 GOTO 8030
8500 REM 'GENERIC ERROR ROUTINE
8510 IF ERR<>0 THEN GOTO 8550
8520 PRINT @(0,22),'CE','RB',"Record busy, 'CR' to Retry, 'F4' to Abort : ",
8530 INPUT (0,LEN=1)*,
8540 IF CTL<>4 THEN PRINT @(0,22),'CE',"Retrying...",; RETRY ELSE GOTO 9500
8550 PRINT @(0,22),'CE','RB',"Error "+STR(ERR)+" has occurred on line "+STR(TCB(5))+" of ",PGN,", 'CR' to retry, 'F4' to end : ",
8560 GOTO 8530
8860 PRINT @(0,22),'CL',; RETURN 
9000 ! ERROR LOGIC
9010 EXIT ERR
9500 EXIT 
9999 END 
