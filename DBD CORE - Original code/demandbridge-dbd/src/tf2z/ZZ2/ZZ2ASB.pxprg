0010 REM "Screen entry VDT image editor <ZZ2ASB>
0030 REM "(C) 1985,1986,1987 Basic Ideas, Inc; Atlanta, Ga.
0035 REM "3.7 - 02/01/94 - 13.29 - jsc
0040 REM "Copyright 1994 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0120 DIM S$(12); SETERR 9000
0121 SETERR 0000
0210 DEF FNS$(Q$)=Q$(1,POS("  "=Q$+"  ")-1)
0310 IOLIST @(12,23),"Press ",FNS$(C0$(353,8))," for Editor Key Data; ",FNS$(C0$(313,8))," to End Editor",@(0,22)
0380 IOLIST B$
0390 IOLIST @(0,23),'CL',@(0,22),'CL',IOL=0310
0600 DIM S$(20),V2$(0)
0605 W0=80,W1=19,W2=240,B0$="",R5$=CHR(192),D0=1,C9$=""
0606 W2=240,Z7$="ZZ2ASS"
0610 IF V0$="" THEN DIM V0$(W0*W1); IF A$(61,1)="R" THEN GOSUB 8100
0612 IF LEN(V0$)<W0*W1 THEN V0$=V0$+" "; GOTO 0612
0620 GOSUB 5800
0640 GOSUB 6900
0940 X0=1+R0*W0,X=0,Y=3+R0,R1=X0; IF A$(61,1)="H" THEN R1=1,X0=R1,Y=3
0950 GOTO 1000
0990 PRINT IOL=0310
0995 PRINT 'RB',
1000 REM "ENTER NEXT CHARACTER
1010 PRINT 'SB',
1015 IF C0>0 THEN PRINT 'CI',
1075 IF D0>0 THEN PRINT @(70,0),"(",X:"#0",",",Y:"#0",")",
1080 IF Y<=21 THEN PRINT @(X,Y), ELSE Y=21; GOTO 0990
1120 READ RECORD (Z[9],SIZ=1)Q$
1140 GOSUB 8000; IF C8$>"" THEN IF POS(C$=C8$,2)=0 THEN GOTO 0990
1150 IF C0>=0 THEN GOTO 1200
1155 IF Q$=$08$ THEN C0=1; GOTO 1200
1160 IF POS(CHR(X)+CHR(Y)=B0$,2)>0 THEN GOTO 0990
1165 IF Q$<CHR(32) THEN GOTO 0990
1170 IF LEN(V0$(X0))>W0 THEN IF Q$="-" AND X=0 AND POS(" "<>V0$(X0,W0))=0 THEN Q$="-80"; GOTO 3400
1180 V0$(X0,1)=Q$; PRINT @(X,Y),Q$,
1185 X0=X0+1
1190 GOTO 1555
1200 REM "CONTROL
1220 ON C0 GOTO 4000,1400,1420,1440,1460,1480,1500,1600,1600,1900,2000,2100,2200,2600,3000,3100,3300,3500,3600,3700,3800,3900,3950,4600,0990
1400 REM "LEFT 1 CHAR
1405 IF X0>R1 THEN X0=X0-1; GOTO 1550 ELSE GOTO 0990
1420 REM "RIGHT 1 CHAR
1425 IF X<W0 THEN X0=X0+1; GOTO 1550 ELSE GOTO 0990
1440 REM "UP 1 LINE
1445 IF X0>W0+R1-1 THEN X0=X0-W0; GOTO 1550 ELSE GOTO 0990
1460 REM "DOWN 1 LINE 
1465 IF Y-3<W1-1 THEN X0=X0+W0; GOTO 1550 ELSE GOTO 0990
1480 X0=INT(X0/W0)*W0+1; GOTO 1550
1500 REM "POSITION
1505 Q$="     "
1515 PRINT @(0,22),"Enter the screen position in an X,Y or X only format:",
1520 CALL "ZZENTR","SX",A{ALL},Q$,X4$,X3$,54,22,1,5,C0,"","","ENTER THE SCREEN POSITION AS XX OR XX,YY","","","",""; PRINT @(0,22),'CL',; IF C0<0 THEN GOTO 1000
1525 Q$=FNS$(Q$),Q=NUM(Q$,ERR=1526); IF Q>=0 AND Q<W0 THEN X0=(Y-3)*W0+Q+1; GOTO 1550 ELSE GOTO 0990
1530 P=POS(","=Q$); IF P=0 THEN GOTO 0990 ELSE Q0=NUM(Q$(1,P-1),ERR=0990),Q1=NUM(Q$(P+1),ERR=0990)
1535 IF Q0<0 OR Q0>=W0 OR Q1<3 OR Q1>W1 THEN GOTO 0990
1540 X0=Q0+(Q1-3)*W0+1; GOTO 1550
1550 REM "AFTER CURSOR POSITIONING CONTROL KEYS...
1555 Y=INT((X0-1)/W0),X=X0-Y*W0-1,Y=Y+3
1560 IF C9$="" THEN GOTO 1000
1580 ON POS(C9$="A") GOTO 1840
1600 REM "ATTACH SCREEN
1605 GOSUB 6500
1610 P$="ATTACH"; IF C0=8 THEN P$="COPY"
1615 DIM A[4,2]; A[0,0]=X0,A[1,0]=1,A[2,0]=1,C9=C0,A1=1,C9$="A",E3=0,P8=X,P9=Y
1616 IF V0$(X0,1)<>" " THEN P=POS("  "=V0$(X0+1)+"  "); A[1,0]=P
1620 GOSUB 6100; GOSUB 6000
1625 READ RECORD (Z[9],SIZ=1)Q$
1630 GOSUB 8000
1635 IF C0=9 THEN GOTO 1700 ELSE IF POS(STR(C0:"00")="01020304",2)=0 THEN PRINT 'RB',; GOTO 1630
1645 P=POS(STR(C0:"0")="13X24")-3,A1=1; IF P<0 THEN A1=0; IF P=-3 THEN PRINT 'RB',; GOTO 1630
1650 Q2=A[0,0]+A[1,0],Q0=1,Q1=A[2,0]
1655 IF C0=2 THEN IF X+A[1,0]>W0 THEN GOTO 1680 ELSE A[1,0]=A[1,0]+1
1660 IF C0=1 THEN IF A[1,0]=0 THEN GOTO 1680 ELSE A[1,0]=A[1,0]-1,Q2=Q2-1
1665 IF C0=3 THEN IF A[2,0]<=1 THEN GOTO 1680 ELSE A[2,0]=A[2,0]-1,Q2=Q2-A[1,0]+A[2,0]*W0,Q1=1,Q0=A[1,0]
1670 IF C0=4 THEN IF A[2,0]>W1-1 THEN GOTO 1680 ELSE Q2=Q2-A[1,0]+A[2,0]*W0,A[2,0]=A[2,0]+1,Q1=1,Q0=A[1,0]
1675 CALL Z7$,X3$,A1$,Z[8],W0,W1,V0$,Q2,Q0,Q1,A1,V1$,V2$,0,0,0,0,W2,0,0,X4$
1678 GOTO 1625
1680 PRINT 'RB',; GOTO 1625
1690 GOTO 1625
1700 P$="MOVE"; GOSUB 6100
1705 IF A[1,0]=0 THEN V1$=W9$; GOSUB 6720; GOTO 1940
1720 REM "SET MOVE PIECE
1725 P1$=""
1730 FOR P=0 TO A[2,0]-1
1735 P1$=P1$+V0$(A[0,0]+P*W0,A[1,0])
1740 IF C9=7 THEN V0$(A[0,0]+P*W0,A[1,0])=""
1745 NEXT P
1750 GOSUB 6800
1800 REM "MOVE SCREEN PIECE P1$ AROUND
1810 C8$="010203040506091819",S0=A[0,0],S1=A[1,0],S2=A[2,0]
1820 GOTO 1000
1840 REM "RESTORE CURRENT SCREEN
1850 A1=0; GOSUB 6000
1860 A[0,0]=X0
1870 REM "PAINT P1$ PICTURE
1875 CALL Z7$,X3$,A1$,Z[8],S1+0,S2+0,P1$,1,S1,S2,1,V1$,V2$,X,Y-3,E2,E3,W2,0,0,X4$
1880 GOTO 1820
1900 REM "DEPOSIT MOVED SCREEN PIECE
1902 IF C9$="" THEN GOTO 0990 ELSE E4=0; FOR P=0 TO S2-1; E4=E4+POS(R5$=V0$(X0+P*W0,S1)); NEXT P
1905 IF E4>0 THEN CALL "ZZPROM","Y",X3$,S,"Data fields reside within these co-ordinates. Continue?","","",0
1910 P$="WAIT"; GOSUB 6100
1920 FOR P=0 TO S2-1
1925 V0$(X0+P*W0,S1)=P1$(P*S1+1,S1)
1930 NEXT P
1935 GOSUB 6600
1940 P$="",A1=0; GOSUB 6100
1980 C8$="",C9$="",P1$=""
1990 GOTO 1550
2000 REM "DELETE LINE
2090 GOTO 1000
2100 REM "INSERT LINE
2190 GOTO 0100
2200 REM "INSERT TEXT
2390 GOTO 1550
2600 REM "DELETE TEXT
2690 RETURN 
3000 REM "CLEAR TO END
3010 FOR P=0 TO 80-X-1
3015 IF V0$(X0+P,1)<>" " THEN V0$(X0+P,1)=""; IF POS(CHR(X+P)+CHR(Y)=B0$,2)=0 THEN PRINT @(P+X,Y)," ",
3020 NEXT P
3090 GOTO 1000
3100 REM "END EDITOR
3270 IF POS(" "<>G0$)=0 THEN G0$=""
3280 L0=1,V2$=""
3290 GOTO 9800
3300 REM "REPEAT CHARACTER
3305 DIM Q$(3)
3310 PRINT IOL=0390,@(0,22),"ENTER THE CHARACTER TO REPEAT OR 'CR':",@(42),"ENTER THE NUMBER OF CHARS: ",
3340 CALL "ZZENTR","SX",A{ALL},Q$,X4$,X3$,40,22,1,1,C0,"","","ENTER THE CHARACTER TO REPEAT OR PRESS RETURN TO ABORT","","","",""
3345 IF C0<0 THEN GOTO 3480
3380 CALL "ZZENTR","Z",A{ALL},Q$,X4$,X3$,69,22,2,2,C0,"","","ENTER THE NUMBER OF TIMES TO REPEAT OR RETURN TO ABORT","","","",""
3390 IF ABS(C0)=2 THEN GOTO 3340
3400 REM "PUT IN REPEAT CHAR
3410 DIM Q$(NUM(Q$(2,2),ERR=3480),Q$(1,1))
3420 V0$(X0,LEN(Q$))=Q$
3430 PRINT @(X,Y),Q$,
3450 REM "
3480 REM "
3485 PRINT IOL=0390
3495 GOTO 1000
3500 REM "HELP SCREEN
3520 CALL "XZCTRL",X3$,X4$,C0$,""
3530 GOSUB 5800
3590 GOTO 1000
3600 REM "SET/RELEASE TABS
3610 C$=STR(X:"00"),C=POS(C$=T0$,2)
3615 IF C=0 THEN PRINT @(X,2),"+",; C=POS(C$<=T0$,2); IF C=0 THEN T0$=T0$+C$; GOTO 1000 ELSE T0$=T0$(1,C-1)+C$+T0$(C); GOTO 1000
3620 T0$=T0$(1,C-1)+T0$(C+2); PRINT 'BG',@(X,2),X3$(235,1),'EG',; GOTO 1000
3700 REM "TAB OVER
3710 C$=STR(X:"00"),C=POS(C$<T0$,2); IF C=0 THEN GOTO 0990 ELSE C=NUM(T0$(C,2))-X; X0=X0+C; GOTO 1550
3800 REM "RETURN
3810 X0=(Y-2)*W0+1; GOTO 1550
3900 REM "NEXT WORD
3910 IF V0$(X0,1)<>" " THEN P=POS("  "=V0$(X0)+"  ") ELSE P=POS(" "<>V0$(X0))-1
3920 X0=X0+P; GOTO 1550
3950 IF D0<>0 THEN D0=0 ELSE D0=1
3960 IF D0=0 THEN PRINT @(70,0),'CL',
3990 GOTO 1000
4000 REM "ENTER DATA FIELD
4005 IF V0$(X0,1)<>" " THEN GOTO 0990
4010 P$=STR(Y:"00")+STR(X:"00"),P=POS(P$=V1$,4),S3=0
4020 IF P>0 THEN GOTO 4100 ELSE P=LEN(V1$)+1
4030 IF X0$="CS2ZBA" THEN GOTO 1000
4040 CALL "ZZPROM","Y",X3$,S3,"Do you want to add a data field at this screen position?","","",0; ON S3 GOTO 4041,1000
4045 DIM B$(W2); B$(7,4)=P$,V1$=V1$+P$,S3=-1,B$(5,1)="S"
4047 B$(1,2)=STR(INT(P/4+1):"00")
4050 IF X0>30 THEN Q1$=V0$(X0-30,30) ELSE Q1$=V0$(1,X0-1)
4052 IF LEN(Q1$)>X THEN Q1$=Q1$(1+30-X)
4055 IF LEN(Q1$)>0 THEN IF Q1$(1,1)=" " THEN Q1$=Q1$(POS(" "<>Q1$+"*")-1)
4058 Q=POS("  "=Q1$); IF Q>0 THEN Q1$=Q1$(Q+2); GOTO 4055
4059 Q1$=FNS$(Q1$)
4060 IF Q1$>"" THEN IF Q1$(LEN(Q1$),1)=":" THEN Q1$=Q1$(1,LEN(Q1$)-1) ELSE IF Q1$(LEN(Q1$))="?" THEN B$(5,1)="B",B$(11,3)="001"
4061 IF POS("Date"=Q1$)>0 OR POS("date"=Q1$)>0 THEN B$(5,1)="D",B$(11,3)="006" ELSE IF POS("Phone"=Q1$)>0 OR POS("telephone"=Q1$)>0 OR POS("Telphone"=Q1$)>0 THEN B$(5,1)="P",B$(11,3)="014" ELSE IF POS("amount"=Q1$)>0 OR POS("Amount"=Q1$)>0 THEN B$(5,1)="N",B$(11,3)="922" ELSE IF POS("Account"=Q1$)>0 OR POS("account"=Q1$)>0 OR POS("acct"=Q1$)>0 THEN B$(5,1)="A",B$(11,3)="012",B$(14,3)="G/L"
4062 IF Q1$>"" THEN IF Q1$(1,1)="-" THEN Q1$=Q1$(2)
4065 IF Q1$>"" THEN IF Q1$(1,1)=" " THEN Q1$=Q1$(2); GOTO 4065
4075 B$(22,30)=Q1$
4095 GOTO 4200
4100 REM "EXISTING STRING
4110 B$=A1$+P$; READ (Z[8],KEY=B$(1,11))IOL=0380
4120 B$=B$(12)
4200 REM "MAINTAIN DATA ITEM
4210 GOSUB 4400
4220 CALL "ZZ2ASZ",X3$,X4$,Q1$,Q2$,B$,S3
4225 Q$=B$(7,4)
4230 IF S3=3 THEN GOTO 4300
4260 B$=A1$+Q$+B$
4262 WRITE (Z[8],KEY=B$(1,11))IOL=0380
4265 IF B$(14,2)<>"  " THEN GOSUB 4500
4270 GOSUB 7000; B0$=B0$+B9$
4280 GOSUB 5815
4285 PRINT IOL=0310
4290 GOTO 1000
4300 REM "DELETE ITEM
4310 REMOVE (Z[8],KEY=A1$+Q$,DOM=4311)
4320 P=POS(B$(7,4)=V1$,4)
4325 V1$=V1$(1,P-1)+V1$(P+4),Q$=B$(7,4),B$=""
4330 GOSUB 7000; P=POS(B9$=B0$); IF P>0 THEN B0$=B0$(1,P-1)+B0$(P+LEN(B9$)+1)
4380 GOSUB 5815
4390 GOTO 1000
4400 REM "GET RID OF RESERVE POSITIONS
4410 Q$=B$(7,4); GOSUB 7000; A=POS(B9$=B0$); IF A>0 THEN B0$=B0$(1,A-1)+B0$(A+LEN(B9$))
4420 RETURN 
4500 REM "RESEQUENCE DATA ITEMS
4505 B0=NUM(B$(12,2)),B1=NUM(B$(14,2))
4510 IF B1<1 THEN RETURN ELSE IF B1*4>LEN(V1$) THEN B1=LEN(V1$)/4
4512 B0=B0-1,B1=B1-1
4515 V$=V1$(B0*4+1,4),V1$=V1$(1,B0*4)+V1$((B0+1)*4+1)
4520 V1$=V1$(1,B1*4)+V$+V1$(B1*4+1)
4530 FOR B1=1 TO LEN(V1$) STEP 4
4535 READ (Z[8],KEY=A1$+V1$(B1,4))IOL=0380
4540 B$(12,4)=STR((B1+3)/4:"00")+"  "
4542 WRITE (Z[8],KEY=B$(1,11))IOL=0380
4545 NEXT B1
4590 RETURN 
4600 REM "Toggle Graphics Mode
4605 P$="GRAPHICS"; GOSUB 6100
4610 CALL "ZZ2ASG",X3$,G0$,C0$,1,V0$
4615 IF POS(" "<>G0$)=0 THEN G0$=""
4620 P$=""; GOSUB 6100
4690 GOTO 1000
4740 CALL "ZZ2ASG"
4800 REM 
4805 V1$=""
4810 READ (Z[8],KEY=A1$,DOM=4811)
4815 READ (Z[8],END=4830)B$
4820 IF B$(1,LEN(A1$))=A1$ THEN V1$=V1$+B$(8,4); GOTO 4815
4840 GOTO 0010
5800 REM "SCREEN
5810 GOSUB 5900
5815 REM "BOTTOM PART OF SCREEN
5820 DIM A[4,2]; A[0,0]=1,A[1,0]=W0,A[2,0]=W1,A0=1,A1=0,E3=1,E0=0,E1=0,E2=0; GOSUB 6000
5830 GOSUB 6720
5840 IF G0$>"" THEN CALL "ZZ2ASG",X3$,G0$,C0$,0,""
5880 PRINT IOL=0310
5890 RETURN 
5900 REM "TOP OF SCREEN
5905 X$=FNS$(A$(11,50)); IF LEN(A1$)=4 THEN X$=FNS$(A$(5,50))
5910 PRINT 'CS','SB',
5915 PRINT @(39-INT(LEN(X2$)/2),0),X2$
5920 IF X0$="ZZ2ASA" THEN Q$=A$(1,6); IF A$(7,1)<>" " THEN Q$=Q$+"-"+A$(7,1)
5921 IF X0$="CS2ZBA" THEN Q$="OLS"+A$(2,3)
5922 IF X0$="ZZ2MMA" THEN Q$=A$(2,3); IF A$(1,1)<>" " THEN Q$=A$(1,1)+"-"+Q$
5925 PRINT @(0,0),"Screen: ",Q$,@(39-INT(LEN(X$)/2),1),X$
5940 CALL "ZZDISP","-","02","",X3$,"","",0,0,X4$
5980 GOSUB 6200
5990 RETURN 
6000 REM "DISPLAY SCREEN - A0=1 MEANS WHOLE SCREEN,A1=1=REVERSE VIDEO
6050 CALL Z7$,X3$,A1$,Z[8],W0,W1,V0$,A[0,0],A[1,0],A[2,0],A1,V1$,V2$,E0,E1,E2,E3,W2,A0,E4,X4$
6090 RETURN 
6100 REM "MESSAGE"
6105 PRINT (0,ERR=6106)@(0,1),S$(1,14),@(0),'BR','BB',P$,'ER','EB',
6110 RETURN 
6200 REM "DISPLAY TAB SETTINGS
6205 PRINT 'SB',
6210 IF T0$="" THEN GOTO 6290
6220 FOR P=1 TO LEN(T0$) STEP 2
6225 PRINT @(NUM(T0$(P,2)),2),"+",
6230 NEXT P
6290 RETURN 
6500 REM "CHANGE DATA FIELDS TO CHARS
6505 IF V1$="" THEN GOTO 6590
6510 FOR P=1 TO LEN(V1$) STEP 4
6515 Q=(NUM(V1$(P,2))-3)*W0+NUM(V1$(P+2,2))+1
6520 V0$(Q,1)=R5$
6530 NEXT P
6535 DIM A[4,2]; A[0,0]=1,A[1,0]=W0,A[2,0]=W1,A0=1,A1=0,E2=1; GOSUB 6000
6550 W9$=V1$,V1$=""
6590 RETURN 
6600 REM "RESTORE DATA FIELDS
6610 P=POS(R5$=V0$); IF P=0 THEN GOTO 6620 ELSE V0$(P,1)=""; GOTO 6610
6620 REM "CK FOR DATA FIELDS
6625 IF POS(R5$=P1$)=0 THEN GOTO 6700
6630 V2$="",X9$="",V3$=W9$; FOR P=1 TO LEN(V8$) STEP 4; READ (Z[8],KEY=A$(1,7)+V8$(P,4))Q$; V2$=V2$+Q$(12),Q9=LEN(Q$(12)); NEXT P
6640 FOR P=1 TO LEN(V8$) STEP 4
6645 Y0=NUM(V8$(P,2)),Y1=NUM(V8$(P+2,2)),P0=POS(V8$(P,4)=V3$,4)
6650 Y0=Y0+(Y-P9),Y1=Y1+(X-P8)
6655 W9$(P0,4)=STR(Y0:"00")+STR(Y1:"00")
6660 Q$=V8$(P,4); GOSUB 7000; A0=POS(B9$=B0$); IF A0>0 THEN B0$=B0$(1,A0-1)+B0$(A0+LEN(B9$))
6662 Q$=V2$(INT(P/4)*Q9+1,Q9)
6665 Q$(7,4)=W9$(P0,4); IF POS(Q$(53,1)="FA")=0 THEN GOTO 6685
6667 REM "FILE VERIFY POSITIONS
6670 Y0=NUM(Q$(121,2)),Y1=NUM(Q$(123,2))
6675 Y1=Y1+(Y-P9),Y0=Y0+(X-P8)
6680 Q$(121,2)=STR(Y0:"00"),Q$(123,2)=STR(Y1:"00")
6685 B0$=B0$+B9$; V2$(INT(P/4)*Q9+1,Q9)=Q$,X9$=X9$+K$; NEXT P
6690 FOR P=1 TO LEN(X9$) STEP 11; REMOVE (Z[8],KEY=X9$(P,11)); NEXT P
6692 FOR P=1 TO LEN(V8$)/4; Q$=V2$((P-1)*Q9+1,Q9),K$=A$(1,7)+Q$(7,4),Q$=K$+Q$; WRITE (Z[8],KEY=K$)Q$; NEXT P
6700 REM "PAINT DATA FIELDS 
6705 A1=0; GOSUB 6000
6715 V1$=W9$
6720 DIM A[4,2]; A[0,0]=1,A[1,0]=W0,A[2,0]=W1,A0=1,A1=0,E2=2; GOSUB 6000
6790 RETURN 
6800 REM "GET DATA FIELDS IN MOVED PIECE
6810 P0=1,P1=0,V8$=""
6820 P=POS(R5$=P1$(P0)); IF P=0 THEN GOTO 6890
6830 P1=P+P0-1
6840 Q$=STR(Y+INT((P1-1)/A[1,0]):"00")+STR(MOD(P1-1,A[1,0])+X:"00")
6850 V8$=V8$+Q$,P0=P1+1
6860 GOTO 6820
6890 RETURN 
6900 REM "GET RESERVE POSITIONS - ALL
6905 IF V1$="" THEN GOTO 6990 ELSE B0$="",P$="WAIT..."; GOSUB 6100
6910 FOR A=1 TO LEN(V1$) STEP 4
6915 Q$=V1$(A,4); GOSUB 7000
6920 B0$=B0$+B9$
6930 NEXT A
6985 P$=""; GOSUB 6100
6990 RETURN 
7000 REM "ADD RESERVE POSITIONS 
7010 B9$=""
7015 K$=A1$+Q$; READ (Z[8],KEY=K$,DOM=7090)Q$; Q$=Q$(12)
7020 W9=-1
7030 Q=NUM(Q$(11,3),ERR=7090),W=NUM(Q$(9,2)); IF POS(Q$(5,1)="NC")>0 THEN CALL "ZZMASK",Q$(11,3)+Q$(6,1),M$; Q=LEN(M$),M$="" ELSE DIM W$(Q,"X"); CALL "ZZDISP",Q$(5,2)+"X",W$,"",X3$,D$,"",0,0,""; Q=LEN(D$)
7060 FOR B=W TO Q+W-1; B9$=B9$+CHR(B)+CHR(NUM(Q$(7,2))); NEXT B
7090 RETURN 
7500 REM "GET EXISTING DATA SETTINGS
7510 V1$=""
7520 READ (Z[8],KEY=A1$,DOM=7521)
7530 Z9$=KEY(Z[8],END=7560); READ (Z[8],END=7560)Q$
7532 IF Q$="" THEN REMOVE (Z[8],KEY=Z9$); GOTO 7530
7535 IF Q$(1,7)=A1$ THEN V1$=V1$+Q$(8,4); GOTO 7530
7570 GOTO 0010
8000 REM "GET CONTROL VALUE
8005 C0=-1
8020 P=POS(HTA(Q$)=C0$,20); IF P=0 THEN GOTO 8090 ELSE Q0$=Q$
8025 GOTO 8050
8040 READ RECORD (Z[9],SIZ=1,TIM=1,ERR=8070)Q$
8045 Q0$=Q0$+Q$
8050 IF LEN(Q0$)>6 THEN PRINT 'RB'; GOTO 8090
8055 Q$=HTA(Q0$),Q$=Q$+S$(1,12-LEN(Q$))
8060 P=POS(Q$=C0$,20); IF P=0 THEN GOTO 8040 ELSE P=INT(P/20)
8065 C0=P,C$=STR(C0:"00"); GOTO 8090
8070 PRINT 'RB'
8090 RETURN 
8100 REM "RANGE TYPE SCREEN
8140 R0=3
8190 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9040 CALL "ZZERRM",Y8$,"ZZ2ASB",Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9800 REM "RETURN TO 1ST OVERLAY
9820 RUN X0$
9900 REM "END PROGRAM
9920 BEGIN 
9940 RUN "ZMENU"
9999 END 
